<?wh
LOADLIB "mod::publisher/lib/embedvideo.whlib";
LOADLIB "mod::system/lib/remoting/whservice.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";


PUBLIC OBJECTTYPE VimeoProvider EXTEND EmbedVideoProviderBase
<
  UPDATE PUBLIC RECORD FUNCTION Search2(STRING searchfor, RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    options := ValidateOptions(CELL[maxresults := 25], options);
    IF(searchfor = "")
      RETURN [ erorr := "", results := RECORD[] ];

    RECORD searchres := InvokeHostedWHService("socialite/searchvimeo2", searchfor, [ languagecode := GetTidLanguage(), maxresults := options.maxresults ]);
    IF(searchres.error != "")
      RETURN [ error := searchres.error, results := RECORD[] ];

    RETURN [ error := ""
           , results := this->EnrichVideos(SELECT *
                                   , videoid := id
                                   , creationdate := (CellExists(videos, "creationdate") ? creationdate : upload_date) // TEMPORARY
                                FROM searchres.results AS videos)
    ];
  }

  UPDATE PUBLIC RECORD ARRAY FUNCTION Search(STRING searchfor, INTEGER maxresults)
  {
    RETURN this->Search2(searchfor, [ maxresults := maxresults ]).results;
  }

  UPDATE PUBLIC RECORD FUNCTION DescribeVideo(STRING videoid)
  {
    RECORD video := InvokeHostedWHService("socialite/describevimeovideo", videoid);
    IF (NOT Recordexists(video))
      RETURN DEFAULT RECORD;
    RETURN RECORD(this->EnrichVideos(SELECT *, videoid := id FROM [ video ]));
  }

  UPDATE PUBLIC STRING FUNCTION GetIFramePlayerURL(STRING videoid)
  {
    RETURN "//player.vimeo.com/video/"|| EncodeURL(videoid);
  }

  UPDATE PUBLIC STRING FUNCTION GetTitle()
  {
    RETURN "Vimeo";
  }
  UPDATE PUBLIC STRING FUNCTION GetIcon()
  {
    RETURN "tollium:social_media/vimeo";
  }
>;
