<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/virtualfs/base.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/devtools/support.whlib";

PUBLIC OBJECTTYPE VirtualWebdavInfoFS EXTEND VirtualFSBase
<
  MACRO NEW()
  {
  }

  UPDATE PUBLIC BLOB FUNCTION GetFile(STRING path)
  {
    IF(NOT GetEffectiveUser()->HasRight("system:sysop")) //for safety, only sysops get access to editor support
      THROW NEW VirtualFSException("XS", "No access");

    STRING dir := GetDirectoryFromPath(path);

    //We trust editors for 15 seconds. But we need to return a URL that works from the client's perspective
    RETURN GenerateWHConnectFile(GetClientRequestURL(), 15000, "webdav", dir);
  }

  UPDATE PUBLIC RECORD FUNCTION GetPathInfo(STRING path)
  {
    RETURN [ name := GetNameFromPath(path)
           , type := 0
           , size := 0
           , read := TRUE
           , write := FALSE
           , modificationdate := GetCurrentDatetime()
           , creationdate := GetCurrentDatetime()
           ];
  }

>;
