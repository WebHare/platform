<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::devsupport.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/language.whlib";
LOADLIB "mod::system/lib/webserver/errors.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";
LOADLIB "mod::system/lib/internal/remoting/transports.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/urlhistory.whlib";

PUBLIC STRING __errorrequesturl;

PUBLIC BOOLEAN FUNCTION IsRobotURLPath(STRING path)
{
  RETURN path = "autodiscover/autodiscover.xml"
         OR path = "favicon.ico"
         OR path = "wp-login.php"
         OR path LIKE "wp-admin/*"
         OR path LIKE "apple-touch-icon-*.png";
}

PUBLIC RECORD FUNCTION GetErrorMessages(STRING langcode, INTEGER errorcode)
{
  RECORD basetexts := langcode LIKE "nl*" ? langtexts.nl : langtexts.en;
  RECORD retval;
  IF(errorcode IN [401,403,404,500])
    retval := GetCell(basetexts,"error" || errorcode);
  ELSE
    retval := basetexts.error_unknown;

  INSERT CELL infoformanager := basetexts.infoformanager
         INTO retval;
  RETURN retval;
}

STRING FUNCTION Fixup(STRING filename)
{
  IF(filename LIKE "direct::*") //can't leak these to webservers!
    filename := GetNameFromPath(filename);
  RETURN filename;
}

PUBLIC MACRO PrintErrorBody(RECORD msg, INTEGER errorcode, RECORD harescriptinfo)
{
  Print("\n<p>" || EncodeHTML(msg.text) || "</p>");
  Print('\n<div class="wh-errorinfo" style="margin-top:30px; font-size:80%">');
  STRING id_str := CellExists(harescriptinfo,'groupid') ? `ID: ${harescriptinfo.groupid}, ` : "";
  Print("\n<p>" || EncodeHTML(msg.infoformanager) || ` ${id_str}code ${errorcode}, ${FormatDatetime("%Y-%m-%d %H:%M:%S GMT +0000", GetCurrentDatetime())}`);
  Print('</p>');
  Print('</div>');
}

MACRO SendJSONRPCError(RECORD harescriptinfo)
{
  RECORD ARRAY allerrors :=
        SELECT *
          FROM harescriptinfo.allerrors AS err
      ORDER BY istrace, istrace ? code : #err;

  OBJECT transport := GetTransportFromRequest(DEFAULT BLOB, "application/json", FALSE);

  RECORD reqdata, encoded_result;
  reqdata := transport->DecodeRequest(GetRequestBody(), "");
  encoded_result := transport->EncodeErrors(allerrors, reqdata.requesttoken);

  FOREVERY (RECORD header FROM encoded_result.headers)
    AddHTTPHeader(header.field, header.value, FALSE);

  SendBlobTo(0, encoded_result.data);
  RETURN;
}

RECORD FUNCTION FixupHarescripterrorInfo(INTEGER errorcode, RECORD harescriptinfo)
{
  RECORD data := GetRequestErrorInfo();

  IF(errorcode = 500 AND NOT RecordExists(harescriptinfo))
  {
    harescriptinfo := CELL
        [ allerrors := data.errors
        , data.groupid
        , data.resources
        ];
  }
  RETURN CELL[...harescriptinfo, statuscode := errorcode, statusmessage := data.statusheader ];
}

/** Generate an error, using webdesign if possible for nicer formatting
    @param errorcode HTTP error code
    @param harescriptinfo
    @cell harescriptinfo.groupid Group ID of the script that failed
    @cell harescriptinfo.allerrors List of errors
    @cell(string array) harescriptinfo.resources List of resources loaded in the failing script
*/
PUBLIC MACRO GenerateHTTPErrorPage(INTEGER errorcode, RECORD harescriptinfo)
{
  harescriptinfo := FixupHarescripterrorInfo(errorcode, harescriptinfo);

  IF(errorcode = 500)
  {
    IF (IsRequestPost() AND Tokenize(GetWebHeader("Content-Type"),';')[0] = "application/json")
    {
      //FIXME why isn't this handler limited to /wh_services/ folders ?
      TRY
      {
        SendJSONRPCError(harescriptinfo);
        RETURN;
      }
      CATCH ; //ignore. try to handle as a 'normal' harescript error, as we aren't talking JSON/RPC apparently
    }
  }

  STRING url := GetRequestURL();
  RECORD unpacked := UnpackURL(url);
  IF(unpacked.urlpath LIKE "modules/system/errors/40?.shtml*")
  {
    url := GetWebVariable("url"); //allow testing
    unpacked := UnpackURL(url);
  }

  //Test against well known probes. No point in conjuring up advanced error handling for those
  IF(errorcode = 404 AND IsRobotURLPath(unpacked.urlpath))
    RETURN;

  IF (NOT HavePrimaryTransaction())
    OpenPrimary();

  IF(errorcode = 500 AND IsWHDebugOptionSet("etr"))
  {
    MakeFunctionPtr(Resolve("errorhandler-hserror.whlib#GenerateHarescriptErrorPage"))(harescriptinfo);
    RETURN;
  }

  RECORD urlinfo := LookupPublisherURL(url);

  IF(errorcode = 404 AND GetRequestMethod()="GET")
  {
    IF(urlinfo.site != 0)
    {
      RECORD urlhistory := GetApplyTesterForObject(urlinfo.site)->GetURLHistorySettings();

      IF(urlhistory.redirecton404)
      {
        TryRedirectUsingURLHistory();
      }
    }
  }

  //Add extra info if status was added as HTTP/2 cannot pass on status messages
  IF(harescriptinfo.statusmessage != "")
    AddHTTPHeader("X-WebHare-Error-Status", harescriptinfo.statuscode || " " || harescriptinfo.statusmessage, TRUE);

  IF(NOT RecordExists(urlinfo) OR urlinfo.site = 0)
    RETURN; //cannot handle in design, so let the webserver figure it out

  /* We used to look at the siteroot thinking 'your supportserrors etc better be consistent'
     but now we think "that's actually completely your problem!" if you make seemingly
     inconsistent settings */
  OBJECT applytester := GetApplyTesterForObject(urlinfo.file ?? urlinfo.folder);
  IF(NOT ObjectExists(applytester))
    THROW NEW Exception("Unable to locate the base information for #" || urlinfo.site);

  RECORD webdesigninfo := applytester->GetWebDesignObjinfo();
  IF(NOT RecordExists(webdesigninfo))
    RETURN;

  IF ((errorcode = 0 OR webdesigninfo.supportserrors)
     AND (webdesigninfo.supportsaccessdenied != FALSE OR errorcode NOT IN [401,403]))
  {
    IF (CellExists(harescriptinfo, "resources"))
    {
      // Register resources from failed script as resources of the error handler too, so
      // if the error handler fails, we'll report the resources of the original script too
      FOREVERY (STRING resourcename FROM harescriptinfo.resources)
        __HS_INTERNAL_REGISTERLOADEDRESOURCE(resourcename);
    }


    OBJECT webdesign := InstantiateWebDesign(applytester, webdesigninfo, errorcode, DEFAULT OBJECT, DEFAULT OBJECT, FALSE);
    webdesign->htmlclasses := webdesign->htmlclasses CONCAT [ "wh-errorpage", "wh-errorpage--" || errorcode];
    webdesign->SetJSPluginConfig("system:errorhandler", CELL[harescriptinfo.statuscode, harescriptinfo.statusmessage]);

    webdesign->PrepareErrorPage(errorcode, harescriptinfo, url);
    webdesign->RunPage();
    RETURN;
  }
}
