<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

PUBLIC STRING FUNCTION GetWebserverBaseOutputFolder()
{
  RETURN GetWebHareConfiguration().basedataroot || 'output/';
}

/** @cell inrows.type Webserver type
    @cell inrows.diskfolder Webserver disk folder */
PUBLIC RECORD ARRAY FUNCTION EnrichWebserversWithOutputFolder(RECORD ARRAY inrows)
{
  FOREVERY(RECORD row FROM inrows)
  {
    STRING outputfolder;
    IF(row.type=1)
    {
      outputfolder := MergePath(GetWebhareConfiguration().varroot, "system.webharebackend");
    }
    ELSE IF(row.diskfolder = "" OR IsPathAbsolute(row.diskfolder))
    {
      outputfolder := row.diskfolder;
    }
    ELSE
    {
      outputfolder := MergePath(GetWebserverBaseOutputFolder(), row.diskfolder);
    }

    IF (outputfolder != "" AND outputfolder NOT LIKE '*/') //need to append a slash?
      outputfolder := outputfolder || '/';

    INSERT CELL outputfolder := outputfolder INTO row;
    inrows[#row] := row;
  }
  RETURN inrows;
}

PUBLIC STRING FUNCTION GetFSObjectDiskOutputPath(INTEGER res)
{
  RECORD resinfo := SELECT webservers.diskfolder
                         , siteoutputfolder := sites.outputfolder
                         , fs_objects.fullpath
                         , webservers.type
                      FROM system.webservers
                         , system.sites
                         , system.fs_objects
                     WHERE fs_objects.id = res
                           AND fs_objects.parentsite = sites.id
                           AND sites.outputweb = webservers.id;
  IF(RecordExists(resinfo))
  {
    STRING diskfolder := EnrichWebserversWithOutputFolder([ resinfo ])[0].outputfolder;
    STRING path := CollapsePath(diskfolder || "/" || resinfo.siteoutputfolder  || "/" || resinfo.fullpath);
    IF(resinfo.fullpath LIKE "*/" AND path NOT LIKE "*/")
      path := path || "/";
    RETURN path;
  }
  RETURN "";
}

PUBLIC RECORD FUNCTION DescribeDiskOutput(INTEGER siteid, STRING subfolder) //TODO we strongly overlap with above GetFSObjectDiskOutputPath and should probably merge
{
  STRING webserver_diskroot;
  STRING outputextension := ".html";

  IF(siteid = whconstant_whfsid_webharebackend)
  {
    //Force the backend site to the proper location - this ensures we can still publish the applicationportal file for use by the Rescue port before the primary output is configured
    webserver_diskroot := MergePath(GetWebhareConfiguration().varroot, "system.webharebackend");
  }
  ELSE
  {
    RECORD primaryoutput := SELECT outputweb, outputfolder
                              FROM system.sites
                             WHERE id = siteid;

    IF(primaryoutput.outputweb=0) //unpublished?
      RETURN DEFAULT RECORD;

    RECORD ARRAY webinfos := SELECT id, diskfolder, webservers.outputextension, type
                               FROM system.webservers
                              WHERE id = primaryoutput.outputweb;

    webinfos := EnrichWebserversWithOutputFolder(webinfos); //This may still return system.webharebackend. It often does for testfw-sites
    webserver_diskroot := webinfos[0].outputfolder;
    IF(webserver_diskroot = "")
      RETURN DEFAULT RECORD; //rule-only webserver?

    outputextension := webinfos[0].outputextension;
    webserver_diskroot := MergePath(webserver_diskroot, ToLowercase(primaryoutput.outputfolder));
  }

  STRING diskfolder;
  IF(webserver_diskroot = "")
    RETURN DEFAULT RECORD;

  diskfolder := MergePath(webserver_diskroot, ToLowercase(subfolder));
  RETURN CELL[ outputextension
             , outputfolder := diskfolder
             ];
}

