<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/harescript/preload.whlib";
LOADLIB "mod::system/lib/internal/modules/bridgesupport.whlib";
LOADLIB "mod::system/lib/internal/webserver/support.whlib";

LOADLIB "mod::publisher/lib/internal/webdesign/webcontext.whlib";
LOADLIB "mod::publisher/lib/internal/pagerendering.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";

OBJECT trans;
RECORD rpc_result;
OBJECT rpc_witty;
INTEGER currentfileid;

BOOLEAN FUNCTION AllowedToCache(RECORD rec)
{
  IF(GetWebHeader("User-agent") LIKE "Consilio/*")
    RETURN FALSE;

  IF(rec.cachettl <= 0)
    RETURN FALSE;
  IF(GetRequestMethod() NOT IN ["HEAD","GET"])
    RETURN FALSE;

  FOREVERY(STRING cookie FROM rec.cacheblacklistcookies)
    IF(GetWebCookie(cookie)!="")
      RETURN FALSE;
  FOREVERY(STRING webvar FROM rec.cacheblacklistvariables)
    IF(GetWebVariable(webvar)!="")
      RETURN FALSE;

  RETURN TRUE;
}

STRING FUNCTION WebListToString(RECORD ARRAY inlist, STRING ARRAY whitelist)
{
  IF(Length(whitelist)=0)
    RETURN "";
  BOOLEAN hashall := Length(whitelist) = 1 AND whitelist[0]="*";

  RETURN Detokenize( (SELECT AS STRING ARRAY ToUppercase(name) || "\t" || value
                        FROM inlist
                       WHERE (hashall ? TRUE : ToUppercase(name) IN whitelist)
                    ORDER BY ToUppercase(name),value),"\t");
}

STRING FUNCTION CalculateCacheKey(INTEGER fileid, RECORD rec, STRING suburl)
{
  STRING hashbase := fileid
                     || "\n" || WebListToString(GetAllWebVariables(), rec.cachewebvariables)
                     || "\n" || WebListToString(GetAllWebCookies(), rec.cachewebcookies)
                     || "\n" || suburl;

  RETURN hashbase;
}

RECORD FUNCTION GenerateCacheablePage(RECORD redirectinfo, RECORD options) //FIXME catch sendwebfile, redirect, headers etc
{
  INTEGER str := CreateStream();
  INTEGER saveout := RedirectOutputTo(str);
  IF(NOT ObjectExists(trans)) //Not opened yet by the cache, so get it ourselves
    trans := OpenPrimary();

  OBJECT pageobject := RunThePage(currentfileid, redirectinfo, currentfileid, options);
  BLOB output := MakeBlobFromSTream(str);
  RedirectOutputTo(saveout);

  RECORD cachesettings;
  IF(ObjectExists(pageobject))
    cachesettings := pageobject->GetDynamicPageCacheSettings();
  IF(NOT RecordExists(cachesettings))
    cachesettings := [ ttl := redirectinfo.cachettl * 1000 ];

  RETURN CELL[ ...cachesettings
             , value := [ data := output
                        , headers := sendhttpheaders
                        ]
             ];
}

PUBLIC MACRO DoWHFSExecuteJS(INTEGER targetfile, RECORD options DEFAULTSTO DEFAULT RECORD) {
  DATETIME start := GetCurrentDatetime();
  RECORD response := CallJS("@webhare/router/src/corerouter.ts#executeContentPageRequestHS", targetfile, CELL[ ...options, webreq := EncodeWebRequestForJS() ]);
  //x-hs-host, if present, is always the last header
  IF(Length(response.headers) > 0 AND response.headers[END-1][0]="x-hs-host") {
    INTEGER timespent := GetMsecsDifference(start, GetCurrentDatetime());
    response.headers[END-1][1] := response.headers[END-1][1] || " whfsexecute=" || timespent;
  }

  ExecuteWebResponse(response);
}

PUBLIC MACRO DoWHFSExecute(INTEGER fileid, RECORD rec, RECORD options DEFAULTSTO DEFAULT RECORD) {
  IF(rec.js = TRUE) {
    DoWHFSExecuteJS(fileid);
    RETURN; //for clarity, never reached - ExecuteWebResponse in DoWHFSExecuteJS implies SendWebFile and terminates
  }

  currentfileid := fileid;
  __currentwhfsscriptid := fileid;
  __pageinfo := __SplitBaseAndSubUrl(GetClientRequestURL(), rec.objurlpath);

  IF(AllowedToCache(rec))
  {
    //ADDME Can we add the whlib modification itself to the cachekey? should we?
    STRING cachekey := CalculateCacheKey(currentfileid, rec, __pageinfo.subpath);
    RECORD page := GetAdhocCached(CELL[ cachekey, options ], PTR GenerateCacheablePage(rec, options));

    DELETE FROM sendhttpheaders; //delete current headers (if cache miss) before readding
    FOREVERY(RECORD header FROM page.headers)
      AddHTTPHeader(header.header, header.data, header.always_add);
    SendWebFile(page.data);
  }

  IF(NOT ObjectExists(trans)) //Not opened yet by the cache, so get it ourselves
    trans := OpenPrimary();

  RunThePage(currentfileid, rec, currentfileid, options);
}


PUBLIC MACRO RunDynamicPageInWebdesign(STRING funcname, STRING webdesignurl, RECORD options) {
  IF(IsWasm()) {
    abort(funcname);
  }

  TRY {
    OBJECT webdesign := GetWebDesignForURL(webdesignurl);
    OBJECT page := MakeFunctionPtr(funcname)(webdesign, options);
    IF(NOT ObjectExists(page))
      AbortWithHTTPError(404, "No page available for this URL");

    webdesign->RunPageWithContents(page->GetPageBody());
  } CATCH(OBJECT<RequiresJSRouterException> e) {
    DATETIME start := GetCurrentDatetime();

    RECORD response := CallJS("@webhare/router/src/corerouter.ts#executeSHTMLRequestHS", EncodeWebRequestForJS(), webdesignurl, funcname, options);
    //x-hs-host, if present, is always the last header
    IF(Length(response.headers) > 0 AND response.headers[END-1][0]="x-hs-host") {
      INTEGER timespent := GetMsecsDifference(start, GetCurrentDatetime());
      response.headers[END-1][1] := response.headers[END-1][1] || " whfsexecute=" || timespent;
    }

    ExecuteWebResponse(response);
  }
}
