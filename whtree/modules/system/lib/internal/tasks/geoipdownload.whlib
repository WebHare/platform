<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/archiving.whlib";
LOADLIB "wh::internet/http.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internet/webbrowser.whlib";
LOADLIB "mod::system/lib/configure.whlib";

PUBLIC OBJECTTYPE GeoIPCheck EXTEND CustomCheckBase
<
  UPDATE PUBLIC RECORD ARRAY FUNCTION GetIssueList()
  {
    DATETIME oldest := MAX_DATETIME;
    STRING basedir := MergePath(GetWebHareConfiguration().varroot, `geoip`);

    FOREVERY(STRING database FROM ["Country","City"])
    {
      STRING downloadedpath := MergePath(basedir, `geoip-${ToLowercase(database)}.mmdb`);
      RECORD currentversionprops := GetDiskFileProperties(downloadedpath);

      IF(RecordExists(currentversionprops) AND currentversionprops.modified != DEFAULT DATETIME AND currentversionprops.modified < oldest)
        oldest := currentversionprops.modified;
    }

    IF(oldest != MAX_DATETIME)
    {
      INTEGER age := GetDatetimeDifference(oldest, GetCurrentDatetime()).days;
      IF(age >= 30)
        RETURN RECORD[ [ msg:= `:The geoip database in ${basedir} is ${age} days old. The EULA requires you to update or delete this database after 30 days` ]
                    ];
    }
    RETURN RECORD[];
  }
>;



PUBLIC RECORD FUNCTION UpdateGEOIPDatabases(STRING licensekey)
{
  IF(licensekey = "")
    THROW NEW Exception(`No license key set`);

  // Url with links to downloads: https://dev.maxmind.com/geoip/geoip2/geolite2/
  OBJECT browser := NEW WebBrowser;
  STRING geoiproot := GetWebHareConfiguration().varroot || "geoip";
  CreateDiskDirectoryRecursive(geoiproot, TRUE);

  STRING ARRAY errors, messages;

  //Remove geoip legacy
  DeleteDiskWildcardsRecursive(MergePath(geoiproot,"source"));
  DeleteDiskWildcardsRecursive(MergePath(geoiproot,"db_*.dat"));

  FOREVERY(STRING database FROM ["Country","City"])
  {
    STRING sourceurl := UpdateURLVariables(`https://download.maxmind.com/app/geoip_download`,
                                             [ edition_id := `GeoLite2-${database}`
                                             , license_key := licensekey
                                             , suffix := "tar.gz"
                                             ]);

    STRING downloadedpath := MergePath(GetWebHareConfiguration().varroot, `geoip/geoip-${ToLowercase(database)}.mmdb`);
    RECORD currentversionprops := GetDiskFileProperties(downloadedpath);

    RECORD ARRAY headers;
    IF(RecordExists(currentversionprops) AND currentversionprops.modified != DEFAULT DATETIME)
    {
      INSERT [ field := "If-Modified-Since", value := FormatHttpDateTime(currentversionprops.modified) ] INTO headers AT END;
      INSERT "We currently have version: " || FormatDatetime("%Y-%m-%d %H:%M:%S", currentversionprops.modified) || " of " || database INTO messages AT END;
    }

    browser->SendRawRequest("GET", sourceurl, headers, DEFAULT BLOB);
    IF(browser->GetHTTPStatusCode() = 200)
    {
      BLOB mmdb := SELECT AS BLOB data FROM UnpackArchive(browser->content) WHERE name LIKE "*.mmdb";
      IF(Length(mmdb) = 0)
      {
        INSERT `Download of '${database}' database failed` INTO messages AT END;
        CONTINUE;
      }

      StoreDiskFile(downloadedpath, mmdb, [ overwrite := TRUE ]);
      INSERT "Updated GeoIP database from " || sourceurl INTO messages AT END;
    }
    ELSE
    {
      INSERT "GeoIP database at " || sourceurl || ": " || browser->GetHTTPStatusText() INTO messages AT END;
    }
  }

  RETURN CELL [ success := Length(errors) = 0, errors, messages ];
}
