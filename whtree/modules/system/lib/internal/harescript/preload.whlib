<?wh
(*ISSYSTEMLIBRARY*)

LOADLIB "wh::internal/interface.whlib";
LOADLIB "wh::internal/transservices.whlib";
LOADLIB "wh::internal/debug.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::system.whlib";

LOADLIB "mod::system/lib/internal/whcore_interface.whlib";
LOADLIB "mod::system/lib/internal/cluster/logwriter.whlib";

MACRO EnableFunctionProfile() __ATTRIBUTES__(EXTERNAL);
MACRO DisableFunctionProfile() __ATTRIBUTES__(EXTERNAL);
RECORD FUNCTION __INTERNAL_GetRawFunctionProfile() __ATTRIBUTES__(EXTERNAL);
BOOLEAN FUNCTION CreateDiskDirectoryRecursive(STRING directory, BOOLEAN visible) __ATTRIBUTES__(EXTERNAL);
STRING FUNCTION MergePath(STRING first, STRING second) __ATTRIBUTES__ (EXTERNAL);
MACRO __HS_INTERNAL_SETDEBUGGINGTAGS(STRING ARRAY tags) __ATTRIBUTES__(EXTERNAL);


PUBLIC STRING __forcecurrentsite; //used by publishjob.whscr to set the proper resolution base for currentsite::
PUBLIC INTEGER __currentwhfsscriptid; //we need our storedscriptproperty FILEID to support currentsite:: witty loads from SHTML files. see basetest-moduledesign test

__ondebuglog := PTR OnDebugLog;

BLOB FUNCTION GetWebhareResource(STRING path)
{
  RECORD result := MakeFunctionPtr("mod::system/lib/resources.whlib#RetrieveWebHareResource",TYPEID(RECORD),[TYPEID(STRING),TYPEID(RECORD)])(path,DEFAULT RECORD);
  RETURN result.data;
}

MACRO OnDebugLog(STRING source, VARIANT msg)
{
  INTEGER msglen := Length(EncodEJSON(msg));
  IF(msglen > 127*1024)
    LogToJSONLog("system:debug", CELL[ source, groupid := GetCurrentGroupId(), session := GetExternalSessionData(), overflow := msglen ]);
  ELSE
    LogToJSONLog("system:debug", CELL[ source, groupid := GetCurrentGroupId(), session := GetExternalSessionData(), data := msg ]);
}

MACRO GotUnhandledRejection(RECORD data)
{
  LogNoticeException("script-unhandledrejection", "", data.e, CELL[ data := CELL[ data.promise_id ] ]);
}

MACRO GotRejectionHandled(RECORD data)
{
  LogToJSONLog("system:notice", [ type := "script-rejectionhandled", groupid := GetCurrentGroupId(), session := GetExternalSessionData(), promise_id := data.promise_id ]);
}

MACRO GotTooManyUnhandledRejections()
{
  LogToJSONLog("system:notice", [ type := "script-toomanyunhandledrejections", groupid := GetCurrentGroupId(), session := GetExternalSessionData() ]);
}

MACRO ReadDebugConfig()
{
  STRING debug_str := GetEnvironmentVariable("WEBHARE_DEBUG");
  IF (debug_str != "")
  {
    ReadDebugSetting(debug_str);
    RETURN;
  }

  RECORD systemconfig := __SYSTEM_GETSYSTEMCONFIG();
  IF (CellExists(systemconfig, "DEBUGCONFIG") AND TypeID(systemconfig.debugconfig) = TypeID(RECORD))
  {
    // Do NOT crash on configuration record errors
    TRY
    {
      __debugconfig := ValidateOptions(
          [ tags :=             STRING[]
          , outputsession :=    "default"
          , context :=          ""
          ], systemconfig.debugconfig);
      __HS_INTERNAL_SETDEBUGGINGTAGS(__debugconfig.tags);
      IF (ObjectExists(GetPrimaryWebhareTransactionObject()))
        GetPrimaryWebhareTransactionObject()->__UpdateDebugConfig(DEFAULT RECORD);
      RETURN;
    }
    CATCH
    {
      //Ignore. leave __debugconfig as it is
    }
  }

  //If we have initial settings (set by wh console) apply those.
  debug_str := GetEnvironmentVariable("__WEBHARE_DEBUG_INITIALSETTING");
  IF(debug_str != "")
    __SYSTEM_SETSYSTEMCONFIG(CELL[...systemconfig, debugconfig := ReadDebugSetting(debug_str) ]);
}

STRING profilingtype;
DATETIME profilingstart;
STRING profilingsession;
STRING profilingcontext;
BOOLEAN gotfirstconfig;

MACRO HandleConfigChange()
{
  RECORD oldconfig := __debugconfig;
  ReadDebugConfig();
  IF (EncodeHSON(oldconfig) != EncodeHSON(__debugconfig))
  {
    HandleDebugConfig(NOT gotfirstconfig);
    gotfirstconfig := TRUE;
  }
}

MACRO HandleDebugConfig(BOOLEAN initialapply)
{
  STRING wantprofiletype, wantcontext;

  IF(initialapply AND RecordExists(__debugconfig) AND "brk" IN __debugconfig.tags)
    Debugger();

  IF(RecordExists(__debugconfig))
  {
    wantcontext := __debugconfig.context;
    IF("apr" IN __debugconfig.tags)
      wantprofiletype := "apr";
    ELSE IF("cov" IN __debugconfig.tags)
      wantprofiletype := "cov";
  }

  IF(wantprofiletype != profilingtype OR profilingcontext != wantcontext) //either the type of profile or our context changed
  {
    IF(profilingtype != "")
      FinishProfiling(); //close any running profile session. clears variable profilingtype

    IF(wantprofiletype != "")
    {
      profilingtype := wantprofiletype;
      profilingstart := GetCurrentDateTime();
      profilingsession := __debugconfig.outputsession;
      profilingcontext := wantcontext;
      __OnBeforeTerminateScript := PTR FinishProfiling;

      IF(profilingtype = "apr")
        EnableFunctionProfile();
      ELSE
        EnableCoverageProfile();
    }
  }
}

MACRO GotConfigChange(STRING event, RECORD data)
{
  HandleConfigChange();
}

RegisterEventCallback("system:systemconfig", PTR GotConfigChange);

HandleConfigChange();

RECORD FUNCTION GetScriptData()
{
  STRING script;
  RECORD libinfo := __HS_INTERNAL_GetLibrariesInfo(TRUE); // get the first directly loaded
  IF (LENGTH(libinfo.libraries) != 0)
    script := libinfo.libraries[0].liburi;

  RECORD authrec := GetAuthenticationRecord();
  RETURN CELL
      [ script
      , profilingstart
      , groupid :=                GetCurrentGroupId()
      , authenticationrecord :=   GetAuthenticationRecord()
      , debugcontext :=           profilingcontext
      ];
}

MACRO SaveProfileFile(STRING type, STRING ext, VARIANT profiledata)
{
  RECORD data := CELL
      [ type
      , ...GetScriptData()
      ];

  STRING encodeddata := JSONENCODETOSTRING(data, FALSE, DEFAULT RECORD, DEFAULT RECORD);
  BLOB encodedrawdata := JSONENCODETOBLOB(profiledata, TRUE, DEFAULT RECORD, DEFAULT RECORD);

  STRING timestamp := Substitute(SubString(EncodeHSON(GetCurrentDatetime()), 7, 19), ".", "");
  STRING filename := timestamp || "-" || GetCurrentGroupId() || ext;

  STRING outputdir := __SYSTEM_WHCOREPARAMETERS().basedataroot || "caches/platform/hs-profiles/" || profilingsession;

  CreateDiskDirectoryRecursive(outputdir, TRUE);
  INTEGER out := __HS_OpenDiskFile(MergePath(outputdir, filename), TRUE, TRUE, TRUE, TRUE);
  IF (out != 0)
  {
    PrintTo(out, encodeddata);
    PrintTo(out, "\n");
    SendBlobTo(out, encodedrawdata);

    __HS_CloseFile(out);

    __HS_EVENT_BROADCAST("system:storedprofile." || profilingsession,
        [ __recordexists := FALSE
        , __sourcegroup := GetCurrentGroupId()
        ], FALSE);
  }
}

MACRO SaveCoverageProfile()
{
  DisableCoverageProfile();
  SaveProfileFile("coverageprofile", ".hscov", __INTERNAL_GETCOVERAGEPROFILEDATA());
  profilingtype := "";
  profilingsession := "";
  profilingcontext := "";
}

MACRO SaveFunctionProfile()
{
  DisableFunctionProfile();
  SaveProfileFile("functionprofile", ".prof", __INTERNAL_GetRawFunctionProfile());
  profilingtype := "";
  profilingsession := "";
  profilingcontext := "";
}

MACRO FinishProfiling()
{
  IF (profilingtype = "cov")
    SaveCoverageProfile();
  ELSE IF (profilingtype = "apr")
    SaveFunctionProfile();
}

MACRO Shutdown() __ATTRIBUTES__(DEINITMACRO)
{
  FinishProfiling();
}

__GetHSResourceCall := PTR GetWebhareResource;
__UnhandledRejection := PTR GotUnhandledRejection;
__RejectionHandled := PTR GotRejectionHandled;
__TooManyUnhandledRejections := PTR GotTooManyUnhandledRejections;
