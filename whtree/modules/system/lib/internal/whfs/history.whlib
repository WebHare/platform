<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

//To avoid races, create the versionfolder for a site immediately when creating the site. But having multiple shouldn't really mater
PUBLIC INTEGER FUNCTION EnsureVersionFolder(INTEGER site)
{
  //Always prefer the 'oldest' version folder. cleanupversions will merge newers into the oldest anyway
  INTEGER versionroot := SELECT AS INTEGER id
                           FROM system.fs_objects
                          WHERE parent = whconstant_whfsid_versions
                                AND filelink = site
                       ORDER BY creationdate;
  IF(versionroot = 0)//create that first
  {
    DATETIME now := GetCurrentdatetime();
    versionroot := MakeAutonumber(system.fs_objects, "id");

    INSERT INTO system.fs_objects(id, parent, name, filelink, creationdate, modificationdate, isfolder)
           VALUES(versionroot, 11, ToString(versionroot), site, now, now, TRUE);
  }
  RETURN versionroot;
}

PUBLIC BOOLEAN FUNCTION IsHistoryEnabledForSite(INTEGER parentsite)
{
  //TODO check if this is worth caching..
  RETURN parentsite != 0 AND RecordExists(SELECT FROM system.sites WHERE id = VAR parentsite AND historydays > 0);
}

PUBLIC MACRO AddCreateHistory(RECORD metadata)
{
  IF(NOT TestFlagFromPublished(metadata.published, PublishedFlag_History))
    RETURN;

  OBJECT commithandler := GetWHFSCommitHandler();
  RECORD editevent := RecordLowerBound(commithandler->historyitems, [ fsobject := metadata.id ], [ "fsobject" ]);

  INTEGER historyeventid := MakeAutonumber(system.fs_history, "id");
  INSERT INTO system.fs_history(id, fs_object, user, type, when)
         VALUES(historyeventid, metadata.id, GetEffectiveUserId(), whconstant_historytype_create, metadata.creationdate);

  INSERT CELL[ fsobject := metadata.id
             , historyid := historyeventid
             , iscreate := TRUE
             ] INTO commithandler->historyitems AT editevent.position;
}

PUBLIC MACRO AddModifyHistory(DATETIME now, RECORD oldversion, RECORD newversion)
{
  IF(NOT TestFlagFromPublished(newversion.published, PublishedFlag_History))
    RETURN;

  OBJECT commithandler := GetWHFSCommitHandler();
  RECORD editevent := RecordLowerBound(commithandler->historyitems, [ fsobject := oldversion.id ], [ "fsobject" ]);
  IF(editevent.found) //we already have created a system.fs_history this transaction...
  {
    IF(NOT commithandler->historyitems[editevent.position].iscreate) //we leave creates alone, just pretend it was properly created the first time around
      UPDATE system.fs_history SET when := now WHERE id = commithandler->historyitems[editevent.position].historyid;
  }
  ELSE
  {
    INTEGER historyeventid := MakeAutonumber(system.fs_history, "id");
    INSERT INTO system.fs_history(id, fs_object, user, type, when)
           VALUES(historyeventid, oldversion.id, GetEffectiveUserId(), whconstant_historytype_modify, now);

    INSERT CELL[ fsobject := oldversion.id
               , historyid := historyeventid
               , iscreate := FALSE
               ] INTO commithandler->historyitems AT editevent.position;
  }
}
