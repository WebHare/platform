<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/tasks.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

RECORD FUNCTION GetCachablePreviewableTypes()
{
  INTEGER ARRAY types :=
      SELECT AS INTEGER ARRAY id
        FROM system.fs_types
       WHERE generatepreview
    ORDER BY id;

  RETURN
      [ value :=        types
      , eventmasks :=   [ "publisher:internal.siteprofiles.recompiled" ]
      ];
}

PUBLIC RECORD FUNCTION DefaultAfterMetadataUpdate(RECORD indata)
{
  INTEGER ARRAY preview_types := GetAdhocCached([ type := "genpreview" ], PTR GetCachablePreviewableTypes);

  IF (LowerBound(preview_types, indata.newtype).found)
    ScheduleManagedTask("publisher:generatewidgetpreview", [ id := indata.id ], DEFAULT RECORD);
  ELSE IF (LowerBound(preview_types, indata.oldtype).found)
    OpenWHFSType("http://www.webhare.net/xmlns/publisher/previewinfo")->SetInstancedata(indata.id, DEFAULT RECORD);

  RETURN indata;
}
