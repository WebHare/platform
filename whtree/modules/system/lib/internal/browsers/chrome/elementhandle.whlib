<?wh

LOADLIB "mod::system/lib/internal/browsers/chrome/helper.whlib";

/*
https://github.com/GoogleChrome/puppeteer/blob/master/docs/api.md#class-elementhandle
*/

PUBLIC STATIC OBJECTTYPE ElementHandle
<
  OBJECT pvt_client;
  OBJECT pvt_frame;
  OBJECT pvt_mouse;

  RECORD pvt_remoteobject;
  BOOLEAN pvt_disposed;

  PUBLIC PROPERTY frame(pvt_frame, -);

  MACRO NEW(OBJECT client, OBJECT frame, OBJECT mouse, RECORD remoteobject)
  {
    this->pvt_client := client;
    this->pvt_frame := frame;
    this->pvt_mouse := mouse;

    this->pvt_remoteobject := remoteobject;
  }

  PUBLIC ASYNC MACRO Dispose()
  {
    IF (this->pvt_disposed)
      RETURN;
    this->pvt_disposed := TRUE;
    ReleaseObject(this->pvt_client, this->pvt_remoteobject);
  }

  PUBLIC STRING FUNCTION GetRemoteObjectId()
  {
    RETURN this->pvt_disposed ? "" : this->pvt_remoteobject.objectid;
  }

  ASYNC FUNCTION VisibleCenter()
  {
    STRING script :=
      `element => {
            if (!element.ownerDocument.contains(element))
              return null;
            element.scrollIntoViewIfNeeded();
            const rect = element.getBoundingClientRect();
            return {
              x: (Math.max(rect.left, 0) + Math.min(rect.right, window.innerWidth)) / 2,
              y: (Math.max(rect.top, 0) + Math.min(rect.bottom, window.innerHeight)) / 2
            };
          }`;

    RECORD center := AWAIT this->pvt_frame->Evaluate(script, this);
    IF (NOT RecordExists(center))
      THROW NEW Exception("No node found for element");
    RETURN center;
  }

  PUBLIC ASYNC FUNCTION __getBoundingClientRect() //we could also implement Boundingbox, on top of this or directly through its puppeteer API ?
  {
    STRING script :=
      `element =>
         {
           const rect = element.getBoundingClientRect();
           return { width: rect.width, height: rect.height, top: rect.top, bottom: rect.bottom, left: rect.left, right: rect.right };
         }`;

    RETURN AWAIT this->pvt_frame->Evaluate(script, this);
  }

  PUBLIC ASYNC MACRO Click(RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    RECORD center := AWAIT this->VisibleCenter();
    this->pvt_mouse->Click(INTEGER(center.x), INTEGER(center.y), options);
  }
>;
