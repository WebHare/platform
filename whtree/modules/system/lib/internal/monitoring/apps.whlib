<?wh
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::internal/interface.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";

PUBLIC OBJECTTYPE TolliumAppListBase EXTEND DashboardPanelBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD ARRAY moduleapps;
  RECORD ARRAY moduleappslookup;

  /** @cell(string) id
      @cell(object) link
  */
  RECORD ARRAY applicationstarters;

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init()
  {
    this->RefreshModuleInformation();

    RegisterEventCallback("system:softreset", PTR this->GotSoftReset);
    RegisterEventCallback("tollium:applicationstarter.exists", PTR this->GotApplicationStarter);

    BroadcastEvent("tollium:applicationstarter.query", DEFAULT RECORD);
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO GotSoftReset(STRING event, RECORD data)
  {
    this->RefreshModuleInformation();
  }

  MACRO GotApplicationStarter(STRING event, RECORD data)
  {
    RECORD pos := RecordLowerBound(this->applicationstarters, data, [ "ID" ]);
    IF (pos.found)
      RETURN;

    // HS native apps currently run inside the webserver, but HS wasm and TS apps never do
    OBJECT link := ConnectToGlobalIPCPort("tollium:link." || data.id);
    IF (NOT ObjectExists(link))
      RETURN;

    INSERT
        [ id :=     data.id
        , link :=   link
        ] INTO this->applicationstarters AT pos.position;

    this->RefreshDashboardPanel();
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO RefreshModuleInformation()
  {
    // we store this now, because GetWebHareModules doesn't use a cache, so it reloads all moduledefinitions.
    // unfortunately there's also no event for reloading modules/softreset....

    this->moduleapps := SELECT name
                             , isbuiltinmodule
                             , applications := RecordExists(portal) ? portal.applications : DEFAULT RECORD ARRAY
                          FROM GetWebHareModules(); // or GetWebHareModules()

    // create a list for quick lookup of the title based on the application name

    this->moduleappslookup := DEFAULT RECORD ARRAY;

    FOREVERY(RECORD module FROM this->moduleapps)
    {
      this->moduleappslookup := this->moduleappslookup
                                CONCAT
                                // add applications
                                (SELECT fullname := app.name
                                      , title := GetTid(title)
                                      , description := GetTid(description)
                                   FROM module.applications AS app);

    }
  }

  RECORD ARRAY FUNCTION GetCurrentApps()
  {
    RECORD ARRAY running_jobs;
    FOREVERY (RECORD starter FROM this->applicationstarters)
    {
      RECORD rec := starter.link->DoRequest([ task := "getapps" ]);
      IF (rec.status = "gone")
      {
        starter.link->Close();
        DELETE FROM this->applicationstarters WHERE id = starter.id;
      } ELSE {
        running_jobs := running_jobs CONCAT
            SELECT *
                , starter_id := starter.id
              FROM rec.msg.apps;
      }
    }

    RECORD default_job :=
        [ statistics :=     DEFAULT RECORD
        , creationdate :=   DEFAULT DATETIME
        ];

    RECORD ARRAY apps :=
        SELECT *
             , title := (SELECT AS STRING title FROM this->moduleappslookup WHERE fullname = running_jobs.app) // RecordLowerBound!
          FROM running_jobs
         WHERE stopped = VAR MAX_DATETIME; //VAR to prevent datetime.whlib from being compiled away

    apps :=
        SELECT *
             , use :=
                    [ authobjectid :=     authenticationrecord.tollium.user.authobjectid
                    , wrdentityid :=      authenticationrecord.tollium.user.wrdentityid
                    , sessionid :=        appid
                    , username :=         authenticationrecord.tollium.user.login
                    , realname :=         authenticationrecord.tollium.user.realname
                    , stats :=            statistics
                    , started :=          creationdate
                    , lastactive :=       lastactivity
                    , lastpoll :=         DEFAULT DATETIME
                    , active :=           TRUE
                    , browser :=          browsertriplet
                    ]
          FROM apps;

    //IF (LENGTH(apps) != 0)
     // ABORT(apps);
    RETURN apps;
  }

  MACRO AbortApp(STRING starter_id, STRING appid)
  {
    FOREVERY (RECORD starter FROM this->applicationstarters)
      IF (starter.id = starter_id)
        starter.link->DoRequest(CELL[ task := "abortapp", appid ]);
  }

  BOOLEAN FUNCTION HaveContact()
  {
    RETURN LENGTH(this->applicationstarters) != 0;
  }
>;
