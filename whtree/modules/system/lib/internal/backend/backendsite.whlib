<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

PUBLIC OBJECT FUNCTION EnsureSystemBackendSite()
{
  OBJECT target := OpenWHFSObject(whconstant_whfsid_webharebackend);
  IF(NOT ObjectExists(target))
    target := OpenWHFSRootObject()->CreateFolder([ id := whconstant_whfsid_webharebackend, name := "WebHare backend" ]);
  ELSE IF(target->name != "WebHare backend")
    target->UpdateMetadata( [ name := "WebHare backend" ]);

  IF(NOT RecordExists(SELECT FROM system.sites WHERE id=whconstant_whfsid_webharebackend))
    CreateSiteFromFolder(whconstant_whfsid_webharebackend);

  RETURN OpenSite(whconstant_whfsid_webharebackend);
}

PUBLIC MACRO UpdateSystemBackendSite()
{
  OBJECT backendsite := EnsureSystemBackendSite();
  OBJECT target := backendsite->rootobject;

  //Clean out 'old' webhare backend sites
  OBJECT designfolder := target->OpenByName("design");
  IF(ObjectExists(designfolder))
  {
    DELETE FROM system.fs_objects WHERE parent = designfolder->id AND name IN ["css","img","js","language","lib","pages","witty","document.rtd","rtd-structure.xml","webharebackend.siteprl"];
    IF(Length(SELECT FROM system.fs_objects WHERE parent=designfolder->id)=0)
      DELETE FROM system.fs_objects WHERE id = designfolder->id;
  }

  OBJECT robotstxt := target->EnsureFile( [ name := "robots.txt"
                                          , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/plaintextfile")->id
                                          , publish := TRUE
                                          ], [ data := StringToBlob("# No need to index the WebHare backend\n\nUser-agent: *\nDisallow: /\n")]
                                          );

  OBJECT portalfile := target->EnsureFile( [ name := "portal", typens := "http://www.webhare.net/xmlns/tollium/applicationportal", publish := TRUE ]);
  target->UpdateMetadata([indexdoc := portalfile->id ]);

  //create a testportal file for external CI tests. notice that even if published, this file will be not work on non-development dtap stages
  target->EnsureFile( [ name := "testportal", typens := "http://www.webhare.net/xmlns/tollium/applicationportal", publish := GetDtapStage() = "development" ]);

  DELETE FROM system.fs_objects WHERE parent=whconstant_whfsid_webharebackend AND name IN [ "sites", "favicon.ico", "docs" ];

  OBJECT webharefolder := target->EnsureFolder( [ name := "webhare" ]);
  OBJECT checkfolder := webharefolder->EnsureFolder( [ name := "check" ]);

  //TODO Stop injecting code directly into WHFS. But we should probably replace this with an official oauth-ed WebHare REST API anyway
  OBJECT webhareindex := webharefolder->EnsureFile(
    [ name := "index.shtml"
    , type := whconstant_whfstype_shtmlfile
    , data := StringToBlob('<?wh LOADLIB "wh::internet/urls.whlib";LOADLIB "mod::system/lib/webserver.whlib"; Redirect(UpdateURLVariables("../", [ app := GetWebVariable("app") ] ));')
    , publish := TRUE
    ]);
  webharefolder->UpdateMetadata([indexdoc := webhareindex->id]);
  OBJECT checkindex := checkfolder->EnsureFile(
    [ name := "index.shtml"
    , type := whconstant_whfstype_shtmlfile
    , data := StringToBlob('<?wh LOADLIB "mod::tollium/webdesigns/webinterface/pages/check/check.whlib" EXPORT RunDynamicPage;')
    , publish := TRUE
    ]);
  checkfolder->UpdateMetadata([indexdoc := checkindex->id]);

  UPDATE system.sites
     SET name := "WebHare backend"
   WHERE id = whconstant_whfsid_webharebackend;

  //Ensure it points to an interface webserver
  INTEGER setoutputweb := backendsite->outputweb;
  RECORD destoutput := SELECT * FROM system.webservers WHERE type = 1 AND id = setoutputweb;
  IF(NOT RecordExists(destoutput))
  {
    RECORD bestmatch; //just pick any interface server
    bestmatch := SELECT * FROM system.webservers WHERE type=1 ORDER BY id;

    IF(RecordExists(bestmatch))
      setoutputweb := bestmatch.id;
  }

  backendsite->UpdateSiteMetadata(
      [ sitedesign := "tollium:webinterface"
      , outputfolder := "/"
      , outputweb := setoutputweb
      , locked := FALSE
      ]);
}
