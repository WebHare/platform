<?wh

LOADLIB "mod::system/lib/webserver.whlib";


PUBLIC RECORD FUNCTION FoldHeaders(RECORD ARRAY webheaders)
{
  RETURN RepackRecord(SELECT name := ANY(field)
                           , value := Detokenize(GroupedValues(value), ToUppercase(ANY(field)) = "COOKIE" ? ";" : ", ")
                        FROM webheaders
                    GROUP BY ToUppercase(field));
}

PUBLIC RECORD FUNCTION EncodeWebRequestForJS() //returns a WebRequestInfo
{
  RETURN CELL[ url := GetRequestURL()
             , headers := FoldHeaders(GetAllWebHeaders())
             , method := ToUppercase(GetRequestMethod())
             , body := GetRequestBody()
             , sourceip := GetClientRemoteIP()
             , webserver := GetClientWebserver()
             , binding := GetClientBinding()
             ];
}

PUBLIC MACRO ExecuteWebResponse(RECORD webresponse) //expects a WebResponseInfo
{
  AddHTTPHeader("Status", ToString(webresponse.status), FALSE);

  FOREVERY(VARIANT header FROM webresponse.headers)
    AddHTTPHeader(header[0], header[1], ToLowercase(header[0]) = "set-cookie"); //append cookies, overwrite the rest

  SendWebFile(webresponse.body);
}
