<?wh

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";


PUBLIC VARIANT FUNCTION InvokeInsideJob(STRING func, VARIANT ARRAY args, RECORD options)
{
  IF(CellExists(options,'openprimary') AND options.openprimary)
    OpenPrimary();
  IF(CellExists(options,'autocommit') AND options.autocommit)
    GetPrimary()->BeginWork();

  VARIANT retval := CallAnyPtrVA(MakeFunctionPtr(func), args);
  IF(CellExists(options,'autocommit') AND options.autocommit)
    GetPrimary()->CommitWork();

  RETURN retval;
}

PUBLIC RECORD FUNCTION FoldHeaders(RECORD ARRAY webheaders)
{
  RETURN RepackRecord(SELECT name := ANY(field)
                           , value := Detokenize(GroupedValues(value), ToUppercase(ANY(field)) = "COOKIE" ? ";" : ", ")
                        FROM webheaders
                    GROUP BY ToUppercase(field));
}

PUBLIC RECORD FUNCTION EncodeWebRequestForJS() //returns a WebRequestInfo
{
  RETURN CELL[ url := GetRequestURL()
             , headers := FoldHeaders(GetAllWebHeaders())
             , method := ToUppercase(GetRequestMethod())
             , body := GetRequestBody()
             , sourceip := GetClientRemoteIP()
             ];
}

PUBLIC MACRO ExecuteWebResponse(RECORD webresponse) //expects a WebResponseInfo
{
  AddHTTPHeader("Status", ToString(webresponse.status), FALSE);

  FOREVERY(RECORD header FROM UnpackRecord(webresponse.headers)) {
    STRING headername := ToUppercase(header.name);
    IF(headername = "SET-COOKIE") {
      IF(Length(webresponse.setcookie) = 0)
        THROW NEW Exception(`internal error: no setCookie in WebResponseInfo but there was a set-cookie header`);

      CONTINUE;
    }
    AddHTTPHeader(ToLowercase(headername), header.value, FALSE);
  }

  FOREVERY(STRING cookie FROM webresponse.setcookie)
    AddHTTPHeader("Set-Cookie", cookie, TRUE);

  SendWebFile(webresponse.body);
}
