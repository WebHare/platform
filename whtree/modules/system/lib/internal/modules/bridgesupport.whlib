<?wh

LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";


PUBLIC VARIANT FUNCTION InvokeInsideJob(STRING func, VARIANT ARRAY args, RECORD options)
{
  IF(CellExists(options,'openprimary') AND options.openprimary)
    OpenPrimary();
  RETURN CallAnyPtrVA(MakeFunctionPtr(func), args);
}

PUBLIC RECORD FUNCTION EncodeWebRequestForJS() //returns a WebRequestInfo
{
  RETURN CELL[ url := GetRequestURL()
             , headers := RepackRecord(SELECT name := field, value FROM GetAllWebHeaders())
             , method := ToLowercase(GetRequestMethod())
             , body := BlobToString(GetRequestBody()) //TODO non-utf8 safe bodies won't survive this
             ];
}

PUBLIC MACRO ExecuteWebResponse(RECORD webresponse) //expects a WebResponseInfo
{
  AddHTTPHeader("Status", ToString(webresponse.status), FALSE);

  STRING ARRAY added;
  FOREVERY(RECORD header FROM UnpackRecord(webresponse.headers))
  {
    BOOLEAN add := ToUppercase(header.name) IN added;
    INSERT ToUppercase(header.name) INTO added AT END;
    AddHTTPHeader(ToLowercase(header.name), header.value, add);
  }
  SendWebFile(StringToBlob(webresponse.body));
}
