<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::xml/xsd.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";

LOADLIB "mod::system/lib/validation.whlib";
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";

CONSTANT INTEGER default_test_timeout := 15 * 60 * 1000;

PUBLIC RECORD basetestsettings :=
 [ tags := DEFAULT STRING ARRAY
 ];

RECORD ARRAY modulevalidationconfigcache;

RECORD FUNCTION ApplyTestSettings(RECORD settings, OBJECT elt)
{
  IF(elt->HasAttribute("tags"))
    settings.tags := ParseXSList(elt->GetAttribute("tags"));
  RETURN settings;
}

RECORD FUNCTION GetCachedModuleValidationConfig(STRING modulename)
{
  RECORD pos := RecordLowerBound(modulevalidationconfigcache, CELL[ modulename ], [ "MODULENAME" ]);
  IF (NOT pos.found)
    INSERT CELL[ modulename, ...GetModuleValidationConfig(modulename) ] INTO modulevalidationconfigcache AT pos.position;

  RETURN modulevalidationconfigcache[pos.position];
}

STRING ARRAY FUNCTION UpdateDebugFlags(STRING ARRAY baseflags, STRING ARRAY updates) {
  FOREVERY (STRING flag FROM updates) {
    IF (flag LIKE "-*")
      baseflags := ArrayDelete(baseflags, Substring(flag, 1));
    ELSE {
      flag := flag LIKE "+*" ? Substring(flag, 1) : flag;
      IF (flag NOT IN baseflags)
        INSERT flag INTO baseflags AT END;
    }
  }
  RETURN baseflags;
}

PUBLIC STATIC OBJECTTYPE RunTests
<
  PUBLIC BOOLEAN print_debug;
  PUBLIC RECORD ARRAY cmdparams;

  STRING tempdir;

  STRING basetesturl;

  MACRO NEW()
  {
    this->tempdir := GenerateTemporaryPathnameFromBasepath(GetTempDir() || "beta-" || FormatDatetime("%Y%m%d%H%M%S", GetCurrentDatetime()) || "-");
    IF (this->tempdir NOT LIKE "*/")
      this->tempdir := this->tempdir || "/";

    this->basetesturl := ResolveToAbsoluteURL(GetPrimaryWebhareInterfaceURL(), "/.system/jstests/");
  }

  STRING FUNCTION SubstituteArg(STRING testfile, STRING arg)
  {
    arg := Substitute(arg, "$webhare_dir$", GetInstallationRoot());
    arg := Substitute(arg, "$testbasedir$", GetDirectoryFromPath(testfile));
    arg := Substitute(arg, "$temp_dir$", this->tempdir);
    RETURN Substitute(arg, "$binary_extension$", "");
  }

  RECORD ARRAY FUNCTION UpdateParams(STRING testfile, RECORD ARRAY params, RECORD ARRAY updates)
  {
    FOREVERY (RECORD rec FROM updates)
    {
      IF(RecordExists(SELECT FROM params WHERE COLUMN name = rec.name))
      {
        IF (this->print_debug)
          Print("; Setting test param " || rec.name || " := '" || rec.value || "'\n");
        UPDATE params SET value := this->SubstituteArg(testfile, rec.value) WHERE COLUMN name = rec.name;
      }
      ELSE
      {
        IF (this->print_debug)
          Print("; Adding test param " || rec.name || " := '" || rec.value || "'\n");
        INSERT [ name := rec.name, value := this->SubstituteArg(testfile, rec.value) ] INTO params AT END;
      }
    }
    RETURN params;
  }

  RECORD FUNCTION GetTestArguments(OBJECT node, RECORD ARRAY params, STRING testfile)
  {
    STRING ARRAY args;
    STRING ARRAY runscriptargs;

    FOREVERY (OBJECT arg FROM node->childnodes->GetCurrentElements())
    {
      STRING param := arg->GetAttribute("param");
      STRING value := arg->GetAttribute("value");

      IF (param != "")
      {
        RECORD r := SELECT * FROM params WHERE name = param;
        IF (NOT RecordExists(r))
          THROW NEW Exception("Unknown parameter '" || param || "'");

        value := r.value;
      }
      ELSE
        value := this->SubstituteArg(testfile, value);

      IF (arg->nodename = "arg")
        INSERT value INTO args AT END;
    }
    RETURN [ args:=args
           , runscriptargs := runscriptargs
           ];
  }

  RECORD ARRAY FUNCTION ParseXMLNode(RECORD testsettings, STRING testfile, OBJECT node, STRING testname, RECORD ARRAY params, BOOLEAN skipauto, STRING webroot, STRING sitename, RECORD validationconfig, STRING groupengine, STRING ARRAY groupdebugflags)
  {
    STRING url := webroot != "" ? UpdateURLVariables(ResolveToAbsoluteURL(webroot, "/.system/jstests/"), [ site := sitename ]) : "";
    RECORD ARRAY tests;

    FOREVERY (OBJECT elt FROM node->childnodes->GetCurrentElements())
    {
      IF(NOT IsNodeApplicableToThisWebHare(elt))
        CONTINUE;
      SWITCH (elt->nodename)
      {
      CASE "param"
        {
          STRING name := elt->GetAttribute("name");
          STRING value := elt->GetAttribute("value");

          params := this->UpdateParams(testfile, params, [ [ name := name, value := value ] ]);
        }
      CASE "group"
        {
          STRING name := elt->GetAttribute("name");
          BOOLEAN inner_skipauto := skipauto OR elt->GetAttribute("skipautotests") IN [ "1", "true" ];
          STRING engine := groupengine ?? elt->GetAttribute("engine");
          STRING ARRAY debugflags := UpdateDebugFlags(groupdebugflags, ParseXSList(elt->GetAttribute("debugflags")));

          STRING inner_site := sitename;
          STRING inner_webroot := webroot;

          IF(elt->HasAttribute("site"))
          {
            inner_site := elt->GetAttribute("site");
            OBJECT subsite := OpenSiteByName(inner_site);
            inner_webroot := ObjectExists(subsite) ? subsite->webroot : GetPrimaryWebhareInterfaceURL();
          }

          STRING subtestname := testname;
          IF(name!="")
            subtestname := subtestname || (subtestname != "" ? "." : "") || name;

          RECORD localtestsettings := ApplyTestSettings(testsettings, elt);
          tests := tests CONCAT this->ParseXMLNode(localtestsettings, testfile, elt, subtestname, params, inner_skipauto, inner_webroot, inner_site, validationconfig, engine, debugflags);
        }

      CASE "jstest"
        {
          OBJECT testnode := elt->GetChildElementsByTagName("testscript")->Item(0);
          IF(NOT ObjectExists(testnode))
            testnode := elt;

          RECORD arginfo := this->GetTestArguments(testnode, params, testfile);

          STRING name := elt->GetAttribute("name");
          STRING file := testnode->GetAttribute("file");
          IF(name="")
          {
            name := Tokenize(file,'.')[0];
            IF(Length(arginfo.args)>0)
            {
              name := name || "-" || Detokenize(arginfo.args,"-");
            }
          }

          STRING subtestname := testname;
          IF(name!="")
            subtestname := subtestname || (subtestname != "" ? "." : "") || name;

          STRING path := this->SubstituteArg(testfile, file);
          STRING subfile := IsPathAbsolute(path) ? path : MergePath(GetDirectoryFromPath(testfile), path);

          RECORD test := MakeMergedRecord(ApplyTestSettings(testsettings, elt)
                        ,[ type := elt->nodename
                         , script := path
                         , testname := subtestname
                         , skipauto := skipauto OR elt->GetAttribute("skipautotests") IN [ "1", "true" ]
                         , testscript := [ args := arginfo.args
                                         , scriptpath := subfile
                                         ]
                         , baseurl := url
                         , sitename := sitename
                         , flaky := ParseXSBoolean(testnode->GetAttribute("flaky"))
                         , xfail := ParseXSBoolean(testnode->GetAttribute("xfail"))
                         , timeout := ParseXSInt(testnode->GetAttribute("timeout")) ?? default_test_timeout
                         , validationconfig := validationconfig
                         , env := RECORD[]
                         , compatibility := testnode->GetAttribute("compatibility")
                         ]);

          FOREVERY(OBJECT env FROM testnode->GetElements("env"))
          {
            INSERT CELL[ name := env->GetAttribute("name")
                       , value := env->GetAttribute("value")
                       ] INTO test.env AT END;
          }
          INSERT test INTO tests AT END;
        }

      CASE "test"
        {
          STRING script := elt->GetAttribute("script");
          STRING path := elt->GetAttribute("path");
          STRING name := elt->GetAttribute("name");
          STRING engine := groupengine ?? elt->GetAttribute("engine");
          STRING ARRAY debugflags := UpdateDebugFlags(groupdebugflags, ParseXSList(elt->GetAttribute("debugflags")));
          params := this->UpdateParams(testfile, params, this->cmdparams);
          IF (path != "")
          {
            path := this->SubstituteArg(testfile, path);

            STRING subfile := (IsPathAbsolute(path) ? path : MergePath(GetDirectoryFromPath(testfile), path)) || "/testinfo.xml";
            IF(name="")
              name := Tokenize(path,'/')[0];

            STRING subtestname := testname;
            IF(name!="")
              subtestname := subtestname || (subtestname != "" ? "." : "") || name;

            BOOLEAN localskipauto := skipauto OR elt->GetAttribute("skipautotests") IN [ "1", "true" ];
            BOOLEAN okifmissing := ParseXSBoolean(elt->GetAttribute("okifmissing"));
            RECORD localtestsettings := ApplyTestSettings(testsettings, elt);
            tests := tests CONCAT this->GatherTests(localtestsettings, subfile, subtestname, localskipauto, webroot, sitename, okifmissing, validationconfig, engine, debugflags);
          }
          ELSE IF (script != "")
          {
            STRING type := "test";
            STRING errormsg;
            BOOLEAN xfail := ToUppercase(elt->GetAttribute("xfail")) IN [ "1", "TRUE" ];

            RECORD arginfo;
            TRY
            {
              arginfo := this->GetTestArguments(elt, params, testfile);
            }
            CATCH(OBJECT e)
            {
              errormsg := e->what;
              type := "fail";
            }

            INSERT MakeMergedRecord(ApplyTestSettings(testsettings, elt), CELL
                [ type
                , errormsg
                , testname := (testname = "" ? "" : testname || ".") || (name ?? GetbasenameFromPath(script))
                , skipauto := skipauto OR elt->GetAttribute("skipautotests") IN [ "1", "true" ]
                , script
                , engine
                , params
                , xfail
                , runscriptarguments := RecordExists(arginfo) ? arginfo.runscriptargs : DEFAULT STRING ARRAY
                , arguments := RecordExists(arginfo) ? arginfo.args : DEFAULT STRING ARRAY
                , baseurl := url
                , sitename
                , flaky := ParseXSBoolean(elt->GetAttribute("flaky"))
                , runmode := elt->GetAttribute("runmode")
                , debugflags
                , validationconfig
                , timeout := ParseXSInt(elt->GetAttribute("timeout")) ?? default_test_timeout
                , env := RECORD[]
                ]) INTO tests AT END;
          }
        }
        DEFAULT
        {
          THROW NEW Exception("Unrecognized node '" || elt->nodename || "' in file " || testfile);
        }
      }
    }

    RETURN tests;
  }

  BOOLEAN FUNCTION IsMatch(STRING name, STRING ARRAY masks, STRING ARRAY skips)
  {
    FOREVERY (STRING skip FROM skips)
      IF (ToUppercase(name) LIKE ToUppercase(skip))
        RETURN FALSE;
    IF (LENGTH(masks) = 0)
      RETURN TRUE;
    FOREVERY (STRING mask FROM masks)
      IF (ToUppercase(name) LIKE ToUppercase(mask) OR ToUppercase(name) LIKE ToUppercase(mask||".*"))
        RETURN TRUE;
    RETURN FALSE;
  }

  RECORD ARRAY FUNCTION GatherTests(RECORD testsettings, STRING testfile, STRING rootpath, BOOLEAN localskipauto, STRING webroot, STRING sitename, BOOLEAN okifmissing, RECORD validationconfig, STRING groupengine, STRING ARRAY groupdebugflags)
  {

  //  IF (print_debug)
  //    Print("; mask: " || AnyToString(testpath);

    //Get info from test XML file
    RECORD ARRAY tests;
    OBJECT dom;
    TRY
    {
      dom := RetrieveCachedXMLResource(testfile).doc;
    }
    CATCH(OBJECT <RetrieveResourceException> e)
    {
      IF(okifmissing)
        RETURN DEFAULT RECORD ARRAY;
      THROW;
    }
    RECORD ARRAY root_parameters :=
      [ [ name := "scriptpath", value := GetDirectoryFromPath(testfile) ]
      ];

    tests := this->ParseXMLNode(testsettings, testfile, dom, rootpath, root_parameters, localskipauto, webroot, sitename, validationconfig, groupengine, groupdebugflags);
    RETURN tests;
  }

  STRING FUNCTION ReadTestMask(STRING inmask)
  {
    STRING testmask := inmask = "" ? "*" : inmask;
    IF(testmask='all')
      testmask := '*';
    ELSE IF(testmask LIKE '*.all')
      testmask := Left(testmask,Length(testmask)-4) || ".*";
    RETURN testmask;
  }

  STRING ARRAY FUNCTION ReadTestMasks(STRING ARRAY inmasks)
  {
    IF (LENGTH(inmasks) = 0)
      RETURN [ "*" ];
    RETURN SELECT AS STRING ARRAY this->ReadTestMask(val) FROM ToRecordArray(inmasks, "VAL");
  }

  RECORD FUNCTION GetCacheableTestList()
  {
    RETURN
        [ ttl :=          60 * 1000
        , value :=        this->GatherAllModuleTests()
        , eventmasks :=   [ "system:modulefolder.mod::*/tests/*" ]
        ];
  }

  PUBLIC RECORD ARRAY FUNCTION GetTestList()
  {
    RETURN GetAdhocCached(
        [ type :=           "testlist"
        ], PTR RunInSeparatePrimary(PTR this->GetCacheableTestList));
  }

  PUBLIC RECORD ARRAY FUNCTION FilterTestList(RECORD ARRAY tests, STRING ARRAY masks, STRING ARRAY skips)
  {
    STRING ARRAY testmasks := this->ReadTestMasks(masks);
    tests := SELECT *
               FROM tests
              WHERE this->IsMatch(testname, testmasks, skips);
    RETURN tests;
  }

  /** @param masks
      @param skips
  */
  PUBLIC RECORD ARRAY FUNCTION GatherAllTests(STRING ARRAY masks, STRING ARRAY skips)
  {
    RETURN this->FiltertestList(this->GetTestList(), masks, skips);
  }

  RECORD ARRAY FUNCTION GatherAllModuleTests()
  {
    RECORD ARRAY tests;

    FOREVERY(STRING mod FROM GetInstalledModuleNames())
    {
      STRING modtestfile := "mod::" || mod || "/tests/testinfo.xml";
      RECORD res := RetrieveWebHareResource(modtestfile, [ allowmissing := TRUE ]);
      IF(NOT RecordExists(res))
        CONTINUE;

      RECORD validationconfig := GetModuleValidationConfig(mod);
      tests := tests CONCAT this->GatherTests(basetestsettings, modtestfile, mod="webhare_testsuite"?"":mod, FALSE, this->basetesturl, "", FALSE, validationconfig, "", STRING[]);
    }

    RETURN tests;
  }
>;
