<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::regex.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::dbase/postgresql.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";
LOADLIB "mod::system/lib/internal/whconfig.whlib";
LOADLIB "mod::system/lib/internal/dbase/base.whlib";


PUBLIC OBJECTTYPE PostgreSQL EXTEND MonitorPanelBase
< OBJECT trans;
  RECORD ARRAY columns;


  MACRO Init()
  {
    this->trans := GetPrimaryWebhareTransactionObject();
    this->columns := ^transactions->columns;
    RegisterEventCallback("system:systemconfig", PTR this->GotConfigChange);

    this->RefreshConfig();
  }

  MACRO GotConfigChange(STRING event, RECORD data)
  {
    this->RefreshConfig();
  }

  MACRO RefreshConfig()
  {
    RECORD debugconfig := GetDatabaseDebugConfig();
    ^readonly->value := debugconfig.readonly;
    ^logstacktraces->value := debugconfig.logstacktraces > 0;

    RECORD ARRAY columns := this->columns;
    IF (NOT ^logstacktraces->value)
      DELETE FROM columns WHERE name = "debuginfo";

    RECORD ARRAY rows := ^transactions->rows;
    ^transactions->columns := columns;
    ^transactions->rows := rows;
  }

  RECORD FUNCTION DecodeWHLogInfo(STRING logline)
  {
    RECORD retval :=
        [ debuginfo :=      ""
        , debuginfohint :=  ""
        ];

    SWITCH (Left(logline, 1))
    {
      CASE "t"
      {
        STRING ARRAY parts := Tokenize(SubString(logline, 2, LENGTH(logline) - 3), ",");
        retval.debuginfo := (
            SELECT AS STRING uri
              FROM ToRecordArray(parts, "uri")
             WHERE uri != "mod::system/lib/database.whlib"
               AND uri NOT LIKE "wh::dbase/*"
               AND uri NOT LIKE "wh::internal/trans*"
               ) ?? parts[0];
        retval.debuginfohint := "Stack trace:\n" || Detokenize(parts, "\n");
      }
    }

    RETURN retval;
  }

  UPDATE PUBLIC MACRO RefreshMonitorPanel()
  {
    RECORD ARRAY translist := this->trans->__ExecSQL("SELECT * FROM pg_stat_activity ORDER BY pid");

    FOREVERY (RECORD trans FROM translist)
    {
      STRING debuginfotxt;
      IF (trans.query LIKE `/?whlog:*`)
      {
        INTEGER pos := SearchSubString(trans.query, "*/");
        debuginfotxt := SubString(trans.query, 8, pos - 8);
        trans.query := SubString(trans.query, pos + 2);
      }
      translist[#trans] := CELL[ ...trans, ...this->DecodeWHLogInfo(debuginfotxt) ];
    }


    RECORD trans_default :=
        [ usename := ""
        , application_name := ""
        , backend_start := DEFAULT DATETIME
        , query_start := DEFAULT DATETIME
        , xact_start := DEFAULT DATETIME
        ];


    this->transactions->rows :=
        SELECT *
             , rowkey :=  pid
             , isself :=  pid = this->trans->backendpid
          FROM translist
         WHERE backend_type = "client backend";
  }

  MACRO GotLogStackTracesChange()
  {
    RECORD database := GetDatabaseDebugConfig();
    database.logstacktraces := ^logstacktraces->value ? 10 : 0;
    UpdateSystemConfigurationRecord(CELL[ database ]);
  }
>;
