<?wh
LOADLIB "wh::internet/tcpip.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/moduledefparser.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/webserver/config.whlib";
LOADLIB "mod::system/tolliumapps/webservers/support.whlib";


RECORD ARRAY FUNCTION GetAvailableModuleRoleSets()
{
  RECORD ARRAY retval;

  FOREVERY(RECORD mod FROM (SELECT * FROM GetWebHareModules() ORDER BY name))
  {
    OBJECT xmlfile := GetModuleDefinitionXML(mod.name);
    IF(NOT ObjectExists(xmlfile) OR NOT ObjectExists(xmlfile->documentelement))
      CONTINUE;

    retval := retval CONCAT ListModuleWebruleSets(GetModuleDefinitionXMLResourceName(mod.name), xmlfile);
  }

  retval := SELECT *, title := GetTid(title) ?? rowkey FROM retval;
  RETURN SELECT * FROM retval ORDER BY ToUppercase(title);
}


PUBLIC OBJECTTYPE AccessRules EXTEND TolliumScreenBase
<
  INTEGER webserverid;

  MACRO Init(RECORD params)
  {
    this->webserverid := params.id;
    this->Refresh();
  }

  MACRO Refresh()
  {
    // Get all unique accesrule id's which have an ip rule
    INTEGER ARRAY ruleswithips := SELECT AS INTEGER ARRAY DISTINCT(accessid) FROM system.access_ips;

    this->accessrulelist->rows :=
      SELECT rowkey := id
           , path
           , webhareusers := (authtype = 2)
           , externalusers := authlist
           , description
           , canview := matchtype IN [0,1]
           , protected := (authtype > 0 OR authlist OR authscript != "" OR id IN ruleswithips) ? 1 : 0
           , style := disabled ? "disabled" : ""
        FROM system.access
       WHERE access.webserver = this->webserverid;
  }

  MACRO DoAdd()
  {
    OBJECT screen := this->LoadScreen(".updateaccessrule", [ id := 0, webserver := this->webserverid ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoEdit()
  {
    OBJECT screen := this->LoadScreen(".updateaccessrule", [ id := this->accessrulelist->value[0], webserver := this->webserverid ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoView(OBJECT urlreceiver)
  {
    STRING webserverroot := SELECT AS STRING baseurl FROM system.webservers WHERE id = this->webserverid;
    urlreceiver->SendURL(webserverroot || Substring(this->accessrulelist->selection[0].path,1));
  }

  MACRO DoIPFilters()
  {
    OBJECT screen := this->LoadScreen(".ipfilters", [ id := this->accessrulelist->value[0] ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoWebhareUsers()
  {
    OBJECT screen := this->LoadScreen(".webhareusers", [ id := this->accessrulelist->value[0] ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoExternalUsers()
  {
    OBJECT screen := this->LoadScreen(".externalusers", [ id := this->accessrulelist->value[0] ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoDelete()
  {
    IF (this->RunMessageBox(".deleteaccessrule") != "yes")
      RETURN;

    OBJECT work := this->beginwork();

    DELETE FROM system.access
           WHERE id IN this->accessrulelist->value;

    IF (NOT work->Finish())
      RETURN;

    ReloadWebhareConfig(TRUE, FALSE);

    this->Refresh();
  }
>;

PUBLIC OBJECTTYPE UpdateAccessRule EXTEND TolliumScreenBase
<
  INTEGER accessid;
  INTEGER webserverid;

  MACRO Init(RECORD params)
  {
    this->accessid := params.id;
    this->webserverid := params.webserver;
    ^alternatepath->startfolder := GetWebserverBaseOutputFolder();
    ^singlefilepath->startfolder := GetWebserverBaseOutputFolder();
    ^errorpath->startfolder := GetWebserverBaseOutputFolder();

    RECORD accessrec := SELECT * FROM system.access WHERE id = this->accessid;

    this->hostingsrc->value := "0";
    this->installmodule->options := [[rowkey:="",title:="",invalidselection:=TRUE]] CONCAT GetAvailableModuleRoleSets();

    IF(this->webserverid = 0)
      this->appliesto->value := GetTid("system:sysmgmt.webservers.updateaccessrule.allservers");
    ELSE
      this->appliesto->value := SELECT AS STRING baseurl FROM system.webservers WHERE id=this->webserverid;

    IF (RecordExists(accessrec))
    {
      this->enabled->value := NOT accessrec.disabled;
      this->path->value := accessrec.path;
      this->errorpath->value := FormatOutputPath(accessrec.errorpath);
      this->authtype->value := accessrec.authtype;
      this->authrequirement->value := accessrec.authrequirement;
      this->SetHostingPath(accessrec.hostingsrc, accessrec.hostingpath, accessrec.redirectcode);
      this->authlist->value := accessrec.authlist;
      this->externalscript->value := accessrec.authscript != "";
      this->externalscriptname->value := accessrec.authscript;
      this->description->value := accessrec.description;
      this->matchtype->value := accessrec.matchtype;
      this->disablecaching->value := accessrec.disablecaching;
      this->maxage->value := accessrec.maxage;
      this->fixcase->value := accessrec.fixcase;
      this->applysource->value := accessrec.applysource;
    }
  }

  STRING FUNCTION GetHostingPath()
  {
    SWITCH (this->hostingsrc->value)
    {
      CASE "2"
      {
        STRING path := UnmapOutputPath(this->alternatepath->value);
        IF(path NOT LIKE "*/")
          path := path || "/";
        RETURN path;
      }
      CASE "3" { RETURN UnmapOutputPath(this->singlefilepath->value); }
      CASE "4"
      {
        //Add slash if urlpath is empty and doesnt even have the slash to prevent 'http://nu.nl' as redirect (#1162)
        RETURN this->redirectpath->value || (UnpackURL(this->redirectpath->value).urlpathslash ? "" : "/");
      }
      CASE "6" { RETURN this->installmodule->value; }
    }

    RETURN "";
  }

  MACRO SetHostingPath(INTEGER hostingsrc, STRING hostingpath, INTEGER redirectcode)
  {
    SWITCH (hostingsrc)
    {
      CASE 2 { this->alternatepath->value := FormatOutputPath(hostingpath); this->hostingsrc->value := "2"; }
      CASE 3 { this->singlefilepath->value := FormatOutputPath(hostingpath); this->hostingsrc->value := "3"; }
      CASE 4,5
      {
        this->redirectpath->value := hostingpath;
        this->hostingsrc->value := "4";
        this->keepsubpath->value := hostingsrc=5;
        IF(this->redirectcode->IsValidValue(redirectcode))
          this->redirectcode->value := redirectcode;
      }
      CASE 6
      {
        IF(this->installmodule->IsValidValue(hostingpath))
          this->installmodule->value := hostingpath;
        this->hostingsrc->value := "6";
      }
    }
  }

  STRING FUNCTION GetHSRights()
  {
    STRING ARRAY parts;

    IF (this->externaldb->value)
      INSERT "REMOTEDB" INTO parts AT END;

    IF (this->fulldb->value)
      INSERT "FULLDB" INTO parts AT END;

    IF (this->tcpip->value)
      INSERT "TCPIP" INTO parts AT END;

    IF (this->super->value)
      INSERT "SUPER" INTO parts AT END;

    RETURN DeTokenize(parts, ",");
  }

  MACRO SetHSRights(STRING hsrights)
  {
    STRING ARRAY parts := Tokenize(hsrights, ",");

    this->externaldb->value := ("REMOTEDB" IN parts);
    this->fulldb->value := ("FULLDB" IN parts);
    this->tcpip->value := ("TCPIP" IN parts);
    this->super->value := ("EXEC" IN parts) OR ("SUPER" IN parts);
  }

  STRING FUNCTION GetSanitizedPath()
  {
    STRING path := this->path->value;

    IF (this->matchtype->value != 2 AND path NOT LIKE "/*") //make sure all non-wildcard paths start with a slash
      path := "/" || path;

    RETURN path;
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    IF (NOT this->authrequirement->value AND this->authtype->value = 0)
      work->AddErrorFor(this->authtype, GetTid("system:sysmgmt.webservers.updateaccessrule.errors.login_required"));

    STRING sanitizedpath := this->GetSanitizedPath();

    IF (this->hostingsrc->value = "4" AND this->matchtype->value != 1 AND this->keepsubpath->value=TRUE)
    {
      work->AddErrorFor(this->hostingsrc, GetTid("system:sysmgmt.webservers.updateaccessrule.errors.cannot_redirect_to_initial_matchtype", GetTid("system:sysmgmt.webservers.updateaccessrule.matchtype"), GetTid("system:sysmgmt.webservers.updateaccessrule.matchtype-initial")));
    }

    IF (work->HasFailed())
      RETURN work->Finish();

    IF (this->accessid = 0)
    {
      this->accessid := MakeAutonumber(system.access, "ID");
      INSERT INTO system.access(id, webserver) VALUES (this->accessid, this->webserverid);
    }

    RECORD data :=
    [ path := sanitizedpath
    , errorpath := UnmapOutputPath(this->errorpath->value)
    , authtype := this->authtype->value
    , authrequirement := this->authrequirement->value
    , hostingsrc := ToInteger(this->hostingsrc->value, 0) + (this->keepsubpath->value ? 1 : 0)
    , hostingpath := this->GetHostingPath()
    , authlist := this->authlist->value
    , authscript := this->externalscript->value ? this->externalscriptname->value : ""
    , description := this->description->value
    , matchtype := this->matchtype->value
    , redirectcode := this->redirectcode->value
    , disablecaching := this->disablecaching->value
    , maxage := this->maxage->value
    , fixcase := this->fixcase->value
    , applysource := this->applysource->value
    , disabled := NOT this->enabled->value
    ];

    UPDATE system.access SET RECORD data WHERE id = this->accessid;

    IF (NOT work->Finish())
      RETURN FALSE;

    ReloadWebhareConfig(TRUE, FALSE);

    RETURN TRUE;
  }
>;

PUBLIC OBJECTTYPE IPFilters EXTEND TolliumScreenBase
<
  INTEGER accessid;

  MACRO Init(RECORD params)
  {
    this->accessid := params.id;
    this->Refresh();
  }

  BOOLEAN FUNCTION HaveAllowButNoDeny()
  {
    RECORD ARRAY rows := this->ipfilterlist->rows;
    RETURN RecordExists(rows) AND NOT RecordExists(SELECT FROM rows WHERE NOT is_allow);
  }

  MACRO Refresh()
  {
    this->ipfilterlist->rows :=
      SELECT rowkey := mask
           , mask := CanonicalizeIPAddress(mask, TRUE)
           , status := is_allow ? GetTid("system:sysmgmt.webservers.ipfilters.allowed") : GetTid("system:sysmgmt.webservers.ipfilters.denied")
           , is_allow
        FROM system.access_ips
       WHERE accessid = this->accessid;

    this->warningpanel->visible := this->HaveAllowButNoDeny();
  }

  MACRO DoAdd()
  {
    OBJECT screen := this->LoadScreen(".updateipfilter", [ accessid := this->accessid, mask := "" ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoEdit()
  {
    OBJECT screen := this->LoadScreen(".updateipfilter", [ accessid := this->accessid, mask := this->ipfilterlist->value ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoDelete()
  {
    IF (this->RunMessageBox(".deleteipfilter") = "yes")
    {
      OBJECT work := this->beginwork();

      DELETE FROM system.access_ips
       WHERE accessid = this->accessid
             AND mask = this->ipfilterlist->value;

      IF (NOT work->Finish())
        RETURN;

      ReloadWebhareConfig(TRUE, FALSE);
      this->Refresh();
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    IF (this->HaveAllowButNoDeny())
      RETURN this->RunMessageBox(".useipfiltersiwithoutdeny") = "yes";

    RETURN TRUE;
  }
>;

PUBLIC OBJECTTYPE UpdateIPFilter EXTEND TolliumScreenBase
<
  INTEGER accessid;
  STRING oldmask;

  MACRO Init(RECORD params)
  {
    this->accessid := params.accessid;
    this->oldmask := params.mask;

    RECORD maskrec :=
      SELECT *
        FROM system.access_ips
       WHERE accessid = this->accessid
             AND mask = params.mask;

    IF (RecordExists(maskrec))
    {
      this->mask->value := CanonicalizeIPAddress(maskrec.mask, TRUE);
      this->allow_access->value := maskrec.is_allow;
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->beginwork();
    STRING standardized_mask := CanonicalizeIPAddress(this->mask->value, TRUE);
    IF(standardized_mask = "")
      work->AddErrorFor(this->mask, GetTid("system:sysmgmt.webservers.updateipfilter.errors.invalid_mask"));
    ELSE IF (this->oldmask = "" AND RecordExists(SELECT FROM system.access_ips WHERE accessid = this->accessid AND CanonicalizeIPAddress(mask, TRUE) = standardized_mask))
      work->AddErrorFor(this->mask, GetTid("system:sysmgmt.webservers.updateipfilter.errors.duplicate_mask"));

    IF (work->HasFailed())
      RETURN work->Finish();

    IF (this->oldmask = "")
    {
      INSERT INTO system.access_ips(accessid, mask, is_allow)
           VALUES (this->accessid, standardized_mask, this->allow_access->value);
    }
    ELSE
    {
      UPDATE system.access_ips
         SET mask := standardized_mask
           , is_allow := this->allow_access->value
       WHERE accessid = this->accessid
             AND mask = this->oldmask;
    }

    IF (NOT work->Finish())
      RETURN FALSE;

    ReloadWebhareConfig(TRUE, FALSE);
    RETURN TRUE;
  }
>;

PUBLIC OBJECTTYPE WebhareUsers EXTEND TolliumScreenBase
<
  INTEGER accessruleid;

  MACRO Init(RECORD params)
  {
    this->objectrightslist->InitForCommonDialog();
    this->objectrightslist->SelectObject("system:accessrules", params.id);
  }
>;

PUBLIC OBJECTTYPE ExternalUsers EXTEND TolliumScreenBase
<
  INTEGER accessruleid;

  MACRO Init(RECORD params)
  {
    this->accessruleid := params.id;
    this->Refresh();
  }

  MACRO Refresh()
  {
    this->userlist->rows :=
      SELECT rowkey := username
           , username
           , description
        FROM system.access_externalusers
       WHERE accessid = this->accessruleid;
  }

  MACRO DoAdd()
  {
    OBJECT screen := this->LoadScreen(".updateexternaluser", [ accessrule := this->accessruleid, user := "" ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoEdit()
  {
    OBJECT screen := this->LoadScreen(".updateexternaluser", [ accessrule := this->accessruleid, user := this->userlist->value[0] ] );

    IF (screen->RunModal() = "ok")
      this->Refresh();
  }

  MACRO DoDelete()
  {
    IF (this->RunMessageBox(".deleteuser") != "yes")
      RETURN;

    OBJECT work := this->beginwork();

    DELETE FROM system.access_externalusers WHERE accessid = this->accessruleid AND username IN this->userlist->value;

    IF (NOT work->Finish())
      RETURN;

    ReloadWebhareConfig(TRUE, FALSE);

    this->Refresh();
  }
>;

PUBLIC OBJECTTYPE UpdateExternalUser EXTEND TolliumScreenBase
< INTEGER accessruleid;
  STRING user;
  STRING oldusername_uc; // used to check for duplicates

  MACRO Init(RECORD params)
  {
    this->accessruleid := params.accessrule;
    this->user := params.user;

    IF (this->user != "")
    {
      RECORD userrec :=
        SELECT *
          FROM system.access_externalusers
         WHERE accessid = this->accessruleid
           AND username = this->user;

      this->oldusername_uc := ToUpperCase(userrec.username);

      this->username->value := userrec.username;
      this->description->value := userrec.description;
      this->password->value := userrec.userpassword;
      this->password_again->value := userrec.userpassword;
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->beginwork();

    // Check password fields
    IF (this->password->value != "" AND this->password_again->value != "" AND this->password->value != this->password_again->value)
      work->AddError(GetTid("system:sysmgmt.webservers.updateexternaluser.errors.passwords_dont_match"));

    // Check duplicate usernames
    IF (RecordExists(SELECT FROM system.access_externalusers
                      WHERE ToUpperCase(username) = ToUpperCase(this->username->value)
                            AND accessid = this->accessruleid
                            AND ToUpperCase(this->username->value) != this->oldusername_uc))
    {
      work->AddError(GetTid("system:sysmgmt.webservers.updateexternaluser.errors.duplicate_username", this->username->value));
    }

    IF (this->user = "")
    {
      INSERT INTO system.access_externalusers(accessid, username, description, userpassword)
        VALUES(this->accessruleid, this->username->value, this->description->value, this->password->value);
    }
    ELSE
    {
      UPDATE system.access_externalusers
         SET username := this->username->value
           , description := this->description->value
           , userpassword := this->password->value
       WHERE accessid = this->accessruleid
         AND username = this->user;
    }

    IF (NOT work->Finish())
      RETURN FALSE;

    ReloadWebhareConfig(TRUE, FALSE);

    RETURN TRUE;
  }
>;
