<?wh
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";

PUBLIC OBJECTTYPE MimeTypes EXTEND TolliumScreenBase
<
  RECORD ARRAY FUNCTION OnGetMimetypes()
  {
    STRING ARRAY parsetypetexts := [ GetTid("system:sysmgmt.webservers.mimetype.parsetype-0")
                                   , GetTid("system:sysmgmt.webservers.mimetype.parsetype-1")
                                   , GetTid("system:sysmgmt.webservers.mimetype.parsetype-2")
                                   , GetTid("system:sysmgmt.webservers.mimetype.parsetype-3")
                                   , GetTid("system:sysmgmt.webservers.mimetype.parsetype-4")
                                   ];

    RETURN SELECT rowkey := id
                , mimetype
                , extension
                , handling := parsetype>=0 AND parsetype<=4 ? parsetypetexts[parsetype] : ""
             FROM system.mimetypes;
  }

  MACRO OnRowEdit(RECORD row)
  {
    IF(RecordExists(row))
      this->RunScreen("#mimetype", [ id := row.rowkey ]);
    ELSE
      this->RunScreen("#mimetype", [ id := 0 ]);
  }

  MACRO OnRowDelete(RECORD row)
  {
    IF (this->RunMessageBox(".deletemimetype",  row.extension) != "yes")
      RETURN;

    OBJECT work := this->beginwork();
    DELETE FROM system.mimetypes WHERE id = row.rowkey;
    IF (NOT work->Finish())
      RETURN;

    ReloadWebhareConfig(TRUE, FALSE);
  }
>;

PUBLIC OBJECTTYPE MimeType EXTEND TolliumScreenBase
<
  INTEGER mimetypeid;

  MACRO Init(RECORD params)
  {
    this->mimetypeid := params.id;

    IF (this->mimetypeid != 0)
    {
      RECORD data := SELECT * FROM system.mimetypes WHERE id = this->mimetypeid;

      this->extension->value := data.extension;
      this->mimetype->value := data.mimetype;
      this->parsetype->value := ToString(data.parsetype);

      this->frame->title := GetTid("system:sysmgmt.webservers.mimetype.windowtitle_edit", data.extension);
    }
    ELSE
    {
      this->frame->title := GetTid("system:sysmgmt.webservers.mimetype.windowtitle_add");
      this->parsetype->value := "0";
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    IF (this->extension->value != "")
    {
      IF (Left(this->extension->value,1)=".")
        work->AddError(GetTID("system:sysmgmt.webservers.mimetype.errors.extension_starting_dot"));
      // Check duplicate
      ELSE IF (RecordExists(SELECT FROM system.mimetypes
                             WHERE ToUpperCase(extension) = ToUpperCase(this->extension->value)
                                   AND id != this->mimetypeid))
        work->AddError(GetTID("system:sysmgmt.webservers.mimetype.errors.duplicate_extension", this->extension->value));
    }

    IF (this->mimetypeid = 0) // Add
    {
      INSERT INTO system.mimetypes (extension, mimetype, parsetype)
           VALUES (this->extension->value, this->mimetype->value, ToInteger(this->parsetype->value,0));
    }
    ELSE // Edit
    {
      UPDATE system.mimetypes
         SET extension := this->extension->value
           , mimetype := this->mimetype->value
           , parsetype := ToInteger(this->parsetype->value,0)
       WHERE id = this->mimetypeid;
    }

    IF (NOT work->Finish())
      RETURN FALSE;

    ReloadWebhareConfig(TRUE, FALSE);

    RETURN TRUE;
  }
>;
