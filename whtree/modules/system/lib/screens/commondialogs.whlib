<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/smtp.whlib";
LOADLIB "wh::internet/mime.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/mailer.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC STATIC OBJECTTYPE Mailer EXTEND TolliumScreenBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  BOOLEAN pvt_to_is_bcc_description;
  STRING pvt_description_to;
  RECORD ARRAY sentheaders;
  RECORD toppart;

  // ---------------------------------------------------------------------------
  //
  // Public variables and properties
  //

  /* Here is the glue to merge with the old ComposeMailDialog */

  PUBLIC PROPERTY mailfrom(^mailerfrom->value, ^mailerfrom->value);

  PUBLIC PROPERTY mailto(^mailerto->value, ^mailerto->value);

  PUBLIC PROPERTY mailcc(^mailercc->value, ^mailercc->value);

  PUBLIC PROPERTY mailbcc(^mailerbcc->value, ^mailerbcc->value);

  PUBLIC PROPERTY mailsubject(^mailersubject->value, ^mailersubject->value);

  /** Set to TRUE to use the mailto value as a description, actually send mail to additionalbcc */
  PUBLIC PROPERTY to_is_bcc_description(pvt_to_is_bcc_description, SetToIsBCCDescription);

  /** If to_is_bcc_description is set, use this address as the address for the description, otherwise the sender address is used */
  PUBLIC PROPERTY description_to(pvt_description_to,pvt_description_to);

  PUBLIC PROPERTY readonlyfrom(GetReadonlyFrom,SetReadonlyFrom);

  PUBLIC PROPERTY mailbody(^textareahtml->value, -);

  PUBLIC PROPERTY allowattachments(^attachments->visible, ^attachments->visible);

  PUBLIC PROPERTY mailattachments(^attachments->value, ^attachments->value);

  PUBLIC STRING ARRAY additionalbcc;

  PUBLIC FUNCTION PTR onqueuedmail;

  // ---------------------------------------------------------------------------
  //
  // Init & submit
  //

  PUBLIC MACRO Init()
  {
    ^mailerfrom->placeholder := ReadRegistryKey("system.services.smtp.mailfrom");
    ^mailerfrom->required := ^mailerfrom->placeholder = "";
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    IF (NOT work->HasFailed())
    {
      RECORD exp := ^textareahtml->value;
      IF(NOT RecordExists(exp))
        exp := [ htmltext := DEFAULT BLOB, embedded := DEFAULT RECORD ARRAY ];

      RECORD ARRAY embed := SELECT data
                                 , filename := contentid || extension
                                 , contenttype := mimetype
                                 , contentid := "<" || contentid || ">"
                                 , contentdisposition := "inline"
                              FROM exp.embedded;

      RECORD toppart := CreateMIMEMailingTopPart(DEFAULT BLOB, exp.htmltext, embed);

      STRING ARRAY receivers := this->additionalbcc;
      FOREVERY(STRING tok FROM tokenize((this->pvt_to_is_bcc_description ? "" : ^mailerto->value) || "," || ^mailercc->value || "," || ^mailerbcc->value,","))
      {
        tok:=TrimWhitespace(tok);
        IF(tok!="")
          INSERT tok INTO receivers AT END;
      }

      STRING mailfrom := ^mailerfrom->value ?? ^mailerfrom->placeholder;
      RECORD ARRAY headers := GetSMTPEmailHeader(mailfrom, ^mailersubject->value,"");
      IF (this->pvt_to_is_bcc_description)
      {
        // Use the BCC description ('To' field value) as name and the sender's e-mail if not otherwise specified using
        // description_to
        STRING sender := this->pvt_description_to ?? SplitEmailName(mailfrom).email;
        INSERT INTO headers(field,value) VALUES("To", MakeEmailAddress(^mailerto->value, sender, ^mailerto->value != "")) AT END;
      }
      ELSE IF (Length(^mailerto->value)>0)
        INSERT INTO headers(field,value) VALUES("To", ^mailerto->value) AT END;
      IF (Length(^mailercc->value)>0)
        INSERT INTO headers(field,value) VALUES("Cc", ^mailercc->value) AT END;

      toppart := AddAttachmentsToMail(toppart, SELECT name := filename, data, contenttype := mimetype FROM ^attachments->value);
      INTEGER ARRAY taskids := QueueMailInWork("Tollium Mailer", receivers, mailfrom, headers, toppart);
      this->sentheaders := headers;
      this->toppart := toppart;

      IF (this->onqueuedmail != DEFAULT FUNCTION PTR)
        this->onqueuedmail(work, CELL[ taskids
                                     , headers
                                     , toppart
                                     , subject := ^mailersubject->value
                                     , mailfrom := mailfrom
                                     , mailto := TokenizeEmailAddresses(^mailerto->value)
                                     , mailcc := TokenizeEmailAddresses(^mailercc->value)
                                     , mailbcc := TokenizeEmailAddresses(^mailerbcc->value)
                                     ]);
    }

    RETURN work->Finish();
  }

  BOOLEAN FUNCTION Cancel()
  {
    IF(RecordExists(this->mailbody) = FALSE)
      RETURN TRUE;

    RETURN this->RunSimpleScreen("confirm", this->GetTid(".confirm_cancel")) = "yes";
  }

  // ---------------------------------------------------------------------------
  //
  // Getters and setters
  //

  MACRO SetToIsBCCDescription(BOOLEAN value)
  {
    ^mailerto->enabled := NOT value;
    this->pvt_to_is_bcc_description := value;
  }

  MACRO SetReadonlyFrom(BOOLEAN value)
  {
    ^mailerfrom->enabled := NOT value;
  }

  BOOLEAN FUNCTION GetReadonlyFrom()
  {
    RETURN NOT ^mailerfrom->enabled;
  }

  // ---------------------------------------------------------------------------
  //
  // List fills
  //

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  // ---------------------------------------------------------------------------
  //
  // Public API
  //

  PUBLIC BLOB FUNCTION GetSentMail()
  {
    RETURN EncodeMIMEMessage(this->sentheaders, this->toppart);
  }

  PUBLIC BOOLEAN FUNCTION RunDialog()
  {
    RETURN this->RunModal()="ok";
  }

  PUBLIC MACRO SetHTMLContents(STRING html)
  {
    ^textareahtml->value := [ htmltext := StringToBlob(html)
                                 , embedded := DEFAULT RECORD ARRAY
                                 ];
  }

  PUBLIC MACRO SetFromRTD(RECORD rtd)
  {
    IF (RecordExists(rtd.instances))
      THROW NEW Exception("Using embedded instances is not supported yet");

    ^textareahtml->value := CELL[ rtd.htmltext, rtd.embedded ];
  }

  PUBLIC MACRO Quote(STRING html)
  {
    this->SetHTMLContents("<blockquote style='margin:0 0 0 10px; border-left: 1px solid red'>" || html || "<blockquote>");
  }

  /** @short add an attachment
      @param filedata in the attachment
      @param filename of the attachment/file
      @param mimetype mimetype override, if empty the mimetype will be detected
  */
  PUBLIC MACRO AddAttachment(BLOB filedata, STRING filename, STRING mimetype)
  {
    RECORD wrapped := WrapBlob(filedata,filename);
    IF(mimetype != "")
      wrapped.mimetype := mimetype;

    ^attachments->AddWrappedAttachment(wrapped);
  }

  /** @short Add a wrappped blob as an attachment
      @param wrapped The wrapped blob
  */
  PUBLIC MACRO AddWrappedAttachment(RECORD wrapped)
  {
    ^attachments->AddWrappedAttachment(wrapped);
  }
>;

PUBLIC STATIC OBJECTTYPE MailViewer EXTEND TolliumScreenBase
<
  BLOB original;

  PUBLIC MACRO Init(RECORD data)
  {
    this->original := data.mailblob;
    ^mailcontents->LoadEML(data.mailblob);
  }

  PUBLIC MACRO DoViewSource()
  {
    OBJECT dialog := this->LoadScreen(".viewsource", [ message := this->original ]);
    dialog->RunModal();
  }

  PUBLIC BOOLEAN FUNCTION RunDialog()
  {
    RETURN this->RunModal()="ok";
  }
>;

PUBLIC OBJECTTYPE ViewSource EXTEND TolliumScreenBase
< PUBLIC MACRO Init(RECORD data)
  {
    STRING datastr := BlobToString(data.message); //FIXME limit size?

    datastr := SubStitute(datastr, "\r\n", "\n");
    datastr := SubStitute(datastr, "\r", "\n");

    this->text->value := datastr;
  }
>;

PUBLIC OBJECTTYPE MailQueueDialog EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    //RegisterEventCallback("system:softreset", PTR this->OnSoftReset);
    OBJECT screen := this->LoadScreen("mod::system/screens/monitor/mailqueue.xml#mailqueuepanel", [ runembedded := TRUE, filter := data.sourcefilter ]);
    IF (ObjectExists(screen))
      screen->__pvt_yourmonitor := PRIVATE THIS; //ADDME use some sort of messaging interface? better not to expose privatedata

    this->monitor->contents := screen;
    this->monitor->contents->EnableRefresh();
    this->monitor->contents->RefreshDashboardPanel();
  }
>;

PUBLIC OBJECTTYPE RightsProblem EXTEND TolliumScreenBase
< PUBLIC MACRO Init(RECORD data)
  {
    IF (this->tolliumcontroller->debugging)
      this->RunMessageBox(".notenoughrights_spec", data.needed_rights);
    ELSE
      this->RunMessageBox(".notenoughrights");
    this->tolliumresult := "cancel";
  }
>;
