<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::wrd/lib/internal/auth/usereditpolicy.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";


PUBLIC STATIC OBJECTTYPE EditUnit EXTEND TolliumScreenBase
<
  INTEGER parent;

  MACRO Init(RECORD data)
  {
    ^usereditpolicy->options := RECORD
        [ [ rowkey := "", title :=    "" ]
        , ...(SELECT TEMPORARY title := GetTid/*nocheck*/(tid)
                   , rowkey := name
                   , title := title
                FROM GetUserEditPolicies()
            ORDER BY title)
        ];

    ^entity->Load(data.unit);
    IF(data.unit = 0)
      ^entity->extradata := [ wrd_leftentity := data.parent ];

    ^override_passwordchecks->enabled := this->contexts->user->HasRight("system:sysop");
  }

  INTEGER FUNCTION Submit()
  {
    OBJECT work := this->BeginWork( [ mutex := "wrd:usermgmt" ]);
    INTEGER parent := ^entity->entityid = 0 ? ^entity->extradata.wrd_leftentity : ^entity->wrdentity->GetField("WRD_LEFTENTITY");

    this->contexts->userapi->__CheckName(this->contexts->userapi->GetUnit(parent)
                                        ,^entity->^wrd_title->value
                                        ,^entity->entityid
                                        ,FALSE //foruser
                                        ,work);

    INTEGER unitid := ^entity->Store(work);
    IF(NOT work->HasFailed())
      this->contexts->userapi->SyncUnitAfterUpdate(unitid);

    IF(NOT work->Finish())
      RETURN 0;

    //TODO should become obsolete, WRD events should suffice?
    BroadcastEvent("system:unit.change." || parent, DEFAULT RECORD);
    RETURN unitid;
  }
>;


PUBLIC STATIC OBJECTTYPE EditRole EXTEND TolliumScreenBase
<
  INTEGER parent;

  MACRO Init(RECORD data)
  {
    ^entity->Load(data.role);
    IF(data.role = 0)
       ^entity->extradata := [ wrd_leftentity := data.parent ];
  }

  INTEGER FUNCTION Submit()
  {
    OBJECT work := this->BeginWork( [ mutex := "wrd:usermgmt" ]);
    INTEGER parent := ^entity->entityid = 0 ? ^entity->extradata.wrd_leftentity : ^entity->wrdentity->GetField("WRD_LEFTENTITY");

    this->contexts->userapi->__CheckName(this->contexts->userapi->GetUnit(parent)
                                        ,^entity->^wrd_title->value
                                        ,^entity->entityid
                                        ,FALSE //foruser
                                        ,work);

    INTEGER unitid := ^entity->Store(work);
    IF(NOT work->HasFailed())
      this->contexts->userapi->SyncRoleAfterUpdate(unitid);

    IF(NOT work->Finish())
      RETURN 0;

    //TODO should become obsolete, WRD events should suffice?
    BroadcastEvent("system:unit.change." || parent, DEFAULT RECORD);
    RETURN unitid;
  }
>;
