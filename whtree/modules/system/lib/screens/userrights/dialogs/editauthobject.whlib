<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::system/lib/internal/modules/backendhooks.whlib";

LOADLIB "mod::wrd/lib/internal/auth/support.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";


PUBLIC STATIC OBJECTTYPE EditUnit EXTEND TolliumScreenBase
<
  INTEGER parent;

  MACRO Init(RECORD data)
  {
    SetupBackendScreenHooks(PRIVATE this, CELL[ screenname := "platform:unitproperties" ]);

    ^unitentity->Load(data.unit);

    INTEGER parent;
    IF(data.unit = 0) {
      parent := data.parent;
      ^unitentity->extradata := [ wrd_leftentity := data.parent ];
    } ELSE {
      parent := ^unitentity->wrdentity->GetField("WRD_LEFTENTITY");
    }

    ^override_passwordchecks->enabled := this->contexts->user->HasRight("system:sysop");
    ^override_expiration->enabled := this->contexts->user->HasRight("system:sysop");
    ^maintabs->RunExtensionsPostInit();

    //Get base settings
    IF(NOT ^override_passwordchecks->value OR NOT ^override_expiration->value) {
      RECORD fallbacks := GetSchemaUnitPolicy(this->contexts->wrdschema, parent);

      IF(NOT ^override_passwordchecks->value) //if not overridden, copy parents value
        ^unitentity->^passwordchecks->value := fallbacks.passwordchecks;

      IF(NOT ^override_expiration->value)
        ^unitentity->^expiration->value := fallbacks.expiration;
    }
  }

  STRING FUNCTION SelectDuration(STRING value)
  {
    RETURN this->RunScreen("mod::system/tolliumapps/config/settings.xml#selectduration", CELL[ value, withtime := FALSE ]);
  }
  STRING FUNCTION MapDuration(STRING duration)
  {
    RETURN GetDurationTitle(duration);
  }
  INTEGER FUNCTION Submit()
  {
    OBJECT work := this->BeginWork( [ mutex := "wrd:usermgmt" ]);
    INTEGER parent := ^unitentity->entityid = 0 ? ^unitentity->extradata.wrd_leftentity : ^unitentity->wrdentity->GetField("WRD_LEFTENTITY");

    this->contexts->userapi->__CheckName(this->contexts->userapi->GetUnit(parent)
                                        ,^unitentity->^wrd_title->value
                                        ,^unitentity->entityid
                                        ,FALSE //foruser
                                        ,work);

    INTEGER unitid := ^unitentity->Store(work);
    ^maintabs->SubmitExtensions(work);
    IF(NOT work->HasFailed())
      this->contexts->userapi->SyncUnitAfterUpdate(unitid);

    IF(NOT work->Finish())
      RETURN 0;

    //TODO should become obsolete, WRD events should suffice?
    BroadcastEvent("system:unit.change." || parent, DEFAULT RECORD);
    RETURN unitid;
  }
>;


PUBLIC STATIC OBJECTTYPE EditRole EXTEND TolliumScreenBase
<
  INTEGER parent;

  MACRO Init(RECORD data)
  {
    ^entity->Load(data.role);
    IF(data.role = 0)
       ^entity->extradata := [ wrd_leftentity := data.parent ];
  }

  INTEGER FUNCTION Submit()
  {
    OBJECT work := this->BeginWork( [ mutex := "wrd:usermgmt" ]);
    INTEGER parent := ^entity->entityid = 0 ? ^entity->extradata.wrd_leftentity : ^entity->wrdentity->GetField("WRD_LEFTENTITY");

    this->contexts->userapi->__CheckName(this->contexts->userapi->GetUnit(parent)
                                        ,^entity->^wrd_title->value
                                        ,^entity->entityid
                                        ,FALSE //foruser
                                        ,work);

    INTEGER unitid := ^entity->Store(work);
    IF(NOT work->HasFailed())
      this->contexts->userapi->SyncRoleAfterUpdate(unitid);

    IF(NOT work->Finish())
      RETURN 0;

    //TODO should become obsolete, WRD events should suffice?
    BroadcastEvent("system:unit.change." || parent, DEFAULT RECORD);
    RETURN unitid;
  }
>;
