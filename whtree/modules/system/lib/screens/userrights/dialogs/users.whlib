<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::system/lib/configure.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";
LOADLIB "mod::wrd/lib/internal/support.whlib";
LOADLIB "mod::wrd/lib/internal/auth/support.whlib";

LOADLIB "mod::system/lib/screens/userrights/support.whlib";
LOADLIB "mod::tollium/lib/backendhooks.whlib";


PUBLIC STATIC OBJECTTYPE EditUser EXTEND TolliumScreenBase <
  // -----------------------------------------------------------------------------
  //
  // Global variables and such
  //

  OBJECT pvt_user;

  OBJECT pvt_parent;

  OBJECT pvt_orgparent;

  /// Whether the edited user is the current user
  BOOLEAN isself;

  FUNCTION PTR notificationsubmit;

  /// Loaded extension fragment
  OBJECT loadedfragment;

  RECORD policy;

  // -----------------------------------------------------------------------------
  //
  // Public properties
  //

  PUBLIC PROPERTY user(pvt_user, -);

  // -----------------------------------------------------------------------------
  //
  // Tollium init
  //

  MACRO Init(RECORD data) {
    ^screentabs->extendposition := 1;
    SetupBackendScreenHooks(PRIVATE this, CELL[ screenname := "platform:userproperties" ]);

    RECORD prefs := CollectUserPreferenceOptions(this->tolliumcontroller);
    ^language->options := prefs.languages;

    this->pvt_user := data.user;

    IF (ObjectExists(this->pvt_user))
      this->pvt_parent := this->contexts->userapi->GetUnitOf(this->pvt_user);
    ELSE
      this->pvt_parent := data.parent;

    this->pvt_orgparent := this->pvt_parent;

    ^username->cellname := this->contexts->userapi->wrdschema->accountlogintag;

    IF(this->contexts->wrdschema->accountemailtag = ""
       OR this->contexts->wrdschema->accountlogintag = this->contexts->wrdschema->accountemailtag) {
      ^email->composition := DEFAULT OBJECT;
      ^email->visible := FALSE;
      ^email->enabled := FALSE;
      IF(this->contexts->wrdschema->accountlogintag = this->contexts->wrdschema->accountemailtag)
        ^username->title := GetTid("~email");
    } ELSE {
      ^email->cellname := this->contexts->wrdschema->accountemailtag;
      ^email->required := this->contexts->wrdschema->accounttype->GetAttribute(this->contexts->wrdschema->accountemailtag).isrequired;
    }

    this->policy := ObjectExists(data.user)
       ? GetUserUnitPolicy(this->contexts->wrdschema, data.user->entityid)
       : GetSchemaUnitPolicy(this->contexts->wrdschema, this->pvt_parent->entityid);

    ConfigurePasswordFieldForPolicy(^authenticationsettings, this->policy);

    ^userobj->LoadEntity(this->contexts->userapi->wrdschema->accounttype, ObjectExists(this->pvt_user) ? this->pvt_user->entityid : 0);
    ^authenticationsettings->cellname := this->contexts->wrdschema->accountpasswordtag;

    IF(NOT ObjectExists(this->pvt_user))
    {
      ^wrd_guid->value := this->contexts->userapi->wrdschema->accounttype->GetNewEntityGUID();
      ^language->SetValueIfValid(ReadRegistryKey("system.userrights.newuserdefaults_language"));
    }
    ELSE
    {
      ^language->SetValueIfValid(this->pvt_user->GetLocale().language);
      IF (ObjectExists(this->pvt_user->entity))
      {
        ^wrd_guid->value := this->pvt_user->entity->GetField("WRD_GUID");
        //TODO we might want to extract lastip/lastlogin from the audit log ? Or just integrate the auditlog more directly!
        //IF("WHUSER_LASTIP" IN SELECT AS STRING ARRAY localtag FROM this->contexts->userapi->wrdschema->accounttype->ListAttributes(0) WHERE attributetype = 2)
        //{
        //  ^lastip->value := this->pvt_user->entity->GetField("WHUSER_LASTIP");
        //  ^lastip->visible := TRUE;
        //}
      }
      ^wrd_guid->enabled := FALSE;

      ^enableguidedit->visible := this->tolliumuser->HasRight("system:sysop");
      ^password->visible := FALSE;
    }

    RECORD unitinfo;
    BOOLEAN isnew := NOT ObjectExists(this->pvt_user);
    this->isself := isnew = FALSE AND ObjectExists(this->tolliumuser->entity) AND this->tolliumuser->entity->id = this->pvt_user->entityid;
    ^authenticationsettings->managementmode := isnew = FALSE AND NOT this->isself;

    IF (isnew) // new user
    {
      // change appearance of screen to 'New user'
      ^creationdate->visible  := FALSE;

      this->frame->title           := GetTid("system:userrights.screentitles.adduser");
      ^lastlogin->visible     := FALSE;
    }
    ELSE        // edit/properties user
    {
      ^lastlogin->visible     := TRUE;
    }

    ^advanced->visible := this->tolliumuser->HasRight("system:sysop");

    IF(NOT isnew)
      ^userobj->Invalidate();

    // Run the postinit for all extensions
    ^screentabs->RunExtensionsPostInit();
  }

  // -----------------------------------------------------------------------------
  //
  // Callbacks
  //

  // -----------------------------------------------------------------------------
  //
  // Actions
  //

  BOOLEAN FUNCTION Submit() {
    OBJECT work := this->BeginUnvalidatedWork();
    ^userinfopage->ValidateValue(work);

    this->contexts->userapi->__CheckName(this->pvt_parent, ^username->value, ObjectExists(this->pvt_user) ? this->pvt_user->entityid : 0, TRUE, work);

    IF (NOT ValidateWRDGUID(^wrd_guid->value))
      work->AddErrorFor(^wrd_guid, GetTid("system:userrights.dialogues.invalidguid"));
    ELSE IF (^wrd_guid->enabled)
    {
      IF (^userobj->wrdtype->Search("WRD_GUID", ^wrd_guid->value) NOT IN [ 0, ^userobj->entityid ])
        work->AddErrorFor(^wrd_guid, GetTid("system:userrights.dialogues.guidalreadyused"));
    }

    IF (work->HasFailed())
      RETURN work->Finish();

    BOOLEAN isnew;

    IF (NOT ObjectExists(this->pvt_user))
    {
      // New user
      isnew := TRUE;
      IF (NOT this->tolliumuser->HasRightOn("system:manageunit", this->pvt_parent->authobjectid))
        work->AddError(GetTid("system:userrights.dialogues.notunitmanager"));
    }
    ELSE
    {
      // Existing user
      IF (NOT this->tolliumuser->HasRightOn("system:manageunit", this->pvt_user->authobjectid))
        work->AddError(GetTid("system:userrights.dialogues.notunitmanagerforuser", this->pvt_user->GetUserRightsName()));
      ELSE IF (this->pvt_orgparent->entityid != this->pvt_parent->entityid)
      {
        IF (NOT this->tolliumuser->HasRightOn("system:manageunit", this->pvt_orgparent->authobjectid))
          work->AddError(GetTid("system:userrights.errors.notunitmanageronparentforusermove"));
      }
    }

    IF (work->HasFailed())
      RETURN work->Finish();

    OBJECT user;
    RECORD extrafields :=
        [ whuser_unit := this->pvt_parent->entityid
        ];

    IF(NOT ObjectExists(this->pvt_user))
      extrafields := CellInsert(extrafields, this->contexts->wrdschema->accountpasswordtag, CreateAuthenticationSettingsFromPasswordHash(^password->value));

    INTEGER id := ^userobj->StoreEntity(work, extrafields);
    IF (id != 0) {
      ^screentabs->SubmitExtensions(work);

      // Updating the GUID must run via the v2 api, can't use StoreEntity for that.
      ^userobj->wrdentity->UpdateEntity([ wrd_guid := ^wrd_guid->value ]);

      this->contexts->userapi->SyncUserAfterUpdate(id);

      user := this->contexts->userapi->GetUser(id); //reload user, guid might have changed
    } ELSE {
      user := this->pvt_user;
    }

    IF(NOT work->HasFailed() AND ^userobj->^wrdauth_account_status->HasChanged()) {
      this->tolliumcontroller->wrdauthplugin->LogCustomAuditEvent("platform:accountstatus",
        [ account := ^userobj->entityid
        , login := ^username->value
        , message := [ oldstatus := ^userobj->^wrdauth_account_status->originalvalue
                     , newstatus := ^userobj->^wrdauth_account_status->value
                     ]
        ]);
    }

    IF(ObjectExists(user)) //we didn't crash yet ?
      user->SetLocale(CELL[...user->GetLocale(), language := ^language->value]);
    IF (NOT work->Finish())
      RETURN FALSE;

    this->pvt_user := user;

    // Broadcast changes to units
    BroadcastEvent("system:unit.change." || this->pvt_parent->entityid, DEFAULT RECORD);
    IF (ObjectExists(this->pvt_orgparent) AND this->pvt_orgparent->entityid != this->pvt_parent->entityid)
      BroadcastEvent("system:unit.change." || this->pvt_orgparent->entityid, DEFAULT RECORD);

    // Broadcast userprefs changes, refresh the tollium frontend application menu
    IF(NOT isnew) {
      BroadcastEvent("tollium:userprefs." || user->entityid, DEFAULT RECORD);
      this->user->BroadcastToClient([ type := "tollium:shell.refreshmenu" ]);
    }

    RETURN TRUE;
  }

  // -----------------------------------------------------------------------------
  //
  // Helper functions
  //

  // -----------------------------------------------------------------------------
  //
  // Public API
  //

>;

PUBLIC OBJECTTYPE SetUserDefaults EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    RECORD prefs := CollectUserPreferenceOptions(this->tolliumcontroller);

    this->language->options := prefs.languages;
    this->decimalsep->options := prefs.decimalseps;
    this->thousandsep->options := prefs.thousandseps;

    this->language->value :=    ReadRegistryKey("system.userrights.newuserdefaults_language");
    this->timezone->value :=    ReadRegistryKey("system.userrights.newuserdefaults_timezone");
    this->decimalsep->value :=  ReadRegistryKey("system.userrights.newuserdefaults_decimalsep");
    this->thousandsep->value := ReadRegistryKey("system.userrights.newuserdefaults_thousandsep");
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    IF (this->decimalsep->value = this->thousandsep->value)
      work->AddError(GetTid("system:sysmgmt.decimalsepandthousandsepmustdiffer"));

    IF (NOT work->HasFailed())
    {
      WriteRegistryKey("system.userrights.newuserdefaults_language", this->language->value);
      WriteRegistryKey("system.userrights.newuserdefaults_timezone", this->timezone->value);
      WriteRegistryKey("system.userrights.newuserdefaults_decimalsep", this->decimalsep->value);
      WriteRegistryKey("system.userrights.newuserdefaults_thousandsep", this->thousandsep->value);
    }
    RETURN work->Finish();
  }
>;
