<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screens/userrights/support.whlib";
LOADLIB "mod::system/lib/internal/rightsmgmt.whlib";
LOADLIB "mod::system/lib/internal/rightsinfo.whlib";

LOADLIB "mod::tollium/lib/dialogs.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/screens/userrights/commondialogs-api.whlib";


INTEGER ARRAY FUNCTION GetAuthobjectsWithWebhareRole(INTEGER roleid)
{
  RETURN SELECT AS INTEGER ARRAY authobjects.id
           FROM system.rolegrants
              , system.authobjects
          WHERE rolegrants.grantee = authobjects.id
            AND rolegrants.role = roleid;
}

STATIC OBJECTTYPE ObjectTreeRightsExport
< // ---------------------------------------------------------------------------
  //
  // Variables
  //
  OBJECT userapi;

  OBJECT tolliumuser;

  OBJECT rightsinfo;

  STRING objecttypename;

  INTEGER objectid;

  // ---------------------------------------------------------------------------
  //
  // Public properties
  //

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO NEW(OBJECT contexts, STRING objecttypename, INTEGER objectid)
  {
    this->userapi := contexts->userapi;
    this->tolliumuser := contexts->user;
    this->rightsinfo := GetRightsInfoObject(contexts->user);

    this->objecttypename := objecttypename;
    this->objectid := objectid;
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  STRING FUNCTION GetObjectPath(INTEGER objectid)
  {
    RECORD pathrec := GetPresentationRightsObjectPath(this->objecttypename, objectid, this->tolliumuser->authobject);
    IF (NOT RecordExists(pathrec) OR NOT pathrec.visible)
      RETURN "";

    RECORD ARRAY path := pathrec.path;
    IF (this->objectid != 0)
    {
      WHILE (LENGTH(path) != 0 AND path[0].id != this->objectid)
        DELETE FROM path AT 0;

      // Hide?
      IF (LENGTH(path) = 0)
        RETURN "";
    }
    RETURN "/" || Detokenize((SELECT AS STRING ARRAY name FROM path WHERE id != 0), "/");
  }

  // ---------------------------------------------------------------------------
  //
  // Public API
  //

  PUBLIC RECORD ARRAY FUNCTION GenerateExport()
  {
    RECORD ARRAY rights :=
        SELECT *
          FROM this->rightsinfo->rights
         WHERE objecttypename = this->objecttypename;

    STRING ARRAY rightnames := SELECT AS STRING ARRAY name FROM rights;

    RECORD ARRAY grants;
    FOREVERY (RECORD right FROM rights)
    {
      grants := grants CONCAT
          SELECT grantee
               , objectid :=      COLUMN "object"
               , withgrantoption
               , rightname :=     right.name
               , righttitle :=    right.title
            FROM GetDirectGrantsForRight(right.name);
    }
    // Resolve all paths
    RECORD ARRAY paths :=
        SELECT objectid
             , path :=      this->GetObjectPath(objectid)
          FROM grants
      GROUP BY objectid
      ORDER BY objectid;
    // Delete the ones that are invisible
    DELETE FROM paths WHERE path = "";

    // Add the paths, filter out the invisible ones
    {
      RECORD ARRAY newgrants;
      FOREVERY (RECORD rec FROM grants)
      {
        RECORD pos := RecordLowerBound(paths, rec, [ "OBJECTID" ]);
        IF (pos.found)
        {
          INSERT CELL path := paths[pos.position].path INTO rec;
          INSERT rec INTO newgrants AT END;
        }
      }

      grants := newgrants;
    }

    INTEGER ARRAY all_grantees :=
        SELECT AS INTEGER ARRAY DISTINCT grantee
          FROM grants;

    RECORD ARRAY all_role_grantees :=
        SELECT id
          FROM system.authobjects
         WHERE id IN all_grantees
           AND type = 3;

    IF(Length(all_role_grantees) > 0)
    {
      INTEGER ARRAY visible_role_grantees := this->tolliumuser->HasRightOnMultiple("system:browseunits", SELECT AS INTEGER ARRAY id FROM all_role_grantees);
      DELETE FROM all_role_grantees WHERE id NOT IN visible_role_grantees;

      IF(Length(all_role_grantees) > 0) //still present
        all_role_grantees := this->userapi->EnrichRoles("ID", all_role_grantees, [ celltype := "authobjectid" ]);
    }

    INTEGER ARRAY all_user_grantees :=
        SELECT AS INTEGER ARRAY id
          FROM system.authobjects
         WHERE id IN all_grantees
           AND type = 1;

    RECORD ARRAY rows :=
        SELECT rowkey :=  "user-" || grantee || "-" || objectid || "-" || rightname
             , type :=    "user"
             , *
             , viarole := ""
          FROM grants
         WHERE grantee IN all_user_grantees;

    FOREVERY (RECORD rec FROM grants)
    {
      RECORD matchrole := SELECT * FROM all_role_grantees WHERE id = rec.grantee;
      IF (RecordExists(matchrole))
      {
        FOREVERY (INTEGER rolegrantee FROM GetAuthobjectsWithWebhareRole(rec.grantee))
          INSERT
              [ rowkey :=           "role-" || rec.grantee || "-" || rolegrantee || "-" || rec.objectid || "-" || rec.rightname
              , type :=             "role"
              , grantee :=          rolegrantee
              , objectid :=         rec.objectid
              , path :=             rec.path
              , withgrantoption :=  rec.withgrantoption
              , rightname :=        rec.rightname
              , righttitle :=       rec.righttitle
              , viarole :=          GetQualifiedAuthObjectName(rec.grantee) //TODO can't we use the names gathered in 'EnrichRoles' ?
              ] INTO rows AT END;
      }
    }


    {
      RECORD ARRAY newrows;
      INTEGER ARRAY userids := SELECT AS INTEGER ARRAY DISTINCT grantee FROM rows;

      RECORD ARRAY users :=
          SELECT id
               , type
               , unit :=      parent
               , login :=     name
               , authobjectparentid := parent
            FROM system.authobjects
           WHERE id IN userids
             AND type = 1
             AND this->tolliumuser->HasRightOn("system:browseunits", id);

      users := this->tolliumuser->authapi->EnrichUsers("userid", (SELECT id, type, unit, authobjectparentid, userid := id FROM users), [ celltype := "authobjectid" ] );
      users := EnrichWithUnitPath(users, this->tolliumuser);
      users := SELECT * FROM users ORDER BY id;

      FOREVERY (RECORD rec FROM rows)
      {
        RECORD pos := RecordLowerBound(users, [ id := rec.grantee ], [ "ID" ]);
        IF (pos.found)
        {
          INSERT CELL username := users[pos.position].unitpath || "/" || users[pos.position].login INTO rec;
          INSERT CELL fullname := users[pos.position].fullname INTO rec;
          INSERT rec INTO newrows AT END;
        }
      }

      rows := newrows;
    }

    RETURN
        SELECT username
             , fullname
             , right :=       righttitle
             , path
             , direct :=      type = "user" ? 1 : 2
             , viarole
          FROM rows
         WHERE this->objectid = 0 ? TRUE : path != ""
      ORDER BY username, path;
  }

  PUBLIC MACRO DownloadList(OBJECT parent)
  {
    RunColumnFileExportDialog(parent,
      [ rows := this->GenerateExport()
      , columns :=  [ [ name := "path",     title := GetTid("system:userrights.exports.csvcolumns.path"),     type := "text" ]
                    , [ name := "username", title := GetTid("system:userrights.exports.csvcolumns.username"), type := "text" ]
                    , [ name := "fullname", title := GetTid("system:userrights.exports.csvcolumns.fullname"), type := "text" ]
                    , [ name := "right",    title := GetTid("system:userrights.exports.csvcolumns.right"),    type := "text" ]
                    , [ name := "viarole",  title := GetTid("system:userrights.exports.csvcolumns.viarole"),  type := "text" ]
                    ]
      , exporttitle := GetTid("system:userrights.exports.exportobjectrights")
      ]);
  }
>;


PUBLIC OBJECTTYPE ObjectTree EXTEND TolliumFragmentBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  INTEGER cbid;

  // ---------------------------------------------------------------------------
  //
  // Public variables
  //

  PUBLIC FUNCTION PTR onselect;

  PUBLIC PROPERTY value(this->objectlist->value, this->objectlist->value);

  /// Boolean that indicates that multiple rightsgroups/objecttypes have been selected
  PUBLIC PROPERTY typesmixed(GetTypesMixed, -);

  /// List of rights that are relevant for the currenty selected rightsgroup/object(type)
  PUBLIC PROPERTY relevantrights(GetRelevantRights, -);

  /// Selectmode, one of 'single', 'multiple'
  PUBLIC PROPERTY selectmode(GetSelectMode, SetSelectMode);

  /// Borders
  PUBLIC PROPERTY borders(this->objectlist->borders, this->objectlist->borders);

  // ---------------------------------------------------------------------------
  //
  // Constructor & tollium registration
  //

  PUBLIC UPDATE MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
  }

  UPDATE MACRO PreInitComponent()
  {
    this->cbid := RegisterMultiEventCallback("system:unit.change.*", PTR this->OnUpdateEvent);

    this->objectlist->filteritems := PTR this->FilterObjectTreeItems;
    this->objectlist->EnsurePreInit();
    this->objectlist->ReloadList();
  }

  PUBLIC UPDATE MACRO OnUnloadComponent()
  {
    IF (this->cbid != 0)
      UnregisterCallback(this->cbid);
    this->cbid := 0;
    TolliumFragmentBase::OnUnloadComponent();
  }

  // ---------------------------------------------------------------------------
  //
  // Getters & setters
  //

  BOOLEAN FUNCTION GetTypesMixed()
  {
    RETURN this->objectlist->typesmixed;
  }

  RECORD ARRAY FUNCTION GetRelevantRights()
  {
    RETURN this->objectlist->relevantrights;
  }

  STRING FUNCTION GetSelectMode()
  {
    RETURN this->objectlist->selectmode;
  }

  MACRO SetSelectMode(STRING selectmode)
  {
    this->objectlist->selectmode := selectmode;
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnUpdateEvent(STRING event, RECORD ARRAY data)
  {
    this->objectlist->ReloadList();
  }

  MACRO OnSelectHandler()
  {
    IF (this->onselect != DEFAULT FUNCTION PTR)
      this->onselect();
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  RECORD ARRAY FUNCTION OnExtendObjectTreeChildren(RECORD ARRAY children, RECORD parent)
  {
    IF (NOT RecordExists(parent) OR parent.objecttypename = "")
    {
      // These are global rights
      children :=
          SELECT relevant_rights := (SELECT AS STRING ARRAY name FROM this->objectlist->GetRelevantRightsForRow(sel))
               , *
            FROM children AS sel;

      children :=
          SELECT root_grantable_objects := this->owner->tolliumuser->GetRootObjectsForGrantableRights(relevant_rights)
               , *
            FROM children AS sel;

      children :=
          SELECT cangrantany := 0 IN root_grantable_objects
               , *
            FROM children AS sel;
    }
    ELSE
    {
      children :=
          SELECT relevant_rights := parent.relevant_rights
               , root_grantable_objects := parent.root_grantable_objects
               , cangrantany := parent.cangrantany OR objectid IN parent.root_grantable_objects
               , *
            FROM children;
    }

    RETURN children;
  }

  RECORD ARRAY FUNCTION FilterObjectTreeItems(STRING objecttypename, RECORD ARRAY initems)
  {
    SWITCH (objecttypename)
    {
      CASE "system:units"
      {
        RETURN this->FilterItemsForWRDSchema(initems);
      }
    }
    RETURN initems;
  }

  RECORD ARRAY FUNCTION FilterItemsForWRDSchema(RECORD ARRAY initems)
  {
    /// FIXME: This should be done in the UnitObjecttypeDescriber, but that will have to wait until we've redone the rightsAPI
    INTEGER ARRAY existing := SELECT AS INTEGER ARRAY obj->authobjectid
                                FROM ToRecordArray(this->contexts->userapi->GetObjectsByAuthobjectID(SELECT AS INTEGER ARRAY id FROM initems), "obj");
    RETURN
        SELECT * FROM initems
                WHERE id IN existing;
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoGrantRightOnObject()
  {
    RECORD val := this->objectlist->value;

    IF (val.rightsgroup != "")
      MakeAddGrantOnRightsGroupDialog(this->owner, val.rightsgroup)->RunModal();
    ELSE
    {
      // Exectly one object is selected (min=1,max=1 on enableon)
      INTEGER objectid := this->objectlist->selectmode = "single"
          ? val.objectid
          : val.objectids[0];

      MakeAddGrantOnObjectDialog(this->owner, val.objecttypename, objectid)->RunModal();
    }
  }

  MACRO DoExportObjectRights()
  {
    RECORD val := this->objectlist->value;
    OBJECT exporter := NEW ObjectTreeRightsExport(this->contexts, val.objecttypename, val.objectid);
    exporter->DownloadList(this->owner);
  }

  MACRO DoReload()
  {
    this->objectlist->ReloadList();
  }
>;
