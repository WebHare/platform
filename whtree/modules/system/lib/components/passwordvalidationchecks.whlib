<?wh

LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/auth/passwordpolicy.whlib";
LOADLIB "mod::wrd/lib/internal/auth/support.whlib";


PUBLIC OBJECTTYPE ComposableFragmentBase EXTEND TolliumFragmentBase
< MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
  }
>;


PUBLIC STATIC OBJECTTYPE PasswordValidationChecks EXTEND ComposableFragmentBase
<
  /// @type(string) Password validation checks string
  PUBLIC PROPERTY value(GetValue, SetValue);

  // ---------------------------------------------------------------------------
  //
  // Getters / setters
  //

  UPDATE MACRO SetEnabled(BOOLEAN newenabled)
  {
    TolliumFragmentBase::SetEnabled(newenabled);

    ^password_hibp->enabled := newenabled;
    ^password_minlength->enabled := newenabled;
    ^password_lowercase->enabled := newenabled;
    ^password_uppercase->enabled := newenabled;
    ^password_digits->enabled := newenabled;
    ^password_symbols->enabled := newenabled;
    ^password_noreuse->enabled := newenabled;
    ^password_maxage->enabled := newenabled;
    ^password_require2fa->enabled := newenabled;
  }

  MACRO SetValue(STRING newvalue)
  {
    RECORD ARRAY parsed_validationchecks := ParsePasswordChecks(newvalue);
    ^password_hibp->value := RecordExists(SELECT FROM parsed_validationchecks WHERE check = "hibp");
    ^password_minlength->value := SELECT AS INTEGER value FROM parsed_validationchecks WHERE check = "minlength";
    ^password_lowercase->value := SELECT AS INTEGER value FROM parsed_validationchecks WHERE check = "lowercase";
    ^password_uppercase->value := SELECT AS INTEGER value FROM parsed_validationchecks WHERE check = "uppercase";
    ^password_digits->value := SELECT AS INTEGER value FROM parsed_validationchecks WHERE check = "digits";
    ^password_symbols->value := SELECT AS INTEGER value FROM parsed_validationchecks WHERE check = "symbols";
    ^password_noreuse->value := SELECT AS STRING duration FROM parsed_validationchecks WHERE check = "noreuse";
    ^password_maxage->value := SELECT AS STRING duration FROM parsed_validationchecks WHERE check = "maxage";
    ^password_require2fa->value := RecordExists(SELECT FROM parsed_validationchecks WHERE check = "require2fa");
  }

  STRING FUNCTION GetValue()
  {
    STRING ARRAY passwd_checks;
    IF (^password_hibp->value)
      INSERT "hibp" INTO passwd_checks AT END;
    IF (IsValueSet(^password_minlength->value))
      INSERT `minlength:${^password_minlength->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_lowercase->value))
      INSERT `lowercase:${^password_lowercase->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_uppercase->value))
      INSERT `uppercase:${^password_uppercase->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_digits->value))
      INSERT `digits:${^password_digits->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_symbols->value))
      INSERT `symbols:${^password_symbols->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_maxage->value))
      INSERT `maxage:${^password_maxage->value}` INTO passwd_checks AT END;
    IF (IsValueSet(^password_noreuse->value))
      INSERT `noreuse:${^password_noreuse->value}` INTO passwd_checks AT END;
    IF (^password_require2fa->value)
      INSERT "require2fa" INTO passwd_checks AT END;

    RETURN Detokenize(passwd_checks, " ");
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks / mappings
  //

  STRING FUNCTION GotBrowseMaxAge(STRING value)
  {
    RETURN this->owner->RunScreen("mod::system/tolliumapps/config/settings.xml#selectduration", CELL[ value, withtime := FALSE ]);
  }

  STRING FUNCTION GotBrowseNoReuse(STRING value)
  {
    RETURN this->owner->RunScreen("mod::system/tolliumapps/config/settings.xml#selectduration", CELL[ value, withtime := FALSE ]);
  }

  STRING FUNCTION MapDuration(STRING duration)
  {
    RETURN GetDurationTitle(duration);
  }
>;
