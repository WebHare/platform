<?wh
//Mimicks @webhare/test and @mod-tollium/js/testtool

LOADLIB "mod::tollium/lib/internal/tollium-testframework-wrapper.whlib"; //to avoid warnings about TT

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";


ASYNC MACRO TT_LaunchApp(STRING appname, RECORD params DEFAULTSTO DEFAULT RECORD)
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp(appname,params));

}
ASYNC MACRO TT_LaunchScreen(STRING resource)
{
  IF(NOT IsAbsoluteResourcePath(resource))
    THROW NEW Exception(`openScreen requires an absolute resource, got ${resource}`);

  AWAIT TT_LaunchApp("webhare_testsuite:runscreen", [ calltype := "direct", params := [ resource ] ]);
}

MACRO TT_Run(VARIANT ARRAY testlist, RECORd options DEFAULTSTO DEFAULT RECORD)
{
  MACRO PTR ARRAY tests;
  FOREVERY(VARIANT test FROM testlist)
  {
    IF(TYPEID(test) = TYPEID(STRING))
      INSERT PTR Print("Test: " || test || "...\n") INTO tests AT END;
    ELSE IF(TYPEID(test) = TYPEID(MACRO PTR))
      INSERT test INTO tests AT END;
    ELSE
      THROW NEW Exception(`Can't understand test step #${#test}`);
  }

  options := CELL[ ...options
                 , testusers := [[ login := "sysop", grantrights := ["system:sysop"]  ]
                                ,...(CellExists(options,"testusers") ? options.testusers : RECORD[])
                                ]
                 ];

  Runtestframework(tests, options);
}

OBJECT FUNCTION TT_Comp(STRING name, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([allowmissing := FALSE],options);

  IF(NOT ObjectExists(topscreen))
    THROW NEW Exception(`No screen is open! looking for '${name}'`);

  //TODO how long will we be (And do we want to be??) compatible with the previous TT implementation?
  RETURN __LookupComponentByTestReference(topscreen, name, options);
}

ASYNC MACRO TT_Escape()
{
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

/** @private don't appear in symbol searches
*/
PUBLIC RECORD test :=
  [ assert := PTR TestAssert
  , run := PTR TT_Run
  ];

/** @private  don't appear in symbol searches, don't conflict with the other tt
*/
PUBLIC RECORD tt :=
  [ launchscreen := PTR TT_LaunchScreen
  , comp := PTR TT_Comp
  , escape := PTR TT_Escape
  ];
