<?wh
LOADLIB "wh::devsupport.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";
LOADLIB "mod::system/lib/internal/whcore_interface.whlib";

/** @short Webserver error handling
    @long This library should only be loaded by scripts implementing server-parsed (SHTML) pages.
*/

/** Get raw request error information
    @return All errors, an error stracktrace, the group ID of the script that triggered the error and the list of loaded resources
    @cell return.groupid The group ID of the script that generated the error
    @cell return.errors List of errors
    @cell return.errors.message Error message
    @cell return.errors.filename Name of the file affected by the error or stacktrace
    @cell return.errors.code If >=0, the error code. If <0, the stacktrace ordering number (lower numbers are closer to the point of error)
    @cell return.errors.line HareScript line at which the error appeared
    @cell return.errors.col HareScript column at which the error appeared
    @cell return.errors.iserror True if this is an error message, false if this is a warning (NOTE: warnings are not yet returned by the WebHare webserver)
    @cell return.errors.istrace True if this error is part of a stacktrace, false if it is part
    @cell return.errors.param1 Argument 1 for the error message, or name of the active function if this record is part of a stacktrace
    @cell return.errors.param2 Argument 2 for the error message
    @cell return.resources Loaded resources in the script that generated the error
*/
RECORD FUNCTION __WHS_GetErrorInfo() __ATTRIBUTES__(EXTERNAL, EXECUTESHARESCRIPT);

/** @short Get HareScript error information
    @long This function retrieves a list of HareScript errors generated by a failing page. This function is can only be used in HareScript error handler pages
    @return @includecelldef #__WHS_GetErrorInfo.return
*/
PUBLIC RECORD FUNCTION GetRequestErrorInfo()
{
  RECORD errorinfo := __WHS_GetErrorInfo();
  IF (LENGTH(errorinfo.errors) != 0 AND NOT IsWHDebugOptionSet("etr"))
  {
    errorinfo.errors :=
        [ [ message :=      `Error ID: ${errorinfo.groupid}`
          , code :=         263
          , filename :=     ""
          , func :=         ""
          , line :=         0
          , col :=          0
          , istrace :=      FALSE
          , iserror :=      TRUE
          , param1 :=       errorinfo.groupid
          , param2 :=       ""
          ]
        ];
  }

  RETURN errorinfo;
}


/** @short Get HareScript error information
    @long This function retrieves a list of HareScript errors generated by a failing page. This function is can only be used in HareScript error handler pages
    @return A record array listing all errors, and an error stracktrace @includecelldef #__WHS_GetErrorInfo.return.errors
*/
PUBLIC RECORD ARRAY FUNCTION GetHarescriptErrors()
{
  RETURN GetRequestErrorInfo().errors;
}


/** @short Send an error to the webservers error log
    @param message Message to log
*/
PUBLIC MACRO LogWebserverError(STRING message)
{
  __WHS_LogWebserverError(message);
}

/** @short Standard HareScript error generator */
PUBLIC MACRO CreateHarescriptErrorPage(RECORD ARRAY errors, RECORD ARRAY stacktrace)
{
  errors := SELECT file
                 , line
                 , col
                 , code
                 , text := GetHareScriptMessageText(TRUE, code, param1, param2)
              FROM errors;

  stacktrace := SELECT file
                      , line
                      , col
                      , code
                      , func := param1 != "" AND param1 NOT LIKE ":*" ? param1 : ""
                   FROM stacktrace;

  // Firefox 3 will send: Content-Type: application/x-todd-query; charset=UTF-8
  IF(Left(GetWebHeader("Content-Type"), 24)="application/x-todd-query")
  {
    /* Create a TODD-parseable error report */
    AddHTTPHeader("Content-Type","application/json",FALSE);

    RECORD instr := [ instr := "harescripterror"
                    , errors := errors
                    , stacktrace := stacktrace
                    ];
    Print(EncodeJSON([instr]));
    RETURN;
  }

  BOOLEAN wrap_in_html := GetWebHeader("X-Tollium-Webforms-Req") != "1";

  IF(wrap_in_html)
  {
    Print('<!DOCTYPE html>\n');
    Print('<html xmlns="http://www.w3.org/1999/xhtml">\n');
    Print('<head>\n');
    Print('<meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />\n');
    Print('<style rel="stylesheet" type="text/css" />\n');
    Print('body {font-family: arial, sans-serif; font-size: 9pt; background-color: white;}\n');
    Print('h1 {font-size: 14pt; font-weight: bold; color: #356699; }\n');
    Print('ul {margin: 0; padding: 0; list-style-type: none;}\n');
    Print('a {color: #356699}\n');
    Print('fieldset {margin-bottom: 20px;}\n');
    Print('</style>');
    Print('</head><body>');
  }
  ELSE
  {
    Print('<div>');
  }
  Print('<h1>An error occurred</h1><p>');
  Print('<p>Something went wrong on the server.<br /> We apologize for the inconvenience.<br /><br />We would appreciate it if you could report the following to us:</p>');
  Print('<p>- - - - - - - - - - - - - - - - - - - - - - - - - - - - -</p>');

  Print('<fieldset><legend>Error:</legend>');
  Print('<ul>');
  FOREVERY(RECORD error FROM errors)
  {
    IF (error.text LIKE "*Custom error message: *+RECORD*" OR error.text LIKE "*Custom error message: *+----*")
      Print('<li><span style="white-space: pre;font-family: monospace">');
    ELSE
      Print('<li><span style="white-space: pre">');
    Print(EncodeHTML(error.file));

    Print(' line ' || error.line || ', column ' || error.col || ': ');
    Print(EncodeHTML(error.text));
    Print('</span></li>');
  }
  Print('</ul>');
  Print('</fieldset>');

  IF (Length(stacktrace)>0)
  {

    Print('<fieldset><legend>Stack trace:</legend>');
    Print('<ul>');

    Forevery(RECORD trace FROM stacktrace)
    {
      Print('<li>');
      Print(EncodeHTML(trace.file));

      Print(' line ' || trace.line || ', column ' || trace.col);
      IF (trace.func!="")
        Print(' in function ' || EncodeHTML(trace.func));
      Print('</li>');
    }
    Print('</ul>');

    Print('</fieldset>');

    Print('<p>- - - - - - - - - - - - - - - - - - - - - - - - - - - - -</p>');
  }
  ELSE
  {
    Print('<p>- - - - - - - - - - - - - - - - - - - - - - - - - - - - -</p>');
  }

  IF(wrap_in_html)
    Print('</body>');
  ELSE
    Print('</div>');
  Print("\n<!-- IE pretty http error message prevention                                                ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("                                                                                            ");
  Print("-->\n");
}
