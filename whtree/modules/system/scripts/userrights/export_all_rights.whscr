<?wh

LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::filetypes/csv.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/internal/rightsmgmt.whlib";
LOADLIB "mod::system/lib/internal/backend/users.whlib";
LOADLIB "mod::system/lib/userrights.whlib";


OBJECT trans := OpenPrimary();
OBJECT userapi := GetPrimaryWebhareUserApi();

RECORD ARRAY allusers :=
    SELECT *
      FROM userapi->accounttype->RunQuery(
                [ outputcolumns :=
                      [ id :=         "WRD_ID"
                      , realname :=   "WRD_TITLE"
                      , email :=      userapi->wrdschema->accountemailtag
                      , guid  :=      "WRD_GUID"
                      ]
                ])
  ORDER BY guid;

RECORD ARRAY allobjs := SELECT TEMPORARY up := RecordLowerBound(allusers, authobjects, [ "GUID" ])
                             , authobjects.id
                             , authobjects.name
                             , realname :=  up.found ? allusers[up.position].realname : "<other schema>"
                             , email :=     up.found ? allusers[up.position].email : "<other schema>"
                             , unitid := units.id
                             , unitname := units.name
                             , otherschema := NOT up.found
                          FROM system.authobjects, system.authobjects as units
                               WHERE units.id = authobjects.parent;

RECORD ARRAY allrights := GetListOfRights();

//OBJECT user := OpenSystemAuthObject(1);
trans->BeginWork();
OBJECT user := EnsureCreatedMagicSysop("<shelluser>");
trans->CommitWork();

Print("id;name;realname;email;unitid;unitname;right;grantoption;objecttype;objectid;objectname;grantor;grantorname;grantorrealname;grantoremail\n");

FOREVERY(RECORD r FROM allrights)
{
  RECORD ARRAY grants := GetDirectGrantsForRight(r.rightname);
  FOREVERY(RECORD grant FROM grants)
  {
    RECORD grantorrec := SELECT * FROM allobjs WHERE id = grant.grantor;
    IF(NOT RecordExists(grantorrec))
      PrintTo(2,"Grantor #" || grant.grantor || " missing\n");

    RECORD granteerec := SELECT * FROM allobjs WHERE id = grant.grantee;
    IF(NOT RecordExists(granteerec))
      PrintTo(2,"Grantee #" || grant.grantee || " missing\n");

    STRING line := grant.grantee || ";"
          || EncodeExcelCSV(RecordExists(granteerec) ? granteerec.name : "") || ";"
          || EncodeExcelCSV(RecordExists(granteerec) ? granteerec.realname : "") || ";"
          || EncodeExcelCSV(RecordExists(granteerec) ? granteerec.email : "") || ";"
          || (RecordExists(granteerec) ? granteerec.unitid : 0) || ";"
          || EncodeExcelCSV(RecordExists(granteerec) ? granteerec.unitname : "") || ";"
          || EncodeExcelCSV(r.rightname) || ";"
          || EncodeExcelCSV(grant.withgrantoption?"true":"false") || ";"
          ;

    IF(r.isglobal)
    {
      line := line || ";;";
    }
    ELSE
    {
      TRY
      {
        RECORD pathrec := user->GetObjectPath(r.objecttypename, grant."object");
        IF (NOT RecordExists(pathrec) OR NOT pathrec.visible)
          CONTINUE;

        STRING ARRAY pathitems := SELECT AS STRING ARRAY name FROM pathrec.path WHERE id != 0;
        line := line || EncodeExcelCSV(r.objecttypename) || ";";
        line := line || grant."object" || ";";
        line := line || EncodeExcelCSV(Detokenize(pathitems,"/")) || ";";
      }
      CATCH (OBJECT e)
      {
        LogHarescriptException(e);
        PrintTo(2, "Exception looking up object path (type: " || r.objecttypename || "): " || e->what || "\n");
        CONTINUE;
      }
    }
    line := line
          || grant.grantor || ";"
          || EncodeExcelCSV(RecordExists(grantorrec) ? grantorrec.name : "") || ";"
          || EncodeExcelCSV(RecordExists(grantorrec) ? grantorrec.realname : "") || ";"
          || EncodeExcelCSV(RecordExists(grantorrec) ? grantorrec.email : "") || ";";
    Print(line || "\n");
  }
}
