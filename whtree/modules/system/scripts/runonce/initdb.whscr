<?wh
/** Setup WHFS and basic database keys. This script runs in 'afterregistryupdate'
    and blocks WebHare startup so keep it as compact as possible
*/

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::internet/tcpip.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/support.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/backend/backendsite.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::wrd/lib/api.whlib";

MACRO InitializeFileTypes()
{
  //TODO can't we ship with a basic, completely compiled siteprofile and simply execute that one for initial registration ?
  RECORD ARRAY ftypedata := [ [ id := 1, namespace := "http://www.webhare.net/xmlns/publisher/foreignfolder", isfoldertype := TRUE, isfiletype := FALSE ]
                            , [ id := 2, namespace := "http://www.webhare.net/xmlns/publisher/systemfolder",  isfoldertype := TRUE, isfiletype := FALSE ]
                            , [ id := 3, namespace := "http://www.webhare.net/xmlns/publisher/photoalbum",    isfoldertype := TRUE, isfiletype := FALSE ]
                            // , [ id := 4, namespace := "http://www.webhare.net/xmlns/publisher/mswordfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 5, namespace := "http://www.webhare.net/xmlns/publisher/htmlfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 6, namespace := "http://www.webhare.net/xmlns/publisher/javascriptfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 7, namespace := "http://www.webhare.net/xmlns/publisher/semidynamicfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 9, namespace := "http://www.webhare.net/xmlns/publisher/cssfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 10, namespace := "http://www.webhare.net/xmlns/publisher/targzfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 11, namespace := "http://www.webhare.net/xmlns/publisher/pdffile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 12, namespace := "http://www.webhare.net/xmlns/publisher/imagefile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 13, namespace := "http://www.webhare.net/xmlns/publisher/multimediafile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 14, namespace := "http://www.webhare.net/xmlns/publisher/zipfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 15, namespace := "http://www.webhare.net/xmlns/publisher/archivefile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 16, namespace := "http://www.webhare.net/xmlns/publisher/libraryfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 18, namespace := "http://www.webhare.net/xmlns/publisher/externallink",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 19, namespace := "http://www.webhare.net/xmlns/publisher/internallink",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 20, namespace := "http://www.webhare.net/xmlns/publisher/contentlink",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 21, namespace := "http://www.webhare.net/xmlns/publisher/plaintextfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 23, namespace := "http://www.webhare.net/xmlns/publisher/xmlfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 24, namespace := "http://www.webhare.net/xmlns/publisher/contentlisting",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 25, namespace := "http://www.webhare.net/xmlns/publisher/shtmlfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 26, namespace := "http://www.webhare.net/xmlns/publisher/wittyfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 28, namespace := "http://www.webhare.net/xmlns/publisher/templatefile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 35, namespace := "http://www.webhare.net/xmlns/publisher/dynamicfoldercontents",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 36, namespace := "http://www.webhare.net/xmlns/tollium/applicationportal",  isfoldertype := FALSE, isfiletype := TRUE ]
                            //, [ id := 37, namespace := "http://www.webhare.net/xmlns/tollium/frontendapplication",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 38, namespace := "http://www.webhare.net/xmlns/publisher/shtmlwithdesignfile",  isfoldertype := FALSE, isfiletype := TRUE ]
                            , [ id := 40, namespace := "http://www.webhare.net/xmlns/publisher/draftmetadata",  isfoldertype := FALSE, isfiletype := FALSE ]
                            , [ id := 0, namespace := "http://www.webhare.net/xmlns/publisher/sitesettings",  isfoldertype := FALSE, isfiletype := FALSE, members :=
                                [//set up properties needed for siteprofile compiler and backend site
                                 [ name := "sitedesign", type := "string" ]
                                ,[ name := "webfeatures", type := "stringarray" ]
                                ]
                              ]
                            ];

  RECORD ARRAY existing_types := SELECT id, namespace FROM system.fs_types;
  INTEGER ARRAY existing_ids  := SELECT AS INTEGER ARRAY id FROM existing_types;

  FOREVERY(RECORD ftype FROM ftypedata)
  {
    INTEGER currentid := SELECT AS INTEGER id FROM existing_types WHERE namespace = ftype.namespace;
    IF(ftype.id != 0 AND currentid != 0 AND currentid != ftype.id) //the type already exists, but with the wrong id.
    {
      STRING newname := ftype.namespace||"-"||GenerateUFS128BitId();
      PrintTo(2,`Renaming conflicting namespace #${currentid} ${ftype.namespace} to ${newname}\n`);
      UPDATE system.fs_types SET namespace := newname WHERE id = currentid;

      currentid := 0; //moved the old one out of the way, no we can recreate it
    }

    IF(currentid = 0) //create it
    {
      INTEGER newid := ftype.id ?? MakeAutonumber(system.fs_types, "ID");
      INSERT INTO system.fs_types(id, namespace, isfoldertype, isfiletype, cloneoncopy, cloneonarchive) VALUES(newid, ftype.namespace, ftype.isfoldertype, ftype.isfiletype, TRUE, TRUE);
      IF(CellExists(ftype,'members'))
      {
        FOREVERY(RECORD memberrec FROM ftype.members)
          INSERT [ name := memberrec.name, fs_type := newid, type := GetFSMemberTypeID(memberrec.type)] INTO system.fs_members;
      }
      GetWHFSCommitHandler()->FSTypesChanged();
    }
  }
}

RECORD ARRAY folderchecklist:= [ [ id := whconstant_whfsid_repository   //folder #1
                                 , parent := 0
                                 , name := "Repository"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_lostandfound /*6*/
                                 , parent := 1
                                 , name := "lost & found"
                                 , type := whconstant_whfstype_systemfolder
                                 ]
                                 // 9 was reserved for 'examples'
                               , [ id := whconstant_whfsid_private
                                 , parent := 0
                                 , name := "webhare-private"
                                 , type := whconstant_whfstype_systemfolder
                                 ]
                                 //12 was reserved for 'webhare-moduledata'
                               , [ id := whconstant_whfsid_wrdstore
                                 , parent := whconstant_whfsid_private
                                 , name := "wrd"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_private_system
                                 , parent := whconstant_whfsid_private
                                 , name := "system"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_autosaves
                                 , parent := whconstant_whfsid_private_system
                                 , name := "whfs-autosaves"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_drafts
                                 , parent := whconstant_whfsid_private_system
                                 , name := "whfs-drafts"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_webharebackend
                                 , parent := 0
                                 , name := "WebHare backend"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                                 // NOTE 17, "whfs-versionhistory", whconstant_whfsid_versionhistory is reserved
                                 // NOTE 18, "whfs-versionarchive",  whconstant_whfsid_versionarchive is reserved
                               , [ id := whconstant_whfsid_versions
                                 , parent := whconstant_whfsid_private_system
                                 , name := "whfs-versions"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_registerslots
                                 , parent := whconstant_whfsid_private_system
                                 , name := "whfs-register-slots"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_private_rootsettings
                                 , parent := whconstant_whfsid_private_system
                                 , name := "webhare-publisher-rootsettings"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_shorturl
                                 , parent := whconstant_whfsid_private_system
                                 , name := "shorturl"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_whfs
                                 , parent := whconstant_whfsid_private_system
                                 , name := "whfs"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               , [ id := whconstant_whfsid_whfs_snapshots
                                 , parent := whconstant_whfsid_whfs
                                 , name := "snapshots"
                                 , type := whconstant_whfstype_normalfolder
                                 ]
                               ];

IF(GetEnvironmentVariable("WEBHARE_CI") != "") { //precreate /webhare-tests/ or EnsureWHFSTestRoot may race against itself (eg CI scripts versus webhare_testsuites:reset)
  INSERT CELL[ id := whconstant_whfsid_webhare_tests
             , parent := 0
             , name := "webhare-tests"
              , type := whconstant_whfstype_normalfolder
              ] INTO folderchecklist AT END;
}

DATETIME initnow := GetCurrentDatetime();
OBJECT trans := OpenPrimary();

/*
   Initialize/repair the repository and basic filetypes
*/
trans->BeginWork();

InitializeFileTypes();

INTEGER ARRAY createdfolders;

INTEGER rescue_system_folder := SELECT AS INTEGER id FROM system.fs_objects WHERE parent = whconstant_whfsid_private AND name="system" AND id != whconstant_whfsid_private_system;
IF(rescue_system_folder != 0) //we have one to rescue!
  UPDATE system.fs_objects SET name := "system$$$torescue" WHERE id = rescue_system_folder;

FOREVERY(RECORD tocheck FROM folderchecklist)
{
  OBJECT folder := OpenWHFSObject(tocheck.id);
  IF(NOT ObjectExists(folder))
  {
    IF(tocheck.parent!=0 AND NOT ObjectExists(OpenWHFSObject(tocheck.parent)))
      ABORT("Parent #" || tocheck.parent || " for required folder " || tocheck.name || " is unexpectedly missing");

    INSERT INTO system.fs_objects(id,name,creationdate,modificationdate,parent,isfolder,type, ispinned)
           VALUES(tocheck.id,tocheck.name,initnow,initnow,tocheck.parent,TRUE,2, TRUE);

    INSERT tocheck.id INTO createdfolders AT END;
  }
  ELSE IF(tocheck.parent != folder->parent OR tocheck.type != folder->type OR NOT folder->ispinned)
  {
    UPDATE system.fs_objects SET parent := tocheck.parent
                               , type := tocheck.type
                               , ispinned := TRUE
                           WHERE id = tocheck.id;
  }
}

IF(rescue_system_folder != 0) //clear out the old folder
{
  UPDATE system.fs_objects SET parent := whconstant_whfsid_private_system WHERE parent = rescue_system_folder;
  DELETE FROM system.fs_objects WHERE id = rescue_system_folder;
}

// Delete the siteprofile XML version if it still exists
OBJECT oldsiteprofile := OpenWHFSObjectByPath("/webhare-private/publisher/compiled-siteprofiles.xml");
IF(ObjectExists(oldsiteprofile))
  oldsiteprofile->RecycleSelf();

// Ensure we have a repository site too
BOOLEAN isfreshwebhare;
IF(NOT RecordExists(SELECT FROM system.sites WHERE id=1))
{
  isfreshwebhare := TRUE;
  // Initializing the Repository...
  CreateSiteFromFolder(whconstant_whfsid_repository);
}

OBJECT usermgmtschema := OpenWRDSchema("system:usermgmt");
IF(NOT ObjectExists(usermgmtschema))
{
  /* We need to initialize system:usermgmt early on new installations, or GetPrimaryWebhareUserAPI() may break during
    poststart due to no accounttype yet (we used to delay accounttype setting until poststart, racing other poststart scripts) */
  CreateWRDSchema("system:usermgmt", [ initialize := TRUE
                                     , schemaresource := "mod::system/data/wrdschemas/default.wrdschema.xml"
                                     , usermgmt := TRUE
                                     ]);
}

// Ensure submodule folders exist
EnsureModulePrivateFolders();

STRING hostname := ReadRegistryKey("system.global.servername");
IF(hostname = "")
{
  hostname := GetSystemHostName(TRUE);
  WriteRegistryKey("system.global.servername", hostname);
}
IF(ReadRegistryKey("system.global.installationid") = "")
  WriteRegistryKey("system.global.installationid", GenerateUFS128BitId());
IF(ReadRegistryKey("system.global.servertype") = "")
  WriteRegistryKey("system.global.servertype", IsModuleInstalled("webhare_testsuite") ? "development" : "test");

IF(ReadRegistryKey("system.tasks.maintenanceoffset") = 0)
  WriteRegistryKey("system.tasks.maintenanceoffset", Random(1,240));

IF(ReadRegistryKey("system.webserver.security.cachesecret")="")
  WriteRegistryKey("system.webserver.security.cachesecret", GenerateUFS128BitId()||GenerateUFS128BitId());
IF(ReadRegistryKey("system.webserver.security.cookiesecret")="")
  WriteRegistryKey("system.webserver.security.cookiesecret", GenerateUFS128BitId()||GenerateUFS128BitId());
IF(ReadRegistryKey("system.webserver.security.debugsecret")="")
  WriteRegistryKey("system.webserver.security.debugsecret", GenerateUFS128BitId()||GenerateUFS128BitId());
IF(ReadRegistryKey("system.webserver.security.gcmsecret")="")
  WriteRegistryKey("system.webserver.security.gcmsecret", EncodeUFS(DecodeUFS(GenerateUFS128BitId())||DecodeUFS((GenerateUFS128BitId()))));

/* NOT sure if we want to make this envvar official .. using it for freshdbconsole but a YAML file with initial
   (or always applied?) configuration would be more flexible */
STRING initial_backend := GetEnvironmentVariable("WEBHARE_INITIAL_BACKEND")
                          ?? (GetEnvironmentVariable("WEBHARE_CI") != "" ? "http://127.0.0.1/" : "");

IF(initial_backend LIKE "http:*") //no https support (yet? but bothering letsencrypt for CI is not nice)
{
  IF(NOT RecordExists(SELECT FROM system.webservers)
     AND NOT RecordExists(SELECT FROM system.ports)
     AND NOT RecordExists(SELECT FROM system.sites WHERE id = whconstant_whfsid_webharebackend) ) //we are completely new!
  {
    RECORD unpacked := UnpackURL(initial_backend);

    // Setup port and bindings for http://127.0.0.1/
    INTEGER webserverid := MakeAutoNumber(system.webservers,"id");
    INTEGER portid := MakeAutoNumber(system.ports,"id");
    INSERT INTO system.ports(id,ip,port,virtualhost)
           VALUES (portid, "", unpacked.port, unpacked.isdefaultport); //we assume 80/443 should be virtualhosted

    //on a DB with just 1 webserver of interface type, UpdateSystemBackendSite will automatically pick that one
    INSERT INTO system.webservers(id,type,baseurl,outputextension,port)
           VALUES (webserverid, 1, initial_backend, '.html',unpacked.isdefaultport ? 0 : portid);

    Print(`WEBHARE_CI: Configured a webinterface at ${initial_backend}\n`);
    OBJECT backendsite := CreateSiteFromFolder(whconstant_whfsid_webharebackend);
    backendsite->SetPrimaryOutput(webserverid, "/");
  }

  IF (ReadRegistryKey("system.services.smtp.mailfrom") = "") //Ensure mailfrom is set, needed for some tets
    WriteRegistryKey("system.services.smtp.mailfrom", "defaultmailfrom@beta.webhare.net");
}

UpdateSystemBackendSite(); //this will also trigger a site profile (re)compilation! and a CreateAppliedPromise "siteprofilerefs" through UpdateSiteMetadata

trans->CommitWork();
