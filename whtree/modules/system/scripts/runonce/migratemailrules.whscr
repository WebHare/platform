<?wh

LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


TABLE
    < INTEGER "id" NULL := 0
    , STRING "tag"
    , STRING "mask"
    , STRING "sendermask"
    , STRING "serverurl"
    , INTEGER "ordering"
    , STRING "outgoingmailaddress"
    , BOOLEAN "outgoingaddressrewrite"
    , STRING "description"
    , STRING "originmask"
    , INTEGER "ruletype"
    , INTEGER "suggestionfuzz"
    ; KEY id
    > mailqueue_route;

OBJECT trans := OpenPrimary();
IF(trans->ColumnExists("system","mailqueue_route","id"))
{
  mailqueue_route := BindTransactionToTable(trans->id,"system.mailqueue_route");

  trans->BeginWork();
  OBJECT configschema := OpenWRDSchema("system:config");
  INTEGER changeset := configschema->CreateChangeset();
  configschema->^mailroute->DeleteAllEntities();
  configschema->^mailsender->DeleteAllEntities();
  configschema->^mailrecipient->DeleteAllEntities();
  configschema->^mailcorrection->DeleteAllEntities();

  RECORD ARRAY migrateroutes := SELECT * FROM mailqueue_route WHERE tag NOT LIKE "webhare_testsuite:*";

  RECORD ARRAY routes := SELECT * FROM migrateroutes WHERE ruletype = 0 ORDER BY ordering, id;

  //If the *last* route is a 'send everything to default destination' route, just delete it. we no longer require such a route
  IF(Length(routes) >= 1 AND routes[END-1].outgoingmailaddress = "" AND routes[END-1].serverurl = "" AND routes[END-1].originmask IN ["","*"] AND routes[END-1].sendermask IN ["","*"] AND routes[END-1].mask IN ["","*"])
    DELETE FROM routes AT END-1;

  FOREVERY(RECORD route FROM routes)
    configschema->^mailroute->CreateEntity(CELL[ wrd_title := route.description
                                               , wrd_ordering := #route+1
                                               , route.originmask
                                               , sendermasks := ToRecordArray(Tokenize(route.sendermask,' '), 'mask')
                                               , recipientmasks := ToRecordArray(Tokenize(route.mask,' '), 'mask')
                                               , route.serverurl
                                               , rewritemailaddress := route.outgoingmailaddress
                                               , includeoriginaladdress := route.outgoingaddressrewrite
                                               , wrd_tag := ToUpperCase(route.tag)
                                               ], CELL[changeset]);

  RECORD ARRAY sendermasks := SELECT * FROM migrateroutes WHERE ruletype = 1;
  IF(Length(sendermasks) = 0) //not setting a sendermask used to imply: all senders are valid
    sendermasks := [[ mask := "*", description := "" ]];
  FOREVERY(RECORD mask FROM sendermasks)
    configschema->^mailsender->CreateEntity(CELL[ wrd_title := mask.description, mask.mask ], CELL[changeset]);

  RECORD ARRAY recipientmasks := SELECT * FROM migrateroutes WHERE ruletype = 2;
  IF(Length(recipientmasks) = 0)
    recipientmasks := [[ mask := "*", description := "" ]];
  FOREVERY(RECORD mask FROM recipientmasks)
    configschema->^mailrecipient->CreateEntity(CELL[ wrd_title := mask.description, mask.mask ], CELL[changeset]);

  RECORD ARRAY mailcorrections := SELECT * FROM migrateroutes WHERE ruletype IN [3,4] ORDER BY ordering, id;
  FOREVERY(RECORD correction FROM mailcorrections)
      configschema->^mailcorrection->CreateEntity(CELL[ wrd_title := correction.description
                                                      , wrd_ordering := #correction+1
                                                      , masks := ToRecordArray(Tokenize(correction.mask,' '), 'mask')
                                                      , block := correction.ruletype=4
                                                      , correction.suggestionfuzz
                                                      , rewriteto := correction.outgoingmailaddress
                                                      ], CELL[changeset]);

  trans->DropTable("system", "mailqueue_route");
  trans->CommitWork();
}
