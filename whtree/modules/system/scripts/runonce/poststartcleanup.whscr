<?wh
// clean up the old /.system/dl/~ic (And ~ec, ~fc) caches
// Update the timestamp in system/moduledefinition.xml for <runonce tag="poststartcleanup_..." to rerun this script after an update

LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";


//4.29 or earlier: cleanup legacy image cache
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().ephemeralroot || "imgcache");

//4.29: Cleanup legacy publisher designfiles cached on disk
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "publisher.cache");
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "publisher.cd");
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "publisher.ea");
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "publisher.pb");
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "publisher.pd");

IF(GetEnvironmentVariable("WEBHARE_IN_DOCKER") != "")
{
  //4.30: Legacy ephemeral data - now inside the docker
  DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "ephemeral/babelcache");
  DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "ephemeral/compilecache");

  //4.30: Loadreports have been removed (they were a docker-only thing)
  DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "loadreports");
}

//4.30 we've switched to system.uc2 some time ago, we can remove system.uc
DeleteDiskDirectoryRecursive(GetWebHareConfiguration().varroot || "ephemeral/system.uc");

OBJECT trans := OpenPrimary();
trans->BeginWork();

IF(trans->ColumnExists("SYSTEM","AUTHOBJECTS","ID")) //4.28 or earlier
  DELETE FROM system_internal.authobjects WHERE type IN [4,5];

IF(trans->ColumnExists("SYSTEM","FS_OBJECTS","ID"))
{
  OBJECT examplefolder := OpenWHFSObject(9); //4.28 or earlier - cleanup examplefolder
  IF(ObjectExists(examplefolder))
  {
    examplefolder->UpdateMetadata([ispinned:=FALSE]);
    examplefolder->RecycleSelf(); //remove example folder
  }

  //4.29: Cleanup legacy designfiles cached in publisher
  OBJECT legacy_designfiles := OpenWHFSObjectByPath("/webhare-private/publisher/designfiles");
  IF(ObjectExists(legacy_designfiles))
    legacy_designfiles->RecycleSelf();
}

DELETE FROM system.fs_history WHERE type IN [1,2,3]; //"3" was found in actual use up to 2016, to be sure we will just remove 1 t/m 3

/* 4.32 to prevent old standalone TPLs (type 22, http://www.webhare.net/xmlns/publisher/standalonetemplatefile)
   from leaking their code when published as a simple file, we'll convert them to unpublishable WHLIBs */

RECORD ARRAY fix_standalone_templates := SELECT id FROM system.fs_objects WHERE type = 22;
FOREVERY(RECORD tpl FROM fix_standalone_templates)
  OpenWHFSObject(tpl.id)->UpdateMetadata([ type := 16, publish := FALSE ]);

trans->CommitWork();
