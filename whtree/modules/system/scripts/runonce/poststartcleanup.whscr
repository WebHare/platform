<?wh

LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::system/lib/userrights.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


// Reset for 5.0 as 4.35 is a required update, but I'm sure there will be new poststart cleanup updates so we'll keep this script
OpenPrimary();
STRING dataroot := GetWebhareConfiguration().basedataroot;
STRING installationroot := GetWebhareConfiguration().installationroot;

// 5.0.0 - drop manually configured tasks
// 5.1 update to also delete tasks not conforming to the `module.task` pattern
RECORD ARRAY legacytasks := SELECT * FROM system_internal.tasks WHERE tag NOT LIKE "*.*";
IF(Length(legacytasks) > 0)
{
  GetPrimary()->BeginWork();
  Print("Deleting the following legacy tasks:\n");
  DumpValue(legacytasks, [ name := "", format := "boxed" ]);
  DELETE FROM system_internal.tasks WHERE tag NOT LIKE "*.*";
  GetPrimary()->CommitWork();
}

// 5.0.0 - Cleanup stray pid files
DeleteDiskFile(MergePath(dataroot, ".dbserver.pid"));

// 5.1 (for next postartcleanup-tag 'bump') - Cleanup stray pid files, other obsolete experiments
DeleteDiskFile(MergePath(dataroot, "webhare.pid"));
DeleteDiskDirectoryRecursive(MergePath(dataroot, "dev"));
DeleteDiskFile(installationroot || ".webhare-envsettings.sh");
//speculative cleanup - though those dirs may not be empty everywhere yet.. so only clean if already empty
DeleteDiskDirectory(installationroot || "log");
DeleteDiskDirectory(installationroot || "output");
DeleteDiskDirectory(installationroot || "var");
DeleteDiskDirectory(installationroot || "tmp");

//5.2 moved nodejs, delete old version and other basedataroot junk
DeleteDiskDirectoryRecursive(MergePath(dataroot, "nodejs"));

//5.1 moved webhare-config/tsconfig.json, delete old version
IF (RecordExists(GetDiskFileProperties(MergePath(dataroot, "webhare-config/tsconfig.json"))))
{
  DeleteDiskFile(MergePath(dataroot, "webhare-config/tsconfig.json"));
  DeleteDiskDirectory(MergePath(dataroot, "webhare-config"));
}

//5.2 - *really* remove legacy indexmanager data files, 5.0.2 attempted to delete it as a file
DeleteDiskDirectoryRecursive(MergePath(dataroot, "index"));
//Didn't bump cleanup task's tag for this one, this cleanup only affects users who jump from 4.35 to 5.02 immediately
FOREVERY(RECORD failedindex FROM ReadDiskDirectory(dataroot,"oldindex-????????-*"))
  DeleteDiskDirectoryRecursive(failedindex.path);

//5.2 - no longer supported (since earlier WebHares already)
DeleteDiskFile(MergePath(dataroot, "currentserverconfig.xml"));
DeleteDiskFile(MergePath(dataroot, "etc/serverconfig.xml"));

DeleteDiskDirectory(MergePath(dataroot, "etc")); //only delete if really empty.. what else might still be here?

//5.2 - drop migration issue table
IF(GetPrimary()->ColumnExists("webhare_internal" ,"postgresql_migration_issues", "id"))
{
  GetPrimary()->BeginWork();
  GetPrimary()->DropTable("webhare_internal" ,"postgresql_migration_issues");
  GetPrimary()->CommitWork();
}

//5.3 - whuser_systemmessage_snoozes is obsolete
OBJECT usermgmtschema := GetPrimaryWebHareAuthPlugin()->wrdschema;
OBJECT snoozestype := usermgmtschema->GetType("whuser_systemmessage_snoozes");
IF(ObjectExists(snoozestype))
{
  GetPrimary()->BeginWork();
  snoozestype->DeleteSelf();
  GetPrimary()->CommitWork();
}

//5.4 - Drop COMPILED-SITEPROFILES.BIN in WHFS
OBJECT oldsiteprofiles := OpenWHFSObjectByPath("/webhare-private/publisher/compiled-siteprofiles.bin");
IF(ObjectExists(oldsiteprofiles))
{
  GetPrimary()->BeginWork();
  oldsiteprofiles->DeleteSelf();
  GetPrimary()->CommitWork();
}

//5.5 Drop publisher feedback schema unless not empty
OBJECT feedbackschema := OpenWRDSchema("publisher:feedback");
IF(ObjectExists(feedbackschema))
{
  RECORD ARRAY issues := feedbackschema->^feedback->RunQuery(
    [ outputcolumns := CELL[ "wrd_id" ]
    ]
  );
  IF(Length(issues) = 0)
  {
    GetPrimary()->BeginWork();
    feedbackschema->DeleteSelf();
    GetPrimary()->CommitWork();
  }
}

//5.7 Old generated folder, shrinkwrap is gone
DeleteDiskDirectoryRecursive(MergePath(installationroot, "modules/system/js/internal/generated"));
DeleteDiskFile(MergePath(installationroot, "modules/system/whres/buildinfo"));
DeleteDiskFile(MergePath(dataroot, "system.last-shrinkwrap-var"));

//5.7 Obsolete assetpack state
DeleteDiskDirectoryRecursive(MergePath(dataroot, "storage/platform/assetpacks"));

//5.7 Obsolete eslintrc.json
DeleteDiskFile(MergePath(dataroot, ".eslintrc.json"));

//5.9 Delete visible version of cli-autocomplete.sock
DeleteDiskFile(MergePath(dataroot, "cli-autocomplete.sock"));
