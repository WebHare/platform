<?wh

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/userrights.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/webserver/auth.whlib";

LOADLIB "mod::publisher/lib/siteprofiles.whlib";


BOOLEAN enable_logging := FALSE;
OBJECT trans;

TRY
{
  trans := OpenPrimary();
}
CATCH(OBJECT e)
{
  AddHTTPHeader("Status","500",FALSE);
  SendWebfile(GetWebhareResource("mod::system/web/errors/nodatabase.html"));
}

trans->BeginWork();

/** Returns the authentication plugin to use to get the user
*/
OBJECT FUNCTION GetAuthPlugin(RECORD props)
{
  // Get last set applysource in the list of access rules. This determines the file of which the URL
  // is used to get the wrdauth plugin.
  INTEGER applysource :=
      SELECT AS INTEGER data.applysource
        FROM GetWebHareAccessRules() AS rules
       WHERE data.applysource != 0
    ORDER BY #rules DESC;

  // Database rule, use user API.
  IF (applysource != 0)
  {
    // Get the web context of the applysource and the plugin for that context
    OBJECT webcontext := GetWebContext(applysource);
    OBJECT authplugin := webcontext->GetPlugin("http://www.webhare.net/xmlns/wrd", "wrdauth");

    IF (ObjectExists(authplugin))
    {
      IF (enable_logging)
        LogDebug("system:backend:auth", "Use plugin from applysource " || applysource);

      RETURN authplugin;
    }
  }

  // Fallback: the primary webhare plugin
  OBJECT authplugin := GetPrimaryWebhareAuthPlugin();

  IF (enable_logging)
    LogDebug("system:backend:auth", "Use primary webhare plugin");

  RETURN authplugin;
}

RECORD rule := GetCurrentWebhareAccessRule();
IF (rule.data.authtype = 0 AND NOT rule.data.authlist)
  ABORT("This script should not have been called, no authentication is required");

IF (enable_logging)
  LogDebug("system:backend:auth", "Invocating script for authentication rule " || rule.id, rule);

IF (IsFullAccessIP())
{
  IF (enable_logging)
    LogDebug("system:backend:auth", "Allow access for full access ip " || GetClientRemoteIP());

  RETURN;
}

OBJECT authplugin := GetAuthPlugin(rule);

IF (enable_logging)
  LogDebug("system:backend:auth", "Handle login");

// If Basic auth is specified,we'll allow it
RECORD authheader := GetParsedAuthenticationHeader();
IF(authheader.type = "BASIC")
{
  RECORD match := SELECT * FROM system.access_externalusers WHERE ToUppercase(username) = ToUppercase(TrimWhitespace(authheader.username));
  IF(RecordExists(match) AND TrimWhitespace(authheader.password) = match.userpassword)
  {
    //Authenticate for direct access
    AcceptBasicAuthCredentials(match.username, 0);
    RETURN;
  }
}

// Handle login, determine current user
RECORD res := authplugin->__HandleAuthScriptLogin(
    [ use_list :=       rule.data.authlist ? rule.id : 0
    , use_wrdauth :=    rule.data.authtype != 0
    , rulepath :=       rule.data.path
    ]);

IF (enable_logging)
  LogDebug("system:backend:auth", "Login ok, type: " || res.type);

BOOLEAN checked;
SWITCH (res.type)
{
  CASE "wrdauth"
  {
    IF (enable_logging)
      LogDebug("system:backend:auth", "Res: ", res.sessionid, res.user->entityid, res.backurl);

    // Have res.sessionid, res.user, res.backurl

    // Authtype 2 (users with rights on url)
    checked := rule.data.authtype != 2 OR res.user->HasRightOn("system:urlaccess", rule.id);
  }
  CASE "external"
  {
    checked := RecordExists(
        SELECT
          FROM system.access_externalusers
         WHERE username = res.username);
  }
}

IF (checked)
{
  IF (enable_logging)
    LogDebug("system:backend:auth", "success, authenticating web session");

  authplugin->__AuthenticateWebSession(res);

  IF (res.backurl != "")
    Redirect(res.backurl);
}
ELSE
{
  AddHTTPHeader("Status", "403", FALSE);
}

trans->CommitWork();
