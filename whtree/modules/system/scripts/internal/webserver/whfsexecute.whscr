<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/harescript/preload.whlib";
LOADLIB "mod::system/lib/internal/webserver/support.whlib";

LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/webcontext.whlib";
LOADLIB "mod::publisher/lib/internal/pagerendering.whlib";

OBJECT trans;
RECORD rpc_result;
OBJECT rpc_witty;
INTEGER currentfileid;

BOOLEAN FUNCTION AllowedToCache(RECORD rec)
{
  IF(GetWebHeader("User-agent") LIKE "Consilio/*")
    RETURN FALSE;

  IF(rec.cachettl <= 0)
    RETURN FALSE;
  IF(GetRequestMethod() NOT IN ["HEAD","GET"])
    RETURN FALSE;

  FOREVERY(STRING cookie FROM rec.cacheblacklistcookies)
    IF(GetWebCookie(cookie)!="")
      RETURN FALSE;
  FOREVERY(STRING webvar FROM rec.cacheblacklistvariables)
    IF(GetWebVariable(webvar)!="")
      RETURN FALSE;

  RETURN TRUE;
}

STRING FUNCTION WebListToString(RECORD ARRAY inlist, STRING ARRAY whitelist)
{
  IF(Length(whitelist)=0)
    RETURN "";
  BOOLEAN hashall := Length(whitelist) = 1 AND whitelist[0]="*";

  RETURN Detokenize( (SELECT AS STRING ARRAY ToUppercase(name) || "\t" || value
                        FROM inlist
                       WHERE (hashall ? TRUE : ToUppercase(name) IN whitelist)
                    ORDER BY ToUppercase(name),value),"\t");
}

STRING FUNCTION CalculateCacheKey(INTEGER fileid, RECORD rec, STRING suburl)
{
  STRING hashbase := fileid
                     || "\n" || WebListToString(GetAllWebVariables(), rec.cachewebvariables)
                     || "\n" || WebListToString(GetAllWebCookies(), rec.cachewebcookies)
                     || "\n" || suburl;

  RETURN hashbase;
}

RECORD FUNCTION GetCacheableFileData(INTEGER fileid)
{
  IF(HavePrimaryTransaction())
    ABORT("A primary transaction is already open - you may have to update the access rule and disable transactions");

  trans := OpenPrimary();
  RECORD info := GetSystemRedirectInfo(fileid);
  IF(NOT info.success)
  {
    LogError("system:redirect", "GetCacheableFileData", info);
    AbortWithHTTPError(404, "Not found - See notice log for details");
  }

  // Invalidate on recompilation of siteprofiles, or on any modification of the (parent)folder
  STRING ARRAY eventmasks := [ "publisher:internal.siteprofiles.recompiled" ];
  FOREVERY(INTEGER folderid FROM info.folders)
    INSERT "system:whfs.folder." || folderid INTO eventmasks AT END;

  RETURN
      [ ttl :=        5 * 60 * 1000
      , value :=      info
      , eventmasks := eventmasks
      ];
}

RECORD FUNCTION GenerateCacheablePage(RECORD redirectinfo) //FIXME catch sendwebfile, redirect, headers etc
{
  INTEGER str := CreateStream();
  INTEGER saveout := RedirectOutputTo(str);
  IF(NOT ObjectExists(trans)) //Not opened yet by the cache, so get it ourselves
    trans := OpenPrimary();

  OBJECT pageobject := RunThePage(currentfileid, redirectinfo, currentfileid);
  BLOB output := MakeBlobFromSTream(str);
  RedirectOutputTo(saveout);

  RECORD cachesettings;
  IF(ObjectExists(pageobject))
    cachesettings := pageobject->GetDynamicPageCacheSettings();
  IF(NOT RecordExists(cachesettings))
    cachesettings := [ ttl := redirectinfo.cachettl * 1000 ];

  RETURN CELL[ ...cachesettings
             , value := [ data := output
                        , headers := sendhttpheaders
                        ]
             ];
}

///////////////////////////////////////////////////////////////////////////
//
// Main code
//
PUBLIC MACRO __Dummy() //exists only to make systemredirect.whlib happy
{

}

currentfileid := GetAuthenticationRecord().whfsexecute;
__currentwhfsscriptid := currentfileid;

// Get the library data for the library
RECORD rec := GetAdhocCached([ fileid := currentfileid ], PTR GetCacheableFileData(currentfileid));
IF (NOT RecordExists(rec)) // FIXME: nicer error pages?
  THROW NEW Exception("Could not locate type for fs object #" || currentfileid);

__pageinfo := __SplitBaseAndSubUrl(GetClientRequestURL(), rec.objurlpath);

IF(AllowedToCache(rec))
{
  //ADDME Can we add the whlib modification itself to the cachekey? should we?
  STRING cachekey := CalculateCacheKey(currentfileid, rec, __pageinfo.subpath);
  RECORD page := GetAdhocCached([ cachekey := cachekey ]
                                , PTR GenerateCacheablePage(rec)
                                );

  DELETE FROM sendhttpheaders; //delete current headers (if cache miss) before readding
  FOREVERY(RECORD header FROM page.headers)
    AddHTTPHeader(header.header, header.data, header.always_add);
  SendWebFile(page.data);
}

IF(NOT ObjectExists(trans)) //Not opened yet by the cache, so get it ourselves
  trans := OpenPrimary();
RunThePage(currentfileid, rec, currentfileid);

