<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/internal/resources.whlib";


STATIC OBJECTTYPE BridgeConnection
<
  RECORD ARRAY myjobs;

  MACRO NEW()
  {
  }

  PUBLIC ASYNC FUNCTION invokeAnyFunction(STRING funcname, VARIANT ARRAY args, RECORD options)
  {
    RETURN AWAIT AsyncCallFunctionFromJob("mod::system/lib/internal/modules/bridgesupport.whlib#InvokeInsideJob", funcname, args, options);
  }

  PUBLIC STRING FUNCTION OpenHSVM()
  {
    //FIXME Detect link termination and destroy associated jobs
    RECORD jobrecord := CreateJob(Resolve("thebridge_hsvm.whscr"));
    IF(Length(jobrecord.errors) > 0)
      THROW NEW Exception(`Failed to start HSVM job: ${jobrecord.errors[0].message}`);

    jobrecord.job->AsyncWaitRead(MAX_DATETIME)->Then(PTR this->CheckJobErrors(jobrecord.job));
    jobrecord.job->Start();
    INSERT jobrecord INTO this->myjobs AT END;

    RETURN "system:bridge_hsmv_" || jobrecord.job->groupid;
  }

  MACRO CheckJobErrors(OBJECT job)
  {
    RECORD ARRAY errors := job->GetErrors();
    IF(Length(errors) > 0)
    {
      Print(FormatHarescriptErrors(errors));
      LogHarescriptException(NEW HarescriptErrorException(errors));
    }
    job->Close();
    DELETE FROM this->myjobs WHERE myjobs.job = VAR job;
  }

  PUBLIC RECORD FUNCTION GetConfig()
  {
    //Greet it
    RECORD config := GetWebhareConfiguration();
    RECORD moduleroots;
    FOREVERY(STRING module FROM GetInstalledModuleNames())
      moduleroots := CellInsert(moduleroots, module, [ root := GetModuleInstallationRoot(module)]);

    RECORD configpacket :=
      [ installationroot := config.installationroot
      , dataroot := config.varroot
      , backendurl := GetPrimaryWebhareInterfaceURL()
      , module := moduleroots
      ];

    RETURN configpacket;
  }
>;

OBJECT FUNCTION Constructor()
{
  RETURN NEW BridgeConnection;
}

BOOLEAN debug := PickFirst(GetConsoleArguments()) = "--debug";

OpenPrimary();

//TODO it'd be better if we supported codeupdates, and had a way to detach when updated and send new connections a new version of the service. probably requires servicemanager to be able to 'start when requested'
RunWebHareService("system:thebridge", PTR Constructor, [ autorestart := debug ]);
