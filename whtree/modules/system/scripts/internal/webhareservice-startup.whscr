<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/restart_reset.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/internal/whconfig.whlib";
LOADLIB "mod::system/lib/internal/webserver/output-mapping.whlib";

RECORD settings := ParseArguments(GetConsoleArguments()
                                 , [ [ name := "debug", type := "switch" ]
                                   , [ name := "profile", type := "switch" ]
                                   ]);
OBJECT trans;

SetConsoleLineBuffered(FALSE);

IF(NOT RecordExists(settings))
{
  Print("invalid syntax\n");
  SetConsoleExitCode(1);
  RETURN;
}

BOOLEAN showinfo;
IF(settings.debug OR IsDebugTagEnabled("startup"))
  showinfo := TRUE;

IF(showinfo)
  Print("Waiting for the database to come online\n");

WHILE(TRUE)
{
  TRY
  {
    trans := OpenPrimary([waituntil := AddTimeToDate(60*1000, GetCurrentDatetime())]);
    IF(ObjectExists(trans))
      BREAK;
  }
  CATCH(OBJECT e)
  {
    Print("Database server is unavailable: " || e->what || "\n");
  }
  Sleep(5000); //try again in 5 seconds
}

IF(showinfo)
  Print("We have a database!\n");

DATETIME start := GetCurrentDatetime();
SetWHServiceState("startup", [ start := start ]);

// WH5.3 prestart fix: move system.webharebackend to path under output/
STRING old_webharebackend_dir := MergePath(GetEnvironmentVariable("WEBHARE_DATAROOT"), "system.webharebackend");
IF(RecordExists(GetDiskFileProperties(old_webharebackend_dir)))
{
  DeleteDiskDirectoryRecursive(GetWebHareBackendServerFolder()); //delete any data in place. this may happen on dev servers if they updated WebHare without restarting
  MoveDiskPath(old_webharebackend_dir, GetWebHareBackendServerFolder());
}

//Continu with the softreset path
RunRestartReset(showinfo, FALSE);

FOREVERY(STRING err FROM GetModuleDependencyIssues())
  Print(err||"\n");

SetWHServiceState("started", [ start := start, finished := GetCurrentDatetime() ]);
