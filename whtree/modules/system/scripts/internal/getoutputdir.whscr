<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::system/lib/internal/webserver/config.whlib";

OpenPrimary();

STRING url := GetConsoleArguments()[0];
RECORD res := LookupPublisherURL(url);
IF(res.folder=0)
{
  Print("Did not find url: "|| url || "\n");
  SetConsoleExitCode(1);
  RETURN;
}

RECORD site := SELECT * FROM system.sites WHERE id = res.site;
STRING outputpath := GetWebserverOutputFolder(site.outputweb);
IF(outputpath="")
{
  PrintTo(2,"The site '" || site.name || "' containing folder #" || res.folder || " is not being published\n");
  SetConsoleExitCode(1);
  RETURN;
}

outputpath := MergePath(outputpath, site.outputfolder);
outputpath := MergePath(outputpath, (SELECT AS STRING fullpath FROM system.fs_objects WHERE id = res.folder));

PrintTo(2,"Going to site '" || site.name || "' folder #" || res.folder || "\n");
Print(outputpath || '\n');
