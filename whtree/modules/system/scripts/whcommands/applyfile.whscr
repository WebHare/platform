<?wh
// TODO deprecate us with a keyword based 'wh apply'

LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/moduleimexport.whlib";
LOADLIB "mod::system/lib/validation.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";


RECORD args := ParseArguments(GetConsoleArguments(),
    [ [ name := "filename", type := "param", required := TRUE ]
    ]);
IF(NOT RecordExists(args))
{
  Print("Syntax: wh apply <filename>\n");
  TerminateScriptWithError("Syntax error");
}

STRING path := args.filename;
IF(NOT IsAbsoluteResourcePath(path))
  path := ResolveToAbsolutePath(path);

STRING resname := GetResourceNameFromDiskPath(path);
IF(resname NOT LIKE "mod::*")
  THROW NEW Exception(`Path ${path} not mapped back to a module`);

// This may be a new module activating, update MODULES so whcompile has the new module paths.
IF(resname LIKE "*/moduledefinition.xml")
{
  ReloadWebhareConfig2("MODULES");
  //TODO: ReloadWebhareConfig2 uses a softreset broadcast path (whmanager) but compilation is a direct http call to the
  //whcompile process so we may race! delay to help us win the race but ideally we'd not use two separate paths
  Sleep(200);
}

RECORD validateres := ValidateSingleFile(resname);
IF(Length(validateres.errors)>0)
{
  FOREVERY(RECORD err FROM validateres.errors)
    PrintTo(2,GetWebHareResourceDiskPath(err.resourcename) || ":" || err.line || ":" || err.col || ": " || err.message || "\n");
  TerminateScriptWithError(`Errors validating ${path}`);
}

IF(resname LIKE "*/moduledefinition.xml")
{
  OpenPrimary();
  STRING mod := GetModuleNameFromResourcePath(resname);
  RECORD res := ActivateInstalledModule(mod, GetModuleInstallationRoot(mod));
  FOREVERY(STRING warning FROM res.warnings)
    Print(`- ${warning}\n`);
  RETURN;
}

TerminateScriptWithError("Unrecognized file type");
