<?wh
// short: Manage tasks

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::devsupport.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::system/lib/tasks.whlib";
LOADLIB "mod::system/lib/internal/tasks.whlib";
LOADLIB "mod::system/lib/internal/tasks/execution.whlib";
LOADLIB "mod::system/lib/internal/tasks/queuemgr.whlib";


MACRO ListQueues(STRING ARRAY params)
{
  IF(Length(params) > 0) //none supported yet
  {
    Print("Syntax: wh tasks listqueues\n");
    TerminateScriptWithError("");
  }

  RECORD ARRAY queuedirs := SELECT * FROM ReadDiskDirectory(GetModuleStorageRoot("system") || "persistentqueues/","*") ORDER BY name;
  FOREVERY(RECORD queuedir FROM queuedirs)
  {
    FOREVERY(RECORD queue FROM SELECT * FROM ReadDiskDirectory(queuedir.path,"*") ORDER BY name)
    {
      Print(`${queuedir.name}:${queue.name}\n`);
    }
  }
}


MACRO ImportTask(STRING ARRAY params)
{
  RECORD subargs := ParseArguments(params,
      [ [ name := "path", type := "param", required := TRUE ]
      ]);

  IF(NOT RecordExists(subargs))
  {
    Print("Syntax: wh tasks import <path>\n");
    TerminateScriptWithError("");
  }

  BLOB input := subargs.path = "-" ? GetSTDINAsBlob() : GetDiskResource(subargs.path);
  BOOLEAN ishson := BlobToString(input,5) = "hson:"; //Pre WH5.7 task format
  RECORD task := ishson ? DecodeHSONBlob(input) : DecodeJSONBlob(input);

  task := EnforceStructure([__taskmeta := [ type := "" ]], task);
  IF(task.__taskmeta.type != "managedtask")
    THROW NEW Exception(`Unrecognized task type '${task.__taskmeta.type}`);

  //DumpValue(task);
  GetPrimary()->BeginWork();
  INTEGER taskid;
  IF(ishson)
  {
    taskid := ScheduleManagedTask(task.tasktype, task.taskdata, [ auxdata := task.auxdata ]);
  }
  ELSE
  {
    //This is raw db data. Can't use ScheduleManagedTask as that would break JSON data
    BLOB auxdata := StringToBlob(DecodeBase64(task.auxdata));
    RECORD taskinfo := GetTaskTypeSettings(task.tasktype, FALSE);
    taskid := MakeAutonumber(system_internal.managedtasks, "id");
    INSERT [ id := taskid
           , creationdate := GetCurrentDateTime()
           , nextattempt := GetCurrentDateTime()
           , tasktype := task.tasktype
           , taskdata := task.taskdata
           , auxdata := auxdata
           , priority := taskinfo.priority
           , timeout := taskinfo.timeout
           ] INTO system.managedtasks;

    GetPrimary()->BroadcastOnCommit("system:managedtasks.any.new", DEFAULT RECORD);
    GetPrimary()->BroadcastOnCommit("system:managedtasks." || task.tasktype || ".new", DEFAULT RECORD);
  }
  GetPrimary()->CommitWork();
  Print(`Created task 'm${taskid}'\n`);
}

MACRO ExportTask(STRING ARRAY params)
{
  RECORD subargs := ParseArguments(params,
      [ [ name := "taskid", type := "param", required := TRUE ]
      , [ name := "path", type := "param", required := TRUE ]
      ]);

  IF(NOT RecordExists(subargs))
  {
    Print("Syntax: wh tasks export <taskid> <path>\n");
    TerminateScriptWithError("");
  }

  RECORD taskdata;
  IF(subargs.taskid LIKE "m*")
  {
    INTEGER gettaskid := ToInteger(Substring(subargs.taskid,1),0);
    RECORD task := SELECT * FROM system_internal.managedtasks WHERE id = gettaskid;
    IF(NOT RecordExists(task))
      THROW NEW Exception(`No such managed task #${gettaskid}`);

    taskdata := CELL[ task.tasktype
                    , taskdata := task.taskdata
                    , auxdata := EncodeBase64(BlobToString(task.auxdata))
                    , __taskmeta := [ type := "managedtask"
                                    , encoded := FALSE
                                    , webhare := GetWebhareVersionNumber()
                                    , source := GetServerName()
                                    , exported := GetCurrentDatetime()
                                    , id := "m" || task.id
                                    ]
                    ];
  }
  ELSE
  {
    THROW NEW Exception(`Task id '${subargs.taskid}' is not a valid task id`);
  }

  IF(subargs.path = "-")
  {
    Print(EncodeJSON(taskdata) || "\n");
  }
  ELSE
  {
    StoreDiskFile(subargs.path, EncodeJSONBlob(taskdata));
  }
}

MACRO RunTask(STRING ARRAY params)
{
  RECORD subargs := ParseArguments(params,
      [ [ name := "taskids", type := "paramlist", required := TRUE ]
      ]);

  IF(NOT RecordExists(subargs))
  {
    Print("Syntax: wh tasks run <taskid>\n");
    TerminateScriptWithError("");
  }

  FOREVERY(STRING taskid FROM subargs.taskids)
    IF(taskid LIKE "m*")
    {
      RECORD taskdata := SELECT *, isephemeral := FALSE, queueid := "db" || Substring(taskid,1)
                            FROM system.managedtasks
                            WHERE id = ToInteger(Substring(taskid,1),0);

      IF(NOT RecordExists(taskdata))
        THROW NEW Exception(`No such task '${taskid}'`);

      RECORD taskinfo := SELECT * FROM GetAllTaskTypeInfo().tasktypes WHERE ephemeral = FALSE AND tasktype = taskdata.tasktype;
      IF(NOT RecordExists(taskinfo))
        THROW NEW Exception(`No definition for tasks of type '${taskdata.tasktype}'`);
      IF (taskinfo.applyerror != "")
        THROW NEW Exception(`Task type '${taskdata.tasktype}' is not enabled on this installation: ${taskinfo.applyerror}`);

      RECORD executeinfo := GetTaskExecuteInfo(taskdata, taskinfo);

      RECORD res := ExecuteManagedTask(executeinfo, TRUE);//<-- debug. make option?
      dumpvalue(res, [ name := "taskresult", format := "tree" ]);
    }
    ELSE
    {
      THROW NEW Exception(`Task id '${taskid}' is not a valid task id`);
    }
  }

MACRO Main()
{
  RECORD ARRAY options := [ [ name := "command", type := "param", required := TRUE ]
                          , [ name := "params", type := "paramlist" ]
                          ];

  RECORD cmdargs := ParseArguments(GetConsoleArguments(), options);

  SWITCH(RecordExists(cmdargs) ? cmdargs.command : "help")
  {
    CASE "listqueues"
    {
      ListQueues(cmdargs.params);
    }
    CASE "export"
    {
      ExportTask(cmdargs.params);
    }
    CASE "import"
    {
      importTask(cmdargs.params);
    }
    CASE "run"
    {
      RunTask(cmdargs.params);
    }
    DEFAULT
    {
      Print("Syntax: wh tasks <command>\n");
      Print("  listqueues:             List task queues\n");
      Print("  run <taskid>:           Run a task\n");
      Print("  export <taskid> <path>: Export the specified task\n");
      Print("  import <path>:          Import an exported task\n");
      TerminateScriptWithError("");
    }
  }
}

OpenPrimary();
Main();
