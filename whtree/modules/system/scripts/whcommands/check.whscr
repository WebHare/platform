<?wh
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/checks.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::system/lib/internal/checks/checker.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";

Print(GetWHVersionInfo().versionname);

OBJECT db;
TRY db := OpenPrimary();
CATCH(OBJECT e)
{
  PrintTo(2," - The database is unavailable: " || e->what||"\n");
  SetConsoleExitCode(2);
  RETURN;
}

RECORD ARRAY messages := GetCurrentCheckIssues();

INTEGER num_snoozed := LENGTH(SELECT FROM messages WHERE snoozed);
DELETE FROM messages WHERE snoozed;

IF(Length(messages)>0)
  SetConsoleExitCode(1);

IF(Length(messages)>1)
{
  Print(" - " || Length(messages) || " issues!");
  IF (num_snoozed != 0)
    PRINT(` (and ${num_snoozed} snoozed ${num_snoozed=1?"message":"messages"})\n`);
  ELSE
    PRINT("\n");
  FOREVERY(RECORD msg FROM messages)
    Print(msg.message_text || "\n");
}
ELSE IF(Length(messages)=1)
{
  Print(" - " || messages[0].message_text);
  IF (num_snoozed != 0)
    PRINT(` (and ${num_snoozed} snoozed ${num_snoozed=1?"message":"messages"})\n`);
  ELSE
    PRINT("\n");
}
ELSE
{
  Print(" - all ok!");
  IF (num_snoozed != 0)
    PRINT(` (${num_snoozed} snoozed ${num_snoozed=1?"message":"messages"})\n`);
  ELSE
    PRINT("\n");
}
