<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::dbase/whdb.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/moduleimexport.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/internal/whconfig.whlib";
LOADLIB "mod::system/lib/validation.whlib";
LOADLIB "mod::system/lib/internal/backend/users.whlib";
LOADLIB "mod::system/lib/internal/dbase/base.whlib";
LOADLIB "mod::system/lib/internal/dbase/postgresql.whlib";

LOADLIB "mod::wrd/lib/internal/metadata/moduledefs.whlib";
LOADLIB "mod::wrd/lib/internal/metadata/applyupdates.whlib";


// command: database <command>
// short: database control


MACRO ShowSyntax(STRING error)
{
  PRINT(`Error: ${error}\n`);
  PRINT("Syntax: wh db <command>\n");
  PRINT("Commands:\n");
  PRINT("  transactions             Show transaction list\n");
  PRINT("  readwritestatus          Get/set read-write status (read-write / read-only)\n");
  TerminateScriptWithError("");
}

RECORD args := ParseArguments(GetConsoleArguments(),
    [ [ name := "command", type := "param", required := TRUE ]
    , [ name := "args", type := "paramlist" ]
    ]);

IF (NOT RecordExists(args))
  ShowSyntax("Illegal syntax");

OBJECT trans := OpenPrimary();

SWITCH (args.command)
{
  CASE "transactions"
  {
    RECORD subargs := ParseArguments(args.args,
        [ [ name := "allinfo", type := "switch" ]
        ]);

    IF (NOT RecordExists(subargs))
      ShowSyntax(`Illegal syntax for command '{${args.command}'`);
    args := CELL[ ...args, ...subargs ];

    IF (GetDatabaseType() = "whdb")
    {
      trans->BeginWork();
      PRINT(AnyToString(__SendWHDBCommand(trans->id,'show transactions'),'boxed'));
      RETURN;
    }

    RECORD ARRAY transactions := trans->__ExecSQL("select * FROM pg_stat_activity");
    IF (args.allinfo)
      PRINT(AnyToString(transactions, "tree"));
    ELSE
    {
      PRINT(AnyToString(
         (SELECT pid
               , application_name
               , backend_start
               , state
           FROM transactions
          WHERE backend_type = "client backend"
       ORDER BY backend_start), "boxed"));
    }
    PRINT(`Total: ${LENGTH(transactions)} connections\n`);
  }
  CASE "locks"
  {
    RECORD subargs := ParseArguments(args.args,
        [ [ name := "allinfo", type := "switch" ]
        ]);

    IF (NOT RecordExists(subargs))
      ShowSyntax(`Illegal syntax for command '{${args.command}'`);
    args := CELL[ ...args, ...subargs ];

    IF (GetDatabaseType() = "whdb")
    {
      PRINT(`Not supported for database type whdb\n`);
      RETURN;
    }

    RECORD monitorinfo := GetDatabaseMonitorInfo(GetPrimary());
    FOREVERY (RECORD owner FROM monitorinfo.blockinglocks)
    {
      PRINT(`Owner: ${owner.application_name}\n`);
      PRINT(` Transaction start: ${FormatISO8601DateTime(owner.xact_start, "", "milliseconds", "CET")}\n`);
      PRINT(` Query start: ${FormatISO8601DateTime(owner.query_start, "", "milliseconds", "CET")}\n`);
      PRINT(` PostgreSQL backend pid: ${owner.pid}\n`);
      IF (owner.debuginfohint != "")
        PRINT(` Debug info: ` || Substitute(owner.debuginfohint, "\n", "\n" || RepeatText(" ", 13)) || "\n");
      FOREVERY (RECORD waiter FROM owner.waiters)
      {
        PRINT(`  Waiter: ${waiter.application_name}\n`);
        PRINT(`   Transaction start: ${FormatISO8601DateTime(waiter.xact_start, "", "milliseconds", "CET")}\n`);
        PRINT(`   Query start: ${FormatISO8601DateTime(waiter.query_start, "", "milliseconds", "CET")}\n`);
        PRINT(`   PostgreSQL backend pid: ${waiter.pid}\n`);
        IF (waiter.tables != "")
          PRINT(`    Locking row in table ${waiter.tables}\n`);
        IF (waiter.debuginfohint != "")
          PRINT(`   Debug info: ` || Substitute(owner.debuginfohint, "\n", "\n" || RepeatText(" ", 15)) || "\n");
      }
    }
  }

  CASE "readwritestatus"
  {
    RECORD subargs := ParseArguments(args.args,
        [ [ name := "y", type := "switch" ]
        , [ name := "newvalue", type := "param" ]
        ]);

    IF (NOT RecordExists(subargs))
      ShowSyntax(`Illegal syntax for command '{${args.command}'`);
    args := CELL[ ...args, ...subargs ];

    IF (args.newvalue NOT IN [ "", "read-only", "read-write" ])
      TerminateScriptWithError("Invalid new value, expected one of 'read-only'/'read-write'");

    IF (args.newvalue != "")
    {
      STRING envsettings := BlobToString(GetDiskResource(MergePath(GetWebhareConfiguration().varroot, ".webhare-envsettings.sh"), [ allowmissing := TRUE ]));
      IF (envsettings LIKE `* WEBHARE_DBASE_READONLY=*` AND envsettings NOT LIKE `* WEBHARE_DBASE_READONLY=""\n`)
        TerminateScriptWithError("The database readonly settings are set globally by 'wh console' and cannot be changed.");

      IF (GetEnvironmentVariable("WEBHARE_DBASE_READONLY") != "")
        Print("Warning: The WEBHARE_DBASE_READONLY environment variable is currently set, the configuration change will only have effect on processes that don't have it set.\n");

      SWITCH (args.newvalue)
      {
        CASE "read-only"
        {
          IF (NOT args.y)
          {
            PRINT("Are you sure you want to switch the database of this WebHare to read-only? Enter 'yes' to proceed. ");
            IF (ReadLineFrom(0, 4096, TRUE) != "yes")
              TerminateScript();
          }

          RECORD dbaseconfig := GetDatabaseRuntimeConfig();
          UpdateSystemConfigurationRecord(CELL[ database := CELL[ ...dbaseconfig, readonly := TRUE ] ]);
          ReloadWebhareConfig2("ALL");
        }
        CASE "read-write"
        {
          RECORD dbaseconfig := GetDatabaseRuntimeConfig();
          UpdateSystemConfigurationRecord(CELL[ database := CELL[ ...dbaseconfig, readonly := FALSE ] ]);
          ReloadWebhareConfig2("ALL");
        }
      }
    }

    PRINT(`Current database status: ${IsDatabaseReadonly() ? "read-only" : "read-write"}\n`);
  }
  DEFAULT
  {
    ShowSyntax(`Unknown command '${args.command}'`);
  }
}
