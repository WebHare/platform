<?wh (*WASMENGINE*)
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/internal/whconfig.whlib";
LOADLIB "mod::system/lib/internal/resources.whlib";

IF(NOT IsWasm()) //checknoerrors is used during CI, so here's our "test" for now
  ABORT("We said '(*" || "WASMENGINE" || "*)' so why aren't we running under WASM?");

/* wh run mod::system/scripts/debug/checknoerrors.whscr
*/

RECORD startupdata := EnforceStructure([ start := DEFAULT DATETIME], PollWHServiceState("startup"));
DATETIME boottime := startupdata.start;
RECORD ARRAY errors := ReadJSONLogLines("system:notice", boottime);
IF(Length(errors) > 0)
{
  SetConsoleExitCode(1);
  FOREVERY(RECORD err FROM errors)
  {
    //buildlogs filter out empty lines somewhere... so prefixing every error also makes it easier to see where the next one starts
    Print(`*** There are errors in the notice log. ${#err+1}/${Length(errors)}\n`);
    IF(CellExists(err,'type') AND err.type IN [ "script-error", "script-unhandledrejection" ])
    {
      IF(CellExists(err,'errors'))
      {
        FOREVERY(RECORD error FROM err.errors)
          Print(`${error.filename}:${error.line}:${error."column"}: ${error.message}\n`);
      }
      ELSE IF(CellExists(err,'message'))
      {
        Print(err.message||'\n');
      }
      ELSE IF(CellExists(err,'error')) //ReportJavascriptError creates these
      {
        Print(err.error||'\n');
      }

      IF(CellExists(err,'trace'))
        Print(FormatHarescriptStackTrace(SELECT filename, line, col := COLUMN "column", func := functionname FROM err.trace));
    }
    ELSE
    {
      dumpvalue(err,'tree');
    }
    Print("\n");
  }
}
