<?wh

LOADLIB "wh::os.whlib";
LOADLIB "wh::dbase/postgresql.whlib";

LOADLIB "mod::system/lib/database.whlib";


RECORD info := ParseArguments(GetConsoleArguments(),
                              [ [ name := "table", type := "stringlist" ]
                              , [ name := "ids", type := "paramlist" ]
                              ]);

OBJECT trans := OpenPrimary();

STRING ARRAY blobids;

FOREVERY(STRING id FROM info.ids)
  INSERT id INTO blobids AT END;

IF(Length(blobids)=0)
  RETURN;

blobids := SortArray(blobids);

RECORD ARRAY relevant_columns := SELECT table_schema,table_name,column_name from postgresql_information_schema.columns where data_type="USER-DEFINED" AND udt_name="webhare_blob";

FOREVERY(RECORD col FROM relevant_columns)
{
  BOOLEAN allow := LENGTH(info."table") = 0;
  FOREVERY (STRING s FROM info."table")
    IF (ToUppercase(s) = col.table_schema OR ToUppercase(col.table_schema || "." || col.table_name) LIKE ToUppercase(s))
      allow := TRUE;
  IF (NOT allow)
    CONTINUE;

  IF(col.table_schema = "webhare_internal")
    CONTINUE;

  STRING name := col.table_schema || "." || col.table_name || "." || col.column_name;
  Print(`${name}\n`);
  FlushOutputBuffer();
  INTEGER numbytes, num_mb; //ADDME switch to integer64 as soon as 2.37 is gone everywhere

  BOOLEAN have_id := trans->ColumnExists(col.table_schema, col.table_name, "ID");

  STRING tablename := PostgreSQLEscapeIdentifier(col.table_schema) || "." || PostgreSQLEscapeIdentifier(col.table_name);
  FOREVERY(STRING id FROM blobids)
  {
    STRING cmd := `select ${have_id ? "id" : "ctid"} from ${tablename} where (${PostgreSQLEscapeIdentifier(col.column_name)}).id = ${PostgreSQLEscapeLiteral("AAAB" || id)}`;
    RECORD ARRAY results := trans->__ExecSQL(cmd);
    FOREVERY(RECORD row FROM results)
      Print(`Match ${name}: ${have_id ? `id=${row.id}` : `ctid=${row.ctid}`} has blob ${id}\n`);
  }
}
