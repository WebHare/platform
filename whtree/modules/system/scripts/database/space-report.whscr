<?wh
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "wh::dbase/dynquery.whlib";
LOADLIB "wh::os.whlib";

OBJECT trans := OpenPrimary();
INTEGER64 totalnumbytes, totalnumblobs;

RECORD ARRAY relevant_columns;

FOREVERY(RECORD schemarec FROM trans->GetSchemaListing())
  FOREVERY(RECORD tablerec FROM trans->GetTableListing(schemarec.schema_name))
  {
    IF(schemarec.schema_name = "webhare_internal" AND tablerec.table_name = "blob")
      CONTINUE;

    FOREVERY(RECORD columnrec FROM trans->GetColumnListing(schemarec.schema_name, tablerec.table_name))
      IF(columnrec.data_type in ["BLOB","webhare_blob"]) //WHDB or PSQL blob types
        INSERT CELL [ table_schema := schemarec.schema_name
                    , tablerec.table_name
                    , columnrec.column_name
                    ] INTO relevant_columns AT END;
  }

FOREVERY(RECORD col FROM relevant_columns)
{
  Print(col.table_schema || "." || col.table_name || "." || col.column_name || ": ");
  FlushOutputBuffer();

  INTEGER64 numbytes, numblobs;

  OBJECT q := NEW DynamicQuery;
  q->AddTable("data", GetPrimary()->id, col.table_schema||'.'||col.table_name, [STRING(col.column_name)]);
  //q->AddConstraint('data.'||col.column_name, "!=", DEFAULT BLOB);
  RECORD ARRAY res := q->Execute();
  FOREVERY(RECORD r FROM res)
  {
    INTEGER64 len := Length64(GetCell(r.data, col.column_name));
    IF(Len>0)
      numblobs := numblobs + 1;
    numbytes := numbytes + len;
  }
  Print((numbytes / 1024 / 1024) || "MB (" || numblobs || " blobs)\n");

  totalnumbytes := totalnumbytes + numbytes;
  totalnumblobs := totalnumblobs + numblobs;
}
Print("Total: " || (totalnumbytes / 1024 / 1024) || "MB (" || totalnumblobs || " blobs)\n");
