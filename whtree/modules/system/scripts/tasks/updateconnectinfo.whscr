<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/cluster/secrets.whlib";


BLOB FUNCTION GenerateWHConnectFile(STRING requesturl, INTEGER trustduration, STRING pathtype, STRING systempath)
{
  IF (pathtype NOT IN [ "webdav", "fs" ])
    THROW NEW Exception(`Illegal path type: '${pathtype}', allowed: 'webdav'/'fs'`);

  DATETIME expires := AddTimeToDate(trustduration, GetCurrentDatetime());
  RECORD info :=
      CELL[ editorservice :=    ResolveToAbsoluteURL(requesturl, "/wh_services/system/editorservice")
          , accesstoken :=      EncryptForThisServer("system:editorservice", [ trusteditoruntil := expires ])
          , translateinfo :=    EncryptForThisServer("system:translateinfo", CELL[ pathtype , systempath ])
          , validuntil :=       GetUnixTimestamp(expires)
          ];

  RETURN EncodeJSONBlob(info);
}

OpenPrimary();
RECORD config := GetWebHareConfiguration();

STRING ARRAY paths := STRING[ config.basedataroot, config.varroot, config.ephemeralroot ];

IF(GetEnvironmentVariable("WEBHARE_NOINSTALLATIONINFO") = "")
  paths := STRING[ ...paths, ...config.moduledirs, config.installationroot ];

FOREVERY (STRING path FROM paths)
  IF (path NOT LIKE "*/")
    paths[#path] := path || "/";

paths := SortArray(paths);

STRING ARRAY rootpaths;
STRING lastpath := "<nomatch>";
FOREVERY (STRING path FROM paths)
{
  BOOLEAN issubdir;
  FOREVERY (STRING rootpath FROM rootpaths)
    IF (path LIKE `${rootpath}*`)
      issubdir := TRUE;
  IF (NOT issubdir)
    INSERT path INTO rootpaths AT END;
}

STRING ARRAY extraroots :=
   SELECT AS STRING ARRAY TEMPORARY trimmedpath := TrimWhitespace(path)
        , trimmedpath LIKE "*/" ? trimmedpath : trimmedpath || "/"
     FROM ToRecordArray(Tokenize(ReadRegistryKey("system.backend.development.whconnectroots"), ","), "PATH")
    WHERE TrimWhitespace(path) != "";

STRING interfaceurl := GetPrimaryWebhareInterfaceURL();
FOREVERY (STRING path FROM rootpaths CONCAT extraroots)
{
  BLOB connectfile := GenerateWHConnectFile(interfaceurl, 86400 * 1000, "fs", path);
  STRING storepath := MergePath(path, ".wh.connectinfo");
  TRY
    StoreDiskFile(storepath, connectfile, [ overwrite := TRUE ]);
  CATCH
  {
    PrintTo(2, `Could not store connectinfo file in ${storepath}\n`);
    SetConsoleExitCode(1);
  }
}
