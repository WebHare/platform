<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/pkcs.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/keystore.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";

RECORD args := ParseArguments(GetConsoleArguments(),
    [ [ name := "debug", type := "switch" ]
    ]);

IF (NOT RecordExists(args))
{
  PRINT("Syntax: wh run mod::system/scripts/tasks/renewcertbot.whscr [ --debug ]\n");
  RETURN;
}

BOOLEAN debug := args.debug;
OBJECT trans := OpenPrimary();

trans->BeginWork();

IF (NOT RecordExists(SELECT FROM ListKeyPairs() WHERE name = "fallback"))
{
  OBJECT evpkey := GenerateCryptoKey("RSA", 2048);
  CreateKeyPair("fallback", StringToBlob(evpkey->privatekey), [ title := "Fallback certificate", description := "Certificate used for all hosts without an matching certificate" ]);
}


OBJECT fallbackkeypair := OpenKeyPairByName("fallback");

RECORD certdata;
STRING certfile := BlobToString(fallbackkeypair->certificatechain);
IF (LENGTH(certfile) != 0)
{
  certdata := DecodePEMFile(certfile);
  IF (certdata.type = "multiple")
    certdata := RECORD(certdata.parts);
  IF (NOT RecordExists(certdata) OR certdata.type != "certificate")
    certdata := DEFAULT RECORD;
}

// Renew a little before the keystore warning at 28 days
DATETIME validitylimit := AddDaysToDate(30, GetCurrentDateTime());

IF (NOT RecordExists(certdata) OR certdata.valid_until < validitylimit)
{
  IF (RecordExists(certdata))
  {
    IF (NOT CheckPKCSSignature(certdata, [ signingcert := certdata ]).success)
    {
      PRINT(`Certificate 'fallback' is not a self-signed certificate\n`);
      RETURN;
    }
  }

  BLOB csr;
  IF (NOT RecordExists(certdata))
  {
    csr := fallbackkeypair->GenerateCSR(
        [ cn := "No matching certificate installed"
        , ou := "Generated by WebHare"
        ]);
  }
  ELSE
  {
    RECORD csroptions :=  CELL
        [ ...RepackRecord(SELECT name := fieldname, value
                          FROM certdata.subjectfields
                         WHERE fieldname IN ["OU", "C", "ST", "L", "O", "CN" ])
        , dnsaltnames :=    STRING[]
        ];

    RECORD altnames_ext :=
        SELECT *
          FROM certdata.extensions
         WHERE extnid = "2.5.29.17";

    IF (RecordExists(altnames_ext))
    {
      csroptions.dnsaltnames :=
        SELECT AS STRING ARRAY dnsname
          FROM altnames_ext.decoded
         WHERE __field = "dNSName";
    }

    csr := fallbackkeypair->GenerateCSR(csroptions);
  }

  RECORD signed := fallbackkeypair->SignCertificateRequest(csr, [ selfsign := TRUE ]);

  fallbackkeypair->UpdateMetadata([ certificatechain := signed.certificatechain ]);
  trans->CommitWork();

  // Reload the webserver config
  ReloadWebhareConfig(TRUE, FALSE);
}

