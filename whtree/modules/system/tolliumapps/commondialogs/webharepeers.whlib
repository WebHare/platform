<?wh
LOADLIB "wh::datetime.whlib";

LOADLIB "mod::system/lib/internal/webharepeers/remoteserverapi.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC STATIC OBJECTTYPE PeerServers EXTEND TolliumScreenBase
<
  PUBLIC OBJECT result;

  MACRO NEW()
  {
    this->contexts->wrdschema := this->contexts->userapi->wrdschema;
  }
  MACRO Init(RECORD data)
  {
    ^peers->entitycontext := [ wrd_leftentity := this->contexts->user->entityid ];

    INTEGER lastserver := this->contexts->user->GetRegistryKey("system.webharepreers.lastserver", 0);
    IF(lastserver != 0)
      ^peers->SetValueIfValid([lastserver]);
  }

  INTEGER FUNCTION OnPeerEdit(OBJECT wrdtype, RECORD context, INTEGER entityid)
  {
    IF(entityid = 0)
    {
      RETURN this->RunScreen("#addpeer"); //ADDME should probably pass the requested scopes
    }
    RETURN ^peers->RunEntityEditScreen("#editpeer", entityid) ?? entityid;
  }

  MACRO DoConnect()
  {
    OBJECT peer := OpenWebHarePeer(this, ^peers->selection.serverurl, [ scopes := ["system:sysop"]
                                                                      , validuntil := AddTimeToDate(15*60*1000, GetCurrentDatetime())
                                                                      ]);
    IF(ObjectExists(peer))
    {
      this->result := peer;
      this->tolliumresult := "ok";
      this->contexts->user->SetRegistryKey("system.webharepreers.lastserver", ^peers->value[0]);
    }
  }
>;

PUBLIC STATIC OBJECTTYPE AddPeer EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    this->contexts->controller->ExecuteClientCall("AskWebHareConnect", [ method := "getRegisteredServers" ] )->Then(PTR this->GotRegisteredServers);
  }
  MACRO GotRegisteredServers(VARIANT servers) //JS Call, so careful with what we process
  {
    ^peer->options := SELECT title := url FROM EnforceStructure([[title:=""]], servers);
  }
  ASYNC MACRO OnConnect(OBJECT urlreceiver)
  {
    OBJECT feedback := this->BeginFeedback();
    IF (feedback->HasFailed())
    {
      urlreceiver->Cancel();
      feedback->Finish();
      RETURN;
    }

    RECORD resp := GetWebHareRequestTokenURL(this, ^peer->value);
    urlreceiver->SendUrl(resp.requesturl);
    IF(AWAIT resp.tokenpromise != "")
    {
      // we're not getting the peer id, so look the peer up by the serverurl
      this->__SetScreenResult(this->contexts->wrdschema->^whuser_peerserver->Search("serverurl", ^peer->value));
    }
  }

  INTEGER FUNCTION Submit()
  {
    RETURN 0;
  }
>;

PUBLIC STATIC OBJECTTYPE ReauthServer EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    ^url->value := data.url;
  }
  ASYNC MACRO DoAuthServer(OBJECT urlreceiver)
  {
    RECORD resp := GetWebHareRequestTokenURL(this, ^url->value);
    urlreceiver->SendUrl(resp.requesturl);
    IF(AWAIT resp.tokenpromise != "")
      this->tolliumresult := "ok";
  }
>;
