<?wh
/* Manage various API keys.
   Quick link: https://my.webhare.dev/?app=system:dashboard/apikeys

   TODO: a more generic version would
         - use sytem:config wrdschema, and mark the key fields as noncopyable
         - support full URL masks or at least initial starts, not just domain
           and perhaps multiple masks *and* fs_object pointers
         - perhaps use both public/private labels or otherwise explicitly mark the private and public part *and* show what the service labels them
           see eg the captchas, google calls the public key the apikey, friendlycaptcha calls the private key the apikey
           an explicit password=true and [ ] show would help a lot to show intended usage
         - offer room for more fields, eg API url.
         - lock entries to prod/accept/test (for easier cloning)
         - or generalize even further, just offer two JSON fields, one which is copyable/syncable and once which isn't
*/

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/api.whlib";

CONSTANT STRING ARRAY secret_fields := ["api_key","site_key"];
RECORD ARRAY apitypes :=
  [ [ type := "platform:google.cloud.frontend", title := "Google cloud - frontend", fields := ["api_key"] ]
  , [ type := "platform:google.cloud.backend", title := "Google cloud - backend", fields := ["api_key"] ]
  , [ type := "platform:friendlycaptcha", title := "Friendly Captcha", fields := ["api_key", "site_key"] ]
  , [ type := "platform:google.recaptcha", title := "Google ReCaptcha", fields := ["api_key", "site_key"] ]
  ];

PUBLIC STATIC OBJECTTYPE ApiKeys EXTEND TolliumScreenBase <
  RECORD api_types;

  MACRO Init(RECORD data) {

    ^maps->ReadFromRegistry();
  }

  BOOLEAN FUNCTION Submit() {
    OBJECT work := this->BeginWork();
    ^maps->WriteToRegistry();
    RETURN work->Finish();
  }

  RECORD ARRAY FUNCTION GetApiKeys() {
    OBJECT wrdschema := OpenWRDSchema("system:config");
    RECORD ARRAY keys := wrdschema->^api_secret->RunQuery(CELL[
      outputcolumns := [ "wrd_id", "wrd_creationdate", "masks", "comment", "api_type"]
    ]);

    RETURN SELECT rowkey := wrd_id
                , creationdate := wrd_creationdate
                , masks := Detokenize(Tokenize(masks,'\n'),'; ')
                , api_type := (SELECT AS STRING title FROM apitypes WHERE apitypes.type = keys.api_type) ?? keys.api_type
                , comment
             FROM keys;
  }

  MACRO EditApiKey(RECORD row) {
    this->RunScreen("#editapikey", [ id := RecordExists(row) ? row.rowkey : 0 ]);
  }

  MACRO DeleteApiKeys(RECORD row) {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".confirmdeleteapikeys")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    OpenWRDSchema("system:config")->^api_secret->CloseEntity(row.rowkey);
    work->Finish();
  }
>;


PUBLIC STATIC OBJECTTYPE EditApiKey EXTEND TolliumScreenBase <
  INTEGER keyid;

  MACRO Init(RECORD data) {
    this->keyid := data.id;
    ^api_type->options := SELECT rowkey := type, title FROM apitypes;
    IF(data.id != 0) {
      RECORD keyrec := OpenWRDSchema("system:config")->^api_secret->GetEntityFields(data.id, ["*"]);
      ^api_secret->value := keyrec;
      ^api_secret->^api_type->enabled := FALSE;

      FOREVERY(STRING field FROM secret_fields)
        IF(CellExists(keyrec.secret, field))
          GetMember(this,"^" || field)->value := GetCell(keyrec.secret, field);
    }
    this->OnTypeChange();
  }

  MACRO OnTypeChange() {
    RECORD type := SELECT * FROM apitypes WHERE apitypes.type = ^api_secret->^api_type->value;
    FOREVERY(STRING field FROM secret_fields) {
      BOOLEAN show := RecordExists(type) AND field IN type.fields;
      GetMember(this,"^" || field)->visible := show;
      GetMember(this,"^" || field)->required := show;
    }
  }

  BOOLEAN FUNCTION Submit() {
    OBJECT work := this->BeginWork();

    BOOLEAN like_recaptcha := ^api_secret->^api_type->value IN ["platform:google.recaptcha","platform:friendlycaptcha"];
    RECORD secret;
    RECORD type := SELECT * FROM apitypes WHERE apitypes.type = ^api_secret->^api_type->value;
    FOREVERY(STRING field FROM secret_fields)
      IF(RecordExists(type) AND field IN type.fields)
        secret := CellInsert(secret, field, GetMember(this,"^" || field)->value);

    IF (this->keyid = 0) {
      OpenWRDSchema("system:config")->^api_secret->CreateEntity(CELL[ ...^api_secret->value, secret ]);
    } ELSE {
      OpenWRDSchema("system:config")->^api_secret->UpdateEntity(this->keyid, CELL[ ...^api_secret->value, secret ]);
    }
    RETURN work->Finish();
  }
>;
