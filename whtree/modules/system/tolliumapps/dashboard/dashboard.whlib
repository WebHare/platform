<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";

LOADLIB "mod::system/lib/dialogs.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/internal/debugger/commonactions.whlib";
LOADLIB "mod::system/lib/internal/modules/defreader.whlib";
LOADLIB "mod::system/lib/internal/moduledefparser.whlib";
LOADLIB "mod::system/lib/resources.whlib";


/* FIXME/ADDME
  - consider how rights fit into this. <accessright> support on monitor panels,
    or autoregistration in database and delegate to sysop?
*/

PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
<
  PUBLIC STRING ARRAY screenparams; // optionally used in child panels

  RECORD ARRAY allgroups;
  RECORD ARRAY allpanels;
  RECORD originalspacers;
  STRING originaltitle;

  MACRO NEW()
  {
    /* under extreme configuration issues, such as a 427->426 downgrade, userapi may not exist
       and that blocked us from reaching the Dashboard to SEE that we were back to 4.26

       TODO maybe messages shouldn't be the PRIMARY monitoring panel or at least
            not autoselected unless you followed Towl?
    */

    IF(ObjectExists(this->contexts->userapi))
      this->contexts->wrdschema := this->contexts->userapi->wrdschema;
  }

  MACRO Init(RECORD data)
  {
    this->originalspacers := ^currentmonitor->spacers;
    this->originaltitle := this->frame->title;
    this->frame->flags.issysop := this->contexts->user->HasRight("system:sysop");

    this->frame->flags.refreshcontents := FALSE;
    ^togglerefreshbutton->pressed := FALSE;

    this->SetupGroups();
    RegisterEventCallback("system:softreset", PTR this->OnSoftReset);

    this->screenparams := data.params;
    IF (RecordExists(data) AND data.calltype = "direct" AND Length(this->screenparams) > 0)
      ^monitorpanels->value := this->screenparams[0];
    ELSE
      ^monitorpanels->value := ^monitorpanels->rows[0].rowkey;
  }

  MACRO UpdateIntervalSetting()
  {
    INTEGER suggestedfrequency;
    IF(ObjectExists(^currentmonitor->contents))
      suggestedfrequency := ^currentmonitor->contents->GetSuggestedRefreshFrequency();
    ELSE
      suggestedfrequency := 60;

    ^togglerefreshbutton->enabled := suggestedfrequency > 0;

    IF(suggestedfrequency > 0 AND ^togglerefreshbutton->pressed)
    {
      this->frame->interval := suggestedfrequency * 1000;
    }
    ELSE
    {
      this->frame->interval := 0;
    }
    this->RefreshNow();
  }

  MACRO RefreshNow()
  {
    DATETIME updstart := GetCurrentDatetime();
    IF(ObjectExists(^currentmonitor->contents))
      ^currentmonitor->contents->RefreshDashboardPanel();

    DATETIME updend := GetCurrentDatetime();
    ^lastupdate->value := this->tolliumuser->FormatTime(updend, "seconds", TRUE) || " (" || GetMsecsDifference(updstart, updend) || "ms)";
  }

  MACRO OnSoftReset(STRING event, RECORD data)
  {
    this->RefreshNow();
  }

  MACRO OnMessage(RECORD message)
  {
    IF (CellExists(message, "PANEL") AND message.panel != "")
    {
      IF(NOT RecordExists(SELECT FROM ^monitorpanels->rows WHERE rowkey = message.panel))
        RETURN; //ignore. you probably lack access to the panel
      ^monitorpanels->value := message.panel;
    }

    this->OnMonitorPanelsSelect();
    IF(MemberExists(^currentmonitor->contents, "ONMESSAGE"))
      ^currentmonitor->contents->OnMessage(message);
  }

  MACRO OnMonitorPanelsSelect()
  {
    RECORD sel := ^monitorpanels->selection;
    IF(NOT RecordExists(sel) OR sel.screen="")
    {
      IF(ObjectExists(^currentmonitor->contents))
        ^currentmonitor->contents->DisableRefresh();
      ^currentmonitor->contents := DEFAULT OBJECT;
      ^currentmonitor->spacers := this->originalspacers;
    }
    ELSE
    {
      IF(NOT ObjectExists(sel.panel))
      {
        //The panel still needs loading!
        OBJECT screen := this->LoadScreen(sel.screen);
        IF (ObjectExists(screen))
        {
          IF(NOT (screen EXTENDSFROM DashboardPanelBase))
            THROW NEW Exception(`The dashboard panel '${sel.screen}' does not extend from DashboardPanelBase`);

          sel.panel := screen;
          sel.panel->__pvt_yourmonitor := PRIVATE THIS; //ADDME use some sort of messaging interface? better not to expose privatedata
          ^monitorpanels->UpdateSingleRow(sel);
        }
        // ELSE: display warning/error??
      }

      IF (ObjectExists(sel.panel))
      {
        ^currentmonitor->contents := sel.panel;
        ^currentmonitor->spacers := sel.panel->frame->bodynode->spacers;
        ^currentmonitor->contents->EnableRefresh();
        ^currentmonitor->contents->RefreshDashboardPanel();
      }
    }

    IF(RecordExists(sel) AND sel.rowkey != ^monitorpanels->rows[0].rowkey) //we don't want the app to open as "WebHare Platform - Dashboard"
      this->frame->title := sel.title || " - " || this->originaltitle;
    ELSE
      this->frame->title := this->originaltitle;

    this->UpdateIntervalSetting();
  }

  MACRO DoClearCaches()
  {
    RunClearCaches(this);
  }

  MACRO DoSoftReset()
  {
    //Confirm it, and don't allow users to disable that confirmation. Soft reset is pretty heavy for a server and should be rarely needed
    IF(this->RunSimpleScreen("confirm", this->GetTid(".confirmsoftreset")) != "yes")
      RETURN;

    RunModuleScriptDialog(this, "mod::system/scripts/whcommands/softreset.whscr", DEFAULT STRING ARRAY);
  }

  MACRO SetupGroups()
  {
    this->allgroups := RECORD[];
    this->allpanels := RECORD[];

    FOREVERY(RECORD mod FROM GetWebHareModules())
    {
      OBJECT moddef;
      TRY
        moddef := GetModuleDefinitionXML(mod.name);
      CATCH
        CONTINUE;

      RECORD dashboardinfo := GetModuleDashboardInfo(mod.name, moddef);
      this->allgroups := this->allgroups CONCAT dashboardinfo.groups;
      FOREVERY(RECORD panel FROM dashboardinfo.panels)
        IF(DoAccessCheckFor(panel.accesscheck, this->contexts->user))
          INSERT panel INTO this->allpanels AT END;
    }

    this->allgroups := SELECT *, title := GetTid(title) FROM this->allgroups;
    this->allgroups := OrderIntercepts(SELECT * FROM this->allgroups ORDER BY ToUppercase(title)).intercepts;
    this->allpanels := SELECT *, title := GetTid(title) FROM this->allpanels;

    //FIXME Stabilize ordering of groups and applets, especially if multiple modules supply them!

    //Kill ungrouped applets, move applets with unknown groups to system:other
    STRING ARRAY groupnames := SELECT AS STRING ARRAY name FROM this->allgroups;
    UPDATE this->allpanels SET grp := "system:other" WHERE grp NOT IN groupnames;

    //Set up the list
    RECORD ARRAY toprows := SELECT rowkey := name
                                 , title
                                 , expanded := TRUE
                                 , subnodes := (SELECT rowkey := name
                                                     , title
                                                     , subnodes := DEFAULT RECORD ARRAY
                                                     , expanded := FALSE
                                                     , panel := DEFAULT OBJECT
                                                     , screen
                                                  FROM this->allpanels
                                                 WHERE allpanels.grp = allgroups.name
                                              ORDER BY ToUppercase(title)
                                               )
                                 , panel := DEFAULT OBJECT
                                 , screen
                              FROM this->allgroups;

    toprows := SELECT * FROM toprows WHERE Length(subnodes) > 0;

    ^monitorpanels->rows := toprows;
  }

  MACRO OnInterval()
  {
    this->RefreshNow();
  }

  MACRO DoToggleRefresh()
  {
    IF (^togglerefreshbutton->pressed)
    {
      ^togglerefreshbutton->pressed := FALSE;
      this->frame->flags.refreshcontents := FALSE;
    }
    ELSE
    {
      ^togglerefreshbutton->pressed := TRUE;
      this->frame->flags.refreshcontents := TRUE;
    }
    this->UpdateIntervalSetting();
  }

  MACRO DoRefresh()
  {
    this->RefreshNow();
  }

  MACRO DoOpenInSeparateTab(OBJECT receiver)
  {
    receiver->SendURL(`/?app=system:dashboard(${EncodeURL(^monitorpanels->value)})`);
  }

  MACRO DoAdHocCache()
  {
    this->RunScreen("/screens/sysmgmt/adhoccache.xml#status");
  }

  MACRO DoRemoting()
  {
    this->RunScreen("/screens/sysmgmt/remoting.xml#main");
  }

  MACRO DoBackend()
  {
    this->RunScreen(Resolve("../config/settings.xml"));
    //FIXME //this->RefreshPermissions(); //in case DTAP mode changed
  }

  MACRO DoWebdavData()
  {
    this->RunScreen("/screens/sysmgmt/webdav.xml#webdav_data");
  }

  MACRO DoMailRoutes()
  {
    this->RunScreen(Resolve("../config/mailrouting.xml#mailroutes"));
  }

  MACRO DoApiKeys()
  {
    this->RunScreen(Resolve("apikeys.xml#apikeys"));
  }

  MACRO DoExportWebserverUsageStats()
  {
    this->RunScreen("mod::system/tolliumapps/webservers/webservers.xml#exportusagestats");
  }
>;
