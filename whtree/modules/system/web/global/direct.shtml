<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/webserver/config.whlib";

STRING suburl := TOkenize(GetRequestURL(), '/.webhare/direct/')[1];
IF(suburl = "uuid")
{
  AddHTTPHeader("Content-Type","text/plain",FALSE);
  Print(GenerateWebserverUUID());
  RETURN;
}
IF(suburl LIKE "certbot/*")
{
  RECORD unpacked := DecryptForThisServer("system:certbot",Substring(suburl,8),[fallback:=DEFAULT RECORD]);
  IF(NOT RecordExists(unpacked))
  {
    Print("The verification token does not appear to be generated by this server");
    RETURN;
  }

  IF(unpacked.uuid != GenerateWebserverUUID())
  {
    Print("UUID mismatch, got " || GenerateWebserverUUID() || " wanted " || unpacked.uuid || " - talking to a clone?");
    RETURN;
  }
  RECORD diff := GetDatetimeDifference(unpacked.now,GetCurrentDatetime());
  IF(diff.days != 0 OR diff.msecs > 60000)
  {
    Print("This token is expired");
    RETURN;
  }
  Print("ok");
  RETURN;
}

AbortWithHTTPError(404);
