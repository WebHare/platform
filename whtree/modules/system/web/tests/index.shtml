<?wh

LOADLIB "wh::witty.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::publisher/lib/siteprofiles.whlib";

LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::system/lib/testfw/runtest.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";


OBJECT trans := OpenPrimary();
OBJECT testrunner := NEW RunTests;
RECORD ARRAY tests := testrunner->GatherAllTests(DEFAULT STRING ARRAY, DEFAULT STRING ARRAY);

RECORD ARRAY testsbygroup;

tests := SELECT *, topgroup := Tokenize(testname,'.')[0] FROM tests WHERE type = "jstest";

FOREVERY(STRING groupname FROM SELECT AS STRING ARRAY DISTINCT topgroup FROM tests)
{
  RECORD ARRAY persite := SELECT sitename
                               , link := ResolveToAbsoluteURL(ANY(baseurl), `/.system/jstests/?site=${EncodeURL(ANY(sitename))}&mask=${EncodEURL(groupname)}.*`)
                               , tests := GroupedValues(tests)
                            FROM tests
                           WHERE topgroup = groupname
                        GROUP BY sitename;

  IF(Length(persite) = 1)
  {
    INSERT [ link := persite[0].link
           , title := groupname
           , sites := RECORD[]
           ] INTO testsbygroup AT END;
  }
  ELSE
  {
    INSERT [ link := ""
           , title := groupname
           , sites := (SELECT title := sitename, link
                         FROM persite)
           ] INTO testsbygroup AT END;
  }
}

testsbygroup := SELECT * FROM testsbygroup ORDER BY ToUppercase(title);

OBJECT webdesign := GetWebdesign(whconstant_whfsid_webharebackend);
webdesign->RunPageWithContents(PTR LoadWittyLibrary("mod::system/lib/testfw/testpage.witty", "HTML")->RunComponent("testoverview",[ testsbygroup := testsbygroup ]));
