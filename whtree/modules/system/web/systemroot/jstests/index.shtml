<?wh

LOADLIB "wh::javascript.whlib";
LOADLIB "wh::witty.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::system/lib/testfw/runtest.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";


OBJECT trans := OpenPrimary();

STRING ARRAY masks;
STRING ARRAY skips;
STRING urlbase := DecodeURL(Tokenize(GetRequestURL(),'?')[0]);
IF(urlbase LIKE "*/!/*")
{
  STRING ARRAY suburltoks := Tokenize(Tokenize(urlbase,'/!/')[1], '/');
  SWITCH (suburltoks[0])
  {
    CASE "testlist"
    {
      OBJECT testfw := NEW RunTests;
      STRING ARRAY urlmasks, urlskips;
      IF (GetWebVariable("mask") != "")
        urlmasks := Tokenize(Substitute(GetWebVariable("mask"), ",", " "), " ");
      IF (GetWebVariable("skip") != "")
        urlmasks := Tokenize(Substitute(GetWebVariable("skip"), ",", " "), " ");

      //ADDME now that the cache is fast enough, post-filter ?
      RECORD ARRAY tests := testfw->GatherAllTests(urlmasks, urlskips);
      DELETE FROM tests WHERE type != "jstest" OR sitename != GetWebVariable("site");

      IF (GetWebVariable("tags") != "")
        tests := SELECT * FROM tests WHERE Length(ArrayIntersection(Tokenize(GetWebVariable("tags"),','), tags))>0;

      SendWebFile(EncodeJSONBlob(
        SELECT name :=    testname
             , url :=     "../testpage/" || testname
             , args :=    testscript.args
             , tags
          FROM tests));
    }
    CASE "testpage"
    {
      //We are generating the frame containing the test script

      STRING testname := suburltoks[1];
      OBJECT testfw := NEW RunTests;
      RECORD ARRAY tests := testfw->GatherAllTests([ testname ], DEFAULT STRING ARRAY);
      DELETE FROM tests WHERE type != "jstest" OR COLUMN testname != VAR testname;
      RECORD affectedtest := tests;

      IF (NOT RecordExists(affectedtest))
        ABORT(`No such test ${testname}`);

      OBJECT testwitty := LoadWittyLibrary("mod::system/lib/internal/jstests/launchpage.witty","HTML-NI");
      IF(Length(suburltoks) <= 2)
      {
        //allow testscripts to do/load whatever they want
        AddHTTPHeader("Content-Security-Policy", "frame-ancestors 'self'", FALSE);

        //we implicitly rely on CallJS in native HS running in a subprocess and us not having to pay the price of a full esbuild start
        RECORD compileresult := CallJS("@mod-platform/js/assetpacks/compiletask.ts#recompileAdhoc",affectedtest.testscript.scriptpath, affectedtest.compatibility ?? whconstant_default_compatibility);

        RECORD headers;
        IF (RecordExists(SELECT FROM compileresult.messages WHERE type = "error"))
        {
          testwitty->RunComponent("compilefailure", CELL[ COMPILE_FAILURE_INFO := CELL[ compileresult, affectedtest ]]);
          RETURN;
        }

        //FIXME we need to get info.dependencies.FILEDEPENDENCIES (and .MISSINGDEPENDENCIES) to the top level devtools websocket to listen for
        RECORD data := [ printheaders := PTR Print(GetAssetpackIntegrationCode(compileresult.last_compile_settings.bundle.config.name))
                       , whconfig := [ dtapstage := GetDTAPStage()
                                     , islive := IsDTAPLive()
                                     , locale := "en-US"
                                     , server := GetWebhareVersionNumber()
                                     ]
                       ];

        //We need to generate a full page for the specified test
        testwitty->RunComponent("emptytestpage", data);
        RETURN;
      }
      ELSE IF(suburltoks[2] LIKE "*.js")
      {
        AddHTTPHeader("Content-Type", "text/javascript", FALSE);
        BLOB jsfile :=  GetWebHareResource(affectedtest.testscript.scriptpath);
        SendWebFile(jsfile);
      }
    }
  }
}

// list overview
STRING testlisturl := GetWebVariable("testlist");
STRING testsiteroot;
IF (testlisturl = "")
{
  // No test list, so this is a webhare test url
  FOREVERY (RECORD rec FROM GetAllWebVariables())
    IF (rec.value != "")
    {
      IF (rec.name = "mask")
        masks := masks CONCAT Tokenize(rec.value, ",");
      ELSE IF (rec.name = "skip")
        skips := skips CONCAT Tokenize(rec.value, ",");
    }

  IF(GetWebVariable("site") != "")
  {
    OBJECT site := OpenSiteByName(GetWebVariable("site"));
    IF(NOT ObjectExists(site))
      THROW NEW Exception("No such site '" || GetWebVariable("site") || "'");
    IF(site->Webroot = "")
      THROW NEW Exception("Site '" || GetWebVariable("site") || "' is not being published");

    STRING serverroot := ResolveToAbsoluteURL(site->webroot, "/"); //jstests are always relative to the server root
    IF(GetClientRequestURL() NOT LIKE serverroot || "*") //accessing from the URL. my.webhare.dev in CI reports can trigger this
      Redirect(serverroot || UnpackURL(GetClientRequestURL()).urlpath);

    //we should always be invoked through a local /.webhare_testsuite/ so we can just generate a local base path
    testsiteroot := '/'||UnpackURL(site->webroot).urlpath;
  }

  testlisturl := UpdateURLVariables("/.system/jstests/!/testlist/",
      [ mask :=       Detokenize(masks, ",")
      , skip :=       Detokenize(skips, ",")
      , tags :=       GetWebVariable("tags")
      , site :=       GetWebVariable("site")
      ]);
}


OBJECT testrunwitty := LoadWittyLibrary("mod::system/lib/internal/jstests/launchpage.witty","HTML");

RECORD data := [ printheaders :=    PTR Print(GetAssetpackIntegrationCode("platform:testsuite"))
               , mask :=            masks
               , skips :=           skips
               , testlisturl :=     testlisturl
               , testsiteroot :=    testsiteroot
               , webhareversionnumber := GetWebhareVersionNumber()
               , hasdevkitmodule := IsModuleInstalled("devkit")
               ];

testrunwitty->RunComponent("hostingpage", data);
