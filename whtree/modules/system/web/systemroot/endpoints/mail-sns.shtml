<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internet/webbrowser.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/tasks.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/smtpmgr.whlib";
LOADLIB "mod::system/lib/internal/cluster/secrets.whlib";


/* For the JSON format for the generic SNS subscribe/unsubscribe messagess:
   http://docs.aws.amazon.com/sns/latest/dg/json-formats.html

   SES specific example message:
   https://docs.aws.amazon.com/ses/latest/DeveloperGuide/event-publishing-retrieving-sns-examples.html
*/

IF(NOT IsRequestPost())
  AbortWithHTTPError(400, "This endpoint only accepts POST");
LogCurrentRequestToRPCLog("system:mail-sns-endpoint");
IF(NOT VerifySNSVerificationKey(GetWebVariable("key")))
  AbortWithHTTPError(403, "Invalid verification key");

//FIXME signature validation. we need to avoid turning into a portscanner or reflector by using SubscriptionConfirmation requests - http://docs.aws.amazon.com/sns/latest/dg/SendMessageToHttp.verify.signature.html
//FIXME but validate the URLs hosting the certificate somehow. we don't want that URL to be a portscan opportunity itself

RECORD data := DecodeJSONBlob(GetRequestBody());

IF(data.type = "SubscriptionConfirmation")
{
  /* Example message. Should move to our TS based parser too ?

  "Type" : "SubscriptionConfirmation",
  "MessageId" : "2efaa...",
  "Token" : "233641...",
  "TopicArn" : "arn:aws:sns:eu-west-1:00000:webhare-mail",
  "Message" : "You have chosen to subscribe to the topic arn:aws:sns:eu-west-1:00000:webhare-mail.\nTo confirm the subscription, visit the SubscribeURL included in this message.",
  "SubscribeURL" : "https://sns.eu-west-1.amazonaws.com/...",
  "Timestamp" : "2026-01-07T10:53:42.494Z",
  "SignatureVersion" : "1",
  "Signature" : "A4ZL...",
  "SigningCertURL" : "https://sns.eu-west-1.amazonaws.com/SimpleNotificationService-7506a1e35b36ef5a444dd1a8e7cc3ed8.pem"
}
*/

  OBJECT browser := NEW WebBrowser;
  IF(UnpackURL(data.SubscribeURL).host NOT LIKE "*.amazonaws.com")
    Abort(`Untrusted subscription host '${UnpackURL(data.SubscribeURL).host}'`);

  LogRPCForWebbrowser("system:mail-sns-endpoint", "confirmation", browser);
  browser->GotoWebPage(data.SubscribeURL);
  RETURN;
}
IF(data.type!="Notification")
  RETURN; //this is 'other' AWS traffic

//Notification is actual SNS content.
RECORD snsmessage := DecodeJSON(data.message);
/* TODO cannot find a record of us receiving this message type recently. I do have a

<ConfirmSubscriptionResponse xmlns="http://sns.amazonaws.com/doc/2010-03-31/">
  <ConfirmSubscriptionResult>
    <SubscriptionArn>arn:aws:sns:eu-west-1:0000:webhare-mail:0000-00-00-000000</SubscriptionArn>
  </ConfirmSubscriptionResult>
  <ResponseMetadata>
    <RequestId>0000-00-00-00000</RequestId>
  </ResponseMetadata>
</ConfirmSubscriptionResponse>

but that's in response to the subscription confirmation above.
*/
IF(CellExists(snsmessage,'notificationtype') AND snsmessage.notificationtype = "AmazonSnsSubscriptionSucceeded")
  RETURN;

RECORD parseresult := CallJS("mod::platform/js/email/aws-ses.ts#parseSNSMessage", BlobToString(GetRequestBody()));
IF(parseresult.basictype = "" OR NOT RecordExists(parseresult.basicobject)) //not caring about this type yet
  RETURN;

STRING recipient := parseresult.recipient;
STRING ourmessageid := parseresult.ourmessageid;
IF(ourmessageid LIKE "<*@smtp.webhare.net>") //this path handles QueueMail(InWork) emails
{
  OBJECT trans := OpenPrimary();
  INTEGER ourtaskid := DecryptForThisServer("system:messageid", Tokenize(Substring(ourmessageid, 1), '@')[0], [fallback := 0]);

  IF(ourtaskid=0)
    RETURN;

  RECORD task := DescribeManagedTask(ourtaskid);
  IF(NOT RecordExists(task) OR task.tasktype != "system:outgoingmail" OR task.data.receiver != recipient)
    RETURN;// ABORT(CELL[task,recipient]);
  IF(CellExists(task.metadata, parseresult.basictype)) //already have delivery info
    RETURN;

  trans->BeginWork();
  SetManagedTaskMetadata(ourtaskid, parseresult.basictype, parseresult.basicobject);
  IF(CellExists(task.data,'callback'))
    ScheduleManagedTask("system:mailstatuscallback", [ ...task.data.callback, type := parseresult.basictype, status := parseresult.basicobject ]);

  trans->CommitWork();
}
ELSE IF(ourmessageid LIKE "<*@news.webhare.net>") //TODO support a future registration api..
{
  MakeFunctionPtr("mod::newsletter/lib/incoming.whlib#ProcessIncomingBounce")(ourmessageid, parseresult.basictype, parseresult.basicobject);
}
ELSE IF(ourmessageid LIKE "<*@connect.webhare.net>")
{
  MakeFunctionPtr("mod::connect/lib/internal/hooks.whlib#ProcessIncomingBounce")(ourmessageid, parseresult.basictype, parseresult.basicobject);
}
ELSE
{
  THROW NEW Exception(`Unrecognized message id: ${ourmessageid}`);
}
