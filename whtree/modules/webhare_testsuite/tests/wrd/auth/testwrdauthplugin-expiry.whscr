<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/tests/wrd/auth/authtest.whlib";

LOADLIB "mod::wrd/lib/internal/auth/support.whlib";
LOADLIB "mod::wrd/lib/internal/auth/pluginhelpers.whlib";
LOADLIB "mod::wrd/lib/internal/wrdsettings.whlib";

INTEGER entityid;

MACRO SetAuthSettings(RECORD newsettings)
{
  testfw->BeginWork();
  SetWRDSetting(testfw->GetWRDSchema(), "login_settings", CELL[...wrdauth_defaultconfiguration, ...newsettings ]);
  testfw->CommitWork();
}

MACRO TestHelpers()
{
  TestEq( MakeDateTime(2021,7,13,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,15,0,0)
                                   , 86400 * 1000i64) // 1 day
        , "Logging in at 17:00:00 CET should give a session until the next day 04:00:00 CET (02:00:00 UTC)");

  TestEq( MakeDateTime(2021,7,13,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,22,0,0)
                                   , 86400 * 1000i64) // 1 day
        , "Logging in at midnight CET should give a session until 04:00:00 CET (02:00:00 UTC) the same day");

  TestEq( MakeDateTime(2021,7,13,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,22,59,59)
                                   , 86400 * 1000i64) // 1 day
        , "Logging in at 00:59:59 CET should give a session until 04:00:00 CET (02:00:00 UTC) the same day (just outside round_min_duration)");

  TestEq( MakeDateTime(2021,7,14,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,23,0,0)
                                   , 86400 * 1000i64) // 1 day
        , "Logging in at 01:00:00 CET should give a session until 04:00:00 CET (02:00:00 UTC) the NEXT day (within round_min_duration)");

  TestEq( MakeDateTime(2021,7,14,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,13,0,0,0)
                                   , 86400 * 1000i64) // 1 day
        , "Logging in at 02:00:00 CET should give a session until 04:00:00 CET (02:00:00 UTC) the NEXT day");

  TestEq( MakeDateTime(2021,7,19,2,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,15,0,0)
                                   , 7 * 86400 * 1000i64) // 7 day
        , "Logging in at 17:00:00 CET with 7 day expiry should give a session until 7 days later 04:00:00 CET (02:00:00 UTC)");


  TestEq( MakeDateTime(2021,7,12,16,0,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,15,0,0)
                                   , 60 * 60 * 1000i64) // 1 hour
        , "Logging in at 17:00:00 CET with 1 hour expiry should give a session until 18:00:00 CET (17:00:00 UTC)");

  TestEq( MakeDateTime(2021,7,12,9,30,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration, round_long_logins_to := -1 ]
                                   , MakeDateTime(2021,7,12, 9,15,0)
                                   , 15 * 60 * 1000i64) // 15 minutes
        , "Killing round_long_logins_to should stop any rounding up/down");

  TestEq( MakeDateTime(2021,7,12,16,15,0)
        , CalculateWRDSessionExpiry(CELL[ ...wrdauth_defaultconfiguration ]
                                   , MakeDateTime(2021,7,12,10,15,0)
                                   , 6 * 3600 * 1000) // 6 hours
        , "Logging in at 12:15:00 CET should give a session until 18:15:00 CET");

  //FIXME what if round_min_duration > session duration?
}

MACRO PrepareExpiryTests()
{
  testfw->BeginWork();

  OBJECT schemaobj := testfw->GetWRDSchema();

  OBJECT entity := schemaobj->^wrd_person->CreateEntity(
      [ wrd_contact_email := "expirytests@beta.webhare.net"
      , whuser_password := CreateAuthenticationSettingsFromPasswordHash(testfw->hashedpasswords.secret)
      , whuser_unit := testfw->testunit
      , wrdauth_account_status := [ status := "active" ]
      ]);

  entityid := entity->id;

  testfw->CommitWork();
}

MACRO TestLoginAPIs()
{
  /* Whitebox tests for WRDAuth cookie integration. Some tests are sensitive to absolute wall clock time so running them through the RPCs will only
     complicate things.

     And it might help to keep the internal APIs a bit cleaner */

  OBJECT wrdplugin := GetTestPlugin();
  RECORD cookies := wrdplugin->__GetWRDAuthIntegration()->__CreateLoginCookies(entityid, cookietype_session, FALSE, [ setcreationdate := MakeDateTime(2021,7,29,15,0,0) ]);
  STRING pubdata := SELECT AS STRING value FROM cookies.headers WHERE field="set-cookie" AND value LIKE "*publicauthdata*";
  RECORD logincookiedetails := DecodeJSON(DecodeURL(Tokenize(Tokenize(pubdata,'=')[1],';')[0]));
  DATETIME expires := MakeDatetimeFromUnixTimestamp(MONEY(logincookiedetails.expiresms / 1000));
  TestEq(MakeDateTime(2021,7,30,4,0,0), UTCToLocal(expires, "Europe/Amsterdam"), "expiry should be rounded down to 04:00:00 CET (02:00:00 UTC)");
}

MACRO TestLoginCookies()
{
  RECORD initialcookie, cookies, result;

  //////////
  //
  // STORY: Make sure plain logins with the default settings work

  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret" ]);
  TestEq(TRUE, result.success);

  //inspect the cookies
  cookies := ExplainWRDCookies();
  TestEq(DEFAULT DATETIME, cookies.login.expires, "The login cookie is supposed to be a session cookie");
  TestEq(TRUE, GetDatetimeDifference(cookies.logindetails.creationdate, GetCurrentDatetime()).msecs < 30000, "Cookie should be created less than 30 seconds in the past");
  TestEq(TRUE, GetDatetimeDifference(GetCurrentDatetime(), cookies.logindetails.expires).days IN [0,1], "Non-persistent sessions are valid for at most a day");

  //sanity check, are we indeed logged in
  result := DebugReq("checkstatus");
  TestEq(TRUE, result.isloggedin);
  TestEq("expirytests@beta.webhare.net", result.loginname);

  //////////
  //
  // STORY: If we disable rounding and set logins to 1ms, we should be able to verify that expires actually works (blocks session renewal)

  // Reset state (but don't accidentally delete wh-debug!).
  SetAuthSettings([ round_long_logins_to := -1, expire_login := 1 ]);
  testfw->browser->DeleteCookies("webharelogin-*");

  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret" ]);
  TestEq(TRUE, result.success);

  cookies := ExplainWRDCookies();
  TestEq(TRUE, GetDatetimeDifference(GetCurrentDatetime(), cookies.logindetails.expires).msecs <= 1, "Cookie should be (about) expired!");
  TestEq(cookietype_session, cookies.logindetails.cookietype);

  //Check the session contents. Should fail because we're already expired
  result := DebugReq("checkstatus");
  TestEq(FALSE, result.isloggedin);
  TestEq(0, result.entityid);

  //Relogin in case checkstatus triggered a cookie clear.
  testfw->browser->DeleteCookies("webharelogin-*");
  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret" ]);
  TestEq(TRUE, result.success);
  TestEq(entityid, result.plugin_entityid, "Verify we're logged in (can't checkstatus!)");

  //Try to renew the session. Should FAIL due to expirys
  result := DebugReq("checkstatus");
  TestEq(FALSE, result.isloggedin);


  //////////
  //
  // STORY: Verify that serverside session renewal (ReestablishSession / cookie base login) does not bump the expiry time.

  // Reset state. Disable all rounding. Let sessions last 60 seconds
  testfw->browser->DeleteCookies("webharelogin-*");
  SetAuthSettings([ round_long_logins_to := -1, expire_login := 60000 ]);

  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret" ]);

  cookies := ExplainWRDCookies();

  initialcookie := cookies.logindetails;

  //Try to renew the session. Should SUCCEEED
  result := DebugReq("checkstatus");
  TestEq(TRUE, result.isloggedin);

  cookies := ExplainWRDCookies();
  TestEq(initialcookie.creationdate, cookies.logindetails.creationdate, "Creationdate should be unchanged");
  TestEq(initialcookie.expires, cookies.logindetails.expires, "Expiry should be unchanged");
}

MACRO TestPersistentLoginCookies()
{
  RECORD initialcookie, cookies, result;

  //////////
  //
  // STORY: Verify persistent session renewal

  testfw->browser->DeleteCookies("webharelogin-*");
  SetAuthSettings(CELL[]); //reset

  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret", persistent := "1" ]);
  TestEq(TRUE, result.success);

  cookies := ExplainWRDCookies();
  TestEq(TRUE, cookies.login.expires > GetCurrentDatetime(), "The login cookie is supposed to be a persistent cookie");
  TestEq(cookietype_persistent, cookies.logindetails.cookietype);

  initialcookie := cookies.logindetails;
  TestEq(TRUE, GetDatetimeDifference(GetCurrentDatetime(), cookies.logindetails.expires).days IN [29,30], "Standard persistent cookies are up to 30 days valid");

  //Try to renew the session. Should SUCCEED
  result := DebugReq("checkstatus");
  TestEq(TRUE, result.isloggedin);

  cookies := ExplainWRDCookies();
  TestEq(initialcookie.creationdate, cookies.logindetails.creationdate, "Creationdate should be unchanged");
  TestEq(initialcookie.expires, cookies.logindetails.expires, "Expiry should be unchanged");
  TestEq(cookietype_persistent, cookies.logindetails.cookietype);


  //////////
  //
  // STORY: Verify persistent session cannot be renewed after expiry

  testfw->browser->DeleteCookies("webharelogin-*");
  SetAuthSettings(CELL[ expire_persistent_login := 1, round_long_logins_to := -1 ]);

  result := DebugReq("login", [ username := "expirytests@beta.webhare.net", pwd := "secret", persistent := "1" ]);
  TestEq(TRUE, result.success);

  cookies := ExplainWRDCookies();
  TestEq(cookietype_persistent, cookies.logindetails.cookietype);

  //Try to renew the session. Should FAIL
  result := DebugReq("checkstatus");
  TestEq(FALSE, result.isloggedin);
}

ASYNC MACRO TestWRDSettings()
{
  SetAuthSettings(CELL[ expire_login := 15*60000 //15 minutes
                      ]);

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("wrd:browser", [ params := STRING[ testfw->GetWRDSchema()->tag ]]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authsettings"));

  TestEq(15i64, TT("expire_login")->value);
  TestEq("minutes", TT("expire_login_unit")->value);

  TestEq(30i64, TT("expire_persistent_login")->value);
  TestEq("days", TT("expire_persistentlogin_unit")->value);

  TestEq(TRUE, TT("roundlogins")->value);
  TestEq(4 * 60 * 60 * 1000, TT("round_long_logins_to")->value);
  TestEq("Europe/Amsterdam", TT("round_long_logins_t_z")->value);
  TestEq(3i64, TT("round_min_duration")->value);
  TestEq("hours", TT("round_min_duration_unit")->value);

  TT("expire_login")->value := 2;
  TT("expire_login_unit")->value := "hours";

  TT("roundlogins")->value := FALSE;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  RECORD settings := GetWRDSetting(testfw->GetWRDSchema(), "login_settings");
  TestEq(2*3600*1000, settings.expire_login);
  TestEq(-1, settings.round_long_logins_to);

  AWAIT ExpectScreenChange(+1, PTR TTClick("authsettings"));
  TestEq(FALSE, TT("roundlogins")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

Runtestframework( [ PTR TestHelpers
                  , PTR PrepareExpiryTests
                  , PTR TestLoginAPIS
                  , PTR TestLoginCookies
                  , PTR TestPersistentLoginCookies
                  , PTR TestWRDSettings
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ]
                     ]);
