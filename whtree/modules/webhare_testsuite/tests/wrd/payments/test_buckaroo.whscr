<?wh
/* Test buckaroo
   WebHare.bv developers can obtain test credentials using:
   Q secrets/load-into-webhare buckaroo
*/

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/internal/wrdtesthelpers.whlib";

BOOLEAN offlineonly := GetTestSecret("BUCKAROO_WEBSITEID") = "" OR GetTestSecret("BUCKAROO_SECRETID") = "";

ASYNC MACRO SetupBuckaroo()
{
  OBJECT paymentapi := GetWRDTestPaymentAPI();
  AWAIT ExpectScreenChange(+1, PTR paymentapi->RunManagePaymentProvidersDialog(GetTestController()));

  AWAIT ExpectScreenChange(+1, PTR topscreen->addprovider->TolliumClick);
  TT("methods")->value := "wrd:buckaroo";
  AWAIT ExpectScreenChange(0, PTR TTClick(":Ok"));
  TT(":Website ID")->value := GetTestSecret("BUCKAROO_WEBSITEID") ?? "dummy-id";
  TT(":Secret ID")->value := GetTestSecret("BUCKAROO_SECRETID") ?? "dummy-secret";
  TT("paymentextension->testmode")->value := TRUE;
  IF(NOT offlineonly)
  {
    AWAIT ExpectAndAnswerMessageBox("OK", PTR TTClick("paymentextension->connect"), [ messagemask := "*succeeded*" ]);
  }

  TT("{select}:Methods")->selection := SELECT * FROM TT("{select}:Methods")->options WHERE ToUppercase(title) IN ["IDEAL","E-MANDATE"];
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(+1, PTR TTClick(":Edit"));
  TestEq(TRUE, TT("paymentextension->testmode")->value);
  TestEq(["iDEAL","E-mandate"], SELECT AS STRING ARRAY title FROM TT("{select}:Methods")->selection);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));
}

ASYNC MACRO NavigateThroughBuckaroo(OBJECT entity, STRING payurl)
{
  OBJECT paymentapi := GetWRDTestPaymentAPI();

  TestEq("pending", entity->GetField("DATA").status);

  //precheck status
  paymentapi->ForceRecheckPayment(entity->id);
  TestEq("pending", entity->GetField("DATA").status);

  //Check the payment in the dialog
  AWAIT ExpectScreenChange(+1, PTR paymentapi->RunShowPaymentDetailsDialog
      ( GetTestController()
      , entity->id));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));

/* TODO we'll revisit reenabling this test as soon as we switch the PSP itself over to TS
  //////////// use chrome headless to execute the payment (buckaroo does a pointless redirect and sends highly broken HTML... let a browser deal with that)
  OBJECT runner :=  WaitForPromise(OpenWebHareService("system:chromeheadlessrunner"));
  RECORD session := AWAIT runner->CreateSession();
  OBJECT connector := NEW ChromeConnector(session.connectorurl, [ debug := TRUE ]);
  OBJECT conn := AWAIT connector->ConnectToSession(session);
  OBJECT pageobj := NEW Page(conn);
  OBJECT navwatcher := NEW NavigationWatcher(conn);

  AWAIT pageobj->Init();
  AWAIT pageobj->Navigate(payurl);

  OBJECT submitbutton := AWAIT WaitForChromeElement(conn, pageobj, "input[value='Submit status']");
  AWAIT submitbutton->Click();

  WHILE(pageobj->mainframe->url NOT LIKE "*wrd/paymentinfo*")
  {
    Print("Now on: " || pageobj->mainframe->url || "\n");
    AWAIT navwatcher->WaitForNavigation();
  }

  Print("Buckaroo returned to: " || pageobj->mainframe->url || "\n");
  TestEq(TRUE, testfw->browser->GotoWebPage(pageobj->mainframe->url));

  RECORD status := DecodeJSONBlob(testfw->browser->content);
  TestEq(entity->id, status.returninfo.paymentid);
  TestEq("approved", status.payinfo.status);
  TestEq("approved", entity->GetField("DATA").status);
*/
  //Check the payment in the dialog
  AWAIT ExpectScreenChange(+1, PTR paymentapi->RunShowPaymentDetailsDialog
      ( GetTestController()
      , entity->id));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));
}

ASYNC MACRO TestPaymentBuckaroo_Ideal()
{
  IF(offlineonly)
    RETURN;

  OBJECT paymentapi := GetWRDTestPaymentAPI();

  testfw->BeginWork();
  RECORD ARRAY methods := SELECT * FROM paymentapi->ListAllPaymentOptions() ORDER BY title;
  RECORD idealmethod := SELECT * FROM methods WHERE ToUppercase(title) = "IDEAL";
  TestAssert(RecordExists(idealmethod));
  // TestEq(TRUE, Length(idealmethod.issuers) > 0);

  OBJECT entity := paymentapi->paymenttype->CreateEntity(CELL[]);
  testfw->CommitWork();

  RECORD properrequest := [ paymentprovider := idealmethod.paymentprovider
                          , paymentoptiontag := idealmethod.paymentoptiontag
                          // , issuer := idealmethod.issuers[0].rowkey
                          , returnurl := GetWRDTestPaymentReturnURL(paymentapi)
                          ];
  IF(testfw->debug)
    Print("Planned return url: " || properrequest.returnurl || "\n");

  RECORD paymentinstruction := paymentapi->StartPayment(entity->id, 219.57, properrequest);
  STRING payurl := ResolveToAbsoluteURL(GetPrimaryWebHareInterfaceURL(), paymentinstruction.submitinstruction.url);
  IF(testfw->debug)
    Print(`Payment url: ${payurl}\n`);

  //abort(); //ABORT here if you want to visit the payment URL yourself
  AWAIT NavigateThroughBuckaroo(entity, payurl);
}

ASYNC MACRO TestPaymentBuckaroo_Emandate()
{
  IF(offlineonly)
    RETURN;

  OBJECT paymentapi := GetWRDTestPaymentAPI();

  testfw->BeginWork();
  OBJECT paypart1 := paymentapi->paymenttype->CreateEntity([wrd_tag := "PAYPART1"]);
  RECORD ARRAY methods := SELECT * FROM paymentapi->ListAllPaymentOptions([types := ["mandate"]]) ORDER BY title;
  RECORD emandatemethod := SELECT * FROM methods WHERE ToUppercase(title) = "E-MANDATE";
  TestAssert(RecordExists(emandatemethod));

  // https://docs.buckaroo.io/docs/e-mandate-requests
  RECORD properrequest := [ paymentprovider := emandatemethod.paymentprovider
                          , paymentoptiontag := emandatemethod.paymentoptiontag
                          // , issuer := emandatemethod.issuers[0].rowkey
                          , returnurl := GetWRDTestPaymentReturnURL(paymentapi)
                          , isrecurring := TRUE
                          , customerid := "C123"
                          ];
  OBJECT captureentity := paymentapi->paymenttype->CreateEntity(CELL[]);

  testfw->CommitWork();

  //For e-mandate - set the maximum amount you intend to charge (we'll need it for B2B mandates in the future? for now it's ignored anyway)
  RECORD paymentinstruction := paymentapi->StartPayment(captureentity->id, 212.0, properrequest);
  STRING payurl := ResolveToAbsoluteURL(GetPrimaryWebHareInterfaceURL(), paymentinstruction.submitinstruction.url);
  IF(testfw->debug)
    Print(`Payment url: ${payurl}\n`);

  //abort(); //ABORT here if you want to visit the payment URL yourself
  AWAIT NavigateThroughBuckaroo(captureentity, payurl);

  //Capture a payment
  RECORD actualdebit1 := paymentapi->StartPayment(paypart1->id, 25, [ capturefrom := captureentity->GetField("DATA"), capturedate := GetCurrentDatetime() ]);
  dumpvalue(actualdebit1);
  TestEq("pending", paypart1->GetField("DATA").status);

  paymentapi->ForceRecheckPayment(paypart1->id);
  TestEq(TRUE, paypart1->GetField("DATA").status IN ["approved","pending"]);
}

IF(offlineonly)
  Print("*** Skipping some tests as TESTSECRET_BUCKAROO_WEBSITEID and TESTSECRET_BUCKAROO_SECRETID are unset\n\n");

RunTestframework([ PTR CreateWRDTestSchema
                 , PTR SetupBuckaroo
                 , PTR RunStandardPaymentMethodTests([ ignorestatuscheckresult := offlineonly ])
                 , PTR TestPaymentBuckaroo_Ideal
                 , PTR TestPaymentBuckaroo_Emandate
                 ]);
