<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::dbase/whdb.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/keystore.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::wrd/lib/database.whlib";
LOADLIB "mod::wrd/lib/dialogs.whlib";
LOADLIB "mod::wrd/lib/imexport.whlib";
LOADLIB "mod::wrd/lib/payments.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::webhare_testsuite/lib/internal/wrdtesthelpers.whlib";


CONSTANT INTEGER keephistorydays := 1;
RECORD save_postimmclosefields;

RECORD FUNCTION FilterFields(RECORD data, STRING ARRAY fields)
{
  RECORD result;
  FOREVERY (STRING s FROM GetSortedSet(fields))
    result := CellInsert(result, s, GetCell(data, s));
  RETURN result;
}

VARIANT FUNCTION RemoveIrrelevantColumns(VARIANT data)
{
  IF (TypeID(data) = TypeID(RECORD))
  {
    FOREVERY (RECORD rec FROM UnpackRecord(data))
      data := CellUpdate(data, rec.name, RemoveIrrelevantColumns(rec.value));
    DELETE CELL __blobsource FROM data;
    DELETE CELL settingid FROM data;
    DELETE CELL whfssettingid FROM data;
  }
  ELSE IF (TypeID(data) IN [ TypeID(RECORD ARRAY), TypeID(VARIANT ARRAY) ])
  {
    FOREVERY (VARIANT v FROM data)
      data[#v] := RemoveIrrelevantColumns(v);
  }
  RETURN data;
}

MACRO TestChanges()
{
  DATETIME now := GetCurrentDateTime();

  OBJECT schemaobj := testfw->GetWRDSchema();
  OBJECT persontype := schemaobj->^wrd_person;

  TestEq(keephistorydays, persontype->keephistorydays);
  TestEq(keephistorydays, schemaobj->^personattachment->keephistorydays);

  INTEGER testpersonid := persontype->Search("WRD_CONTACT_EMAIL", "other@example.com");
  OBJECT testpersonobj := persontype->GetEntity(testpersonid);

  RECORD oldsettings := testpersonobj->GetFields(["*"]);
  //dumpvalue(oldsettings,'tree');
  INTEGER image_blob_id := GetWHDBBlobInternalId(oldsettings.test_image.data);

  {
    testfw->BeginWork();

    // Two separate changes in separate transactions, each within its own changeset
    RECORD prefields := testpersonobj->GetFields([ "wrd_firstname", "test_free", "test_file", "test_array", "wrd_modificationdate" ]);
    testpersonobj->UpdateEntity(
        [ wrd_firstname := "updated first name"
        , test_free := "updated test field"
        , test_file := oldsettings.test_image
        , test_array := [ [ test_int := 1 ] ]
        ]);

    testfw->CommitWork();
    testfw->BeginWork();

    RECORD intfields := testpersonobj->GetFields([ "wrd_firstname", "test_free", "test_file", "test_array", "wrd_modificationdate" ]);
    testpersonobj->UpdateEntity(
        [ test_file := DEFAULT RECORD
        ]);

    RECORD postfields := testpersonobj->GetFields([ "test_file", "wrd_modificationdate" ]);

    testfw->CommitWork();

    // ListChangesets returns all changesets, including those generated by the modifications by CreateWRDTestSchema. We're only
    // interested in the changes made here (i.e. after the test user has been set).
    RECORD ARRAY changesets := SELECT * FROM persontype->ListChangesets(testpersonid) WHERE entity > 0;
    TestEq(3, Length(changesets)); // schema setup, and then two updates with known entity

    RECORD ARRAY changes := persontype->GetChanges(changesets[1].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           intfields.wrd_modificationdate
          , oldsettings :=    prefields
          , modifications :=  intfields
          ]
        ], changes);

    changes := persontype->GetChanges(changesets[2].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           postfields.wrd_modificationdate
          , oldsettings :=    CELL[ intfields.test_file, intfields.wrd_modificationdate ]
          , modifications :=  postfields
          ]
        ], changes);
  }

  RECORD ARRAY arrayvalue := testpersonobj->GetField("test_array");

  testfw->BeginWork();

  DATETIME test_date := MakeDate(2020, 2, 2);
  INTEGER test_domainvalue_1_1 := persontype->GetDomVal("test_single_domain", "test_domainvalue_1_1");
  INTEGER test_domainvalue_1_2 := persontype->GetDomVal("test_single_domain", "test_domainvalue_1_2");
  INTEGER test_domainvalue_2_1 := persontype->GetDomVal("test_multiple_domain", "test_domainvalue_2_1");
  INTEGER test_domainvalue_2_2 := persontype->GetDomVal("test_multiple_domain", "test_domainvalue_2_2");
  INTEGER test_domainvalue_2_3 := persontype->GetDomVal("test_multiple_domain", "test_domainvalue_2_3");

  // Test source_fsobjects in images/files
  OBJECT destlink := OpenTestsuitesite()->OpenByPath("/testpages/imgeditfile.jpeg");
  RECORD testimage := WrapBlob(destlink->data, "goudvis.png");
  RECORD testfile := [ data := StringToBlob("Ik ben een test")
                     , mimetype := "text/plain"
                     , filename := "testfile.txt"
                     , extension := "txt"
                     ];

  testimage.source_fsobject := destlink->id;
  INSERT CELL source_fsobject := destlink->id INTO testfile;

  {
    RECORD prefields := testpersonobj->GetFields(
        [ "richie"
        , "testinstance"
        , "testintextlink"
        , "test_array"
        , "test_free"
        , "test_file"
        , "test_image"
        , "wrd_firstname"
        , "wrd_modificationdate"
        ]);

    // Two changes within a single changeset
    INTEGER changeset := persontype->CreateChangeset();
    testpersonobj->UpdateEntity(
        [ wrd_firstname :=        ""
        , testinstance :=         [ whfstype := "http://www.webhare.net/xmlns/beta/embedblock1"
                                  , id := "TestInstance-1"
                                  , fsref := destlink->id
                                  ]
        , testintextlink :=       MakeIntExtInternalLink(whconstant_whfsid_webharebackend, "")
        , test_array :=           [ CELL[ ...arrayvalue[0], test_int := 2 ] ]
        , test_file :=            testfile
        , test_free :=            "updated test field2"
        , test_image :=           testimage

        , richie :=               wrdtest_withembedded
        ], CELL[ changeset ]);

    RECORD intfields := testpersonobj->GetFields(
        [ "richie"
        , "testinstance"
        , "testintextlink"
        , "test_array"
        , "test_date"
        , "test_free"
        , "test_file"
        , "test_image"
        , "test_multiple_domain"
        , "test_single_domain"
        , "wrd_firstname"
        , "wrd_modificationdate"
        ]);

    TestEq(intfields.test_image.data, testimage.data);

    testpersonobj->UpdateEntity(
        [ test_date :=            test_date
        , test_single_domain :=   test_domainvalue_1_2
        , test_multiple_domain := INTEGER[ test_domainvalue_2_2
                                         , test_domainvalue_2_1
                                         ]
        , test_array :=           [ [ test_int := 3 ] ]
        ], CELL[ changeset ]);

    RECORD postfields := testpersonobj->GetFields(
        [ "test_array"
        , "test_date"
        , "test_multiple_domain"
        , "test_single_domain"
        , "wrd_modificationdate"
        ]);

    testfw->CommitWork();

    RECORD ARRAY changesets := SELECT * FROM persontype->ListChangesets(testpersonid) WHERE entity > 0;
    TestEq(4, Length(changesets)); // schema setup, initial creation and the above modifications

    RECORD ARRAY changes := persontype->GetChanges(changesets[3].id);
    TestEq(changes[0].modifications.test_image.data, testimage.data);

    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           intfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(prefields)
          , modifications :=  RemoveIrrelevantColumns(MakeReplacedRecord(prefields, intfields))
          ]
        , [ id :=             changes[1].id
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           postfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(MakeReplacedRecord(postfields, intfields))
          , modifications :=  RemoveIrrelevantColumns(postfields)
          ]
        ], RemoveIrrelevantColumns(changes));

    // Test merging changes for the same entity
    TestEQ(
        [ [ id :=             changes[0].id // same id as first change of changeset
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           intfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(CELL[ ...intfields, ...prefields ])
          , modifications :=  RemoveIrrelevantColumns(CELL[ ...intfields, ...postfields ])
          ]
        ], RemoveIrrelevantColumns(persontype->GetChanges(changesets[3].id, [ mergechanges := TRUE ])));
  }

  // payment provider
  {
    testfw->BeginWork();

    OBJECT pm1 := schemaobj->^payprov->CreateEntity(
        [ wrd_title := "Ingenico test"
        , method := MakePaymentProviderValue("wrd:ingenico",
                        [ methods := ["ideal","creditcard","deletedmethod"]
                        , rebranded := "webhare_testsuite"
                        , pspid := "PSPID"
                        , sha1_in := "SHA1IN"
                        , sha1_out := "SHA1OUT"
                        , keypair := OpenKeyPairByName("fallback")->id
                        ])
        ]);

    RECORD initfields := pm1->GetFields([ "wrd_title", "method", "wrd_id", "wrd_creationdate", "wrd_guid", "wrd_limitdate", "wrd_modificationdate" ]);

    pm1->UpdateEntity(
        [ method := MakePaymentProviderValue("wrd:ingenico",
                        [ methods := ["ideal","creditcard","deletedmethod"]
                        , rebranded := "webhare_testsuite"
                        , pspid := "PSPID"
                        , sha1_in := "SHA1IN"
                        , sha1_out := "SHA1OUT"
                        , keypair := 0
                        ])
        ]);

    RECORD postfields := pm1->GetFields([ "method", "wrd_modificationdate" ]);

    testfw->CommitWork();

    RECORD ARRAY changesets := schemaobj->^payprov->ListChangesets(pm1->id);
    RECORD ARRAY changes := schemaobj->^payprov->GetChanges(changesets[0].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         pm1->id
          , changetype :=     "new"
          , when :=           initfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(DEFAULT RECORD)
          , modifications :=  RemoveIrrelevantColumns(initfields)
          ]
        , [ id :=             changes[1].id
          , entity :=         pm1->id
          , changetype :=     "edit"
          , when :=           postfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(MakeReplacedRecord(postfields, initfields))
          , modifications :=  RemoveIrrelevantColumns(postfields)
          ]
        ], RemoveIrrelevantColumns(changes));
  }

  // null update filtering
  {
    testfw->BeginWork();

    STRING ARRAY fields :=
        [ "wrd_firstname", "wrd_lastname", "test_single_domain", "test_single_domain2", "test_single_domain3"
        , "test_free", "test_address", "test_email", "test_phone", "test_date", "test_password"
        , "test_multiple_domain", "test_multiple_domain2", "test_multiple_domain3", "test_image"
        , "test_file", "test_time", "test_datetime", "test_array", "test_money", "test_integer"
        , "test_boolean", "test_enum", "test_enumarray", "test_emptyenum", "test_emptyenumarray"
        , "test_record", "richie", "wrd_modificationdate", "testinstance"
        ];

    // fields we are updating
    STRING ARRAY updated_fields :=
        [ "wrd_lastname", "wrd_modificationdate" ];

    RECORD prefields := testpersonobj->GetFields(fields);
/* code for easy debugging why it goes wrong
    PRINT("***\n\n");
    DumpValue(CELL[ prefields.richie ], "tree");
    Debugger();
    testpersonobj->UpdateEntity(CELL[ prefields.richie ]);
    DumpValue(testpersonobj->GetFields([ "richie" ]), "tree");
    RECORD ARRAY xchangesets := SELECT * FROM persontype->ListChangesets(testpersonid) WHERE entity > 0;
    ABORT(persontype->GetChanges(xchangesets[END-1].id, [ mergechanges := TRUE ]), "tree");
*/

    //Test blob rewrapping
    RECORD updaterecord := CELL[ ...prefields, wrd_lastname := "new lastname", DELETE wrd_modificationdate ];
    updaterecord.test_file := testfile;
    updaterecord.test_file.data := StringToBlob(BlobToString(updaterecord.test_file.data)); //prevent same blobid
    updaterecord.richie.htmltext := StringToBlob(BlobToString(updaterecord.richie.htmltext));

    testpersonobj->UpdateEntity(updaterecord);
    RECORD postfields := testpersonobj->GetFields(fields);

    testfw->CommitWork();

    RECORD ARRAY changesets := SELECT * FROM persontype->ListChangesets(testpersonid) WHERE entity > 0;
    TestEq(5, Length(changesets)); // initial creation and the above modifications

    RECORD ARRAY changes := persontype->GetChanges(changesets[END-1].id, [ mergechanges := TRUE ]);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         testpersonobj->id
          , changetype :=     "edit"
          , when :=           postfields.wrd_modificationdate
          , oldsettings :=    RemoveIrrelevantColumns(FilterFields(prefields, updated_fields))
          , modifications :=  RemoveIrrelevantColumns(FilterFields(postfields, updated_fields))
          ]
        ], RemoveIrrelevantColumns(changes));
  }

  // Deleted entities
  {
    testfw->BeginWork();

    OBJECT newperson := schemaobj->^wrd_person->CreateEntity([ wrd_contact_email := "shortlived@beta.webhare.net" ]);
    OBJECT att := schemaobj->^personattachment->CreateEntity([ wrd_leftentity := newperson->id ]);
    att->UpdateEntity([ wrd_leftentity := testpersonobj->id ]);

    RECORD ARRAY changesets := SELECT * FROM schemaobj->^personattachment->ListChangesets(att->id);

    RECORD ARRAY changes := schemaobj->^personattachment->GetChanges(changesets[0].id); // one entity of this type changed
    TestEQ(newperson->id, changes[1].oldsettings.wrd_leftentity);
    TestEQ(testpersonobj->id, changes[1].modifications.wrd_leftentity);

    newperson->DeleteEntity();

    changes := schemaobj->^personattachment->GetChanges(changesets[0].id); // one entity of this type changed
    TestEQ(0, changes[1].oldsettings.wrd_leftentity); // can't reconstruct
    TestEQ(testpersonobj->id, changes[1].modifications.wrd_leftentity);

    testfw->CommitWork();
  }

  // Temporary objects
  {
    testfw->BeginWork();

    OBJECT tempperson := schemaobj->^wrd_person->CreateEntity([ wrd_contact_email := "temporary+1@beta.webhare.net" ], [ temp := TRUE ]);
    TestEQ(RECORD[], schemaobj->^wrd_person->ListChangesets(tempperson->id));

    tempperson->UpdateEntity([ wrd_contact_email := "temporary+2@beta.webhare.net" ]);
    TestEQ(RECORD[], schemaobj->^wrd_person->ListChangesets(tempperson->id));

    tempperson->UpdateEntity([ wrd_contact_email := "temporary+2@beta.webhare.net", wrd_creationdate := GetCurrentDateTime(), wrd_limitdate := MAX_DATETIME ]);
    RECORD postfields := tempperson->GetFields([ "wrd_contact_email", "wrd_id", "wrd_creationdate", "wrd_guid", "wrd_limitdate", "wrd_modificationdate" ]);

    testfw->CommitWork();

    RECORD ARRAY changesets := SELECT * FROM schemaobj->^wrd_person->ListChangesets(tempperson->id);
    TestEQ(1, LENGTH(changesets));

    RECORD ARRAY changes := schemaobj->^wrd_person->GetChanges(changesets[0].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         tempperson->id
          , changetype :=     "new"
          , when :=           postfields.wrd_modificationdate
          , modifications :=  postfields
          , oldsettings :=    DEFAULT RECORD
          ]
        ], changes);

    testfw->BeginWork();
    tempperson->CloseEntity();
    testfw->CommitWork();

    changesets := SELECT * FROM schemaobj->^wrd_person->ListChangesets(tempperson->id);
    TestEQ(2, LENGTH(changesets));

    RECORD postclosefields := tempperson->GetFields([ "wrd_limitdate", "wrd_modificationdate" ]);

    changes := schemaobj->^wrd_person->GetChanges(changesets[1].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         tempperson->id
          , changetype :=     "close"
          , when :=           postclosefields.wrd_modificationdate
          , oldsettings :=    CELL[ postfields.wrd_limitdate, postfields.wrd_modificationdate ]
          , modifications :=  postclosefields
          ]
        ], changes);
  }

  // Entity that is created and closed in the same changeset
  {
    testfw->BeginWork();
    OBJECT immclose := schemaobj->^wrd_person->CreateEntity([ wrd_contact_email := "temporary+1@beta.webhare.net", wrd_limitdate := GetCurrentDateTime() ]);
    testfw->CommitWork();

    RECORD postimmclosefields := immclose->GetFields([  "wrd_contact_email", "wrd_id", "wrd_creationdate", "wrd_guid", "wrd_limitdate", "wrd_modificationdate" ]);

    RECORD ARRAY changesets := SELECT * FROM schemaobj->^wrd_person->ListChangesets(immclose->id);
    TestEQ(1, LENGTH(changesets));

    RECORD ARRAY changes := schemaobj->^wrd_person->GetChanges(changesets[0].id);
    TestEQ(
        [ [ id :=             changes[0].id
          , entity :=         immclose->id
          , changetype :=     "newclose"
          , when :=           postimmclosefields.wrd_modificationdate
          , oldsettings :=    DEFAULT RECORD
          , modifications :=  postimmclosefields
          ]
        ], changes);

    save_postimmclosefields := postimmclosefields;
  }
}

ASYNC MACRO TestHistoryDialogs()
{
  OBJECT schemaobj := testfw->GetWRDSchema();
  OBJECT persontype := schemaobj->^wrd_person;
  INTEGER testpersonid := persontype->Search("WRD_CONTACT_EMAIL", "other@example.com");
  OBJECT testpersonobj := persontype->GetEntity(testpersonid);
  TestEq(TRUE, ObjectExistS(testpersonobj));

  AWAIT ExpectScreenChange(+1, PTR RunWRDHistoryDialog(GetTestController(), testpersonobj));
  TT("changes")->selection := TT("changes")->rows[3]; //select the second changeset, the change on Person
  TT("changedvalues")->selection := SELECT * FROM TT("changedvalues")->rows WHERE prop = "richie";
  TestEQ(TRUE, RecordExists(TT("changedvalues")->selection));

  RECORD ARRAY oldvalue := TT("oldvalue")->ExtractAllLines();
  TestEq("DEFAULT RECORD", oldvalue[0].items[0]->value); //we can't show an emtpy RTD as we lacked the type info

  RECORD ARRAY newvalue := TT("newvalue")->ExtractAllLines();
  TestEqLike("*opens with a heading2*", BlobToString(newvalue[0].items[0]->value.htmltext));

  //Select an array row to ensure deep selections are working
  TT("changedvalues")->selection := SELECT * FROM TT("changedvalues")->rows WHERE prop = "test_array[0].test_int";
  newvalue := TT("newvalue")->ExtractAllLines();
  TestEq("3", newvalue[0].items[0]->value);

  //Test the image!
  TT("changedvalues")->selection := SELECT * FROM TT("changedvalues")->rows WHERE prop = "test_image";
  oldvalue := TT("oldvalue")->ExtractAllLines();
  newvalue := TT("newvalue")->ExtractAllLines();
  TestEq(385, oldvalue[0].items[0]->value.width);
  TestEq(1024, newvalue[0].items[0]->value.width);

  //Let's iterate all the changedvalue rows, just to make sure none of them crashes
  FOREVERY(RECORD row FROM TT("changedvalues")->rows)
    TT("changedvalues")->selection := [row];

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

MACRO TestExportImport()
{
  OBJECT exporter := MakeWRDSchemaExporter(testfw->GetWRDSchema());
  BLOB result := exporter->RunExport();
  testfw->BeginWork();
  DELETE FROM wrd.schemas WHERE id = testfw->GetWRDSchema()->id; //sync delete so we can be sure we can't reference it

  OBJECT importedschema := CreateWRDSchemaFromImport(result);
  OBJECT persontype := importedschema->^wrd_person;
  INTEGER immcloseid := persontype->Search("wrd_contact_email",  "temporary+1@beta.webhare.net", [ historymode := "all" ]);

  RECORD ARRAY changesets := SELECT * FROM persontype->ListChangesets(immcloseid);
  TestEq(1, LENGTH(changesets));

  RECORD ARRAY changes := persontype->GetChanges(changesets[0].id);
  TestEQ(
      [ [ id :=             changes[0].id
        , entity :=         immcloseid
        , changetype :=     "newclose"
        , when :=           save_postimmclosefields.wrd_modificationdate
        , oldsettings :=    DEFAULT RECORD
        , modifications :=  save_postimmclosefields
        ]
      ], changes);

  INTEGER testpersonid := persontype->Search("WRD_CONTACT_EMAIL", "other@example.com");
  TestEq(TRUE, testpersonid != 0);
  OBJECT testpersonobj := persontype->GetEntity(testpersonid);

  changesets := SELECT * FROM persontype->ListChangesets(testpersonid);

  TestEq(5, Length(changesets)); // schema setup, and then two updates with known entity

  changes := persontype->GetChanges(changesets[3].id);

  OBJECT destlink := OpenTestsuitesite()->OpenByPath("/testpages/imgeditfile.jpeg");
  RECORD testimage := WrapBlob(destlink->data, "goudvis.png");
  TestEq(changes[0].modifications.test_image.data, testimage.data);

  testfw->CommitWork();
}

RunTestframework([ PTR CreateWRDTestSchema(CELL[ keephistorydays, forcereload := TRUE, withrichdoc := TRUE ])
                 , PTR TestChanges
                 , PTR TestHistoryDialogs
                 , PTR TestExportImport
                 ], [ testusers := [ [ login := "sysop", grantrights := ["system:sysop"] ] ]
                 ]);
