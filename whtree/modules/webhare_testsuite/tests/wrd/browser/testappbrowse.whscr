<?wh
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/internal/wrdtesthelpers.whlib";

OBJECT announcemententity;

ASYNC MACRO TestWRDBrowser()
{
  //Enable history on wrd_persons so we can test the history browser
  testfw->BeginWork();
  testfw->GetWRDSchema()->^wrd_person->UpdateMetadata([ keephistorydays := 7 ]);
  testfw->GetWRDSchema()->^wrd_person->UpdateAttribute("wrdauth_account_status", [isrequired := FALSE]);
  announcemententity := testfw->GetWRDSchema()->^whuser_announcement->CreateEntity( [ ANNOUNCEMENT_TITLE := "Announcement test"]) ; //helps trigger selectentity crash
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("wrd:browser", [ params := STRING[OpenTestsuiteWRDSchema()->tag]] ));
  TT("freetype")->value := testfw->GetWRDSchema()->^wrd_person->id;
  AWAIT ExpectScreenChange(+1, PTR topscreen->newfree->TolliumClick);
  TT("entity")->GetComponent("wrd_lastname")->value := "TestAppBrowse";
  TT(":Unit")->value := testfw->testunit;
  TestEq(0, TT("entity")->GetComponent("personlink")->value);

  INTEGER johndoe := testfw->GetWRDSchema()->^wrd_person->Search("WRD_CONTACT_EMAIL", "other@example.com");
  TestEQ(TRUE, johndoe != 0);

  TT("entity")->GetComponent("personlink")->value :=  johndoe;
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit);
  TT("quickfilter->freeattr")->value := "WRD_LASTNAME";
  TT("quickfilter->freevalue")->value := "TestAppBrowse";
  TTClick("quickfilter->searchfree");

  TestEq(1, Length(TT("results_frees")->rows), 'only our recent entity should return');
  //ADDME shouldn't we autoselect?
  //TestEq(1, Length(TT("results_frees")->selection));
  TT("results_frees")->selection := TT("results_frees")->rows;
  topscreen->frame->focused := TT("results_frees");

  ///////////////////////
  //
  // STORY: Create history, test it, test restoring it
  INTEGER appbrowse := testfw->GetWRDSchema()->^wrd_person->Search("WRD_LASTNAME", "TestAppBrowse");
  TestEQ(TRUE, appbrowse != 0);

  testfw->BeginWork();
  testfw->GetWRDSchema()->^wrd_person->UpdateEntity(appbrowse, [ richie := wrdtest_withembedded ]);
  testfw->CommitWork();

  testfw->BeginWork();
  testfw->GetWRDSchema()->^wrd_person->UpdateEntity(appbrowse, [ richie := DEFAULT RECORD]);
  testfw->CommitWork();

  //Open history
  AWAIT ExpectScreenChange(+1, PTR TTClick("showhistory"));
  TestEq(6, Length(TT("changes")->rows)); // one changeset with one changed entity
  TT("changes")->selection := TT("changes")->rows[3]; //select the 2nd most recent change
  TT("changedvalues")->selection := SELECT * FROM TT("changedvalues")->rows WHERE prop="richie";
  TestEq(TRUE, RecordExists(TT("changedvalues")->selection));
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick("restorenewvalue"));
  TestEq(wrdtest_withembedded.htmltext, testfw->GetWRDSchema()->^wrd_person->GetEntityField(appbrowse, "RICHIE").htmltext);
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick("restoreoldvalue"));
  TestEq(DEFAULT RECORD, testfw->GetWRDSchema()->^wrd_person->GetEntityFielD(appbrowse, "RICHIE"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  AWAIT ExpectScreenChange(+1, PTR TTClick("mergeentity"));
  TT("ents")->selection := SELECT * FROM TT("ents")->rows WHERE col0="Sysop McTestsuite";
  TestEq(TRUE, RecordExists(TT("ents")->selection));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));
  AWAIT ExpectAndAnswerMessageBox("YES", DEFAULT MACRO PTR);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestWRDBrowserGoto()
{
  AWAIT ExpectScreenChange(+2, PTR TTLaunchApp("wrd:browser"));
  AWAIT ExpectScreenChange(+1, PTR TTClick(":Find by wrdId"));
  TT("id")->value := announcemententity->id;
  AWAIT ExpectScreenChange(-2, PTR TTClick(":OK"));
  TestEq(announcemententity->id, TT("results_frees")->value[0]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestFramework([ PTR CreateWRDTestSchema
                 , PTR TestWRDBrowser
                 , PTR TestWRDBrowserGoto
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"]]]]);
