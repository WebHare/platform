<?wh
//This is a port of basetest.whscr to moduledesigns
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::publisher/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/outputmedia/analyzer.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestTestSite()
{
  WaitForPublishCompletion(OpenTestsuiteSite()->id);

  //some things can already be tested using the webhare testsuite site, so do those first
  OBJECT staticpage := OpenTestsuiteSite()->OpenByPath("testpages/staticpage");
  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/staticpage"));
  OBJECT contentdiv := testfw->browser->document->GetElementById("content");

  TestEq(staticpage->whfspath, testfw->browser->QS("#content")->GetAttribute("data-navigationobjectpath"));
  TestEq(staticpage->whfspath, testfw->browser->QS("#content")->GetAttribute("data-targetobjectpath"));
  TestEq(staticpage->whfspath, testfw->browser->QS("#content")->GetAttribute("data-contentobjectpath"));

  TestEq(TRUE, contentdiv->HasAttribute('data-sitelanguage-en'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-nl'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-ps'));

  contentdiv := testfw->browser->document->GetElementById("content");
  TestEq("en", contentdiv->GetAttribute('data-languagecode'));
  TestEq("", contentdiv->GetAttribute('data-languagecountry'));

  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/staticpage-ps-af"));
  contentdiv := testfw->browser->document->GetElementById("content");

  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-en'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-nl'));
  TestEq(TRUE, contentdiv->HasAttribute('data-sitelanguage-ps'));
  TestEq("ps-AF", testfw->browser->document->documentelement->GetAttribute("lang"));
  TestEq("ps", contentdiv->GetAttribute('data-languagecode'));
  TestEq("AF", contentdiv->GetAttribute('data-languagecountry'));

  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/staticpage-en-gb"));
  contentdiv := testfw->browser->document->GetElementById("content");
  TestEq("en-GB", testfw->browser->document->documentelement->GetAttribute("lang"));
  TestEq("en", contentdiv->GetAttribute('data-languagecode'));
  TestEq("GB", contentdiv->GetAttribute('data-languagecountry'));

  TestEq(TRUE, contentdiv->HasAttribute('data-sitelanguage-en'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-nl'));
  TestEq(FALSe, contentdiv->HasAttribute('data-sitelanguage-ps'));

  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/simpletest"));
  TestEq("simpletest.rtd - OneOfTheSimpleFiles", testfw->browser->QS(`meta[name="description"]`)->GetAttribute("content"));
}

MACRO Prepare()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  RECORD instancedata := siteroot->GetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/webdesign/tests");
  TestEq(TRUE, CellExists(instancedata.file, "__BLOBSOURCE"));

  OBJECT loadlibdoc := siteroot->OpenByPath("loadlib.html");
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(0, GetErrorFromPublished(loadlibdoc->published), "loadlib.html is no longer special so now it publishes");
  TestEq("testoutput/webhare_testsuite.testoutput/site/emptyfilefolder/%5Eindex.html/pagina2.html", UnpackURL(GetFileSubpageURL(emptyfile->id,"pagina2.html")).urlpath);
  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
}


MACRO TestSimplePages()
{
  OBJECT doc;

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT simplexlsx := siteroot->OpenByPath("simple.xlsx");
  TestEq(0, GetErrorFromPublished(simplexlsx->published), "Failed to publish " || simplexlsx->url);

  testfw->BeginWork();

  OBJECT statichtmldoc := siteroot->OpenByPath("static.html");
  testfw->CommitWork();

  RECORD publishres := RunFilePublish(statichtmldoc->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);
  TestEq(2,Length(publishres.files));

  BLOB htmldoc := SELECT AS BLOB data FROM publishres.files WHERE name="static.html";
  doc := MakeXMLDocumentFromHTML(htmldoc);

  OBJECT tiddiv := doc->GetElementById("basetitle");
  TestEq("Basetest title", tiddiv->textcontent);

  OBJECT contentdiv := doc->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));
  TestEq("null", contentdiv->GetAttribute("data-dynamicpageparameters"), "dynamic params should be unset during static publication of a StaticPageBase");

  OBJECT ARRAY bolds := contentdiv->GetChildElementsByTagName("b")->GetCurrentElements();
  TestEq(1, Length(bolds));
  Testeq("test", bolds[0]->firstchild->nodevalue);

  testfw->browser->GotoWebPage(siteroot->url || "loadlib.shtml");
  TestEq(403, testfw->browser->GetHTTPStatusCode(), "SHTML files on the output cannot be accessed as their location is untrusted");
}

MACRO TestDynPage()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  STRING url := siteroot->url || "dynamicpage.shtml";
  testfw->browser->GotoWebPage(url);
  TestEq(403, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);

  testfw->browser->GotoWebPage(siteroot->url || "contentlink.shtml");
  TestEq(403, testfw->browser->GetHTTPStatusCode(), siteroot->url || "contentlink.shtml");
}

MACRO TestErrorPages()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  testfw->browser->GotoWebPage(siteroot->url || "bestaatniet");
  TestEq(404, testfw->browser->GetHTTPStatusCode());

  //SendBlobTo(0, testfw->browser->content);

  OBJECT contentdiv := testfw->browser->document->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv), "Testing " || siteroot->url || "bestaatniet\n");
  TestEqLike("*The requested page could not be found*", contentdiv->innerxml);
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));
}

MACRO TestCaptureSubPaths()
{
  testfw->BeginWork();
  OBJECT captures := testfw->GetTestSite()->rootobject->CreateFolder([ name := "captures" ]);

  OBJECT file_capture_static := captures->CreateFile(
        [ name := "capture_static"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/staticcontent")->id
        , publish := TRUE
        , data := StringToBlob("static file")
        ]);

  OBJECT file_capture_static_webdesign := captures->CreateFile(
        [ name := "capture_static_webdesign"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/staticcontent_webdesign")->id
        , publish := TRUE
        , data := StringToBlob("static file")
        ]);

  OBJECT file_capture_dynamic := captures->CreateFile(
        [ name := "capture_dynamic"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ]);

  OBJECT captureindexdir := captures->CreateFolder([name := "indexdir"]);
  OBJECT file_capture_index := captureindexdir->CreateFile(
        [ name := "capture_index"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ], [ setindex := TRUE ]);

  OBJECT capture_in_capturedir := captureindexdir->CreateFolder([name := "indexdir"]);
  OBJECT capture_in_captureindex := capture_in_capturedir->CreateFile(
        [ name := "capture_in_captureindex"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ], [ setindex := TRUE ]);


  testfw->CommitWork();

  WaitForPublishCompletion(testfw->GetTestSite()->rootobject->id, [ accepterrors := TRUE ]);

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static->url || "with_subpath"));

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static_webdesign->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static_webdesign->url || "with_subpath"));

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_dynamic->url));
  TestEQ("", testfw->browser->document->GetElementById("subpath")->textcontent);
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_dynamic->url || "with_subpath"));
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent);

  ScanSingleFolder(captureindexdir->id);

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_index->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_index->url || "with_subpath"));
  TestEQ(ToString(file_capture_index->id), testfw->browser->QS("#wcsresult")->textcontent);
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent);

  TestEQ(TRUE, testfw->browser->GotoWebPage(capture_in_capturedir->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(capture_in_capturedir->url || "with_subpath"));
  TestEQ(ToString(capture_in_captureindex->id), testfw->browser->QS("#wcsresult")->textcontent);
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent); //ensure we matched the INNER file
}

RunTestframework([ PTR TestTestSite
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, witherrors := TRUE])
                 , PTR Prepare
                 , PTR TestSimplePages
                 , PTR TestDynPage
                 , PTR TestErrorPages
                 , PTR TestCaptureSubPaths
                 ], [ profile := TRUE ]);
