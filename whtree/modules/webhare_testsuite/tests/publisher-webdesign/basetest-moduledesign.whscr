<?wh
//This is a port of basetest.whscr to moduledesigns
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internal/testfuncs.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::publisher/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/outputmedia/analyzer.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestTestSite()
{
  //some things can already be tested using the webhare testsuite site, so do those first
  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/staticpage"));
  OBJECT contentdiv := testfw->browser->document->GetElementById("content");

  TestEq(TRUE, contentdiv->HasAttribute('data-sitelanguage-en'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-nl'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-xyz'));

  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/staticpage-xyz"));
  contentdiv := testfw->browser->document->GetElementById("content");

  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-en'));
  TestEq(FALSE, contentdiv->HasAttribute('data-sitelanguage-nl'));
  TestEq(TRUE, contentdiv->HasAttribute('data-sitelanguage-xyz'));

  TestEq(TRUE, testfw->browser->GotoWebPage(OpenTestsuiteSite()->webroot || "testpages/simpletest"));
  TestEq("simpletest.rtd", testfw->browser->QS(`meta[name="description"]`)->GetAttribute("content"));
}

MACRO Prepare()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  RECORD instancedata := siteroot->GetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/webdesign/tests");
  TestEq(TRUE, CellExists(instancedata.file, "__BLOBSOURCE"));

  OBJECT loadlibdoc := siteroot->OpenByPath("loadlib.html");
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(102, GetErrorFromPublished(loadlibdoc->published), "expected 102 - runtime error publishing #" || loadlibdoc->id); // must be 102 - runtime error
  TestEq("testoutput/webhare_testsuite.testoutput/site/emptyfilefolder/%5Eindex.html/pagina2.html", UnpackURL(GetFileSubpageURL(emptyfile->id,"pagina2.html")).urlpath);
  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
}


MACRO TestSimplePages()
{
  OBJECT doc;

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT simplexlsx := siteroot->OpenByPath("simple.xlsx");
  TestEq(0, GetErrorFromPublished(simplexlsx->published), "Failed to publish " || simplexlsx->url);

  testfw->BeginWork();

  OBJECT statichtmldoc := siteroot->OpenByPath("static.html");
  testfw->CommitWork();

  RECORD publishres := RunFilePublish(statichtmldoc->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);
  TestEq(2,Length(publishres.files));

  BLOB htmldoc := SELECT AS BLOB data FROM publishres.files WHERE name="static.html";
  doc := MakeXMLDocumentFromHTML(htmldoc);

  OBJECT tiddiv := doc->GetElementById("basetitle");
  TestEq("Basetest title", tiddiv->textcontent);

  OBJECT contentdiv := doc->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));
  TestEq(DEFAULT RECORD, DecodeHSON(contentdiv->GetAttribute("data-dynamicpageparameters")), "dynamic params should be unset during static publication of a StaticPageBase");

  OBJECT ARRAY bolds := contentdiv->GetChildElementsByTagName("b")->GetCurrentElements();
  TestEq(1, Length(bolds));
  Testeq("test", bolds[0]->firstchild->nodevalue);

  testfw->browser->GotoWebPage(siteroot->url || "loadlib.shtml");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), "Trying " || siteroot->url || "loadlib.shtml");

  STRING result := BlobToString(testfw->browser->content,-1);
  TestEq("<!-- LOADING IT -->", Left(result,19));

  testfw->browser->GotoWebPage(siteroot->url || "prebuiltshtml");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), "Trying " || siteroot->url || "prebuiltshtml");
  TestEq(siteroot->OpenByPath("prebuiltshtml")->id, ToInteger(testfw->browser->document->GetElementById('wcsresult')->textcontent,0));

  testfw->browser->GotoWebPage(siteroot->url || "prebuilthtml");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), "Trying " || siteroot->url || "prebuilthtml");
  TestEq("WebStaticPage", testfw->browser->document->GetElementById('webstaticpage')->textcontent);

  publishres := RunFilePublish(siteroot->OpenByPath("prebuilthtml")->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');
  TestEq(TRUE, publishres.success);
}

MACRO TestDynPage()
{

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  STRING url := siteroot->url || "dynamicpage.shtml";
  testfw->browser->GotoWebPage(url);
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);

  //sendblobto(0,testfw->browser->content);

  OBJECT contentdiv := testfw->browser->document->GetElementById("content");
  IF(NOT ObjectExists(contentdiv))
    print(siteroot->url || "bestaatniet\n");

  OBJECT testdyn := testfw->browser->document->GetElementById("testdyn");
  TestEq(TRUE, ObjectExists(testdyn));
  TestEq("Dit is <b>bold</b><br/>volgende<br/>regel param:<i>Arg1</i>", testdyn->innerxml, url);
  TestEq(siteroot->url || "dynamicpage.shtml", testdyn->GetAttribute("data-baseurl"));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));

  OBJECT testarthur := testfw->browser->document->GetElementById("meaningoflife");
  TestEq(TRUE, ObjectExists(testarthur));
  TestEq("42", testarthur->innerxml);
}

MACRO TestErrorPages()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  testfw->browser->GotoWebPage(siteroot->url || "bestaatniet");
  TestEq(404, testfw->browser->GetHTTPStatusCode());

  //SendBlobTo(0, testfw->browser->content);

  OBJECT contentdiv := testfw->browser->document->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv), "Testing " || siteroot->url || "bestaatniet\n");
  TestEqLike("*The requested page could not be found*", contentdiv->innerxml);
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));

  OBJECT compileerror := siteroot->OpenByPath("compileerror.shtml");
  OBJECT runtimeerror := siteroot->OpenByPath("exception.shtml");

  testfw->browser->GotoWebPage(siteroot->url || "compileerror.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode());
  TestEqLike("*XYZ*", testfw->browser->document->body->textcontent);

  testfw->browser->GotoWebPage(siteroot->url || "exception.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode());
  TestEqLike("*this is an exception*", testfw->browser->document->body->textcontent);

}

MACRO TestWordDoc()
{
  OBJECT doc;


  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT headingsdoc := siteroot->OpenByPath("headings.doc");

  RECORD publishres := RunFilePublish(headingsdoc->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);
  TestEq(2,Length(publishres.files));

  BLOB htmldoc := SELECT AS BLOB data FROM publishres.files WHERE name="index.html";
  doc := MakeXMLDocumentFromHTML(htmldoc);

  OBJECT contentdiv := doc->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));
  TestWordDocContents(siteroot, contentdiv, "publishres.files: index.html");

  BLOB textdoc := SELECT AS BLOB data FROM publishres.files WHERE name="test.txt";
  TestEq(RepeatText("dit was een test\n", 10), BlobToString(textdoc,-1));

  testfw->browser->GotoWebPage(GetFileSubpageURL(headingsdoc->id, "test.txt"));
  TestEq(200, testfw->browser->GetHTTPStatusCode());
  TestEq(RepeatText("dit was een test\n", 10), BlobToString(testfw->browser->content,-1));

  TestEq(TRUE, GetFileSubpageURL(headingsdoc->id, "test.txt") LIKE GetFileSubpageURL(headingsdoc->id, "") || "*"); //consilio's filter relies on this too
}

MACRO TestWordDocContents(OBJECT siteroot, OBJECT contentdiv, STRING annotation)
{
  TestEq(TRUE, OBjectExists(contentdiv), "No contentdiv - " || annotation);
  OBJECT ARRAY paras := contentdiv->GetChildElementsByTagName("*")->GetCurrentElements();
  TestEq(7,Length(paras)); //7 elements, as one got aborsed
  TestEq("h2",paras[0]->tagname); //mapped to H2 in this site profile
  TestEq("script", paras[6]->tagname); //raw html

  OBJECT firstspan := paras[0]->firstchild;

  TestEq("span", firstspan->tagname);
  TestEq(TRUE, firstspan->GetAttribute("style") LIKE "*font-family*");
  TestEq(FALSE, firstspan->GetAttribute("style") LIKE "*font-size*");
}
MACRO TestDeletedLinkDest()
{
  testfw->BeginWork();

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  siteroot->OpenByPath("dynamicpage.shtml")->RecycleSelf();
  siteroot->OpenByPath("contentlink.shtml")->ScheduleRepublish();

  testfw->CommitWork();
  testfw->WaitForPublishCompletion(siteroot->id);

  testfw->browser->GotoWebPage(siteroot->url || "contentlink.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode(), siteroot->url || "contentlink.shtml");
  TestEqLike("*links to deleted file*", BlobToString(testfw->browser->content), testfw->browser->href);
}

MACRO TestCaptureSubPaths()
{
  testfw->BeginWork();
  OBJECT captures := testfw->GetTestSite()->rootobject->CreateFolder([ name := "captures" ]);

  OBJECT file_capture_static := captures->CreateFile(
        [ name := "capture_static"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/staticcontent")->id
        , publish := TRUE
        , data := StringToBlob("static file")
        ]);

  OBJECT file_capture_static_webdesign := captures->CreateFile(
        [ name := "capture_static_webdesign"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/staticcontent_webdesign")->id
        , publish := TRUE
        , data := StringToBlob("static file")
        ]);

  OBJECT file_capture_dynamic := captures->CreateFile(
        [ name := "capture_dynamic"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ]);

  OBJECT captureindexdir := captures->CreateFolder([name := "indexdir"]);
  OBJECT file_capture_index := captureindexdir->CreateFile(
        [ name := "capture_index"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ], [ setindex := TRUE ]);

  OBJECT file_prebuilt_static := captures->CreateFile(
        [ name := "prebuilt_static"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/prebuiltpage")->id
        , publish := TRUE
        ]);
  file_prebuilt_static->SetInstanceData("http://www.webhare.net/xmlns/publisher/prebuiltpage", [ prebuilttag := "prebuilthtml_csp" ]);

  OBJECT file_prebuilt_dynamic := captures->CreateFile(
        [ name := "prebuilt_dynamic"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/prebuiltpage")->id
        , publish := TRUE
        ]);
  file_prebuilt_dynamic->SetInstanceData("http://www.webhare.net/xmlns/publisher/prebuiltpage", [ prebuilttag := "prebuiltshtml_csp" ]);

  OBJECT capture_in_capturedir := captureindexdir->CreateFolder([name := "indexdir"]);
  OBJECT capture_in_captureindex := capture_in_capturedir->CreateFile(
        [ name := "capture_in_captureindex"
        , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/capturesubpath/dynamiccontent")->id
        , publish := TRUE
        ], [ setindex := TRUE ]);


  testfw->CommitWork();

  testfw->WaitForPublishCompletion(testfw->GetTestSite()->rootobject->id);

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static->url || "with_subpath"));

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static_webdesign->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_static_webdesign->url || "with_subpath"));

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_dynamic->url));
  TestEQ("", testfw->browser->document->GetElementById("subpath")->textcontent);
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_dynamic->url || "with_subpath"));
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent);

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_prebuilt_static->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_prebuilt_static->url || "with_subpath"));

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_prebuilt_dynamic->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_prebuilt_dynamic->url || "with_subpath"));

  ScanSingleFolder(captureindexdir->id);

  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_index->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(file_capture_index->url || "with_subpath"));
  TestEQ(ToString(file_capture_index->id), testfw->browser->QS("#wcsresult")->textcontent);
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent);

  TestEQ(TRUE, testfw->browser->GotoWebPage(capture_in_capturedir->url));
  TestEQ(TRUE, testfw->browser->GotoWebPage(capture_in_capturedir->url || "with_subpath"));
  TestEQ(ToString(capture_in_captureindex->id), testfw->browser->QS("#wcsresult")->textcontent);
  TestEQ("with_subpath", testfw->browser->document->GetElementById("subpath")->textcontent); //ensure we matched the INNER file
}

RunTestframework([ PTR TestTestSite
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, witherrors := TRUE])
                 , PTR Prepare
                 , PTR TestSimplePages
                 , PTR TestDynPage
                 , PTR TestWordDoc
                 , PTR TestErrorPages
                 , PTR TestDeletedLinkDest
                 , PTR TestCaptureSubPaths
                 ], [ profile := TRUE ]);
