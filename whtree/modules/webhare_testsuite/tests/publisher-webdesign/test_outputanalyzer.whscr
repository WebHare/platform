<?wh
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/whfs/contenttypes.whlib";
LOADLIB "mod::publisher/lib/internal/outputmedia/analyzer.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestMoveFile()
{
  testfw->BeginWork();
  OBJECT simplefile := GetTestsuiteTempFolder()->EnsureFolder([name := "folder1"])->CreateFile([name := "simplefile.rtd", publish := TRUE, typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile" ]);
  testfw->CommitWork();

  STRING initialurl := simplefile->link;
  WaitForPublishCompletion(GetTestsuiteTempFolder()->id);
  TestEq(TRUE, testfw->browser->GotoWebPage(initialurl));

  testfw->BeginWork();
  simplefile->MoveTo( GetTestsuiteTempFolder()->EnsureFolder([name := "folder2"]), "");
  testfw->CommitWork();

  simplefile->Refresh();
  WaitForPublishCompletion(GetTestsuiteTempFolder()->id);

  TestEq(TRUE, testfw->browser->GotoWebPage(simplefile->link));

  Print("waiting for old URL to become unavailable\n");
  WHILE(TRUE)
  {
    testfw->browser->GotoWebPage(initialurl);
    IF(testfw->browser->GetHTTPStatusCode() != 200)
      BREAK;

    Sleep(500);
    Print(".");
  }
  Print("\n");
  TestEq(404,testfw->browser->GetHTTPStatusCode()); //there's no url history on the testsite
}


MACRO MyPrepare()
{
  // Hard cleanup for all referenced types
  testfw->BeginWork();
  OBJECT whfs_type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/testdynamicexecution", [ openorphans := TRUE ]);
  IF (ObjectExists(whfs_type))
  {
    DELETE FROM system.fs_objects WHERE COLUMN type = whfs_type->id;
    DELETE FROM system.fs_types WHERE COLUMN id = whfs_type->id;
  }
  testfw->CommitWork();

  TestEq(FALSE, ObjectExists(OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/testdynamicexecution", [ openorphans := TRUE ])));

  PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [ istemplate := FALSE  ]);
}

MACRO TestFixDynamicExecution()
{
  SetupTestModuleToLoadBaseProfile("mod::webhare_testsuite/webfeatures/fixdynamicexecute_nodynexec.siteprl.xml");

  testfw->BeginWork();
  OBJECT workfolder := testfw->GetTestSite()->rootobject->CreateFolder([name := "dynexectests"]);
  OBJECT emptyfile :=  workfolder->CreateFile([name := "emptyfile",  typens := "http://www.webhare.net/xmlns/webhare_testsuite/testdynamicexecution", publish := TRUE ]);
  OBJECT emptyindex := workfolder->CreateFile([name := "emptyindex", typens := "http://www.webhare.net/xmlns/webhare_testsuite/testdynamicexecution", publish := TRUE ]);
  workfolder->UpdateMetadata([ indexdoc := emptyindex->id ]);
  testfw->CommitWork();
  testfw->WaitForPublishCompletion(testfw->GetTestSite()->id);

  testfw->BeginWork();
  RECORD ARRAY issues := ScanSingleFolderForTestFW(workfolder->id);
  TestEq(RECORD[], issues, "There shouldn't be any issues yet");
  testfw->RollbackWork();

  Testeq(TRUE, testfw->browser->GotoWebPage(emptyfile->url));
  TestEq(0, Length(testfw->browser->content));
  Testeq(TRUE, testfw->browser->GotoWebPage(workfolder->url));
  TestEq(0, Length(testfw->browser->content));

  SetupTestModuleToLoadBaseProfile("mod::webhare_testsuite/webfeatures/fixdynamicexecute_dynexec.siteprl.xml");

  testfw->WaitForPublishCompletion(workfolder->id);

  testfw->BeginWork();
  issues := ScanSingleFolderForTestFW(workfolder->id);

  TestEq(2, Length(issues));

  TestEqLike("Republishing*emptyfile*expected index.shtml*", issues[0].msg);
  TestEqLike("Republishing*emptyindex*not named*index.shtml*", issues[1].msg);
  testfw->CommitWork();
  testfw->WaitForPublishCompletion(workfolder->id);

  Testeq(TRUE, testfw->browser->GotoWebPage(emptyfile->url));
  TestEq(TRUE, Length(testfw->browser->content) > 200, "There should be data from the file now");
  Testeq(TRUE, testfw->browser->GotoWebPage(workfolder->url));
  TestEq(TRUE, Length(testfw->browser->content) > 200, "There should be data from the index file now");

  testfw->BeginWork();
  issues := ScanSingleFolderForTestFW(workfolder->id);
  TestEq(RECORD[], issues, "There shouldn't be any issues left after the scans");
  testfw->RollbackWork();
}

RunTestframework([ PTR TestMoveFile
                 , PTR MyPrepare
                 , PTR TestFixDynamicExecution
                 ]);
