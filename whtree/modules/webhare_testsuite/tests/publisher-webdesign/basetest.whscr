<?wh
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internal/testfuncs.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/webcontext.whlib";

STRING siteprof_dynfolder_tryfolder :=
`<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile">
  <sitesettings>
    <webruleset xmlns="http://www.webhare.net/xmlns/system/moduledefinition">
      <webrule path="/dynfolder/" match="initial">
        <tryfile path="siteoutput::[sitename]/dynfolder/index.shtml" />
      </webrule>
    </webruleset>
  </sitesettings>
</siteprofile>`;

MACRO Prepare()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  RECORD instancedata := siteroot->GetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/webdesign/tests");
  TestEq(TRUE, CellExists(instancedata.file, "__BLOBSOURCE"));

  OBJECT loadlibdoc := siteroot->OpenByPath("loadlib.html");
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(102, GetErrorFromPublished(loadlibdoc->published), "expected 102 - runtime error publishing #" || loadlibdoc->id); // must be 102 - runtime error
  TestEq("webhare-testsuite.output/emptyfilefolder/%5Eindex.html/pagina2.html", UnpackURL(GetFileSubpageURL(emptyfile->id,"pagina2.html")).urlpath);
  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
}

MACRO TestSplitBaseAndSubUrl()
{
  STRING ARRAY tests :=
      [ "http://example.com/a.html|"
      , "http://example.com/A.html|"
      , "http://example.com/|"
      , "http://example.com/|index.html"
      , "http://example.com/|Index.html"
      , "http://example.com/dir/|a.html"
      , "http://example.com/dir/|A.html"
      , "http://example.com/a.html/|"
      , "http://example.com/A.html/|"
      , "http://example.com/a.html/|!/"
      , "http://example.com/A.html/|!/"
      , "http://example.com/!ignore/a.html/|!/"
      , "http://example.com/!ignore/a.html/|!/blabla/!f/"
      , "http://example.com/!IgnorO/A.html/|!/BlablA/!f/"
      ];

  FOREVERY (STRING test FROM tests)
  {
    // Determine the reference url (take everything left from '|', remove bang parts, lowercase it)
    STRING refurl := DeTokenize((
        SELECT AS STRING ARRAY ToLowercase(part)
          FROM ToRecordArray(Tokenize(Tokenize(test, "|")[0], "/"), "part")
         WHERE part NOT LIKE "!*"), "/");

    RECORD rec := __SplitBaseAndSubUrl(Substitute(test, "|", ""), UnpackURL(refurl).urlpath);
    TestEQ(test, `${rec.absolutebaseurl}|${rec.subpath}`);

    //Also test when ! is encoded by a client to %21
    rec := __SplitBaseAndSubUrl(Substitute(Substitute(test,'!','%21'), "|", ""), UnpackURL(refurl).urlpath);
    TestEQ(test, `${rec.absolutebaseurl}|${rec.subpath}`);
  }
}

MACRO TestErrorPages()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;

  testfw->browser->GotoWebPage(siteroot->url || "bestaatniet");
  TestEq(404, testfw->browser->GetHTTPStatusCode());

  //SendBlobTo(0, testfw->browser->content);

  OBJECT contentdiv := testfw->browser->document->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv), "Testing " || siteroot->url || "bestaatniet\n");
  TestEqLike("*The requested page could not be found*", contentdiv->innerxml);
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));

  OBJECT compileerror := siteroot->OpenByPath("compileerror.shtml");
  OBJECT runtimeerror := siteroot->OpenByPath("exception.shtml");

  testfw->browser->GotoWebPage(siteroot->url || "compileerror.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode(), testfw->browser->href);
  TestEqLike("*XYZ*", testfw->browser->document->body->textcontent);

  testfw->browser->GotoWebPage(siteroot->url || "exception.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode());
  TestEqLike("*this is an exception*", testfw->browser->document->body->textcontent);

}

MACRO TestWordDoc()
{
  OBJECT doc;


  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT headingsdoc := siteroot->OpenByPath("headings.doc");

  RECORD publishres := RunFilePublish(headingsdoc->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);
  TestEq(2,Length(publishres.files));

  BLOB htmldoc := SELECT AS BLOB data FROM publishres.files WHERE name="index.html";
  doc := MakeXMLDocumentFromHTML(htmldoc);

  OBJECT contentdiv := doc->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));
  TestWordDocContents(siteroot, contentdiv, "publishres.files: index.html");

  BLOB textdoc := SELECT AS BLOB data FROM publishres.files WHERE name="test.txt";
  TestEq(RepeatText("dit was een test\n", 10), BlobToString(textdoc,-1));

  testfw->browser->GotoWebPage(GetFileSubpageURL(headingsdoc->id, "test.txt"));
  TestEq(200, testfw->browser->GetHTTPStatusCode());
  TestEq(RepeatText("dit was een test\n", 10), BlobToString(testfw->browser->content,-1));

  TestEq(TRUE, GetFileSubpageURL(headingsdoc->id, "test.txt") LIKE GetFileSubpageURL(headingsdoc->id, "") || "*"); //consilio's filter relies on this too

}

MACRO TestWordDocContents(OBJECT siteroot, OBJECT contentdiv, STRING annotation)
{
  TestEq(TRUE, OBjectExists(contentdiv), "No contentdiv - " || annotation);
  OBJECT ARRAY paras := contentdiv->GetChildElementsByTagName("*")->GetCurrentElements();
  TestEq(7,Length(paras)); //7 elements, as one got aborsed
  TestEq("h2",paras[0]->tagname); //mapped to H2 in this site profile
  TestEq("script", paras[6]->tagname); //raw html

  OBJECT firstspan := paras[0]->firstchild;

  TestEq("span", firstspan->tagname);
  TestEq(TRUE, firstspan->GetAttribute("style") LIKE "*font-family*");
  TestEq(FALSE, firstspan->GetAttribute("style") LIKE "*font-size*");
}

MACRO TestSimplePages()
{
  OBJECT doc;

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT statichtmldoc := siteroot->OpenByPath("static.html");

  RECORD publishres := RunFilePublish(statichtmldoc->id, FALSE);
  IF(NOT publishres.success)
    dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);
  TestEq(2,Length(publishres.files));

  BLOB htmldoc := SELECT AS BLOB data FROM publishres.files WHERE name="static.html";
  doc := MakeXMLDocumentFromHTML(htmldoc);

  OBJECT contentdiv := doc->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));

  OBJECT ARRAY bolds := contentdiv->GetChildElementsByTagName("b")->GetCurrentElements();
  TestEq(1, Length(bolds));
  Testeq("test", bolds[0]->firstchild->nodevalue);

  testfw->browser->GotoWebPage(siteroot->url || "loadlib.shtml");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), "Trying " || siteroot->url || "loadlib.shtml");

  STRING result := BlobToString(testfw->browser->content,-1);
  TestEq("<!-- LOADING IT -->", Left(result,19));
}

MACRO TestDynPage()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  STRING url := siteroot->url || "dynamicpage.shtml";

  //Test it directly first
  OBJECT webdesign := GetWebDesignForURL(url);
  TestEq(url, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url);
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  RECORD whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters);

  webdesign := GetWebDesignForURL(url || "/!/test123");
  TestEq(url || "/", webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("!/test123", webdesign->pageconfig.dynamicpageparameters.subpath);
  // can't get this url via a browser, will get a 404

  STRING url2 := siteroot->url || "!ignored/dynamicpage.shtml";
  webdesign := GetWebDesignForURL(url2);
  TestEq(url2, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url2);
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters);

  webdesign := GetWebDesignForURL(url2 || "/!/test123");
  TestEq(url2 || "/", webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("!/test123", webdesign->pageconfig.dynamicpageparameters.subpath);
  // can't get this url via a browser, will get a 404

  testfw->browser->GotoWebPage(url);
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);

   //sendblobto(0,testfw->browser->content);

  OBJECT contentdiv := testfw->browser->document->GetElementById("content");
  TestEq(TRUE, ObjectExists(contentdiv));
  TestEq("1", contentdiv->GetAttribute("data-webdesign-prepareforrendering"), "WebDesign::PrepareForRendering NOT invoked on " || testfw->browser->href);

  OBJECT testdyn := testfw->browser->document->GetElementById("testdyn");
  TestEq(TRUE, ObjectExists(testdyn));
  TestEq("Dit is <b>bold</b> param:<i>Arg1</i>", testdyn->innerxml, url);
  TestEq(siteroot->url || "dynamicpage.shtml", testdyn->GetAttribute("data-baseurl"));
  TestEq("//" || Tokenize(siteroot->url,"//")[1] || "dynamic.shtml", contentdiv->GetAttribute("data-dynamiclink"));

  OBJECT testarthur := testfw->browser->document->GetElementById("meaningoflife");
  TestEq(TRUE, ObjectExists(testarthur));
  TestEq("42", testarthur->innerxml);

  //Test a folder with index
  url := siteroot->url || "dynfolder/";
  webdesign := GetWebDesignForURL(url);
  TestEq(url, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url);
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters, testfw->browser->url);

  webdesign := GetWebDesignForURL(url || "test123");
  TestEq(url, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("test123", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url || "test123");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters);

  webdesign := GetWebDesignForURL(url || "test123/");
  TestEq(url, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("test123/", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url || "test123/");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters);

  webdesign := GetWebDesignForURL(url || "!/test123");
  TestEq(url, webdesign->pageconfig.dynamicpageparameters.absolutebaseurl);
  TestEq("!/test123", webdesign->pageconfig.dynamicpageparameters.subpath);
  testfw->browser->GotoWebPage(url || "!/test123");
  TestEq(200, testfw->browser->GetHTTPStatusCode(), testfw->browser->url);
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(webdesign->pageconfig.dynamicpageparameters, whconfig.obj.dynamicpageparameters);
}

MACRO TestSiteConfig()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  RECORD whconfig, whconfig2;

  // List of files with non-empty titles
  RECORD ARRAY expect :=
      [ [ name :=     "headings.doc"
        , title :=    "Eerste heading"
        , id :=       siteroot->OpenByPath("headings.doc")->id
        ]
      ];

  TestEQ(TRUE, testfw->browser->GotoWebPage(siteroot->url));
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(expect, whconfig.obj.siteconfig.files);

  TestEQ(TRUE, testfw->browser->GotoWebPage(siteroot->url || "dynamicpage.shtml"));
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(expect, whconfig.obj.siteconfig.files);

  TestEQ(TRUE, testfw->browser->GotoWebPage(siteroot->url || "dynamicpage.shtml"));
  whconfig2 := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(whconfig.obj.siteconfig.uuid, whconfig2.obj.siteconfig.uuid, "Siteconfig should be cached");
  testfw->WaitForPublishCompletion(siteroot->id); //flush publications before we start modifying things

  // test if siteconfig is updated after file change in site
  testfw->BeginWork();
  siteroot->OpenByPath("dynamicpage.shtml")->UpdateMetadata([ title := "Dynamic" ]);
  testfw->CommitWork();

  RECORD ARRAY expect2 :=
      [ [ name :=     "dynamicpage.shtml"
        , title :=    "Dynamic"
        , id :=       siteroot->OpenByPath("dynamicpage.shtml")->id
        ]
      , ...expect
      ];

  TestEQ(TRUE, testfw->browser->GotoWebPage(siteroot->url || "dynamicpage.shtml"));
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(expect2, whconfig.obj.siteconfig.files);

  testfw->WaitForPublishCompletion(siteroot->id);
  TestEQ(TRUE, testfw->browser->GotoWebPage(siteroot->url));
  whconfig := DecodeJSON(testfw->browser->document->GetElementById("wh-config")->textcontent);
  TestEQ(expect2, whconfig.obj.siteconfig.files);
}

MACRO TestDeletedLinkDest()
{
  testfw->BeginWork();

  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  siteroot->OpenByPath("dynamicpage.shtml")->RecycleSelf();
  siteroot->OpenByPath("contentlink.shtml")->ScheduleRepublish();

  testfw->CommitWork();
  testfw->WaitForPublishCompletion(siteroot->id);

  testfw->browser->GotoWebPage(siteroot->url || "contentlink.shtml");
  TestEq(500, testfw->browser->GetHTTPStatusCode(), siteroot->url || "contentlink.shtml");
  TestEqLike("*links to deleted file*", BlobToString(testfw->browser->content));
}

RunTestframework([ PTR PrepareTestSiteDesignWebsite(siteprof_dynfolder_tryfolder, [witherrors:=TRUE])
                 , PTR Prepare
                 , PTR TestSplitBaseAndSubUrl
                 , PTR TestSimplePages
                 , PTR TestWordDoc
                 , PTR TestDynPage
                 , PTR TestErrorPages
                 , PTR TestWordDoc
                 , PTR TestErrorPages
                 , PTR TestSiteConfig
                 , PTR TestDeletedLinkDest
                 ], [ profile := TRUE ]);
