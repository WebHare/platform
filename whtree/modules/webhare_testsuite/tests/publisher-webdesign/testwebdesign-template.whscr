<?wh
/// This test tests setting up and the functionality of 'publisher:blank'
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::system/lib/resources.whlib";

MACRO TestBasics()
{
  testfw->BeginWork();
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT indexhtmldoc := siteroot->CreateFile([name:="index.html", data := StringToBlob("Een homepage"), publish := TRUE, type := 5 ]);
  testfw->CommitWork();
  WaitForPublishCompletion(siteroot->id);

  RECORD mydesign := SELECT * FROM GetAvailableWebDesigns(FALSE) WHERE rowkey = "webhare_testsuite_temp:testdesign";
  TestEq(TRUE, RecordExists(mydesign), 'my test design not visible in GetAvailableWebDesigns (it broke? cache invalidations? siteprofile compilations missing it ?');

  //Verify moduledef
  OBJECT moduledef := MakeXMLDocument(GetWebhareResource("mod::webhare_testsuite_temp/moduledefinition.xml"));
  OBJECT assetpack := moduledef->GetELementsByTagnameNS("http://www.webhare.net/xmlns/system/moduledefinition","assetpack")->Item(0);
  TestEq("en", assetpack->GetAttribute("supportedlanguages"), "should not be 'default'");

  STRING designbase := GetWebHareResourceDiskPath(mydesign.designroot);
  STRING tryfile := designbase || "testdesign.siteprl.xml";
  TestEq(TRUE, RecordExists(GetDiskFileProperties(tryfile)), "No such file " || tryfile);

  IF(testfw->debug)
    Print(`Testing page: ${indexhtmldoc->url}\n`);

  TestEq(TRUE, testfw->browser->GotoWebPage(indexhtmldoc->url));
  RECORD bundle := ExtractBundleOutputTag(testfw->browser->document);
  TestEq("webhare_testsuite_temp:testdesign", bundle.outputtag, "Verify we got the right webdesign");
}

RunTestframework([ PTR PrepareTestModuleWebDesignWebsite("publisher:blank", [istemplate := TRUE, withcontent := FALSE ])
                 , PTR TestBasics
                 , PTR EnsureValidTestModuleWebDesignWebsiteBUndle
                 ]);
