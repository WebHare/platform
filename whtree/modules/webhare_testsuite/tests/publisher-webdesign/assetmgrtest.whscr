<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::filetypes/css.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/control.whlib";


STRING prodbundlecache := "public, max-age=900";
STRING prodassetcache := "public, max-age=31536000, immutable";
STRING devbundlecache := "public, max-age=0, must-revalidate";
STRING devassetcache := "public, max-age=0, must-revalidate";
STRING notfoundcache := "no-cache, no-store";

OBJECT assetpackcontrol := WaitForPromise(OpenWebHareService("platform:assetpacks", [ arguments := VARIANT[ GetCurrentGroupID() ]]));

RECORD FUNCTION EnsureCompiledAssetpack(STRING assetpackname)
{
  RECORD res := WaitForPromise(assetpackcontrol->WaitForCompile(assetpackname));
  IF(res.haserrors)
    DumpValue(res,'tree:3');
  TestEq(FALSE, res.haserrors, `Compile of assetpack '${assetpackname}' should have succeeded`);
  RETURN CELL[ res.lastcompile ];
}

MACRO PrepTest()
{
  IF(WaitForPromise(assetpackcontrol->GetBundleStatus("webhare_testsuite:basetest")).isdev)
  {
    DATETIME lastcompile := EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile;
    WaitForPromise(assetpackcontrol->UpdateBundleSettings("webhare_testsuite:basetest", [ dev := FALSE ]));

    Print("Disabling webhare_testsuite:basetest dev mode\n");
    WHILE(EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile = lastcompile)
      Sleep(100);
    Print("Disabled webhare_testsuite:basetest dev mode\n");
  }
  ELSE IF(GetEnvironmentVariable("WEBHARE_CI") != "")
  {
    ABORT("If WEBHARE_CI is set, we're running inside CI which is dtapstage == development... so why wasn't the 'new' webhare_testsuite:basetest package originally set as .dev then?");
  }
}

RECORD FUNCTION GetCSSStuff()
{
  STRING cssurl := testfw->browser->href;
  OBJECT stylesheet := MakeCSSStyleSheet(testfw->browser->content);
  OBJECT ARRAY testrules := stylesheet->cssrules->GetRulesForSelector("#test");
  TestEq(1, Length(testrules), "Could not find rule for #test in CSS " || cssurl);

  STRING backgroundurl := testrules[0]->style->GetPropertyValue("background");
  TestEqLike('url("*")', backgroundurl);
  RETURN [ backgroundurl := backgroundurl
         , display := (SELECT AS STRING ARRAY value FROM testrules[0]->style->GetAllProperties("display"))
         , cssurl := cssurl
         ];
}

MACRO TestBasics()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);

  OBJECT doc := testfw->browser->document;
  OBJECT ARRAY links := doc->getElements('link[rel="stylesheet"]');
  TestEq(1, Length(links)); //one style.css from the bundler

  // Ensure the assetpack is compiled at least once
  EnsureCompiledAssetpack("webhare_testsuite:basetest");

  STRING csslink := links[0]->GetAttribute("href");
  IF(testfw->debug)
    Print("CSS link: " || csslink||'\n');
  TestEq(TRUE, testfw->browser->GotoWebPage(csslink), csslink);
  TestEq(IsModuleInstalled("dev") ? devbundlecache : prodbundlecache, testfw->browser->GetResponseHeader("Cache-Control"));

  RECORD cssstuff := GetCSSStuff(); //this was a test against the webpack configuration suddenly running a css autoprefixer destroying the things we did..
  TestEq( ["-webkit-flex", "flex" ], cssstuff.display, "Missing webkit-flex in " || csslink);

  STRING backgroundurl := Tokenize(cssstuff.backgroundurl,'"')[1];
  TestEQ(TRUE, BlobToString(testfw->browser->content) LIKE "*DUMMY-TEXT-TO-VERIFY-ADDTOPACK.CSS-GOT-INCLUDED*");
  TestEQ(TRUE, testfw->browser->GotoWebPage(backgroundurl), "Unable to retrieve " || backgroundurl);
  TestEq(IsModuleInstalled("dev") ? devbundlecache : prodassetcache, testfw->browser->GetResponseHeader("Cache-Control"));
  TestEQ(FALSE, testfw->browser->GotoWebPage(backgroundurl||".bla"), "Unexpectedly able to retrieve " || backgroundurl||".bla");
  TestEq(notfoundcache, testfw->browser->GetResponseHeader("Cache-Control"), testfw->browser->href);

  OBJECT ARRAY scripts := doc->getElements('script[src]');
  STRING jslink := scripts[0]->GetAttribute("src");
  TestEq(TRUE, testfw->browser->GotoWebPage(jslink), jslink);
  //test that dev:false is set (minified)
  BLOB minified := testfw->browser->content;

  DATETIME lastcompile := EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile;

  WaitForPromise(assetpackcontrol->UpdateBundleSettings("webhare_testsuite:basetest", [ dev := TRUE ]));

  Print("Enabling webhare_testsuite:basetest dev mode\n");
  WHILE(EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile = lastcompile)
    Sleep(100);
  Print("Enabled webhare_testsuite:basetest dev mode\n");

  TestEq(TRUE, testfw->browser->GotoWebPage(jslink), testfw->browser->href);
  TestEq("", testfw->browser->GetResponseHeader("Content-Encoding"), "should receive an uncompressed version");
  TestAssert(Length(testfw->browser->content) > 1.2*Length(minified), "Expected nonminified to be at least 20% biger");

  //and now test with accept-encoding. we expect not to receive a GZ file... (we regressed earlier and returned a stale .gz file!)
  TestEq(TRUE, testfw->browser->GotoWebPage(jslink, [ headers := [[ field := "Accept-Encoding", value := "gzip" ]]]), testfw->browser->href);
  TestEq("", testfw->browser->GetResponseHeader("Content-Encoding"), "should still receive an uncompressed version");
  TestEq(TRUE, testfw->browser->GotoWebPage(jslink, [ headers := [[ field := "Accept-Encoding", value := "br" ]]]), testfw->browser->href);
  TestEq("", testfw->browser->GetResponseHeader("Content-Encoding"), "should still receive an uncompressed version");
}

MACRO TestAssetpackManifest()
{
  //TODO verify a gzip version is available too of the manifest (accept-encoding gzip or something like that)
  STRING manifesturl := ResolvetoAbsoluteURL(GetPrimaryWebhareInterfaceURL(), `/.wh/ea/ap/webhare_testsuite.basetest/apmanifest.json`);
  TestEq(TRUE, testfw->browser->GotoWebPage(manifesturl));
  RECORD manifest := DecodeJSONBlob(testfw->browser->content);

  // dumpvalue(manifest.assets,'boxed');

  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE NOT compressed AND (subpath LIKE "*.gz" OR subpath LIKE "*.br")), "no unmarked compressed version in the dependent assets please");
  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE NOT sourcemap AND (subpath LIKE "*.map" OR subpath LIKE "*.map.gz")), "no unmarked sourcemaps in the dependent assets please");
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.mjs"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.mjs.gz"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.mjs.br"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.svg"));
}

MACRO TestAssetpackManifestModule()
{
  TestEq(TRUE, testfw->browser->GotoWebPage(testfw->GetTestSite()->webroot));
  TestEq([ jslink := "/.wh/ea/ap/webhare_testsuite.basetest/ap.mjs", outputtag := "webhare_testsuite:basetest", jstype := "module" ], ExtractBundleOutputTag(testfw->browser->document));

  //TODO verify a gzip version is available too of the manifest (accept-encoding gzip or something like that)
  OBJECT testsitejs := OpenSiteByName("webhare_testsuite.testsitejs");
  STRING manifesturl := ResolvetoAbsoluteURL(testsitejs->webroot, `/.wh/ea/ap/webhare_testsuite.basetestjs/apmanifest.json`);
  TestEq(TRUE, testfw->browser->GotoWebPage(manifesturl));
  RECORD manifest := DecodeJSONBlob(testfw->browser->content);

  // dumpvalue(manifest.assets,'boxed');

  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.mjs.gz" AND NOT compressed), "no unmarked compressed version in the dependent assets please");
  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE NOT sourcemap AND (subpath LIKE "*.map" OR subpath LIKE "*.map.gz")), "no unmarked sourcemaps in the dependent assets please");
  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.js"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.mjs"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.svg"));

  TestEq(TRUE, testfw->browser->GotoWebPage(testsitejs->webroot));
  TestEq([ jslink := "/.wh/ea/ap/webhare_testsuite.basetestjs/ap.mjs", outputtag := "webhare_testsuite:basetestjs", jstype := "module" ], ExtractBundleOutputTag(testfw->browser->document));
}

RunTestframework([ PTR PrepTest
                 , PTR TestAssetpackManifest
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, witherrors := TRUE])
                 , PTR TestBasics
                 , PTR TestAssetpackManifestModule
                 ]);
