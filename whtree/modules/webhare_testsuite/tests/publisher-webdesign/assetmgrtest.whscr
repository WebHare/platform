<?wh
/// This test tests setting up and the functionality of 'publisher:blank'
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/css.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::xml/dom.whlib";
LOADLIB "wh::xml/xsd.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/preview.whlib";


STRING prodbundlecache := "public, max-age=900";
STRING prodassetcache := "public, max-age=31536000, immutable";
STRING devbundlecache := "public, max-age=0, must-revalidate";
STRING devassetcache := "public, max-age=31536000, immutable";
STRING notfoundcache := "no-cache";

MACRO TestViewInterceptions()
{
  RECORD res;
  OBJECT doc;

  res := RewriteAssetpackCode(StringToBlob(
`<html><link rel="stylesheet" href="/.ap/blex_mettesites.ferretpedigrees/ap.css"><script src="/.ap/blex_mettesites.ferretpedigrees/ap.js" async></script></head><body></body></html>`),TRUE);

  doc := MakeXMLDocumentFromHTML(res.content); //convert to HTML so we also have a validation
  Testeq(RECORD[],doc->GetParseErrors());
  TestEq('<link href="/.ap/blex_mettesites.ferretpedigrees.dev/ap.css" rel="stylesheet"/>', doc->QuerySelector("link")->outerhtml);
  TestEq('<script src="/.ap/tollium.polyfills/ap.js"><\/script>', doc->GetElements("script")[0]->outerhtml);
  TestEq('<script src="/.ap/tollium.polyfills/ap.js"><\/script>', doc->GetElements("script")[0]->outerhtml);
  TestEq(TRUE, ObjectExists(doc->GetElementById("wh-publisher-outputtools")));
  TestEq("blex_mettesites:ferretpedigrees", res.outputtag);

  res := RewriteAssetpackCode(StringToBlob(
`<html><link rel="stylesheet" href="/.ap/blex_mettesites.ferretpedigrees/ap.css"><script src="/.ap/blex_mettesites.ferretpedigrees/ap.js" async></script></head><body></body></html>`),FALSE);

  doc := MakeXMLDocumentFromHTML(res.content); //convert to HTML so we also have a validation
  Testeq(RECORD[],doc->GetParseErrors());
  TestEq('<link href="/.ap/blex_mettesites.ferretpedigrees.dev/ap.css" rel="stylesheet"/>', doc->QuerySelector("link")->outerhtml);
  TestEq('<script src="/.ap/tollium.polyfills/ap.js"><\/script>', doc->GetElements("script")[0]->outerhtml);
  TestEq('<script async="" src="/.ap/blex_mettesites.ferretpedigrees.dev/ap.js"><\/script>', doc->GetElements("script")[1]->outerhtml);
  TestEq(FALSE, ObjectExists(doc->GetElementById("wh-publisher-outputtools")));
  TestEq("blex_mettesites:ferretpedigrees", res.outputtag);

//  res := RewriteAssetpackCode(StringToBlob(
//`<html><link rel="stylesheet" href="/.ap/blex_mettesites.ferretpedigrees/ap.css"><script src="/.ap/blex_mettesites.ferretpedigrees/ap.js" async></script></head>mbody></body></html>`));
}

RECORD FUNCTION GetCSSStuff()
{
  STRING cssurl := testfw->browser->href;
  OBJECT stylesheet := MakeCSSStyleSheet(testfw->browser->content);
  OBJECT ARRAY testrules := stylesheet->cssrules->GetRulesForSelector("#test");
  TestEq(1, Length(testrules), "Could not find rule for #test in CSS " || cssurl);

  STRING backgroundurl := testrules[0]->style->GetPropertyValue("background");
  TestEqLike('url("*")', backgroundurl);
  RETURN [ backgroundurl := backgroundurl
         , display := (SELECT AS STRING ARRAY value FROM testrules[0]->style->GetAllProperties("display"))
         , cssurl := cssurl
         ];
}

MACRO TestBasics()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT loadlibdoc := siteroot->OpenByPath("loadlib.html");
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);

  OBJECT doc := testfw->browser->document;
  OBJECT ARRAY links := doc->getElements('link[rel="stylesheet"]');
  TestEq(1, Length(links)); //one style.css from webpack

  // Ensure the assetpack is compiled at least once
  testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest");

  STRING csslink := links[0]->GetAttribute("href");
  TestEq(FALSE, csslink LIKE "*.dev/*"); //should not be dev-tagged
  IF(testfw->debug)
    Print("CSS link: " || csslink||'\n');
  TestEq(TRUE, testfw->browser->GotoWebPage(csslink), csslink);
  TestEq(prodbundlecache, testfw->browser->GetResponseHeader("Cache-Control"));

  RECORD cssstuff := GetCSSStuff();
  TestEq( ["-webkit-flex", "flex" ], cssstuff.display, "Missing webkit-flex in " || csslink);

  STRING backgroundurl := Tokenize(cssstuff.backgroundurl,'"')[1];
  TestEQ(TRUE, BlobToString(testfw->browser->content) LIKE "*DUMMY-TEXT-TO-VERIFY-ADDTOPACK.CSS-GOT-INCLUDED*");
  TestEQ(TRUE, testfw->browser->GotoWebPage(backgroundurl), "Unable to retrieve " || backgroundurl);
  TestEq(prodassetcache, testfw->browser->GetResponseHeader("Cache-Control"));
  TestEQ(FALSE, testfw->browser->GotoWebPage(backgroundurl||".bla"), "Unexpectedly able to retrieve " || backgroundurl||".bla");
  TestEq(notfoundcache, testfw->browser->GetResponseHeader("Cache-Control"), testfw->browser->href);
}

MACRO TestViewSHTML()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT emptyfile := siteroot->OpenByPath("bob.jpg");

  testfw->browser->DeleteCookie("__whpub_preview");

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  TestEq("", testfw->browser->GetResponseHeader("X-WHPub-Preview"));
  INTEGER clength := ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0);

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  TestEq("", testfw->browser->GetResponseHeader("X-WHPub-Preview"));
  TestEq("", testfw->browser->GetResponseHeader("Cache-Control"));
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));

  testfw->browser->SetSessionCookie("__whpub_preview", "x");

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
  TestEq("no-cache", testfw->browser->GetResponseHeader("Cache-Control"));

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));

  testfw->browser->SetSessionCookie("__whpub_preview", "debug"); //triggers potential rewrite - but not for bob.jpg

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
  TestEq("no-cache", testfw->browser->GetResponseHeader("Cache-Control"));

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
}


MACRO TestDevVersion()
{
  //Now act like we're a preview
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");
  testfw->browser->SetSessionCookie("__whpub_preview", "debug");
  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);

  // view.shtml would wait for the assetpack
  testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest.dev");

  OBJECT doc := testfw->browser->document;
  OBJECT ARRAY links := doc->getElements('link[rel="stylesheet"]');
  TestEq(2, Length(links), testfw->browser->href); //one from webdesign and one from the outputtools

  STRING csslink := links[0]->GetAttribute("href");
  TestEqLike("*/*.dev/*", csslink);

  OBJECT ARRAY scripts := doc->getElements('script[src]');
  STRING polyfillsrclink := scripts[0]->GetAttribute("src");
  TestEqLike("*/tollium.polyfills/*", polyfillsrclink);
  STRING srclink := scripts[1]->GetAttribute("src");
  TestEqLike("*/*.dev/*", srclink);

  TestEq(TRUE, testfw->browser->GotoWebPage(srclink), testfw->browser->href);
  TestEq(devbundlecache, testfw->browser->GetResponseHeader("Cache-Control"));

  TestEq(TRUE, testfw->browser->GotoWebPage(csslink), testfw->browser->href);
  TestEq(devbundlecache, testfw->browser->GetResponseHeader("Cache-Control"));

  RECORD cssstuff := GetCSSStuff();
  Print(testfw->browser->href||'\n');
  STRING backgroundurl := Tokenize(cssstuff.backgroundurl,'"')[1];
  TestEQ(TRUE, testfw->browser->GotoWebPage(backgroundurl), "Unable to retrieve " || backgroundurl);
  TestEq(devassetcache, testfw->browser->GetResponseHeader("Cache-Control"));
  TestEQ(FALSE, testfw->browser->GotoWebPage(backgroundurl||".bla"), "Unexpectedly able to retrieve " || backgroundurl||".bla");
  TestEq(notfoundcache, testfw->browser->GetResponseHeader("Cache-Control"), testfw->browser->href);
  TestEq( ["-webkit-flex", "flex" ], cssstuff.display, "Missing webkit-flex in " || testfw->browser->href);
}

MACRO TestAssetpackManifest(BOOLEAN isdevversion)
{
  //TODO verify a gzip version is available too of the manifest (accept-encoding gzip or something like that)
  STRING manifesturl := ResolvetoAbsoluteURL(testfw->GetTestSite()->webroot, `/.ap/webhare_testsuite.basetest${isdevversion?".dev":""}/apmanifest.json`);
  TestEq(TRUE, testfw->browser->GotoWebPage(manifesturl));
  RECORD manifest := DecodeJSONBlob(testfw->browser->content);

  dumpvalue(manifest.assets,'boxed');

  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.js.gz" AND NOT compressed), "no unmarked compressed version in the dependent assets please");
  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE NOT sourcemap AND (subpath LIKE "*.map" OR subpath LIKE "*.map.gz")), "no unmarked sourcemaps in the dependent assets please");
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.js"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "st/*.svg"));
}

RunTestframework([ PTR TestViewInterceptions
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, witherrors := TRUE])
                 , PTR TestBasics
                 , PTR TestAssetpackManifest(FALSE)
                 , PTR TestViewSHTML
                 , PTR TestDevVersion
                 , PTR TestAssetpackManifest(TRUE)
                 ]);
