<?wh
/// This test tests setting up and the functionality of 'publisher:blank'
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::filetypes/css.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";

LOADLIB "mod::publisher/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/preview.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/designfilesapi2.whlib";


STRING prodbundlecache := "public, max-age=900";
STRING prodassetcache := "public, max-age=31536000, immutable";
STRING devbundlecache := "public, max-age=0, must-revalidate";
STRING devassetcache := "public, max-age=0, must-revalidate";
STRING notfoundcache := "no-cache";
INTEGER testbundleid;

MACRO PrepTest()
{
  testbundleid := SELECT AS INTEGER id FROM system_internal.assetpacks WHERE outputtag = "webhare_testsuite:basetest";
  TestEq(TRUE, testbundleid > 0);

  IF(GetAssetPackSettings(testbundleid).dev)
  {
    DATETIME lastcompile := testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile;

    testfw->BeginWork();
    UpdateAssetPackSettings(testbundleid, [ dev := FALSE ]);
    testfw->CommitWork();

    Print("Disabling webhare_testsuite:basetest dev mode\n");
    WHILE(testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile = lastcompile)
      Sleep(100);
    Print("Disabled webhare_testsuite:basetest dev mode\n");
  }
  ELSE IF(GetEnvironmentVariable("WEBHARE_CI") != "")
  {
    ABORT("If WEBHARE_CI is set, we're running inside CI which is dtapstage == development... so why wasn't the 'new' webhare_testsuite:basetest package originally set as .dev then?");
  }
}

RECORD FUNCTION GetCSSStuff()
{
  STRING cssurl := testfw->browser->href;
  OBJECT stylesheet := MakeCSSStyleSheet(testfw->browser->content);
  OBJECT ARRAY testrules := stylesheet->cssrules->GetRulesForSelector("#test");
  TestEq(1, Length(testrules), "Could not find rule for #test in CSS " || cssurl);

  STRING backgroundurl := testrules[0]->style->GetPropertyValue("background");
  TestEqLike('url("*")', backgroundurl);
  RETURN [ backgroundurl := backgroundurl
         , display := (SELECT AS STRING ARRAY value FROM testrules[0]->style->GetAllProperties("display"))
         , cssurl := cssurl
         ];
}

MACRO TestBasics()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT emptyfile := siteroot->OpenByPath("emptyfilefolder/index.html");

  TestEq(0, GetErrorFromPublished(emptyfile->published), "Failed to publish " || emptyfile->url);
  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);

  OBJECT doc := testfw->browser->document;
  OBJECT ARRAY links := doc->getElements('link[rel="stylesheet"]');
  TestEq(1, Length(links)); //one style.css from webpack

  // Ensure the assetpack is compiled at least once
  testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest");

  STRING csslink := links[0]->GetAttribute("href");
  IF(testfw->debug)
    Print("CSS link: " || csslink||'\n');
  TestEq(TRUE, testfw->browser->GotoWebPage(csslink), csslink);
  TestEq(IsModuleInstalled("dev") ? devbundlecache : prodbundlecache, testfw->browser->GetResponseHeader("Cache-Control"));

  RECORD cssstuff := GetCSSStuff(); //this was a test against the webpack configuration suddenly running a css autoprefixer destroying the things we did..
  TestEq( ["-webkit-flex", "flex" ], cssstuff.display, "Missing webkit-flex in " || csslink);

  STRING backgroundurl := Tokenize(cssstuff.backgroundurl,'"')[1];
  TestEQ(TRUE, BlobToString(testfw->browser->content) LIKE "*DUMMY-TEXT-TO-VERIFY-ADDTOPACK.CSS-GOT-INCLUDED*");
  TestEQ(TRUE, testfw->browser->GotoWebPage(backgroundurl), "Unable to retrieve " || backgroundurl);
  TestEq(IsModuleInstalled("dev") ? devbundlecache : prodassetcache, testfw->browser->GetResponseHeader("Cache-Control"));
  TestEQ(FALSE, testfw->browser->GotoWebPage(backgroundurl||".bla"), "Unexpectedly able to retrieve " || backgroundurl||".bla");
  TestEq(notfoundcache, testfw->browser->GetResponseHeader("Cache-Control"), testfw->browser->href);

  OBJECT ARRAY scripts := doc->getElements('script[src]');
  STRING jslink := scripts[0]->GetAttribute("src");
  TestEq(TRUE, testfw->browser->GotoWebPage(jslink), jslink);
  //test that dev:false is set (minified)
  TestEqLike(`*window.whBundles["webhare_testsuite:basetest"]={dev:!1}*`, BlobToString(testfw->browser->content));

  DATETIME lastcompile := testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile;

  testfw->BeginWork();
  UpdateAssetPackSettings(testbundleid, [ dev := TRUE ]);
  testfw->CommitWork();

  Print("Enabling webhare_testsuite:basetest dev mode\n");
  WHILE(testfw->EnsureCompiledAssetpack("webhare_testsuite:basetest").lastcompile = lastcompile)
    Sleep(100);
  Print("Enabled webhare_testsuite:basetest dev mode\n");

  TestEq(TRUE, testfw->browser->GotoWebPage(jslink), testfw->browser->href);
  TestEqLike(`*window.whBundles["webhare_testsuite:basetest"] = { dev: true }*`, BlobToString(testfw->browser->content));
}

MACRO TestViewSHTML()
{
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT emptyfile := siteroot->OpenByPath("bob.jpg");

  testfw->browser->DeleteCookie("__whpub_preview");

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  TestEq("", testfw->browser->GetResponseHeader("X-WHPub-Preview"));
  INTEGER clength := ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0);

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  TestEq("", testfw->browser->GetResponseHeader("X-WHPub-Preview"));
  TestEq("", testfw->browser->GetResponseHeader("Cache-Control"));
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));

  testfw->browser->SetSessionCookie("__whpub_preview", "x");

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
  TestEq("no-cache", testfw->browser->GetResponseHeader("Cache-Control"));

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));

  testfw->browser->SetSessionCookie("__whpub_preview", "debug"); //triggers potential rewrite - but not for bob.jpg

  TestEq(TRUE, testfw->browser->SendRawRequest("HEAD", emptyfile->url, RECORD[], DEFAULT BLOB), "Bob is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
  TestEq("no-cache", testfw->browser->GetResponseHeader("Cache-Control"));

  TestEq(TRUE, testfw->browser->GotoWebPage(emptyfile->url), "Page is not served correctly: " || emptyfile->url);
  Testeq(clength, ToInteger(testfw->browser->GetResponseHeader("Content-Length"),0));
}

MACRO TestAssetpackManifest()
{
  //TODO verify a gzip version is available too of the manifest (accept-encoding gzip or something like that)
  STRING manifesturl := ResolvetoAbsoluteURL(testfw->GetTestSite()->webroot, `/.ap/webhare_testsuite.basetest/apmanifest.json`);
  TestEq(TRUE, testfw->browser->GotoWebPage(manifesturl));
  RECORD manifest := DecodeJSONBlob(testfw->browser->content);

  dumpvalue(manifest.assets,'boxed');

  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.js.gz" AND NOT compressed), "no unmarked compressed version in the dependent assets please");
  TestEq(FALSE, RecordExists(SELECT FROM manifest.assets WHERE NOT sourcemap AND (subpath LIKE "*.map" OR subpath LIKE "*.map.gz")), "no unmarked sourcemaps in the dependent assets please");
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath="ap.js"));
  TestEq(TRUE, RecordExists(SELECT FROM manifest.assets WHERE subpath LIKE "*.svg"));
}

RunTestframework([ PTR PrepTest
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, witherrors := TRUE])
                 , PTR TestBasics
                 , PTR TestViewSHTML
                 , PTR TestAssetpackManifest
                 ]);
