<?wh
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::socialite/lib/google/maps-v3.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


OBJECT FUNCTION MakeGeoCoder(RECORD options DEFAULTSTO DEFAULT RECORD)
{
  RETURN NEW GGeocoder(options);
}

MACRO TestGeoCoder()
{
  IF(GetTestSecret("GOOGLE_APIKEY") = "")
    THROW NEW Exception("TestGeoCoder requires the TESTSECRET_GOOGLE_APIKEY envvar to set ");

  testfw->BeginWork();
  testfw->CleanupWRDSchema(OpenWRDSchema("system:config"), [ [ type := "API_SECRET", tagmasks := [ "TESTFW_*" ] ]]);
  OBJECT keytype := OpenWRDSchema("system:config")->^api_secret;

  RECORD secret := [ api_key := GetTestSecret("GOOGLE_APIKEY") ];
  keytype->CreateEntity(CELL[ wrd_tag := "TESTFW_KEY_1", comment := "Added by test_google", api_type := "platform:google.cloud.backend", masks := "https://backend.testframework.beta.webhare.net/*", secret]);
  keytype->CreateEntity(CELL[ wrd_tag := "TESTFW_KEY_2", comment := "Added by test_google", api_type := "platform:google.cloud.backend", masks := "https://any.testframework.beta.webhare.net/*", secret]);
  keytype->CreateEntity(CELL[ wrd_tag := "TESTFW_KEY_3", comment := "Added by test_google", api_type := "platform:google.cloud.frontend", masks := "https://client.testframework.beta.webhare.net/*", secret]);

  testfw->CommitWork();

  TestThrowsLike("*requires*API key*",PTR MakeGeoCoder);
  TestThrowsLike("No API key found*",PTR MakeGeoCoder([ requesturl := "https://client.testframework.beta.webhare.net/" ])); //keytype does not match

  OBJECT geocoder := MakeGeoCoder([ requesturl := "https://backend.testframework.beta.webhare.net/" ]);
  TestEq(GetTestSecret("GOOGLE_APIKEY"), geocoder->apikey);
  OBJECT pos := geocoder->GetLatLng("Enschede, nederland");
  TestEqFloat(pos->lat, 52.2215372, 0.0001);
  TestEqFloat(pos->lng, 6.89366190, 0.0001);

  geocoder := NEW GGeocoder([ cache := NEW AdhocGeocodeCache, requesturl := "https://backend.testframework.beta.webhare.net/" ]);
  pos := geocoder->GetLatLng("Enschede, nederland");
  TestEqFloat(pos->lat, 52.2215372, 0.0001);
  TestEqFloat(pos->lng, 6.89366190, 0.0001);

  //non existing address should resturn empty object
  pos := geocoder->GetLatLng("ASDLHKJAGHJKADKAGH");
  TestEq(FALSE, ObjectExists(pos));

  geocoder := MakeGeoCoder([ apikey := "xx" ]);
  TestEq("xx", geocoder->apikey);
  TestThrowsLIKE("GetLatLng failed: REQUEST_DENIED:*", PTR geocoder->GetLatLng("ASDLHKJAGHJKADKAGH"));//invalid api key should throw!
}

RunTestframework([ PTR TestgeoCoder
                 ]);
