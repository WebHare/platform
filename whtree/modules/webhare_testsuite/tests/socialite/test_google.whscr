<?wh
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::socialite/lib/database.whlib";
LOADLIB "mod::socialite/lib/google/maps-v3.whlib";

OBJECT FUNCTION MakeGeoCoder(RECORD options DEFAULTSTO DEFAULT RECORD)
{
  RETURN NEW GGeocoder(options);
}

MACRO TestGeoCoder()
{
  IF(GetEnvironmentVariable("TESTSECRET_GOOGLE_APIKEY") = "")
    THROW NEW Exception("TestGeoCoder requires the TESTSECRET_GOOGLE_APIKEY envvar to set ");

  testfw->BeginWork();
  DELETE FROM socialite.google_apikeys WHERE domain LIKE "*.testframework.beta.webhare.net";
  INSERT INTO socialite.google_apikeys(domain,scopetype,apikey)
         VALUES("backend.testframework.beta.webhare.net", 2, GetEnvironmentVariable("TESTSECRET_GOOGLE_APIKEY"));
  INSERT INTO socialite.google_apikeys(domain,scopetype,apikey)
         VALUES("any.testframework.beta.webhare.net", 0, GetEnvironmentVariable("TESTSECRET_GOOGLE_APIKEY"));
  INSERT INTO socialite.google_apikeys(domain,scopetype,apikey)
         VALUES("client.testframework.beta.webhare.net", 1, GetEnvironmentVariable("TESTSECRET_GOOGLE_APIKEY"));
  testfw->CommitWork();

  TestThrows(PTR MakeGeoCoder);
  TestThrows(PTR MakeGeoCoder([ requesturl := "client.testframework.beta.webhare.net" ])); //keytype does not match
  TestThrows(PTR MakeGeoCoder([ requesturl := "client.testframework.beta.webhare.net" ])); //keytype does not match

  OBJECT geocoder := MakeGeoCoder([ requesturl := "backend.testframework.beta.webhare.net" ]);
  TestEq(GetEnvironmentVariable("TESTSECRET_GOOGLE_APIKEY"), geocoder->apikey);
  OBJECT pos := geocoder->GetLatLng("Enschede, nederland");
  TestEqFloat(pos->lat, 52.2215372, 0.0001);
  TestEqFloat(pos->lng, 6.89366190, 0.0001);

  geocoder := NEW GGeocoder([ cache := NEW AdhocGeocodeCache, requesturl := "backend.testframework.beta.webhare.net" ]);
  pos := geocoder->GetLatLng("Enschede, nederland");
  TestEqFloat(pos->lat, 52.2215372, 0.0001);
  TestEqFloat(pos->lng, 6.89366190, 0.0001);

  //non existing address should resturn empty object
  pos := geocoder->GetLatLng("ASDLHKJAGHJKADKAGH");
  TestEq(FALSE, ObjectExists(pos));

  geocoder := MakeGeoCoder([ apikey := "xx" ]);
  TestEq("xx", geocoder->apikey);
  TestThrowsLIKE("GetLatLng failed: REQUEST_DENIED:*", PTR geocoder->GetLatLng("ASDLHKJAGHJKADKAGH"));//invalid api key should throw!
}

RunTestframework([ PTR TestgeoCoder
                 ]);
