<?wh

LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/remoting/client.whlib";
LOADLIB "mod::system/lib/internal/webserver/config.whlib";

MACRo ValidateConfig(RECORD inconfig)
{
  RECORD whsock := SELECT * FROM inconfig.types WHERE extension = "whsock";
  RECORD shtml := SELECT * FROM inconfig.types WHERE extension = "shtml";

  TestEq("application/x-webhare-websocket", whsock.mimetype);
  TestEq(4, whsock.parsetype);
  TestEq("text/html; charset=UTF-8", shtml.mimetype);
  TestEq(1, shtml.parsetype);
}

MACRO TestMinimalConfig_NoDatabase()
{
  //First test WITHOUT database... to ensure minimial config generation doesn't require one
  TestEq(FALSE, HavePrimaryTransaction());

  RECORD minimal := CreateMinimalWebserverConfig();
  ValidateConfig(minimal);
  TestEq(FALSE, HavePrimaryTransaction());
}

RECORD FUNCTION AuthHandler(STRING realm, STRING url)
{
  RETURN
      [ username := testfw->GetUserLogin("sysop")
      , password := testfw->GetUserPassword("sysop")
      ];
}

MACRO TestMinimalConfig()
{
  testfw->browser->onauth := PTR AuthHandler;
  Print("\n** If your webserver becomes unreachable, `wh softreset --web` should fix it\n\n");

  RECORD port := testfw->GetLocalhostWebinterface();
  STRING url := ResolveToAbsoluteURL(port.baseurl, "/wh_services/webhare_testsuite/test/");
  RECORD res := InvokeRemoteFunctionWithBrowser(testfw->browser, url || "testminimalconfig");
  TestEq("sysop@beta.webhare.net", res.login);
  TestEq(TRUE, res.success);
}

MACRO TestFullConfigCompliance()
{
  //even full config should have the minimal configuration
  RECORD config := DownloadWebserverConfig();
  ValidateConfig(config);
}

RunTestFramework([ PTR TestMinimalConfig_NoDatabase
                 ], [ usedatabase := FALSE ]);

testfw := DEFAULT OBJECT;
RunTestFramework([ PTR TestMinimalConfig
                 , PTR TestFullConfigCompliance
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ]
                    ]);
