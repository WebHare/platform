<?wh

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";

LOADLIB "mod::webhare_testsuite/lib/db/tablecreate.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestProcedures()
{
  TestEQ([ [ val := "blabla" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "blabla" ] ]));
  TestEQ([ [ val := "%0Ad%09" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "\nd\t" ] ]));
  TestEQ([ [ val := "%04hey%05" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "\004hey\005" ] ]));
  TestEQ([ [ val := "" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "" ] ]));
  TestEQ([ [ val := "hey%08laat" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "hey\blaat" ] ]));
  TestEQ([ [ val := "%C3%AB" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "\u00eb" ] ]));
  TestEQ([ [ val := "%7B%7D%7C%5C%5E%7E%5B%5D%60" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ "{}|\\^~[]`" ] ]));
  TestEQ([ [ val := "%3B/%3F%3A%40%3D%26" ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_urlencode($1) AS val`, [ args := VARIANT[ ";/?:@=&" ] ]));

  TestEQ([ [ val := TRUE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "good" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "\a" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "!" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "^" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ " a" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a " ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a." ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a*a" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a/a" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a\na" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isvalidwhfsname($1) AS val`, [ args := VARIANT[ "a\aa" ] ]));


  testfw->BeginWork();
  OBJECT foreignfoldersiteroot := testfw->GetWHFSTestRoot()->CreateFolder([name := "webhare_testsuite_foreignfoldersite"]);
  OBJECT foreignfoldersite := CreateSiteFromFolder(foreignfoldersiteroot->id);
  OBJECT parentfolder := foreignfoldersite->rootobject->CreateFolder([ name := "parent" ]);
  OBJECT foreignfolder := parentfolder->CreateFolder([ name := "foreignfolder", type := 1 ]);
  testfw->CommitWork();

  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "/" ] ]));
  TestEQ([ [ val := FALSE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "foreignfolder" ] ]));
  TestEQ([ [ val := TRUE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "parent/foreignfolder" ] ]));
  TestEQ([ [ val := TRUE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "///parent///foreignfolder///" ] ]));
  TestEQ([ [ val := TRUE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "parent/foreignfolder/sub" ] ]));
  TestEQ([ [ val := TRUE ] ], GetPrimary()->__ExecSQL(`SELECT webhare_proc_isforeignfolder($1, $2) AS val`, [ args := VARIANT[ foreignfoldersite->root, "parent/foreignfolder/sub//" ] ]));
}

MACRO TestURL()
{
  OBJECT testroot := GetTestsuiteTempFolder();
  STRING baseurl := testroot->url;

  OBJECT testfolder, testfile, testfile2;

  testfw->BeginWork();

  testfile := testroot->CreateFolder([ name := "test" ]);
  TestEQ(baseurl || "test/", testfile->url);
  TestEQ(baseurl || "test/", CalculateFSObjectFullPathAndURL(testfile->id).url);
  testfile->RecycleSelf();

  testfile := testroot->CreateFile([ name := "test.html" ]);
  TestEQ(baseurl || "test.html", CalculateFSObjectFullPathAndURL(testfile->id).url);
  TestEQ(baseurl || "test.html", CalculateFSObjectFullPathAndURL(testfile->id).url);
  testfile->RecycleSelf();

  testfile := testroot->CreateFile([ name := "index.html" ]);
  testroot->UpdateMetadata([ indexdoc := testfile->id ]);
  testfile->Refresh();
  TestEQ(baseurl || "", testfile->url);
  TestEQ(baseurl || "", CalculateFSObjectFullPathAndURL(testfile->id).url);
  testroot->UpdateMetadata([ indexdoc := 0 ]);
  testfile->RecycleSelf();

  testfile := testroot->CreateFile([ name := "test.link", type := 18, externallink := "http://example.com" ]);
  testfile->Refresh();
  TestEQ("http://example.com", testfile->url);
  TestEQ("http://example.com", CalculateFSObjectFullPathAndURL(testfile->id).url);
  TestEQ(baseurl || "test.link/", CalculateFSObjectFullPathAndURL(testfile->id, [ followlinks := FALSE]).url);
  testroot->UpdateMetadata([ indexdoc := 0 ]);
  testfile->RecycleSelf();

  testfolder := testroot->CreateFolder([ name := "test" ]);
  testfile := testfolder->CreateFile([ name := "index.html" ]);
  testfolder->UpdateMetadata([ indexdoc := testfile->id ]);
  testfile->Refresh();
  TestEQ(baseurl || "test/", testfile->url);
  TestEQ(baseurl || "test/", CalculateFSObjectFullPathAndURL(testfile->id).url);
  testfile->UpdateMetadata([ name := "test.html" ]);
  TestEQ(baseurl || "test/", testfile->url); // still index
  testfile->Refresh();
  TestEQ(baseurl || "test/", testfile->url); // still index
  TestEQ(baseurl || "test/", CalculateFSObjectFullPathAndURL(testfile->id).url); // still index
  testfolder->UpdateMetadata([ indexdoc := 0 ]);
  testfile->Refresh();
  TestEQ(baseurl || "test/test.html", testfile->url); // still index
  TestEQ(baseurl || "test/test.html", CalculateFSObjectFullPathAndURL(testfile->id).url); // still index
  testfile->RecycleSelf();
  testfolder->RecycleSelf();

  testfile := testroot->CreateFile([ name := "test.html" ]);
  testfile2 := testroot->CreateFile([ name := "test2.link", type := 19, filelink := testfile->id ]);
  TestEQ(testfile->url, testfile2->url);
  TestEQ(testfile->url, CalculateFSObjectFullPathAndURL(testfile2->id).url);
  TestEQ(baseurl || "test2.link/", CalculateFSObjectFullPathAndURL(testfile2->id, [ followlinks := FALSE ]).url);
  testfile->RecycleSelf();
  testfile2->RecycleSelf();

  INTEGER type_rtd := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile")->id;

  // published as subdir
  testfolder := testroot->CreateFolder([ name := "test" ]);
  testfile := testfolder->CreateFile([ name := "test2.rtd", type := type_rtd ]);
  TestEQ(baseurl || "test/test2/", testfile->url); // with /, is published as folder, stripped extension
  TestEQ(baseurl || "test/test2/", CalculateFSObjectFullPathAndURL(testfile->id).url); // with /, is published as folder, stripped extension
  testfolder->UpdateMetadata([ indexdoc := testfile->id ]);
  testfile->Refresh();
  TestEQ(baseurl || "test/", testfile->url);
  TestEQ(baseurl || "test/", CalculateFSObjectFullPathAndURL(testfile->id).url);
  testfile2 := testroot->CreateFile([ name := "test3.link", type := 20, filelink := testfile->id ]);
  TestEQ(baseurl || "test3.link/", testfile2->url); // .link is not an hidden extension
  TestEQ(baseurl || "test3.link/", CalculateFSObjectFullPathAndURL(testfile2->id).url);
  TestEQ(baseurl || "test3.link/", CalculateFSObjectFullPathAndURL(testfile2->id, [ followlinks := FALSE ]).url);
  testfile2->UpdateMetadata([ name := "test3.rtd" ]);
  TestEQ(baseurl || "test3/", testfile2->url); // .rtd is an hidden extension
  TestEQ(baseurl || "test3/", CalculateFSObjectFullPathAndURL(testfile2->id).url);
  TestEQ(baseurl || "test3/", CalculateFSObjectFullPathAndURL(testfile2->id, [ followlinks := FALSE ]).url);

  testfile2->RecycleSelf();
  testfile2 := testroot->CreateFile([ name := "test4.whlink", type := 19, filelink := testfile->id, externallink := "#subheading" ]);
  TestEQ(baseurl || "test/#subheading", testfile2->url);
  /* TODO should CalculateFSObjectFullPathAndURL even understand internal link appends ?
  TestEQ(baseurl || "test/#subheading", CalculateFSObjectFullPathAndURL(testfile2->id).url);
  */

  testfile2->RecycleSelf();
  testfile2 := testroot->CreateFile([ name := "test4.whlink", type := 19, filelink := testfile->id, externallink := "/subpath" ]);
  TestEQ(baseurl || "test/subpath", testfile2->url);
  /* TODO should CalculateFSObjectFullPathAndURL even understand internal link appends ?
  TestEQ(baseurl || "test/subpath", CalculateFSObjectFullPathAndURL(testfile2->id).url);
  */

  testfile->UpdateMetadata([ type := 5 ]); // unknown type, not published (kill stripextension flag) too
  testfile2->RecycleSelf();
  testfile2 := testroot->CreateFile([ name := "test3.rtd", type := 20, filelink := testfile->id ]);
  TestEQ(baseurl || "test3.rtd", testfile2->url); // .rtd is an hidden extension, but type 0 doesn't hide extensions
  TestEQ(baseurl || "test3.rtd", CalculateFSObjectFullPathAndURL(testfile2->id).url);
  TestEQ(baseurl || "test3.rtd", CalculateFSObjectFullPathAndURL(testfile2->id, [ followlinks := FALSE ]).url);
  testfolder->RecycleSelf();

  testfw->CommitWork();
}

MACRO TestWebroot()
{
  testfw->BeginWork();

  OBJECT nooutputsiteroot := testfw->GetWHFSTestRoot()->CreateFolder([name := "webhare_testsuite_nooutputsite"]);
  OBJECT nooutputsite := CreateSiteFromFolder(nooutputsiteroot->id);
  TestEq("", SELECT AS STRING webroot FROM system.sites WHERE id = nooutputsite->id);
  TestEq(TRUE, nooutputsite->id IN SELECT AS INTEGER ARRAY id FROM system.sites WHERE webroot="");

  TestEq("", SELECT AS STRING objecturl FROM system.fs_objects WHERE id = nooutputsiteroot->id);
  TestEq("", SELECT AS STRING link FROM system.fs_objects WHERE id = nooutputsiteroot->id);

  OBJECT nooutputsitesub := nooutputsiteroot->CreateFolder([name := "sub"]);
  TestEq("", SELECT AS STRING objecturl FROM system.fs_objects WHERE id = nooutputsitesub->id);
  TestEq("", SELECT AS STRING link FROM system.fs_objects WHERE id = nooutputsitesub->id);

  testfw->CommitWork();
}

MACRO TestAutoNumbers()
{
  // Autonumber outside of transaction left a lingering advisory lock
  TestEQ(RECORD[], GetPrimary()->__ExecSQL(`SELECT * FROM pg_locks WHERE pid = pg_backend_pid() AND locktype = 'advisory'`));
  TestThrowsLike("BeginWork must be called before modifying the database", PTR MakeAutonumber(system.fs_objects, "ID"));
  TestEQ(RECORD[], GetPrimary()->__ExecSQL(`SELECT * FROM pg_locks WHERE pid = pg_backend_pid() AND locktype = 'advisory'`), "MakeAutoNumber left lock when giving an error");

  testfw->BeginWork();

  // Autonumbers should be increasing
  INTEGER a := MakeAutonumber(system.fs_objects, "ID");
  INTEGER ARRAY bs := MakeAutonumbers(system.fs_objects, "ID", 2);
  TestEQ(2, LENGTH(bs));
  TestAssert(a < bs[0]);
  TestAssert(bs[0] < bs[1]);

  INTEGER64 a64 := MakeAutonumber64(system.fs_settings, "ID");
  INTEGER64 ARRAY b64s := MakeAutoNumbers64(system.fs_settings, "ID", 2);
  TestEQ(2, LENGTH(b64s));
  TestAssert(a64 < b64s[0]);
  TestAssert(b64s[0] < b64s[1]);

  testfw->RollbackWork();

  // make sure a pre-existing autonumberwraptest table isn't reused
  ApplyTempDatabaseSchema(`
  <databaseschema xmlns:d="http://www.webhare.net/xmlns/whdb/databaseschema">
    <d:obsoletetable name="autonumberwraptest" />
  </databaseschema>
`);

  ApplyTempDatabaseSchema(`
  <databaseschema xmlns:d="http://www.webhare.net/xmlns/whdb/databaseschema">
    <d:table name="autonumberwraptest">
      <d:integer name="a32" autonumberstart="2147483645"/>
      <d:integer64 name="a64" autonumberstart="2147483645"/>
    </d:table>
  </databaseschema>
`);

  GetPrimary()->BeginWork();

  // Test 32-bit integer wraparound
  TABLE< INTEGER a32, INTEGER64 a64 > tbl_autonumberwraptest := BindTransactionToTable(GetPrimary()->id, "webhare_testsuite_temp.autonumberwraptest");
  TestEQ(2147483645, MakeAutoNumber(tbl_autonumberwraptest, "a32"));
  TestEQ(2147483646, MakeAutoNumber(tbl_autonumberwraptest, "a32"));
  TestEQ(2147483647, MakeAutoNumber(tbl_autonumberwraptest, "a32"));
  TestEQ(2147483645, MakeAutoNumber(tbl_autonumberwraptest, "a32"));
  TestEQ([ 2147483646, 2147483647 ], MakeAutoNumbers(tbl_autonumberwraptest, "a32", 2));
  TestEQ([ 2147483645, 2147483646 ], MakeAutoNumbers(tbl_autonumberwraptest, "a32", 2));
  TestEQ([ 2147483647, 2147483645 ], MakeAutoNumbers(tbl_autonumberwraptest, "a32", 2));

  // Test integer wraparound isn't used for integer64 columns
  TestEQ(2147483645i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  TestEQ(2147483646i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  TestEQ(2147483647i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  TestEQ(2147483648i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  TestEQ(2147483649i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));

  GetPrimary()->__ExecSQL(`ALTER SEQUENCE "webhare_testsuite_temp"."webhare_seq_autonumberwraptest_a64" RESTART`);
  TestEQ(2147483645i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  TestEQ([ 2147483646i64, 2147483647i64, 2147483648i64, 2147483649i64 ], MakeAutoNumbers64(tbl_autonumberwraptest, "a64", 4));

  GetPrimary()->__ExecSQL(`ALTER SEQUENCE "webhare_testsuite_temp"."webhare_seq_autonumberwraptest_a64" RESTART`);
  TestEQ([ 2147483645i64, 2147483646i64, 2147483647i64 ], MakeAutoNumbers64(tbl_autonumberwraptest, "a64", 3));
  TestEQ(2147483648i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));

  GetPrimary()->CommitWork();

  // Test skipping over existing values
  GetPrimary()->BeginWork();
  GetPrimary()->__ExecSQL(`ALTER SEQUENCE "webhare_testsuite_temp"."webhare_seq_autonumberwraptest_a64" RESTART`);
  INSERT INTO tbl_autonumberwraptest(a64) VALUES (2147483646i64);
  TestEQ([ 2147483645i64, 2147483647i64, 2147483648i64 ], MakeAutoNumbers64(tbl_autonumberwraptest, "a64", 3));
  TestEQ(2147483649i64, MakeAutoNumber64(tbl_autonumberwraptest, "a64"));
  GetPrimary()->CommitWork();

  // Test table full
  GetPrimary()->BeginWork();
  INSERT INTO tbl_autonumberwraptest(a32) VALUES (2147483645);
  INSERT INTO tbl_autonumberwraptest(a32) VALUES (2147483646);
  INSERT INTO tbl_autonumberwraptest(a32) VALUES (2147483647);
  TestThrowsLike("*Table webhare_testsuite_temp.autonumberwraptest is full, no more autonumbers available*", PTR MakeAutoNumber(tbl_autonumberwraptest, "a32"));
  GetPrimary()->CommitWork();

  ApplyTempDatabaseSchema(`
  <databaseschema xmlns:d="http://www.webhare.net/xmlns/whdb/databaseschema">
    <d:obsoletetable name="autonumberwraptest" />
  </databaseschema>
`);
}

RunTestframework([ PTR SetupTempSchema
                 , PTR TestProcedures
                 , PTR TestURL
                 , PTR TestWebroot
                 , PTR TestAutoNumbers
                 ]);
