<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";


ASYNC MACRO TestAuthSettings()
{
  // set an old password
  testfw->BeginWork();
  testfw->GetWRDschema()->^wrd_person->UpdateEntity(testfw->GetUserWRDId("lisa@allow2fa.test.webhare.net"),
      [ whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      DEFAULT DATETIME
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            ]
      ]);
  testfw->CommitWork();

  testfw->SetTestUser("lisa@allow2fa.test.webhare.net");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  // Change password
  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", "secret");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  // Change password back to 'secret', allowed because of no reuse restrictions
  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New Password", "secret");
  TTFillByTitle(":Confirm new password", "secret");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape());

  // STORY disallow password reuse when configured
  testfw->BeginWork();
  testfw->GetWRDSchema()->SetSchemaSetting("wrd:auth.passwordpolicy.validationchecks", "noreuse:P2D");
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  // Change password
  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", "secret");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");

  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Ok"));
  TTFillByTitle(":New password", "secret3");
  TTFillByTitle(":Confirm new password", "secret3");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape());

  // STORY: warn on about to expire passwords
  testfw->BeginWork();
  testfw->GetWRDSchema()->SetSchemaSetting("wrd:auth.passwordpolicy.validationchecks", "maxage:P2M");
  testfw->CommitWork();

    // set an old password
  testfw->BeginWork();
  testfw->GetWRDschema()->^wrd_person->UpdateEntity(testfw->GetUserWRDId("lisa@allow2fa.test.webhare.net"),
      [ whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      AddDaysToDate(-35, GetCurrentDateTime())
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            ]
      ]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));
  TestEQ(TRUE, TT("warning_passwordabouttoexpire")->visible);

  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", "secret");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEQ(FALSE, TT("warning_passwordabouttoexpire", [ findinvisible := TRUE ])->visible);

  AWAIT ExpectScreenChange(-1, PTR TTEscape());

  // STORY: warn on about expired passwords
  testfw->BeginWork();
  testfw->GetWRDSchema()->SetSchemaSetting("wrd:auth.passwordpolicy.validationchecks", "maxage:P1M");
  testfw->GetWRDschema()->^wrd_person->UpdateEntity(testfw->GetUserWRDId("lisa@allow2fa.test.webhare.net"),
      [ whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      AddDaysToDate(-35, GetCurrentDateTime())
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            ]
      ]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  TestEQ(TRUE, TT("warning_passwordexpired")->visible);

  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", "secret");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEQ(FALSE, TT("warning_passwordexpired", [ findinvisible := TRUE ])->visible);

  AWAIT ExpectScreenChange(-1, PTR TTEscape());


  testfw->BeginWork();
  OBJECT newunit := testfw->GetWRDSchema()->^whuser_unit->CreateEntity(
      [ wrd_leftentity :=           testfw->testunit
      , wrd_title :=                "pvoverride"
      , override_passwordchecks :=  TRUE
      , passwordchecks :=           "maxage:P1D"
      ]);
  OBJECT bob := testfw->userapi->GetUser(testfw->userapi->CreateUser(
      [ whuser_unit :=        newunit->id
      , wrd_contact_email :=  "bob@allow2fa.test.webhare.net"
      , wrd_firstname :=      "Bob"
      , wrd_lastname :=       "McTestsuite"
      , whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      GetCurrentDateTime()
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            ]      ]));

  testfw->CommitWork();

  __SetEffectiveUser(bob);

  // Because of the short expiry in the new unit, the about to expire warning should be visible immediately after changing a password
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));
  TestEQ(TRUE, TT("warning_passwordabouttoexpire")->visible);
  AWAIT ExpectScreenChange(-1, PTR TTEscape());
}


RunTestframework( [ PTR TestAuthSettings
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ,[ login := "lisa@allow2fa.test.webhare.net", grantrights := ["system:sysop"] ]
                                    ]
                     , debug := TRUE
                     ]
                  );
