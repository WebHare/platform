<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/rtd.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/contenttypes.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";

STRING emptysiteprofile := '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" />';

//PublisherTestSiteInit(DEFAULT BLOB);

OBJECT testsite;

MACRO Prepare()
{
  testfw->BeginWork();

  OBJECT firstfolder := testfw->GetWHFSTestRoot();
  testsite := CreateSiteFromFolder(firstfolder->id);
  testfw->CommitWork();
}

MACRO MockedTypes()
{
  OBJECT normalfoldertype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/normalfolder");
  TestEq("http://www.webhare.net/xmlns/publisher/normalfolder", normalfoldertype->namespace);
  TestEq(TRUE, normalfoldertype->foldertype);
  TestEq(FALSE, normalfoldertype->filetype);
  TestEq(FALSE, normalfoldertype->orphan);
  TestEq(0, Length(normalfoldertype->members));
  TestEq(0, normalfoldertype->id);

  OBJECT unknownfiletype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/unknownfile");
  TestEq("http://www.webhare.net/xmlns/publisher/unknownfile", unknownfiletype->namespace);
  TestEq(FALSE, unknownfiletype->foldertype);
  TestEq(TRUE, unknownfiletype->filetype);
  TestEq(FALSE, unknownfiletype->isacceptableindex);
  TestEq(FALSE, unknownfiletype->ispublishedassubdir);
}

MACRO MergeTypeInto()
{
  OBJECT srctype := OpenWHFSType("http://www.webhare.net/xmlns/beta/dynfolder", [ openorphans := TRUE ]);
  TestEq(TRUE, srctype->foldertype);

  OBJECT desttype := OpenWHFSType("http://www.webhare.net/xmlns/beta/dynfile", [ openorphans := TRUE ]);
  TestEq(FALSE, desttype->foldertype);

  testfw->BeginWork();

  OBJECT testdatafolder := testsite->rootobject->CreateFolder(
            [ name := "converttest"
            , type := srctype->id
            ]);
  OBJECT tempfile := testdatafolder->CreateFile([name:="img1.jpg", type := 12, publish := TRUE]);

  srctype->MergeTypeInto(desttype);
  TestEq(0, Length(SELECT FROM system.fs_objects WHERE type = srctype->id));
  TestEq(FALSE, OpenWHFSObject(tempfile->id)->isactive);

  OBJECT converttestfile := testsite->rootobject->OpenByName("converttest");
  TestEq(TRUE, converttestfile->publish);

  testfw->RollbackWork();
}

MACRO FindTest()
{
  testfw->BeginWork();
  OBJECT firstfolder := testfw->GetWHFSTestRoot();
  testsite->UpdateSiteMetadata([webfeatures := ["webhare_testsuite:autocreatetest"] ]);
  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT findtest1 := firstfolder->CreateFile([name:="findtest1"]);
  OBJECT findtest2 := firstfolder->CreateFile([name:="findtest2"]);
  OBJECT findtest3 := firstfolder->CreateFile([name:="findtest3"]);

  OBJECT ctype := OpenWHFSType("http://www.webhare.net/xmlns/beta/autocreatetest");
  INTEGER ARRAY result;

  findtest1->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ int := 15
      , yesno := TRUE
      ]);
  findtest2->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ int := 30
      ]);
  findtest3->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ yesno := TRUE
      ]);
  findtest1->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ strarray := [ "a" ]
      ]);
  findtest2->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ strarray := [ "a", "a" ]
      ]);
  findtest3->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ strarray := [ "" ]
      ]);

  TestThrows(PTR ctype->FindObjectsByMemberValue("str", ""));
  TestThrows(PTR ctype->FindObjectsByMemberValue("int", 0));
  TestThrows(PTR ctype->FindObjectsByMemberValue("yesno", FALSE));
  TestThrows(PTR ctype->FindObjectsByMemberValue("str", "overlong" || RepeatText("X",2048)));
  TestThrows(PTR ctype->FindObjectsByMemberValue("strarray", ""));

//  ABORT(ctype->FindObjectsByMemberValue("int", 15));

  TestEq([INTEGER(findtest1->id)], ctype->FindObjectsByMemberValue("int", 15));
  TestEq([INTEGER(findtest2->id)], ctype->FindObjectsByMemberValue("int", 30));
  TestEq(DEFAULT INTEGER ARRAY, ctype->FindObjectsByMemberValue("int", 40));
  TestEq([INTEGER(findtest1->id), findtest2->id], ctype->FindObjectsByMemberValue("strarray", "a"));

  result := SELECT AS INTEGER ARRAY val FROM ToRecordArray(ctype->FindObjectsByMemberValue("yesno", TRUE),"val") ORDER BY val;
  TestEq([INTEGER(findtest1->id), findtest3->id], result);

  testfw->RollbackWork();
}

MACRO InplaceUpdateTest()
{
  testfw->BeginWork();

  OBJECT firstfolder := testfw->GetWHFSTestRoot();
  OBJECT testfile := firstfolder->CreateFile([name:="inplaceupdatetest"]);
  OBJECT testfile2 := firstfolder->CreateFile([name:="inplaceupdateotherfile"]);

  RECORD defaultdata :=
      [ stringtest :=     ""
      , arraytest :=      DEFAULT RECORD ARRAY
      , _whfsarray :=     DEFAULT INTEGER ARRAY
      , stringarray :=    DEFAULT STRING ARRAY
      , richtest  :=      DEFAULT RECORD
      , instancetest  :=  DEFAULT RECORD
      , recordtest :=     DEFAULT RECORD
      ];

  BLOB testbloba := StringToBlob("mybloba");
  BLOB testblobb := StringToBlob("myblobb8");
  BLOB testblobc := StringToBlob("myblobc89");

  TestEq(defaultdata,     testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test"));

  OBJECT myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromMHTML(testfw->GetModuleTestBlob("webhare_testsuite","system/testdata/b-lex.mht"));
  RECORD richdocrec := myrichdoc->ExportAsRecord();
  richdocrec.embedded := SELECT *, hash := EncodeUFS(GetHashForBlob(data, "SHA-256")), link := "" FROM richdocrec.embedded;

  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test",
      [ arraytest :=
          [ [ textcell :=         "cell1"
            , _whfscell :=        0
            , stringarray :=      [ "a", "b" ]
            , blobcell :=         [ data := testbloba
                                  , mimetype :=     "text/html"
                                  , filename :=     "filea.html"
                                  ]
            ]
          , [ textcell :=         "cell2"
            , _whfscell :=        0
            , stringarray :=      [ "c" ]
            , blobcell :=         [ data := testblobb
                                  , mimetype :=   "text/html"
                                  , filename :=     "fileb.html"
                                  , extension :=    "html" //allowed to specify but should be ignored!
                                  ]
            , intextcell :=       [ internallink := 0
                                  , externallink := "http://www.b-lex.nl"
                                  , append := ""
                                  ]
            ]
          , [ textcell :=         ""
            , _whfscell :=        0
            , stringarray :=      DEFAULT STRING ARRAY
            , blobcell :=         DEFAULT RECORD
            , intextcell :=       DEFAULT RECORD
            ]
          ]
      , richtest := richdocrec
      , instancetest :=
            [ whfstype := "http://www.webhare.net/xmlns/beta/embedblock1"
            , id := "TestInstance-1"
            , styletitle := ""
            , fsref := 0
            ]
      ]);

  testfw->CommitWork();

  RECORD got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");

  testfw->BeginWork();

  RECORD expect :=
      [ stringtest :=   "x"
      , _whfsarray :=   [ INTEGER(testfile->id), testfile2->id ]
      , stringarray :=  [ "b", "a" ]
      , richtest  :=    richdocrec
      , recordtest :=   [ va := VARIANT[42, "BEAGLE" ]]
      , arraytest :=
            [ [ textcell :=         "cell1"
              , _whfscell :=        0
              , stringarray :=      [ "a", "b" ]
              , blobcell :=         [ data := testbloba
                                    , mimetype :=       "text/html"
                                    , filename :=       "filea.html"
                                    , extension := ".html"
                                    , width :=          0
                                    , height :=         0
                                    , link :=           [linkisobsolete:=FALSE]
                                    , hash :=           "Bf3iZwRB7squ2ZaOUdEAMvH34xsIJSI80pPBfPoYus0"
                                    , rotation := 0
                                    , mirrored := FALSE
                                    , refpoint := DEFAULT RECORD
                                    , __blobsource :=   ""
                                    , source_fsobject := 0
                                    , dominantcolor := ""
                                    ]
              , intextcell :=       DEFAULT RECORD
              ]
            , [ textcell :=         "cell2"
              , _whfscell :=        0
              , stringarray :=      [ "c" ]
              , blobcell :=         [ data := testblobb
                                    , mimetype :=   "text/html"
                                    , filename :=     "fileb.html"
                                    , extension := ".html"
                                    , width :=          0
                                    , height :=         0
                                    , link :=           [linkisobsolete:=FALSE]
                                    , hash :=           "1CnmE60L3fFIRAkN4qyTurNAeW4anI-m5Zrsa20OHEM"
                                    , rotation := 0
                                    , mirrored := FALSE
                                    , refpoint := DEFAULT RECORD
                                    , __blobsource :=   ""
                                    , source_fsobject := 0
                                    , dominantcolor := ""
                                    ]
              , intextcell :=       [ internallink := 0
                                    , externallink := "http://www.b-lex.nl"
                                    , append := ""
                                    ]
              ]
            , [ textcell :=         ""
              , _whfscell :=        0
              , stringarray :=      DEFAULT STRING ARRAY
              , blobcell :=         DEFAULT RECORD
              , intextcell :=       DEFAULT RECORD
              ]
            ]
      , instancetest :=
            [ whfstype := "http://www.webhare.net/xmlns/beta/embedblock1"
            , id := "TestInstance-1"
            , styletitle := ""
            , fsref := 0
            ]
      ];

  // Updates with inner default value fields omitted
  RECORD expect_eq := expect;
  expect_eq.arraytest[2] := DEFAULT RECORD;
  DELETE CELL styletitle FROM expect_eq.instancetest;

  ///////////////////////////
  //  Test whether intextlinks are correctly updated
  got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq([ internallink := 0, externallink := "http://www.b-lex.nl", append := "" ], got.arraytest[1].intextcell);


  // change a single setting in the intextcell RECORD
  RECORD nextupdate := got;
  nextupdate.arraytest[1].intextcell := [ internallink := 0
                                        , externallink := "http://www.b-lex.nl/test/"
                                        , append := ""
                                        ];
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", nextupdate);
  got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq([ internallink := 0, externallink := "http://www.b-lex.nl/test/", append := "" ], got.arraytest[1].intextcell);


  // change a single setting in the intextcell RECORD
  nextupdate := got;
  nextupdate.arraytest[1].intextcell := [ internallink := 0
                                        , externallink := "http://www.b-lex.nl"
                                        , append := ""
                                        ];
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", nextupdate);
  got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq([ internallink := 0, externallink := "http://www.b-lex.nl", append := "" ], got.arraytest[1].intextcell);
  ///////////////////////////

  // Set remaining stuff
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test",
        [ stringtest :=   "x"
        , _whfsarray :=   [ INTEGER(testfile->id), testfile2->id ]
        , stringarray :=  [ "b", "a" ]
        , recordtest :=   [ va := VARIANT[42, "BEAGLE" ]]
        ]);

  got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");

  //Find arraytest[1].blobcell

  testfw->CommitWork();
  TestEqMembers(RecurseDeleteSettingIds(expect, DEFAULT STRING ARRAY), RecurseDeleteSettingIds(got, DEFAULT STRING ARRAY), "*,-LINK");
  RECORD f := testfile->FilterInstanceDataUpdates("http://www.webhare.net/xmlns/beta/test", expect);
  TestEQ(DEFAULT RECORD, testfile->FilterInstanceDataUpdates("http://www.webhare.net/xmlns/beta/test", expect));
  TestEQ(expect, testfile2->FilterInstanceDataUpdates("http://www.webhare.net/xmlns/beta/test", expect));

  testfw->BeginWork();
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", got);

  TestEQ(DEFAULT RECORD, testfile->FilterInstanceDataUpdates("http://www.webhare.net/xmlns/beta/test", expect));
  TestEQ(DEFAULT RECORD, testfile->FilterInstanceDataUpdates("http://www.webhare.net/xmlns/beta/test", expect_eq));

  // Stability of blob settings ids after identity update
  RECORD got2 := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(got.arraytest[0].blobcell.settingid, got2.arraytest[0].blobcell.settingid);
  TestEq(got.arraytest[0].fs_settingid, got2.arraytest[0].fs_settingid);
  TestEq(got.arraytest[1].fs_settingid, got2.arraytest[1].fs_settingid);

  RECORD newval := got;
  newval.arraytest[0].blobcell.data := testblobc;
  TestThrowsLike("Trying to overwrite*", PTR testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", newval));

  newval := got;
  //setting it with the same blob, even if now external source, should still be accepted
  newval.arraytest[0].blobcell.data := StringToBlob(BlobToString(newval.arraytest[0].blobcell.data));
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", newval);

   // Stability of blob elements that are updated in place
  RECORD got3 := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(got.arraytest[0].blobcell.settingid, got3.arraytest[0].blobcell.settingid);
  TestEq(got.arraytest[0].fs_settingid, got3.arraytest[0].fs_settingid);
  TestEq(got.arraytest[1].fs_settingid, got3.arraytest[1].fs_settingid);

  // Switch elements 0 and 1
  newval := got;
  INSERT newval.arraytest[0] INTO newval.arraytest AT 2;
  DELETE FROM newval.arraytest AT 0;

  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", newval);
  RECORD got4 := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(got.arraytest[1].blobcell.settingid, got4.arraytest[0].blobcell.settingid);
  TestEq(got.arraytest[0].blobcell.settingid, got4.arraytest[1].blobcell.settingid);
  TestEq(got.arraytest[0].fs_settingid, got4.arraytest[1].fs_settingid);
  TestEq(got.arraytest[1].fs_settingid, got4.arraytest[0].fs_settingid);

  // Add element copy
  newval := got;
  INSERT newval.arraytest[0] INTO newval.arraytest AT END;

  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", newval);
  RECORD got5 := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(got.arraytest[0].blobcell.settingid, got5.arraytest[0].blobcell.settingid);
  TestEq(got.arraytest[1].blobcell.settingid, got5.arraytest[1].blobcell.settingid);
  TestEq(got.arraytest[0].fs_settingid, got5.arraytest[0].fs_settingid);
  TestEq(got.arraytest[1].fs_settingid, got5.arraytest[1].fs_settingid);

  // got5.arraytest[2] must have unique settingid
  TestEq(FALSE, got5.arraytest[2].fs_settingid IN [ INTEGER(got.arraytest[0].fs_settingid), got.arraytest[1].fs_settingid ]);

  // Copy data to other file, may not alter original data
  testfile2->SetInstanceData("http://www.webhare.net/xmlns/beta/test", got5);
  TestEQ(got5, testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test"));

  // Reset rich document, may not alter original data
  testfile2->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ richtest := DEFAULT RECORD ]);
  TestEQ(got5, testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test"));

  //Partially update an array element, expect unnamed cells to get reset
  RECORD ARRAY inarray := got.arraytest;
  inarray[1] := [ fs_settingid := inarray[1].fs_settingid, stringarray := ["d","e"] ];
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ arraytest := inarray ]);

  got := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(DEFAULT RECORD, got.arraytest[1].blobcell);
  TestEq("", got.arraytest[1].textcell);
  TestEq(["d","e"], got.arraytest[1].stringarray);

  // Test for illegal richdoc updates
  RECORD illegal_richdocrec := richdocrec;
  INSERT DEFAULT RECORD INTO illegal_richdocrec.instances AT END;
  TestThrows(PTR testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ richtest := illegal_richdocrec ]));

  testfw->CommitWork();
}

MACRO TestPhotoAlbum()
{
  testfw->BeginWork();

  OBJECT soontobe_fotoalbum := testsite->rootobject->CreateFolder([name:="imgfolder"]);
  OBJECT img1 := soontobe_fotoalbum->CreateFile([name:="img1.jpg", type := 12, publish := TRUE]);

  TestEq(TRUE, IsBeingPublished(img1->id));
  MarkAsPublished(img1->id, FALSE);
  TestEq(FALSE , IsBeingPublished(img1->id));

  soontobe_fotoalbum->UpdateMetadata([ type := 2/*system folder*/]);
  TestEq(FALSE , IsBeingPublished(img1->id));

  soontobe_fotoalbum->UpdateMetadata([ type := 3/*foto album*/]);
  TestEq(TRUE, IsBeingPublished(img1->id));


  MarkAsPublished(img1->id, FALSE);
  soontobe_fotoalbum->UpdateMetadata([ type := 2/*system folder*/]);
  TestEq(TRUE, IsBeingPublished(img1->id));

  testfw->RollbackWork();
}

MACRO TestArrayMembers()
{
  // Test finding objects by (array) members values
  testfw->BeginWork();
  testsite->UpdateSiteMetadata([ webfeatures := ["webhare_testsuite:conversionstest"]]);
  testfw->CommitWork();

  TestEq([ mystring := ""
         , myarray := RECORD[]
         ], OpenWHFSType("http://www.webhare.net/xmlns/beta/arraymembertest")->defaultinstance);

  TestEq([ mystring := ""
         , myarray := RECORD[ [ fs_settingid := 0, mystring := "", myarray2 := [[ fs_settingid := 0, mystring2 := "" ]] ]]
         ], OpenWHFSType("http://www.webhare.net/xmlns/beta/arraymembertest")->structure);

  testfw->BeginWork();
  OBJECT arraytestfile := testsite->rootobject->CreateFile([ name:="arraytestfile", type := 0]);
  arraytestfile->SetInstanceData("http://www.webhare.net/xmlns/beta/arraymembertest",
      [ mystring := "asdf"
      , myarray := [ [ mystring := "qwer" ] ]
      ]);

  OBJECT whfstype := OpenWHFSType("http://www.webhare.net/xmlns/beta/arraymembertest");
  INTEGER ARRAY foundfile := [ INTEGER(arraytestfile->id) ];
  INTEGER ARRAY nomatches;

  TestThrows(PTR whfstype->FindObjectsByMemberValue("mystr", "x"));
  TestThrows(PTR whfstype->FindObjectsByMemberValue("myarray.mystr", "x"));
  TestThrows(PTR whfstype->FindObjectsByMemberValue("myarr.mystring", "x"));

  TestEq(foundfile, whfstype->FindObjectsByMemberValue("mystring", "asdf"));
  TestEq(foundfile, whfstype->FindObjectsByMemberValue("myarray.mystring", "qwer"));
  TestEq(nomatches, whfstype->FindObjectsByMemberValue("mystring", "qwer"));
  TestEq(nomatches, whfstype->FindObjectsByMemberValue("myarray.mystring", "asdf"));

  TestThrows(PTR whfstype->FindObjectsByMemberValueMask("mystr", "x"));
  TestThrows(PTR whfstype->FindObjectsByMemberValueMask("myarray.mystr", "x"));
  TestThrows(PTR whfstype->FindObjectsByMemberValueMask("myarr.mystring", "x"));

  TestEq(foundfile, whfstype->FindObjectsByMemberValueMask("mystring", "asdf"));
  TestEq(foundfile, whfstype->FindObjectsByMemberValueMask("myarray.mystring", "qwer"));
  TestEq(nomatches, whfstype->FindObjectsByMemberValueMask("mystring", "qwer"));
  TestEq(nomatches, whfstype->FindObjectsByMemberValueMask("myarray.mystring", "asdf"));

  TestEq(foundfile, whfstype->FindObjectsByMemberValueMask("*", "asdf"));
  TestEq(foundfile, whfstype->FindObjectsByMemberValueMask("*", "qwer"));

  testfw->RollbackWork();
}

MACRO TestDirectInstanceData()
{
  testfw->BeginWork();
  testsite->UpdateSiteMetadata([webfeatures := ["webhare_testsuite:autocreatetest"] ]);
  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT testdatafolder := testsite->rootobject->CreateFolder(
            [ name:="instancetestdata"
            , type := OpenWHFSType("http://www.webhare.net/xmlns/beta/autocreatefoldertest")->id
            ]);
  OBJECT testfile := testdatafolder->CreateFile(
            [ name:="testfile"
            , type := OpenWHFSType("http://www.webhare.net/xmlns/beta/autocreatefiletest")->id
            ]);
  testfw->CommitWork();

  testfw->BeginWork();
  // Update instance
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ aninstance :=
          [ whfstype := "http://www.webhare.net/xmlns/beta/instance2"
          , str2 := "str2"
          ]
      ]);

  OBJECT whfstype_instance1 := OpenWHFSType("http://www.webhare.net/xmlns/beta/instance1");
  OBJECT whfstype_instance2 := OpenWHFSType("http://www.webhare.net/xmlns/beta/instance2");

  RECORD rec := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest");
  TestEQ(rec.aninstance, GetWHFSInstanceDirect(rec.aninstance.whfssettingid));

  RECORD irec := SELECT * FROM whfstype_instance2->ListAllInstances() WHERE settingid = rec.aninstance.whfssettingid;
  TestEQ(rec.aninstance, whfstype_instance2->GetInstanceDataByDirectRef(irec.directref));

  // Wrong type
  TestThrows(PTR whfstype_instance2->SetInstanceDataByDirectRef(irec.directref, [ whfstype := "http://www.webhare.net/xmlns/beta/instance3" ]));
  whfstype_instance2->SetInstanceDataByDirectRef(irec.directref, [ str2 := "str3" ]);

  TestEQ(
      [ str2 :=           "str3"
      , whfsfileid :=     0
      , whfssettingid :=  rec.aninstance.whfssettingid
      , whfstype :=       "http://www.webhare.net/xmlns/beta/instance2"
      ], GetWHFSInstanceDirect(rec.aninstance.whfssettingid));

  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest",
      [ rich :=
          [ htmltext :=   StringToBlob("htmltext")
          , embedded :=   DEFAULT RECORD ARRAY
          , instances :=  [ [ instanceid := "TAG1"
                            , data :=
                                 [ whfstype := "http://www.webhare.net/xmlns/beta/instance1"
                                 , str1 := "str3"
                                 ]
                            ]
                          , [ instanceid := "TAG3"
                            , data :=
                                 [ whfstype := "http://www.webhare.net/xmlns/beta/instance3"
                                 , str3 := "str3"
                                 ]
                            ]
                          ]
          ]
      ]);

  rec := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest");
  TestEQ(rec.rich.instances[0].data, GetWHFSInstanceDirect(rec.rich.instances[0].data.whfssettingid));

  irec := SELECT * FROM whfstype_instance1->ListAllInstances() WHERE settingid = rec.rich.instances[0].data.whfssettingid;
  TestEQ(rec.rich.instances[0].data, whfstype_instance1->GetInstanceDataByDirectRef(irec.directref));

//  PRINT("After first set\n");
//  DumpValue((SELECT * FROM system.fs_instances WHERE fs_object = testfile->id), "boxed");
//  DumpValue((SELECT fs_settings.* FROM system.fs_settings, system.fs_instances WHERE fs_instances.fs_object = testfile->id AND fs_instances.id = fs_instance), "boxed");

  whfstype_instance2->SetInstanceDataByDirectRef(irec.directref, [ whfstype := "http://www.webhare.net/xmlns/beta/instance2", str2 := "str2" ]);

  rec := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/autocreatetest");
  TestEQ("TAG1", rec.rich.instances[0].instanceid, "Instanceid isn't preserved for type changes");
  TestEQ("http://www.webhare.net/xmlns/beta/instance2", rec.rich.instances[0].data.whfstype);

  testfw->CommitWork();

  // instances at object
  testfw->BeginWork();

  // Update instance
  testfile->SetInstanceData("http://www.webhare.net/xmlns/beta/instance2",
      [ str2 := "str2"
      ]);

  rec := testfile->GetInstanceData("http://www.webhare.net/xmlns/beta/instance2");
  irec := SELECT * FROM whfstype_instance2->ListAllInstances() WHERE settingid = 0 AND fsobject = testfile->id;
  TestEQ(rec, whfstype_instance2->GetInstanceDataByDirectRef(irec.directref));

  DATETIME now := GetCurrentDateTime();
  whfstype_instance2->SetInstanceDataByDirectRef(irec.directref, [ str2 := "str2" ]);

  TestEQ([ str2 := "str2" ], whfstype_instance2->GetInstanceDataByDirectRef(irec.directref));
  testfile->Refresh();
  TestEQ(TRUE, testfile->modificationdate >= now);

  Sleep(1);
  now := GetCurrentDateTime();
  whfstype_instance1->SetInstanceDataByDirectRef(irec.directref, [ str1 := "str1" ], [ isvisibleedit := FALSE ]);
  TestEQ([ str1 := "str1" ], whfstype_instance1->GetInstanceData(testfile->id));
  TestEQ([ str2 := "" ], whfstype_instance2->GetInstanceData(testfile->id));
  TestEQ([ str2 := "" ], whfstype_instance2->GetInstanceDataByDirectRef(irec.directref));
  testfile->Refresh();
  TestEQ(TRUE, testfile->modificationdate < now); // no update to modificationdate

  testfw->CommitWork();
}

RunTestframework([ PTR Prepare
                 , PTR MockedTypes
                 , PTR MergeTypeInto
                 , PTR FindTest
                 , PTR InplaceUpdateTest
                 , PTR TestPhotoAlbum
                 , PTR TestArrayMembers
                 , PTR TestDirectInstanceData
                 ]);
