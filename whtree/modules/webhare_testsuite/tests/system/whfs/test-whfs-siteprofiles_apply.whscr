<?wh

LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";

STRING FUNCTION GetMyIntercepts(INTEGER obj)
{
  RECORD data := GetApplyTesterForObject(obj)->RunSiteprofileHookTarget("webhare_testsuite:siteprofilehook", [ result := STRING[] ]);
  RETURN Detokenize(data.result, ',');
}

STRING FUNCTION GetMyCustomNodes(INTEGER obj) {
  STRING ARRAY result;
  FOREVERY(RECORD node FROM GetCustomSiteProfileSettings("urn:xyz", "test", obj))
    INSERT node.node->getattribute("data") INTO result AT END;
  RETURN Detokenize(result,',');
}

STRING FUNCTION GetMyCustomNodesThroughYaml(INTEGER obj) {
  STRING ARRAY result;
  FOREVERY(RECORD node FROM GetYamlPluginSettings(obj, "webhareTestsuite:testYaml"))
    INSERT node.data_attribute INTO result AT END;

  RETURN Detokenize(result,',');
}

OBJECT siteroot2;

MACRO TestBeforeSite()
{
  //Verify <fileype rtddoc ordering, we'll use a fakeapplytester outside our basetest sites for this
  OBJECT rtddoc_applytester := GetApplyTesterForFakeObject(whconstant_whfsid_repository, TRUE, OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile")->id);
  TestEq("http://www.webhare.net/xmlns/publisher/defaultrtdtype", rtddoc_applytester->GetRTDTypeSettings().rtdtype);

  OBJECT priotest_applytester := GetApplyTesterForFakeObject(whconstant_whfsid_repository, TRUE, OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/test_rtddoc_priority")->id);
  TestEq("http://www.webhare.net/xmlns/webhare_testsuite/rtd/level1", priotest_applytester->GetRTDTypeSettings().rtdtype);


  testfw->BeginWork();
  siteroot2 := testfw->GetWHFSTestRoot()->EnsureFolder([name := "webhare_testsuite.site2"]);

  OBJECT asystemfolder := GetTestsuiteTempFolder()->CreateFolder([name:="systemfolder", typens := "http://www.webhare.net/xmlns/publisher/systemfolder"]);
  OBJECT aslotsfolder := asystemfolder->CreateFolder([name:="slotsfolder", typens := "http://www.webhare.net/xmlns/publisher/contentlibraries/slots"]);
  OBJECT abeaconfolder := aslotsfolder->CreateFolder([name:="beaconsfolder", typens := "http://www.webhare.net/xmlns/publisher/contentlibraries/beacons"]);
  OBJECT subfile := abeaconfolder->CreateFile([name:="subfile"]);

  testfw->CommitWork();
  TestEq(RECORD[], GetCustomSiteProfileSettings("urn:xyz", "testyaml", siteroot2->id), "The xml nodes should not be visible to us as a toyam= is set up");
  TestEq([[ data_attribute := "applytest-megaglobal", source := [ applyindex := 0, siteprofile := "mod::webhare_testsuite/data/webhare_testsuite.siteprl.xml"] ]
         ,[ data_attribute := "applytest-whfspath", source := [ applyindex := 2, siteprofile := "mod::webhare_testsuite/data/webhare_testsuite.siteprl.xml"] ]
         ], GetYamlPluginSettings(siteroot2->id, "webhareTestsuite:testYaml"));

  TestEq(0, siteroot2->parentsite);
  TestEq('/webhare-tests/webhare_testsuite.testfolder/webhare_testsuite.site2/', siteroot2->whfspath);
  TestEq("applytest-megaglobal,applytest-whfspath", GetMyCustomNodes(siteroot2->id));
  TestEq("applytest-megaglobal,applytest-whfspath", GetMyCustomNodesThroughYaml(siteroot2->id));
  TestEq("intercept-whfspath,intercept-megaglobal", GetMyIntercepts(siteroot2->id));

  TestEq("applytest-megaglobal,parentmask-tmp,parentregex-tmp,is-system-folder", GetMyCustomNodes(asystemfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,is-slots-folder", GetMyCustomNodes(aslotsfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,in-slots-folder,is-beacons-folder", GetMyCustomNodes(abeaconfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,in-slots-folder,in-beacons-folder", GetMyCustomNodes(subfile->id));

  TestEq("applytest-megaglobal,parentmask-tmp,parentregex-tmp,is-system-folder", GetMyCustomNodesThroughYaml(asystemfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,is-slots-folder", GetMyCustomNodesThroughYaml(aslotsfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,in-slots-folder,is-beacons-folder", GetMyCustomNodesThroughYaml(abeaconfolder->id));
  TestEq("applytest-megaglobal,in-system-folder,in-slots-folder,in-beacons-folder", GetMyCustomNodesThroughYaml(subfile->id));
}

MACRO TestRules()
{
  siteroot2 := OpenWHFSObject(siteroot2->id);
  TestEq(OpenSiteByName("webhare_testsuite.site2")->id, siteroot2->parentsite);
  TestEq("applytest-megaglobal,applytest-whfspath", GetMyCustomNodes(siteroot2->id));

  testfw->BeginWork();

  OBJECT applytest1 := GetTestsuiteTempFolder()->CreateFolder([name:="applytest1"]);
  TestEq("/webhare-tests/webhare_testsuite.testsite/tmp/applytest1/", applytest1->whfspath);
  TestEq("applytest-megaglobal,applytest-whfsregex,pathmask-applytest1,parentmask-tmp,parentregex-tmp,pathregex-applytest1", GetMyCustomNodes(applytest1->id));

  applytest1->UpdateMetadata([typens:="http://www.webhare.net/xmlns/webhare_testsuite/applytestfolder"]);
  TestEq("folderapplytestertest,applytest-megaglobal,applytest-whfsregex,pathmask-applytest1,parentmask-tmp,parentregex-tmp,pathregex-applytest1", GetMyCustomNodes(applytest1->id));

  //even apply rules from OTHER websites should apply to us (ie they should escape automatisch siteid= tagging to their implicit apply rules)
  applytest1->UpdateMetadata([typens:="http://www.webhare.net/xmlns/webhare_testsuite/alttestfolder"]);
  TestEqLike("*alttestfolder*", GetMyCustomNodes(applytest1->id));

  OBJECT applytest1sub := applytest1->CreateFolder([name:="sub"]);
  TestEq("applytest-megaglobal,parentregex-tmp-applytest1,siteregex-webharetestsuitesuite", GetMyCustomNodes(applytest1sub->id));

  //Now test some rules applied through our main siteprofile
  OBJECT throughglobalsiteprofile := GetTestsuiteTempFolder()->CreateFolder([name:="applytest-throughglobal"]);
  TestEq("applytest-megaglobal,applytest-throughglobal,applytest-throughglobal-next,applytest-throughglobal-next2,parentmask-tmp,parentregex-tmp", GetMyCustomNodes(throughglobalsiteprofile->id));

  testfw->CommitWork();
}

RunTestFramework( [ PTR TestBeforeSite
                  , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:alttest", [ istemplate := FALSE, withcontent := FALSE, sitename := "webhare_testsuite.site2" ])
                  , PTR TestRules
                  ]);
