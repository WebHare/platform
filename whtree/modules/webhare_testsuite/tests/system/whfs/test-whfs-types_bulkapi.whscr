<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

OBJECT testsite;

MACRO TestBulkApi()
{
  testfw->BeginWork();

  OBJECT firstfolder := testfw->GetWHFSTestRoot();
  testsite := CreateSiteFromFolder(firstfolder->id);
  testsite->UpdateSiteMetadata([ webfeatures := ["webhare_testsuite:conversionstest"]]);

  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT testdatafolder := testsite->rootobject->CreateFolder([ name:="testbulk", type := 0]);
  OBJECT testfile2 := testdatafolder->CreateFile([ name:="testbulk.2", type := 0]);
  OBJECT testfile1 := testdatafolder->CreateFile([ name:="testbulk.1", type := 0]);
  OBJECT mytype := OpenWHFSType("webhare_testsuite:global.generic_test_type");
  OBJECT arrayedtype := OpenWHFSType("http://www.webhare.net/xmlns/beta/conversionstest");

  mytype->SetInstanceData(testfile2->id,
      [ str := "String"
      , int := 42
      , blub := WrapBlob(StringToBlob("Dit is een test"),"blub")
      , blub_img := WrapBlob(GetWebhareResource("mod::system/web/tests/goudvis.png"), "goudvis.png")
      , price := 0m
      , yes_no := FALSE
      , a_float := 0f
      , a_day := MakeDatetime(2012,5,4,3,2,1)
      , a_date_time := MakeDatetime(2010,5,4,3,2,1)
      , an_instance :=
          [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1"
          , str1 := "str1"
          ]
      , url := ""
      , rich :=
          [ htmltext :=   StringToBlob("htmltext")
          , embedded :=   [MakeMergedRecord(WrapBlob(StringToBlob("text"),"text.txt"), [ contentid := "cid:1" ])]
          , instances := [ [ instanceid := "TAG1"
                           , data :=
                                [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1"
                                , str1 := "str1"
                                ]
                           ]
                         , [ instanceid := "TAG3"
                           , data :=
                                [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance3"
                                , str3 := "str3"
                                ]
                           ]
                         ]
          ]
      , a_typed_record :=
          [ int_member := 13331
          , rich_member :=
              [ htmltext :=   StringToBlob("htmltext typedecord")
              ]
          ]
      , an_array :=
          [ [ int_member := 1
            , rich_member :=
                [ htmltext :=   StringToBlob("htmltext")
                , embedded :=   [MakeMergedRecord(WrapBlob(StringToBlob("text"),"text.txt"), [ contentid := "cid:1" ])]
                , instances := [ [ instanceid := "TAG1-1"
                                 , data :=
                                      [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1"
                                      , str1 := "str1-1"
                                      ]
                                 ]
                               , [ instanceid := "TAG1-3"
                                 , data :=
                                      [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance3"
                                      , str3 := "str1-3"
                                      ]
                                 ]
                               ]
                ]
            , a_sub_array :=
                  [ [ sub_int_member := 11
                    , sub_rich_member :=
                        [ htmltext :=   StringToBlob("htmltext")
                        , embedded :=   [MakeMergedRecord(WrapBlob(StringToBlob("text"),"text.txt"), [ contentid := "cid:1" ])]
                        , instances := [ [ instanceid := "TAG11-1"
                                         , data :=
                                              [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1"
                                              , str1 := "str11-1"
                                              ]
                                         ]
                                       , [ instanceid := "TAG11-3"
                                         , data :=
                                              [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance3"
                                              , str3 := "str11-3"
                                              ]
                                         ]
                                       ]
                        ]
                    ]
                  ]
            ]
          , [ int_member := 2
            , rich_member :=
                [ htmltext :=   StringToBlob("htmltext")
                , embedded :=   [MakeMergedRecord(WrapBlob(StringToBlob("text"),"text.txt"), [ contentid := "cid:1" ])]
                , instances := [ [ instanceid := "TAG2-1"
                                 , data :=
                                      [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1"
                                      , str1 := "str2-1"
                                      ]
                                 ]
                               , [ instanceid := "TAG2-3"
                                 , data :=
                                      [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/genericinstance3"
                                      , str3 := "str2-3"
                                      ]
                                 ]
                               ]
                ]
            ]
          ]
      ]);

  arrayedtype->SetInstanceData(testfile2->id, [ arr := [ [ data1 := "1&1", data2 := "1&2", data3 := "1&3", arraydata := [ [ data4 := "1&4" ] ] ]
                                                       , [ data1 := "2&1", data2 := "2&2", data3 := "2&3", arraydata := [ [ data4 := "2&4" ] ] ]
                                                       ]]);

  mytype->SetInstanceData(testfile1->id,
      [ str := ""
      , int := 0
      , blub := DEFAULT RECORD
      , price := 42.42
      , yes_no := TRUE
      , a_float := 3.5f
      , url := "http://example.com/test2"
      ]);

  RECORD ARRAY results;
  results := mytype->GetBulkData([INTEGER(testfile1->id), testfile2->id], ["STR","INT","a_date_time","a_day","an_instance", "URL", "an_array", "blub_img"]);

  TestEq(2,Length(results));
  TestEq(testfile1->id, results[0].id);
  TestEq(testfile2->id, results[1].id);
  TestEq("", results[0].str);
  TestEq(42, results[1].int);
  TestEq(DEFAULT DATETIME, results[0].a_date_time);
  TestEq(MakeDatetime(2010,5,4,3,2,1), results[1].a_date_time);
  TestEq(DEFAULT DATETIME, results[0].a_day);
  TestEq(MakeDate(2012,5,4), results[1].a_day);
  TestEq(DEFAULT RECORD, results[0].an_instance);
  TestEq("http://www.webhare.net/xmlns/webhare_testsuite/genericinstance1", results[1].an_instance.whfstype);
  TestEq("str1", results[1].an_instance.str1);
  TestEq(TRUE, results[1].an_instance.whfssettingid != 0);
  TestEq(0, results[1].an_instance.whfsfileid);
  TestEq(DEFAULT RECORD, results[0].blub_img);
  TestEq("aO16Z_3lvnP2CfebK-8DUPpm-1Va6ppSF0RtPPctxUY", results[1].blub_img.hash);

  TestEq(results[1].an_instance, GetWHFSInstanceDirect(results[1].an_instance.whfssettingid));
  TestEq("http://example.com/test2", results[0].url);
  TestEq("", results[1].url);

  // Request complete 'arr' record array members
  results := arrayedtype->GetBulkData([INTEGER(testfile1->id), testfile2->id],["ARR"]);
  TestEq(0,Length(results[0].arr));
  TestEq(2,Length(results[1].arr));
  TestEq("1&1",results[1].arr[0].data1);
  TestEq("2&3",results[1].arr[1].data3);
  TestEq(1,Length(results[1].arr[0].arraydata));
  //ADDME: Failes; requesting "ARR" should retrieve all subfields recursively
  TestEq("1&4",results[1].arr[0].arraydata[0].data4);

  // Because complete 'arr' record array members are requested, the 'arr.data1' is implicitly returned as well
  results := arrayedtype->GetBulkData([INTEGER(testfile1->id), testfile2->id],["ARR","ARR.DATA1"]);
  TestEq(0,Length(results[0].arr));
  TestEq(2,Length(results[1].arr));
  TestEq("1&1",results[1].arr[0].data1);
  TestEq("2&3",results[1].arr[1].data3);

  // Only the 'arr.data1' member is requested, all other 'arr' members will have their default values
  results := arrayedtype->GetBulkData([INTEGER(testfile1->id), testfile2->id],["ARR.DATA1"]);
  TestEq(0,Length(results[0].arr));
  TestEq(2,Length(results[1].arr));
  TestEq("1&1",results[1].arr[0].data1);
  TestEq(FALSE,CellExists(results[1].arr[1],"data3"));

  // Test for throw on non-existing subfield
  TestThrowsLike("No such member \'arr.data4\' in contenttype http://www.webhare.net/xmlns/beta/conversionstest", PTR arrayedtype->GetBulkData([INTEGER(testfile1->id), testfile2->id],["ARR.DATA4"]));

  // Get RTD with instances
  results := mytype->GetBulkData([INTEGER(testfile2->id)],["RICH"]);
  TestEq(1,Length(results));
  TestEq(2,Length(results[0].rich.instances));
  TestEq('str1', results[0].rich.instances[0].data.str1);

  results := mytype->GetBulkData(INTEGER[ testfile2->id ], [ "an_array"]);
  TestEq('str1-1', results[0].an_array[0].rich_member.instances[0].data.str1);
  TestEq('str2-3', results[0].an_array[1].rich_member.instances[1].data.str3);
  TestEq('str11-1', results[0].an_array[0].a_sub_array[0].sub_rich_member.instances[0].data.str1);
  TestEq(1, results[0].an_array[0].int_member);
  TestEq(11, results[0].an_array[0].a_sub_array[0].sub_int_member);

  results := mytype->GetBulkData(INTEGER[ testfile2->id ], [ "an_array.rich_member", "an_array.A_SUB_ARRAY.SUB_rich_member"]);
  TestEq('str1-1', results[0].an_array[0].rich_member.instances[0].data.str1);
  TestEq('str2-3', results[0].an_array[1].rich_member.instances[1].data.str3);
  TestEq('str11-1', results[0].an_array[0].a_sub_array[0].sub_rich_member.instances[0].data.str1);
  TestEq(FALSE, CellExists(results[0].an_array[0], "int_member"));
  TestEq(FALSE, CellExists(results[0].an_array[0].a_sub_array[0], "sub_int_member"));

  results := mytype->GetBulkData(INTEGER[ testfile2->id ], [ "a_typed_record"]);
  TestEq(13331, results[0].a_typed_record.int_member);
  TestEqLike("*htmltext*", BlobToString(results[0].a_typed_record.rich_member.htmltext));

  results := mytype->GetBulkData(INTEGER[ testfile2->id ], [ "a_typed_record.rich_member"]);
  TestEq(FALSE, CellExists(results[0].a_typed_record,'int_member'));
  TestEqLike("*htmltext*", BlobToString(results[0].a_typed_record.rich_member.htmltext));

  testfw->RollbackWork();
}


RunTestframework([ PTR TestBulkApi
                 ]);
