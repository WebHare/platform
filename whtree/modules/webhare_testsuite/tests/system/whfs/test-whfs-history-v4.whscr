<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/history.whlib";
LOADLIB "mod::publisher/lib/dialogs.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO Prep()
{
  testfw->BeginWork();

  OBJECT testfolderobj2 := CreateObjectWithHistory(testfw->GetUserObject("sysop"), GetTestsuiteTempFolder(),
    [ name := "sub", typens := "http://www.webhare.net/xmlns/publisher/normalfolder" ]);
  TestEqMembers([[ type := "created", user := testfw->GetUserAuthobjectId("sysop"), when := testfolderobj2->creationdate, userdata := [ login := "sysop@beta.webhare.net" ] ]], ListObjectHistory(testfolderobj2->id), "*");
  TestEq(testfolderobj2->creationdate, testfolderobj2->modificationdate);

  OBJECT newfile := CreateObjectWithHistory(testfw->GetUserObject("marge"), testfolderobj2,
    [ name :="test.html"
    , title :="A new title"
    , keywords := "my new keywords"
    , description := "d"
    , data := StringToBlob("I'm a jolly good file")
    , typens := "http://www.webhare.net/xmlns/publisher/htmlfile"
    , instances := [[ whfstype := "http://www.webhare.net/xmlns/beta/test", stringarray := ["a","b","d"]  ]]
    ]);
  TestEq(newfile->creationdate, newfile->modificationdate);
  newfile->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "created_0_1"]);
  newfile->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "created_0_1"]);
  newfile->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "created_0_1"]);

  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1", user := testfw->GetUserAuthobjectId("marge"), when := newfile->modificationdate, userdata := [ login := "marge@beta.webhare.net" ] ]
                ], ListObjectHistory(newfile->id), "*");
  TestEq(['a','b','d'], newfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test").stringarray);

  testfw->CommitWork();
}

MACRO DraftingTest()
{
  testfw->BeginWork();
  testfw->SetTestUser("marge");

  OBJECT testfile := GetTestsuiteTempFolder()->OpenByPath("sub/test.html");
  TestEq(TRUE, ObjectExists(testfile));

  TestEq(0, Length(GetDrafts(testfile->id)), "There shouldn't be anything being editted about this file");
  TestEq(["created"], (SELECT AS STRING ARRAY type FROM ListObjectHistory(testfile->id)), "And no additional history yet");

  TestEq(FALSE, TestFlagFromPublished(testfile->published, PublishedFlag_HasPublicDraft));

  //Start editting this document. Create a private draft (autosave)
  OBJECT draft := StartWorkflowUpdate(testfile->id, [user := GetEffectiveUser()]);
  TestEq(FALSE, draft->ispublic);
  TestEq(TRUE, ObjectExists(draft->fsobject));
  TestEq(FALSE, draft->fsobject->isactive);

  //Such a draft should only bother with workflow controlled data
  TestEq([ no_clone_string := "" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "created_0_1" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq([ name := "test.html", title := "A new title", keywords := "my new keywords", description := "d" ], draft->GetMetadata());

  draft->fsobject->UpdateData(StringToBlob("This just went into a temporary draft"));
  TestThrowsLike("*not*workflow*", PTR draft->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "pvt_draft_0_1" ]));
  draft->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "pvt_draft_0_1" ]);
  TestThrowsLike("*not*workflow*", PTR draft->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "pvt_draft_0_1" ]));

  draft->UpdateMetadata(CELL[ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd" ]);
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd" ], draft->GetMetadata());

  //TODO should drafts have a GetInstanceData? If so, would that then look at the main object for non-workflow data?
  TestEq([ no_clone_string := "" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "pvt_draft_0_1" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "" ], draft->fsobject->GetInstanceData("webhare_testsuite:global.type_clone_only"));

  RECORD ARRAY drafts := GetDrafts(testfile->id);
  TestEq(1, Length(drafts));
  TestEq(FALSE, drafts[0].ispublic);
  TestEq(draft->id, drafts[0].id);
  TestEq(testfw->GetUserAuthobjectId("marge"), drafts[0].user);
  TestEq(TRUE, drafts[0].mine);

  //Let's create another private draft. Ignore it though
  StartWorkflowUpdate(testfile->id, [user := GetEffectiveUser()]);
  TestEq(2, Length(GetDrafts(testfile->id)));

  //Get autosaves for this user
  RECORD ARRAY autosaves := ListUserWHFSDrafts(GetEffectiveUserID());
  TestEq(2,Length(autosaves));

  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1" ]
                ], ListObjectHistory(testfile->id), "*", "Private drafts always stay out of the history");

  //Set a clonable instance on the draft to see if its properly copied
  TestThrowsLike("*not*workflow*", PTR draft->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "draft_0_1"]));
  draft->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "draft_0_1"]);
  TestThrowsLike("*not*workflow*", PTR draft->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "draft_0_1"]));

  //Now create a public draft from the first autosave. A public draft will destroy all autosaves FIXME why should it ? temp history may still have some value see VSCode file history
  //Note that this converts the autosave to a public draft and logs it into the history - no new file is actually created here!
  draft->SaveAsNewPublicDraft(testfw->GetUserObject("marge"));
  TestEq(TRUE, TestFlagFromPublished(OpenWHFSObject(testfile->id)->published, PublishedFlag_HasPublicDraft));

  //This draft is part of history now and shouldn't be easily modified
  TestThrowsLike("*read-only*", PTR draft->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "try_update"]));
  TestThrowsLike("*read-only*", PTR draft->UpdateMetadata(CELL[ title := "Try update" ]));

  // FIXME verify that empty instances properly get copied too

  RECORD ARRAY history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1" ]
                , [ type := "saved", version := "0.2", user := testfw->GetUserAuthobjectId("marge"), userdata := [ login := "marge@beta.webhare.net" ] ]
                ], history, "*");

  // Original file is still untouched
  TestEq([ no_clone_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq("I'm a jolly good file", BlobToString(OpenWHFSObject(testfile->id)->data), "Testfile is still untouched");
  TestEq("test.html", testfile->name);
  TestEq("A new title", testfile->title);
  TestEq("my new keywords", testfile->keywords);
  TestEq("d", testfile->description);

  // But we see new metadata on the public draft
  // TODO shouldn't snapshots have an Object API too? aren't they just Drafts too, just frozen ?
  OBJECT rawsnapshot := OpenWHFSObject(history[1].snapshot);
  TestEq([ no_clone_string := "" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "draft_0_1" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_clone_only"));

  TestEq(rawsnapshot->modificationdate, history[1].when);
  TestEq("This just went into a temporary draft", BlobToString(OpenWHFSObject(history[1].snapshot)->data), "Content of autosave is still updated");

  //Verify the autosaves were cleaned up
  drafts := GetDrafts(testfile->id);
  TestEqMembers([
    [ ispublic := TRUE, mine := TRUE ]
  ], drafts, "*", "SaveAsNewPublicDraft should have discarded all autosaves and private drafts");

  draft := OpenDraft(testfile->id, drafts[0].id);
  TestEq(TRUE, draft->ispublic);

  draft := OpenWHFSDraft(draft->id);
  TestEq(TRUE, draft->ispublic);
  TestEq(testfile->id, draft->source);

  TestEq(FALSE, Objectexists(OpenWHFSDraft(testfile->id)));

  //Create a draft!
  Sleep(1);
  draft := StartWorkflowUpdate(drafts[0].id, [user := GetEffectiveUser()]);
  draft->fsobject->UpdateData(StringToBlob("I'm a jolly good file, but now with more data!"));

  //We need more drafts!
  Sleep(1);
  testfw->SetTestUser("homer");
  draft := StartWorkflowUpdate(drafts[0].id, [user := GetEffectiveUser()]);
  draft->fsobject->UpdateData(StringToBlob("I'm a jolly good file with some data added by Homer!"));

  drafts := GetDrafts(testfile->id);
  TestEq(TRUE, drafts[0].creationdate < drafts[1].creationdate, "Verify sort");
  TestEqMembers([
    [ ispublic := TRUE,  mine := FALSE, user := testfw->GetUserAuthobjectId("marge") ],
    [ ispublic := FALSE, mine := FALSE, user := testfw->GetUserAuthobjectId("marge") ],
    [ ispublic := FALSE, mine := TRUE,  user := testfw->GetUserAuthobjectId("homer")  ]
  ], drafts, "*", "1 public draft and 2 autosaves, one of which is Homer's");


  //Cleanup discardable work from other users. Note that we switched to Homer earlier so we're clearing Marge's drafts
  CleanupOtherPrivateDrafts(testfile->id);
  drafts := GetDrafts(testfile->id);
  TestEq(2, Length(drafts));
  TestEq(FALSE, drafts[0].mine);
  TestEq(TRUE, drafts[1].mine);

  TestEq(TRUE, TestFlagFromPublished(OpenWHFSObject(testfile->id)->published, PublishedFlag_HasPublicDraft));

  //Let's save another draft
  draft := OpenWHFSDraft(drafts[1].id); //This is taking the autosave I created
  TestEq([ with_workflow_string := "draft_0_1" ], draft->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd" ], draft->GetMetadata());
  draft->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "yet-another-draft" ]);
  draft->SaveAsNewPublicDraft(testfw->GetUserObject("homer"));

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved", version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved", version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                ], history, "*");

  TestEq([ with_workflow_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "draft_0_1" ], OpenWHFSObject(history[1].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "yet-another-draft" ], OpenWHFSObject(history[2].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq(OpenWHFSObject(history[2].snapshot)->modificationdate, history[2].when);

  drafts := GetDrafts(testfile->id);
  TestEq(1, Length(drafts));
  TestEq(TRUE, drafts[0].mine);
  TestEq(history[2].snapshot, drafts[0].id);

  ///////////////////
  // STORY: We will now actually publish the final draft. This adds version 1.0 to the history
  draft := OpenWHFSDraft(drafts[0].id);

  DATETIME oldmoddate := testfile->modificationdate;
  draft->SaveAsFinal(testfw->GetUserObject("marge"));

  testfile := OpenWHFSObject(testfile->id);
  TestEq([ with_workflow_string := "yet-another-draft" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq("I'm a jolly good file with some data added by Homer!", BlobToString(OpenWHFSObject(testfile->id)->data));
  //FIXME CONSIDER should SaveAsFinal also rename the file? TestEq("test2.html", testfile->name);
  //               also consider the file might be pinned since the draft started
  //TODO Consider that title/keywords may have been updated OUTSIDE our workflow, handle that too
  TestEq("An even newer title", testfile->title);
  TestEq("my new keywords, plus more", testfile->keywords);
  TestEq("dd", testfile->description);

  TestEq(0, Length(GetDrafts(testfile->id)));
  TestEq(TRUE, testfile->modificationdate > oldmoddate);
  TestEq(FALSE, TestFlagFromPublished(testfile->published, PublishedFlag_HasPublicDraft));

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "1.0", user := testfw->GetUserAuthobjectId("marge"), when := testfile->modificationdate ]
                ], history, "*");

  //Verify the snapshots in the history. They should contain the right info... but they should NOT be the public version!
  //FIXME where did created_0_1 go ?
  TestEq([ with_workflow_string := "yet-another-draft" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "draft_0_1" ], OpenWHFSObject(history[1].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "yet-another-draft" ], OpenWHFSObject(history[2].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "yet-another-draft" ], OpenWHFSObject(history[3].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));

  TestAssert(testfile->id != history[3].snapshot, "Assure us that the 1.0 version is really independent from the live file");
  TestEq(RECORD[], GetDrafts(testfile->id), "Finalizing should have discarded all drafts"); //FIXME we probably don't want this in the future

  /////////////////////
  //
  // STORY: Going straight from autosave to final should not leave a lingering autosave

  //Create someone else's autosave
  StartWorkflowUpdate(testfile->id, [user := testfw->GetUserObject("homer")]);

  //Create our autosave for this draft
  testfw->SetTestUser("marge");
  draft := StartWorkflowUpdate(testfile->id, [user := testfw->GetUserObject("marge")]);
  OpenWHFSType("http://www.webhare.net/xmlns/beta/test")->SetInstanceData(draft->id, [ stringarray := ["rabbits"] ]);
  draft->SaveAsFinal(testfw->GetUserObject("marge"));

  testfile := OpenWHFSObject(testfile->id);
  TestEq(RECORD[], GetDrafts(testfile->id), "Finalizing should have discarded all drafts");

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "1.0", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "approved", version := "2.0", user := testfw->GetUserAuthobjectId("marge"), when := testfile->modificationdate ]
                ], history, "*");

  /////////////////////
  //
  // STORY: Neither should: Autosave, Draft, Edit, Publish leave a lingering autosave
  //

  draft := StartWorkflowUpdate(testfile->id, [user := testfw->GetUserObject("homer")]);
  OpenWHFSType("http://www.webhare.net/xmlns/beta/test")->SetInstanceData(draft->id, [ stringarray := ["robots"] ]);
  draft->SaveAsNewPublicDraft(testfw->GetUserObject("homer"));
  draft := StartWorkflowUpdate(testfile->id, [user := testfw->GetUserObject("homer")]);
  OpenWHFSType("http://www.webhare.net/xmlns/beta/test")->SetInstanceData(draft->id, [ stringarray := ["death"] ]);
  draft->SaveAsFinal(testfw->GetUserObject("homer"));

  TestEq(RECORD[], GetDrafts(testfile->id), "Finalizing should have discarded all drafts");

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "1.0", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "approved", version := "2.0", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "2.1", user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "3.0", user := testfw->GetUserAuthobjectId("homer") ]
                ], history, "*");

  testfw->CommitWork();
}

ASYNC MACRO UnpublishedSavesTest()
{
  testfw->BeginWork();

  OBJECT testfolderobj2 := GetTestsuiteTempFolder()->OpenByName("sub");
  OBJECT unpublishedfile := CreateObjectWithHistory(testfw->GetUserObject("marge"), testfolderobj2,
    [ name := "unpublishedfile.html"
    , title :="An unpublished file"
    , data := StringToBlob("Don't publish me now")
    , typens := "http://www.webhare.net/xmlns/publisher/htmlfile"
    , instances := [[ whfstype := "http://www.webhare.net/xmlns/beta/test", stringarray := ["nonopublish"]  ]]
    , publish := FALSE
    ]);

  OBJECT draft := StartWorkflowUpdate(unpublishedfile->id, [user := testfw->GetUserObject("marge")]);
  OpenWHFSType("http://www.webhare.net/xmlns/beta/test")->SetInstanceData(draft->id, [ stringarray := ["love"] ]);

  draft->SaveAsNewPublicDraft(testfw->GetUserObject("homer"));
  TestEqMembers([[ ispublic := TRUE ]], GetDrafts(unpublishedfile->id), "*", "SaveAsNewPublicDraft should have discarded all autosaves");

  draft->SaveAsFinal(testfw->GetUserObject("homer"));
  TestEqMembers(RECORD[], GetDrafts(unpublishedfile->id), "*", "SaveAsFinal should have discarded all drafts");
  TestEq(FALSE, OpenWHFSObject(unpublishedfile->id)->publish);

  //Create an autosave or an unpublished file and finalize it straight away
  draft := StartWorkflowUpdate(unpublishedfile->id, [user := testfw->GetUserObject("marge")]);
  draft->fsobject->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ stringarray := ["homer"] ]);
  draft->SaveAsFinal(testfw->GetUserObject("homer"));
  TestEqMembers(RECORD[], GetDrafts(unpublishedfile->id), "*", "SaveAsFinal should have discarded all drafts");

  RECORD ARRAY history := ListObjectHistory(unpublishedfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", userdata := [ login := "marge@beta.webhare.net"], user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.2", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "1.0", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "approved", version := "2.0", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer") ]
                ], history, "*");

  testfw->CommitWork();

  //Verify that even sysop cannot edit this a history item
  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ToString(history[3].snapshot)]]));
  BOOLEAN clicked := AWAIT ExpectNoScreenChange(PTR TTClick("publish", [ allowfailure := TRUE ]));
  TestEq(FALSE, clicked);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO ViewHistory() {
  DATETIME startcopy := GetCurrentDatetime();

  //Verify the history info. (lives on Inspect tab until we're ready to expose it to non-sysops)
  OBJECT unpublishedfile := GetTestsuiteTempFolder()->OpenByPath("sub/test.html");

  //https://my.webhare.dev/?app=publisher(/webhare-tests/webhare_testsuite.testsite/tmp/sub/test.html)/inspect
  AWAIT ExpectScreenChange(+1, PTR RunFSObjectInspectDialog(GetTestController(), unpublishedfile->id));
  TT("historyitems")->selection := SELECT * FROM TT("historyitems")->rows WHERE version="0.2"; //this one contained temproary data

  AWAIT ExpectScreenChange(+1, PTR TTClick("{item}:Copy to"));
  TT("folders")->value := GetTestsuiteTempFolder()->id;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  RECORD ARRAY newobjects := SELECT * FROM system.fs_objects WHERE parent = GetTestsuiteTempFolder()->id AND creationdate >= startcopy;
  TestEq(1, Length(newobjects), "There should be one new object created by the copy");
}

RunTestframework([ PTR Prep
                 , PTR DraftingTest
                 , PTR UnpublishedSavesTest
                 , PTR ViewHistory
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "marge" ]
                                   ,[ login := "homer" ]
                                   ]
                    ]);
