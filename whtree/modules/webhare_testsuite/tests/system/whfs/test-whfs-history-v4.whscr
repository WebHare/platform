<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::regex.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/history.whlib";
LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

RECORD FUNCTION GetCloneTestFields(INTEGER id) {
  RETURN CELL[
    ...OpenWHFSType("webhare_testsuite:global.type_no_clone")->GetInstanceData(id),
    ...OpenWHFSType("webhare_testsuite:global.type_with_workflow")->GetInstanceData(id),
    ...OpenWHFSType("webhare_testsuite:global.type_clone_only")->GetInstanceData(id)
  ];
}

MACRO SetCloneTestFields(INTEGER id, RECORD fields) {
  IF(CellExists(fields, "no_clone_string"))
    OpenWHFSType("webhare_testsuite:global.type_no_clone")->SetInstanceData(id, [ no_clone_string := fields.no_clone_string ]);
  IF(CellExists(fields, "with_workflow_string"))
    OpenWHFSType("webhare_testsuite:global.type_with_workflow")->SetInstanceData(id, [ with_workflow_string := fields.with_workflow_string ]);
  IF(CellExists(fields, "clone_only_string"))
    OpenWHFSType("webhare_testsuite:global.type_clone_only")->SetInstanceData(id, [ clone_only_string := fields.clone_only_string ]);
}


MACRO Prep()
{
  testfw->BeginWork();

  testfw->GetUserObject("sysop")->UpdateGrant("grant", "system:fs_fullaccess", GetTestsuiteTempFolder()->id, testfw->GetUserObject("marge"));

  OBJECT testfolderobj2 := GetTestsuiteTempFolder()->CreateFolder([ name := "sub" ], [ createdby := testfw->GetUserObject("sysop") ]);
  TestEqMembers([[ name := "sub", type := "created", user := testfw->GetUserAuthobjectId("sysop"), when := testfolderobj2->creationdate, userdata := [ login := "sysop@beta.webhare.net" ] ]],
                  ListObjectHistory(testfolderobj2->id), "*");
  TestEq(testfolderobj2->creationdate, testfolderobj2->modificationdate);

  OBJECT newfile := testfolderobj2->CreateFile(
    [ title := "Test!"
    , keywords := "my new keywords"
    , description := "d"
    , data := StringToBlob("I'm a jolly good file")
    , typens := "http://www.webhare.net/xmlns/publisher/htmlfile"
    ],[ instances := [[ whfstype := "http://www.webhare.net/xmlns/beta/test", stringarray := ["a","b","d"]  ]]
      , createdby := testfw->GetUserObject("marge")
      ]
    );

  TestEq(FALSE, newfile->publish);
  TestEq(newfile->creationdate, newfile->modificationdate);
  TestEq("test.html", newfile->name);
  newfile->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "created_0_1"]);
  newfile->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "created_0_1"]);
  newfile->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "created_0_1"]);

  TestEqMembers([ [ name := "", type := "created", snapshot := 0, version := "0.1", user := testfw->GetUserAuthobjectId("marge"), when := newfile->modificationdate, userdata := [ login := "marge@beta.webhare.net" ] ]
                ], ListObjectHistory(newfile->id), "*", "No name, we did not explicitly set it");
  TestEq(['a','b','d'], newfile->GetInstanceData("http://www.webhare.net/xmlns/beta/test").stringarray);

  testfw->CommitWork();
}

RECORD FUNCTION AnalyzeURL(STRING url) {
  TestEq(TRUE, testfw->browser->GotoWebPage(url));

  RETURN [ title := testfw->browser->QS("title")->textcontent
         , no_clone_string := ObjectExists(testfw->browser->QS("#no_clone_string")) ? testfw->browser->QS("#no_clone_string")->textcontent : ""
         , with_workflow_string := ObjectExists(testfw->browser->QS("#with_workflow_string")) ? testfw->browser->QS("#with_workflow_string")->textcontent : ""
         , clone_only_string := ObjectExists(testfw->browser->QS("#clone_only_string")) ? testfw->browser->QS("#clone_only_string")->textcontent : ""
         , textcontent := testfw->browser->QS("body")->textcontent
        //TODO test file level metadata
        //  , keywords := testfw->browser->QS("meta[name='keywords']")->GetAttribute("content")
        //  , description := testfw->browser->QS("meta[name='description']")->GetAttribute("content")
         ];
}

RECORD FUNCTION AnalyzePreview(INTEGER objid) {
  RETURN AnalyzeURL(GetPreviewLink(objid));
}

MACRO DraftingTest() {
  testfw->BeginWork();
  testfw->SetTestUser("marge");

  OBJECT testfile := GetTestsuiteTempFolder()->OpenByPath("sub/test.html");
  TestEq(TRUE, ObjectExists(testfile));

  TestEq(["created"], (SELECT AS STRING ARRAY type FROM ListObjectHistory(testfile->id)), "And no additional history yet");

  TestEq(FALSE, TestFlagFromPublished(testfile->published, PublishedFlag_HasPublicDraft));
  testfw->CommitWork();

  //Start editing this document. Create a private draft (autosave)
  OBJECT workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser(),
      useworkflow := TRUE,
      workflowtypes := ["webhare_testsuite:global.type_with_workflow"]
    ]);

  TestThrowsLike("*not*workflow*", PTR workflowmgr->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "created_0_1" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestThrowsLike("*not*workflow*", PTR workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq([ name := "", title := "Test!", keywords := "my new keywords", description := "d", data := StringToBlob("I'm a jolly good file") ], workflowmgr->GetMetadata());

  TestEqMembers([
    title := "Test!",
    with_workflow_string := "created_0_1",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1"
  ], AnalyzePreview(workflowmgr->__GetDraftObject()->id), "*");

  testfw->BeginWork();

  workflowmgr->UpdateData(StringToBlob("This just went into a temporary draft"));

  //Writing triggers an autosave in the history
  TestEqMembers([ [ name := "", type := "created", snapshot := 0, version := "0.1", user := testfw->GetUserAuthobjectId("marge")]
                , [ name := "", type := "autosave", version := "", user := testfw->GetUserAuthobjectId("marge")] //autosaves are unversioned
                ], ListObjectHistory(testfile->id), "*");

  //Unclaimed instances cannot be managed through the workflow
  TestThrowsLike("*not*workflow*", PTR workflowmgr->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "pvt_draft_0_1" ]));
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "pvt_draft_0_1" ]);
  TestThrowsLike("*not*workflow*", PTR workflowmgr->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "pvt_draft_0_1" ]));

  workflowmgr->UpdateMetadata(CELL[ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd" ]);
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd", data := StringToBlob("This just went into a temporary draft")  ], workflowmgr->GetMetadata());

  TestThrowsLike("*not*workflow*", PTR workflowmgr->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "pvt_draft_0_1" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestThrowsLike("*not*workflow*", PTR workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"));

  testfw->CommitWork();

  TestEqMembers([[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  TestEqMembers([
    title := "Test!",
    with_workflow_string := "created_0_1",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1"
  ], AnalyzePreview(testfile->id), "*");

  TestEqMembers([
    title := "An even newer title",
    with_workflow_string := "pvt_draft_0_1",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1"
  ], AnalyzePreview(workflowmgr->__GetAutosavedObject()->id), "*");


  testfw->BeginWork();

  //Let's create another private draft. Ignore it though (TODO does this scenario make sense? it doesn't use the previous autosave as basis? Replace it with multiple historic autosaves later)
  //FIXME this just updates the existing draft. if we really want a new a new private draft we need a ->Checkpoint() API or something which stops updating the previous draft.
  /* FIXME but that's not what we're building in this MR
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "pvt_draft_0_2" ]);
 TestEq(2, Length(GetDrafts(testfile->id)));

  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1" ]
                ], ListObjectHistory(testfile->id), "*", "Private drafts always stay out of the history");
  */

  //Set a clonable instance on the draft to see if its properly copied
  TestThrowsLike("*not*workflow*", PTR workflowmgr->SetInstanceData("webhare_testsuite:global.type_no_clone", [ no_clone_string := "draft_0_1"]));
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "draft_0_1"]);
  TestThrowsLike("*not*workflow*", PTR workflowmgr->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "draft_0_1"]));

  //Now create a public draft from the first autosave. A public draft will destroy all autosaves FIXME why should it ? temp history may still have some value see VSCode file history
  //Note that this converts the autosave to a public draft and logs it into the history - no new file is actually created here!
  workflowmgr->Save(); //commits the autosave changes to the draft


  TestEq(TRUE, RecordExists(workflowmgr->GetCurrentDraft()));
  TestEqMembers([[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetDraftObject()->id), '*');

  TestEq(TRUE, TestFlagFromPublished(OpenWHFSObject(testfile->id)->published, PublishedFlag_HasPublicDraft));
  TestEqMembers([ mine := TRUE ], workflowmgr->GetCurrentDraft(), "*");


  //This draft is part of history now and shouldn't be easily modified
  TestThrowsLike("*readonly*", PTR workflowmgr->__GetDraftObject()->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "try_update"]));
  TestThrowsLike("*readonly*", PTR workflowmgr->__GetDraftObject()->UpdateMetadata(CELL[ title := "Try update" ]));

  RECORD ARRAY history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1" ]
                , [ type := "saved", version := "0.2", user := testfw->GetUserAuthobjectId("marge"), userdata := [ login := "marge@beta.webhare.net" ], snapshot := workflowmgr->__GetDraftObject()->id ]
                ], history, "*");

  // Original file is still untouched
  TestEq([ no_clone_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq("I'm a jolly good file", BlobToString(OpenWHFSObject(testfile->id)->data), "Testfile is still untouched");
  TestEq("test.html", testfile->name);
  TestEq("Test!", testfile->title);
  TestEq("my new keywords", testfile->keywords);
  TestEq("d", testfile->description);

  // But we see new metadata on the public draft
  // TODO shouldn't snapshots have an Object API too? aren't they just Drafts too, just frozen ?
  OBJECT rawsnapshot := OpenWHFSObject(history[1].snapshot);
  TestEq([ no_clone_string := "" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_no_clone"));
  TestEq([ with_workflow_string := "draft_0_1" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "" ], rawsnapshot->GetInstanceData("webhare_testsuite:global.type_clone_only"));

  TestEq(rawsnapshot->modificationdate, history[1].when);
  TestEq("This just went into a temporary draft", BlobToString(OpenWHFSObject(history[1].snapshot)->data), "Content of autosave is still updated");

  testfw->CommitWork();

  // Grab a previewlink to the saved draft
  STRING thisversionlink := GetPreviewLink(history[1].snapshot);
  STRING mostrecentversionlink := GetPreviewLink(history[1].snapshot, [ share := "mostrecentversion" ]);
  RECORD expect_analyze_thisversion := [
    title := "An even newer title",
    with_workflow_string := "draft_0_1",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1",
    //Ensure we get the *new* textcontent (ie: that you can see your draft when previewing an unpublished file)
    textcontent := NEW Regex("This just went into a temporary draft","i")
  ];

  TestEqMembers(expect_analyze_thisversion, AnalyzePreview(testfile->id), "*");

  testfw->BeginWork();

  //Create a draft. Verify it clones metadata properly
  Sleep(1);
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd", data := StringToBlob("This just went into a temporary draft")  ], workflowmgr->GetMetadata());
  workflowmgr->UpdateData(StringToBlob("I'm a jolly good file, but now with more data!"));
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd", data := StringToBlob("I'm a jolly good file, but now with more data!")  ], workflowmgr->GetMetadata());
  workflowmgr->UpdateMetadata([ title := "Jolly Good Autosave"]);
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "jolly-good-autosave" ]);

  testfw->CommitWork();

  TestEqMembers([
    title := "Jolly Good Autosave",
    with_workflow_string := "jolly-good-autosave",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1"
  ], AnalyzePreview(workflowmgr->__GetAutosavedObject()->id), "*");
  TestEqMembers([
    title := "An even newer title",
    with_workflow_string := "draft_0_1",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1"
  ], AnalyzePreview(workflowmgr->__GetDraftObject()->id), "*");

  testfw->BeginWork();
  //We need more drafts!
  Sleep(1);
  testfw->GetUserObject("sysop")->UpdateGrant("grant", "system:fs_fullaccess", GetTestsuiteTempFolder()->id, testfw->GetUserObject("homer"));

  testfw->SetTestUser("homer"); //FIXME we also need to test with a user who has no write access (yet) as they currently cannot see drafts.
  //reopen for homer
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := ["webhare_testsuite:global.type_with_workflow"]
    ]);

  DATETIME now := GetCurrentDateTime();
  workflowmgr->UpdateData(StringToBlob("I'm a jolly good file with some data added by Homer!"));
  TestAssert(workflowmgr->__GetAutosavedObject()->modificationdate >= now, "Verify moddate was updated");

  TestEq(TRUE, TestFlagFromPublished(OpenWHFSObject(testfile->id)->published, PublishedFlag_HasPublicDraft));

  //Let's save another draft
  TestEq([ with_workflow_string := "draft_0_1" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ name := "test2.html", title := "An even newer title", keywords := "my new keywords, plus more", description := "dd", data := StringToBlob("I'm a jolly good file with some data added by Homer!") ], workflowmgr->GetMetadata());
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "yet-another-draft" ]);
  workflowmgr->Save();

  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetDraftObject()->id), '*');

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created", snapshot := 0, version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved", version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved", version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                ], history, "*");

  TestEq([ with_workflow_string := "created_0_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "draft_0_1" ], OpenWHFSObject(history[1].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "yet-another-draft" ], OpenWHFSObject(history[2].snapshot)->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq(OpenWHFSObject(history[2].snapshot)->modificationdate, history[2].when);

  testfw->CommitWork();
  TestEqMembers(expect_analyze_thisversion, AnalyzeURL(thisversionlink), "*", "Locked to its draft");
  TestEqMembers([
    title := "An even newer title",
    with_workflow_string := "yet-another-draft",
    no_clone_string := "created_0_1",
    clone_only_string := "created_0_1",
    textcontent := NEW Regex("I'm a jolly good file with some data added by Homer","i")
  ], AnalyzeURL(mostrecentversionlink), "*", "Autofollow to the most recent draft");

  ///////////////////
  // STORY: We will now actually publish the final draft. This adds version 1.0 to the history
  testfw->BeginWork();
  DATETIME oldmoddate := testfile->modificationdate;
  testfw->SetTestUser("marge");
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := ["webhare_testsuite:global.type_with_workflow"]
    ]);
  workflowmgr->Save([ finalize := TRUE]);
  TestEq(FALSE, RecordExists(workflowmgr->GetCurrentDraft()));
  TestEq(FALSE, OpenWHFSObject(testfile->id)->publish);

  TestEqMembers([[ namespace := "http://www.webhare.net/xmlns/beta/test", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_clone_only", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_no_clone", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := FALSE ]
                ], ListInstances(testfile->id), '*');

  testfile := OpenWHFSObject(testfile->id);
  TestEq([ with_workflow_string := "yet-another-draft" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq("I'm a jolly good file with some data added by Homer!", BlobToString(OpenWHFSObject(testfile->id)->data));
  //FIXME CONSIDER should SaveAsFinal also rename the file? TestEq("test2.html", testfile->name);
  //               also consider the file might be pinned since the draft started
  //TODO Consider that title/keywords may have been updated OUTSIDE our workflow, handle that too
  TestEq("An even newer title", testfile->title);
  TestEq("my new keywords, plus more", testfile->keywords);
  TestEq("dd", testfile->description);
  TestEq("/tmp/sub/test2.html", testfile->fullpath, "The file was now actually renamed!");

  TestEq(TRUE, testfile->modificationdate > oldmoddate);
  TestEq(FALSE, TestFlagFromPublished(testfile->published, PublishedFlag_HasPublicDraft));

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.2", user := testfw->GetUserAuthobjectId("marge") ]
                , [ type := "saved",    version := "0.3", user := testfw->GetUserAuthobjectId("homer") ]
                , [ type := "final",    version := "1.0", user := testfw->GetUserAuthobjectId("marge"), when := testfile->modificationdate ]
                ], history, "*");

  //Verify the snapshots in the history. They should contain the right info... but they should NOT be the public version!
  //FIXME where did created_0_1 go ?
  TestEq([ with_workflow_string := "yet-another-draft" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ with_workflow_string := "draft_0_1" , no_clone_string := "", clone_only_string := "" ], GetCloneTestFields(history[1].snapshot), "The 0.2 snapshot should only have the workflow managed data");
  TestEq([ with_workflow_string := "yet-another-draft" , no_clone_string := "", clone_only_string := "" ], GetCloneTestFields(history[2].snapshot), "The 0.3 snapshot should only have its own data");
  TestEq([ with_workflow_string := "yet-another-draft" , no_clone_string := "", clone_only_string := "created_0_1" ], GetCloneTestFields(history[3].snapshot), "The 1.0 saved should have clonable non-workflow data too");
  TestEq(['http://www.webhare.net/xmlns/beta/test', 'webhare_testsuite:global.type_clone_only', 'webhare_testsuite:global.type_no_clone', 'webhare_testsuite:global.type_with_workflow'], SELECT AS STRING ARRAY namespace FROM ListInstances(testfile->id) ORDER BY namespace);

  TestAssert(testfile->id != history[3].snapshot, "Assure us that the 1.0 version is really independent from the live file");
  TestEq(DEFAULT RECORD, workflowmgr->GetCurrentDraft());
  TestEq(DEFAULT RECORD, workflowmgr->GetResumableAutosave());

  /////////////////////
  //
  // STORY: Going straight from autosave to final should not leave a lingering autosave

  //Create someone else's autosave
  testfw->SetTestUser("homer");
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := ["webhare_testsuite:global.type_with_workflow"]
    ]);
  workflowmgr->UpdateData(StringToBlob("Simple change to trigger an autosave"));

  //Create our autosave for this draft
  testfw->SetTestUser("marge");
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := [ "webhare_testsuite:global.type_with_workflow" ]
    ]);

  workflowmgr->UpdateData(StringToBlob("another change to trigger an autosave"));
  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "pvt_draft_1_1" ]);
  TestThrowsLike("*not*workflow*", PTR workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"), "The type is not readbale yet");

  ////////////
  //STORY: Pretend the editor restarted (by the same user) but now has a different workflow config: type_clone_only is the only one managed
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := [ "webhare_testsuite:global.type_clone_only" ]
    ]);

  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  //We can now read it through the workflowmgr even though the data isn't actually there
  TestEq([ with_workflow_string := "pvt_draft_1_1" ], workflowmgr->__GetAutosavedObject()->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEq([ clone_only_string := "" ], workflowmgr->__GetAutosavedObject()->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq([ clone_only_string := "created_0_1" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"));

  workflowmgr->UpdateData(StringToBlob("yet another change to trigger an autosave"));

  /* TODO
     should this already sync webhare_testsuite:global.type_clone_only as workflow or not, as we haven't observed/touched it yet.
     it's hard to observe as the editdocument app normally creates the autosave and immediately writes back all instances.

     but other users of workflow might care about this specific window between creating an autosave and writing all instances

     i can currently defend both behaviours, for now i'll lock in the 'no need to sync unrelated stuff immediately' approach
     but we could change this in the future
  */
  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                // ,[ scopedtype := "webhare_testsuite:global.type_clone_only", workflow := TRUE] //not expecting it yet
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  //'Dummy' update similar to what the RTD would do when autosaving. But this makes it an explicit workflow:

  workflowmgr->SetInstanceData("webhare_testsuite:global.type_clone_only", workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq([ with_workflow_string := "pvt_draft_1_1" ], workflowmgr->__GetAutosavedObject()->GetInstanceData("webhare_testsuite:global.type_with_workflow"));
  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_clone_only", workflow := TRUE] //there it is!
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  //We should also be able to clear it now
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_clone_only", OpenWHFSType("webhare_testsuite:global.type_clone_only")->defaultinstance);
  TestEq([ clone_only_string := "" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_clone_only", workflow := TRUE] //and it's STILL there
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*');

  workflowmgr->Save([ finalize := TRUE, publish := TRUE ]);
  TestEq(TRUE, OpenWHFSObject(testfile->id)->publish);

  TestEq([ clone_only_string := "" ], testfile->GetInstanceData("webhare_testsuite:global.type_clone_only"));
  TestEq([ with_workflow_string := "pvt_draft_1_1" ], testfile->GetInstanceData("webhare_testsuite:global.type_with_workflow"));

 TestEqMembers([[ namespace := "http://www.webhare.net/xmlns/beta/test", workflow := FALSE ]
               ,[ scopedtype := "webhare_testsuite:global.type_no_clone", workflow := FALSE]
               ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := FALSE]
               ], ListInstances(testfile->id), '*', "Note the lack of type_clone_only");

  /////////////
  // STORY: start a workflow with 'clear' type_clean_only. then update it in the final after starting the workflow and verify it's NOT reflected in the autosave
  workflowmgr := NEW WorkflowManager(testfile->id,
    [ user := GetEffectiveUser()
    , useworkflow := TRUE
    , workflowtypes := [ "webhare_testsuite:global.type_clone_only", "webhare_testsuite:global.type_with_workflow" ]
    ]);
  workflowmgr->UpdateData(StringToBlob("Locking in changes"));
  TestEqMembers([[ scopedtype := "platform:publisher.draftmetadata", workflow := FALSE ]
                ,[ scopedtype := "webhare_testsuite:global.type_clone_only", workflow := TRUE]
                ,[ scopedtype := "webhare_testsuite:global.type_with_workflow", workflow := TRUE]
                ], ListInstances(workflowmgr->__GetAutosavedObject()->id), '*', "UpdateData above started the draft, so all fields should be there");

  testfile->SetInstanceData("webhare_testsuite:global.type_clone_only", [ clone_only_string := "final-update-1" ]);
  testfile->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "final-workflow-1" ]);

  TestEq([ with_workflow_string := "pvt_draft_1_1" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_with_workflow"), "should not see this change");
  TestEq([ clone_only_string := "" ], workflowmgr->GetInstanceData("webhare_testsuite:global.type_clone_only"), "should not see this change");

  workflowmgr->DiscardAutosavedChanges();

  testfile := OpenWHFSObject(testfile->id);
  TestEq(DEFAULT RECORD, workflowmgr->GetCurrentDraft(), "Finalizing should have discarded all drafts");
  TestEq(DEFAULT RECORD, workflowmgr->GetResumableAutosave());

  history := ListObjectHistory(testfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", user := testfw->GetUserAuthobjectId("marge"), name := "" ]
                , [ type := "saved",    version := "0.2", user := testfw->GetUserAuthobjectId("marge"), name := "test2.html" ]
                , [ type := "saved",    version := "0.3", user := testfw->GetUserAuthobjectId("homer"), name := "test2.html" ]
                , [ type := "final",    version := "1.0", user := testfw->GetUserAuthobjectId("marge"), name := "test2.html" ]
                , [ type := "final",    version := "2.0", user := testfw->GetUserAuthobjectId("marge"), name := "test2.html", when := testfile->modificationdate ]
                ], history, "*");
  //FIXME verify we can to 'thisversion' links to both 1.0 and 2.0

  testfw->CommitWork();

}

ASYNC MACRO NamingTest() {
  OBJECT testfolder := GetTestsuiteTempFolder()->OpenByName("sub");
  testfw->BeginWork();

  { // Naming scenarios 1: Create a file with just a title. Save it. Publish it. Only then a name is recorded in metadata
    OBJECT testfile := testfolder->CreateFile([ title := "An unnamed file", typens := "platform:filetypes.plaintext" ], [ createdby := GetEffectiveUser() ]);
    TestEq(FALSE, testfile->publish);
    TestEq("an-unnamed-file.txt", testfile->name, "The system should have auto-named the file");

    OBJECT workflowmgr := NEW WorkflowManager(testfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
    workflowmgr->UpdateData(StringToBlob("Just some changed text"));
    workflowmgr->Save();
    TestEqMembers([ name := "", title := "An unnamed file" ], workflowmgr->GetMetadata(), "*", "Name should be empty, we just hace a title");
    workflowmgr->Save([ finalize := TRUE ]);

    TestEqMembers([[ type := "created",  version := "0.1", name := "" ]
                  ,[ type := "saved",    version := "0.2", name := "" ]
                  ,[ type := "final",    version := "1.0", name := "an-unnamed-file.txt" ]
                ], ListObjectHistory(testfile->id), "*");
    TestEqMembers([ name := "an-unnamed-file.txt" ], workflowmgr->GetMetadata(), "*", "Name should persist after a final publication");
  }

  { // Naming scenarios 2: Create a file with just a title. Save it. Then name it. It should persist from there on. Publish it.
    OBJECT testfile := testfolder->CreateFile([ title := "Naming during draft", typens := "platform:filetypes.plaintext" ], [ createdby := GetEffectiveUser() ]);
    TestEq("naming-during-draft.txt", testfile->name, "The system should have auto-named the file");

    OBJECT workflowmgr := NEW WorkflowManager(testfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
    workflowmgr->UpdateData(StringToBlob("Just some changed text"));
    workflowmgr->Save();

    TestEqMembers([ name := "" ], workflowmgr->GetMetadata(), "*");
    workflowmgr->UpdateMetadata([ name := "new-name-during-draft.txt" ]);
    workflowmgr->Save([ finalize := TRUE]);

    TestEqMembers([ name := "new-name-during-draft.txt" ], workflowmgr->GetMetadata(), "*");

    TestEqMembers([[ type := "created",  version := "0.1", name := "" ]
                  ,[ type := "saved",    version := "0.2", name := "" ]
                  ,[ type := "final",    version := "1.0", name := "new-name-during-draft.txt" ]
                ], ListObjectHistory(testfile->id), "*");
    TestEqMembers([ name := "new-name-during-draft.txt" ], workflowmgr->GetMetadata(), "*", "Name should persist after a final publication");
  }

  { // Naming scenario 3: Create a file with just a title. Name it. Save it. It should have persisted. Name it again. Publish it. It should persist.
    OBJECT testfile := testfolder->CreateFile([ title := "Naming twice", typens := "platform:filetypes.plaintext" ], [ createdby := GetEffectiveUser() ]);
    TestEq("naming-twice.txt", testfile->name, "The system should have auto-named the file");

    OBJECT workflowmgr := NEW WorkflowManager(testfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
    workflowmgr->UpdateMetadata([name := "naming-twice-autosaved.dat"]);
    TestEqMembers([ name := "naming-twice-autosaved.dat" ], workflowmgr->GetMetadata(), "*");
    workflowmgr->Save();
    TestEqMembers([ name := "naming-twice-autosaved.dat" ], workflowmgr->GetMetadata(), "*");
    workflowmgr->UpdateMetadata([name := "naming-twice-draft.dat"]);
    TestEqMembers([ name := "naming-twice-draft.dat" ], workflowmgr->GetMetadata(), "*");
    workflowmgr->Save([ finalize := TRUE ]);
    TestEqMembers([ name := "naming-twice-draft.dat" ], workflowmgr->GetMetadata(), "*");

    TestEqMembers([[ type := "created",  version := "0.1", name := "" ]
                  ,[ type := "saved",    version := "0.2", name := "naming-twice-autosaved.dat" ]
                  ,[ type := "final",    version := "1.0", name := "naming-twice-draft.dat" ]
                ], ListObjectHistory(testfile->id), "*");
    TestEq("naming-twice-draft.dat", OpenWHFSObject(testfile->id)->name, "Name should persist after a final publication");
  }

  { // Naming scenario 4: Create a file with neither name or title. Title it, save it. Lack of name persists.Publish it. Only now is a name recorded based on the title
    OBJECT testfile := testfolder->CreateFile([ name := "new.txt", typens := "platform:filetypes.plaintext" ], [ createdby := GetEffectiveUser(), implicit_name := TRUE ]);
    TestEq("new.txt", testfile->name);

    OBJECT workflowmgr := NEW WorkflowManager(testfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
    TestEqMembers([ name := "" ], workflowmgr->GetMetadata(), "*");
    workflowmgr->UpdateMetadata([title := "New ASCII"]);
    workflowmgr->Save();
    TestEqMembers([ name := "" ], workflowmgr->GetMetadata(), "*");
    TestEq("new-ascii.txt", OpenWHFSObject(testfile->id)->name, "Name is updated immediately so we can find ourselves in the publisher");
    TestEq("New ASCII", OpenWHFSObject(testfile->id)->title, "And title is visible too");
    workflowmgr->Save([ finalize := TRUE ]);
    TestEq("new-ascii.txt", OpenWHFSObject(testfile->id)->name, "Name should persist after a final publication");

    TestEqMembers([[ type := "created",  version := "0.1", name := "" ]
                  ,[ type := "saved",    version := "0.2", name := "" ]
                  ,[ type := "final", version := "1.0", name := "new-ascii.txt" ]
                ], ListObjectHistory(testfile->id), "*");
  }

  testfw->CommitWork();
}

ASYNC MACRO UnpublishedSavesTest()
{
  testfw->BeginWork();

  OBJECT testfolderobj2 := GetTestsuiteTempFolder()->OpenByName("sub");
  OBJECT unpublishedfile := testfolderobj2->CreateFile(
    [ name := "unpublishedfile.html"
    , title := "An unpublished file"
    , data := StringToBlob("Don't publish me now")
    , typens := "http://www.webhare.net/xmlns/publisher/htmlfile"
    , publish := FALSE
    ], [ createdby := testfw->GetUserObject("marge")
       , instances := [[ whfstype := "http://www.webhare.net/xmlns/beta/test", stringarray := ["nonopublish"] ]]
       ]);

  TestEqMembers([[ type := "created",  version := "0.1", userdata := [ login := "marge@beta.webhare.net"], user := testfw->GetUserAuthobjectId("marge"), name := "unpublishedfile.html" ]
              ], ListObjectHistory(unpublishedfile->id), "*", "The 'created' event should show a name as we set it explicitly");

  testfw->SetTestUser("homer");
  OBJECT workflowmgr := NEW WorkflowManager(unpublishedfile->id,
    [ user := GetEffectiveUser(),
      useworkflow := TRUE,
      workflowtypes := ["webhare_testsuite:global.type_with_workflow"]
    ]);

  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "unpublished-draft" ]);
  workflowmgr->Save();

  workflowmgr->Save([ finalize := TRUE ]);
  TestEq(FALSE, RecordExists(workflowmgr->GetCurrentDraft()));
  TestEq(FALSE, OpenWHFSObject(unpublishedfile->id)->publish);

  //Create an autosave or an unpublished file and finalize it straight away
  workflowmgr->SetInstanceData("webhare_testsuite:global.type_with_workflow", [ with_workflow_string := "unpublished-update" ]);
  workflowmgr->Save([ finalize := TRUE ]);

  RECORD ARRAY history := ListObjectHistory(unpublishedfile->id);
  TestEqMembers([ [ type := "created",  version := "0.1", userdata := [ login := "marge@beta.webhare.net"], user := testfw->GetUserAuthobjectId("marge"), name := "unpublishedfile.html" ]
                , [ type := "saved",    version := "0.2", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer"), name := "unpublishedfile.html" ]
                , [ type := "final",    version := "1.0", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer"), name := "unpublishedfile.html" ]
                , [ type := "final",    version := "2.0", userdata := [ login := "homer@beta.webhare.net"], user := testfw->GetUserAuthobjectId("homer"), name := "unpublishedfile.html" ]
                ], history, "*");

  testfw->CommitWork();

  //Verify that even sysop cannot edit this history item
  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ToString(history[3].snapshot)]]));
  BOOLEAN clicked := AWAIT ExpectNoScreenChange(PTR TTClick("publish", [ allowfailure := TRUE ]));
  TestEq(FALSE, clicked);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

//Test whether files that receive updates but had no history at all, properly get an import entry
ASYNC MACRO ImportTest() {
  testfw->BeginWork();

  OBJECT importfile := GetTestsuiteTempFolder()->CreateFile(
    [ title := "Imported!"
    , keywords := "my new import"
    , description := "describe import"
    , data := StringToBlob("I'm a jolly good file")
    , typens := "http://www.webhare.net/xmlns/publisher/htmlfile"
    ]);

  importfile->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ stringarray := ["a","b"]  ]);
  SetCloneTestFields(importfile->id, [ no_clone_string := "import_0_1", with_workflow_string := "import_0_1", clone_only_string := "import_0_1" ]);

  RECORD ARRAY history := ListObjectHistory(importfile->id);
  TestEq(RECORD[], history, "There should be no history yet");

  //This is what we expect task replacecontents to do: backup the old version as an 'import' snapshot and setup a 'final' version
  OBJECT workflowmgr := NEW WorkflowManager(importfile->id,
    [ user := testfw->GetUserObject("marge"),
      useworkflow := TRUE,
      workflowtypes := ["http://www.webhare.net/xmlns/beta/test"]
    ]);
  workflowmgr->UpdateData(StringToBlob("I'm an  updated file"));
  workflowmgr->SetInstanceData("http://www.webhare.net/xmlns/beta/test", [ stringarray := ["x","z"]  ]);
  workflowmgr->Save([ finalize := TRUE ]);

  history := ListObjectHistory(importfile->id);
  TestEqMembers([ [ type := "import",   version := "1.0", userdata := DEFAULT RECORD, user := 0 ]
                , [ type := "final",    version := "2.0", userdata := [ login := "marge@beta.webhare.net"], user := testfw->GetUserAuthobjectId("marge") ]
                ], history, "*");

  TestEq(STRING["a","b"], OpenWHFSObject(history[0].snapshot)->GetInstanceData("http://www.webhare.net/xmlns/beta/test").stringarray);
  TestEq([ no_clone_string := "", with_workflow_string := "import_0_1", clone_only_string := "import_0_1" ], GetCloneTestFields(history[0].snapshot), "The import snapshot should have preserved its clonable instance data");

  TestEq(STRING["x","z"], OpenWHFSObject(history[1].snapshot)->GetInstanceData("http://www.webhare.net/xmlns/beta/test").stringarray);
  TestEq([ no_clone_string := "", with_workflow_string := "import_0_1", clone_only_string := "import_0_1" ], GetCloneTestFields(history[1].snapshot), "The final snapshot should have the clonabledata");

  TestEq(STRING["x","z"], OpenWHFSObject(importfile->id)->GetInstanceData("http://www.webhare.net/xmlns/beta/test").stringarray);
  TestEq([ no_clone_string := "import_0_1", with_workflow_string := "import_0_1", clone_only_string := "import_0_1" ], GetCloneTestFields(importfile->id), "The final snapshot should have ALL data");

  testfw->CommitWork();
}


ASYNC MACRO ViewHistory() {
  DATETIME startcopy := GetCurrentDatetime();

  //Verify the history info. (lives on Inspect tab until we're ready to expose it to non-sysops)
  OBJECT unpublishedfile := GetTestsuiteTempFolder()->OpenByPath("sub/test2.html");

  //https://my.webhare.dev/?app=publisher(/webhare-tests/webhare_testsuite.testsite/tmp/sub/test.html)/versions
  AWAIT ExpectScreenChange(+1, PTR RunFSObjectVersionsDialog(GetTestController(), unpublishedfile->id));
  TT("versions")->selection := SELECT * FROM TT("versions")->rows WHERE versionnumber="0.1"; //this one lacks a snapshot and may crash

  TT("versions")->selection := SELECT * FROM TT("versions")->rows WHERE versionnumber="0.2"; //this one contained temporary data

  AWAIT ExpectScreenChange(+1, PTR TTClick("{item}:Copy to"));
  TT("folders")->value := GetTestsuiteTempFolder()->id;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  RECORD ARRAY newobjects := SELECT * FROM system.fs_objects WHERE parent = GetTestsuiteTempFolder()->id AND creationdate >= startcopy;
  TestEq(1, Length(newobjects), "There should be one new object created by the copy");
  //FIXME verify this copy
}

RunTestframework([ PTR Prep
                 , PTR DraftingTest
                 , PTR NamingTest
                 , PTR UnpublishedSavesTest
                 , PTR ImportTest
                 , PTR ViewHistory
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "marge" ]
                                   ,[ login := "homer" ]
                                   ]
                    ]);
