<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::ipc.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/history.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::webhare_testsuite/lib/publisher/testsupport.whlib";


OBJECT testsite;
OBJECT movetarget;
INTEGER ARRAY listenids;

OBJECT eventmgr;

// Returns events sorted on order the id was added to listenids
RECORD ARRAY FUNCTION ExpectWHFSEvents(INTEGER msgcount, RECORD options DEFAULTSTO DEFAULT RECORD) {
  options := ValidateOptions(
      [ minmsgcount :=   msgcount
      , timeout :=       10000
      ], options);

  DATETIME deadline := AddTimeToDate(options.timeout, GetCurrentDatetime());
  RECORD ARRAY events;
  WHILE (TRUE) {
    RECORD event := eventmgr->ReceiveEvent(deadline);
    IF (event.status = "timeout")
      BREAK;

    IF (event.event != `system:whfs.folder.${event.msg.folder}` AND event.event != `system:whfs-history.folder.${event.msg.folder}`) {
      DumpValue(events, "tree");
      THROW NEW Exception(`Unexpected event ${event.event}, expected system:whfs.folder.${event.msg.folder}`);
    }
    IF (event.msg.folder NOT IN listenids)
      CONTINUE;
    INSERT event.msg INTO events AT END;
    IF (LENGTH(events) > msgcount) {
      DumpValue(events, "tree");
      THROW NEW Exception(`Received too many events, expected ${options.minmsgcount != msgcount?`${options.minmsgcount}-`: ""}${msgcount} got ${LENGTH(events)}`);
    }
    IF (LENGTH(events) = options.minmsgcount)
      deadline := AddTimeToDate(100, GetCurrentDatetime());
  }
  IF (LENGTH(events) < options.minmsgcount) {
    DumpValue(events, "tree");
    THROW NEW Exception(`Expected ${options.minmsgcount != msgcount?`${options.minmsgcount}-`: ""}${msgcount} events, got ${LENGTH(events)}`);
  }
  //DumpValue(events, "tree");
  events :=
      SELECT *
           , DELETE trace
        FROM events
    ORDER BY SearchElement(INTEGER[ testsite->root, movetarget->id, ...listenids ], folder);

  RETURN events;
}

MACRO ListenToId(INTEGER id) {
  INSERT id INTO listenids AT END;
}

MACRO Prepare()
{
  testfw->BeginWork();

  testsite := EnsureTestSite("webhare_testsuite.test_whfs_events", [ recyclesite := TRUE ]);
  //testsite := CreateSiteFromFolder(firstfolder->id);
  movetarget := testsite->rootobject->CreateFolder([ name := "movetarget" ]);
  testfw->CommitWork();
}

MACRO TestEvents() {

  eventmgr := NEW EventManager;
  eventmgr->RegisterInterest(`system:whfs.folder.*`);
  eventmgr->RegisterInterest(`system:whfs-history.folder.*`);
  ListenToId(whconstant_whfsid_autosaves);
  ListenToId(whconstant_whfsid_drafts);
  ListenToId(whconstant_whfsid_whfs_snapshots);
  listenToId(testsite->root);
  listenToId(movetarget->id);


  {
    testfw->BeginWork();
    OBJECT testfile := testsite->rootobject->CreateFile([ name := "testfile.txt" ]);
    testfw->CommitWork();

    TestEQ([
      [
        folder := testsite->root,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "create" ] ] ]
      ]
    ], ExpectWHFSEvents(1));

    testfw->BeginWork();
    testfile->UpdateMetadata([ title := "updated title" ]);
    testfw->CommitWork();

    TestEQ([
      [
        folder := testsite->root,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "update" ] ] ]
      ]
    ], ExpectWHFSEvents(1));

    testfw->BeginWork();
    testfile->UpdateMetadata([ name := "testfile2.txt" ]);
    testfw->CommitWork();

    TestEQ([
      [
        folder := testsite->root,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "rename", "update" ] ] ]
      ]
    ], ExpectWHFSEvents(1));

    testfw->BeginWork();
    testfile->MoveTo(movetarget, "testfile3.txt");
    testfw->CommitWork();

    TestEQ([
      [
        folder := testsite->root,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "move" ] ] ]
      ], [
        folder := movetarget->id,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "moved" ] ] ]
      ]
    ], ExpectWHFSEvents(2));

    testfw->BeginWork();
    testfile->RecycleSelf();
    testfw->CommitWork();

    TestEQ([
      [
        folder := movetarget->id,
        events := STRING[],
        files := [ [ file := testfile->id, isfolder := FALSE, events := [ "del" ] ] ]
      ]
    ], ExpectWHFSEvents(1));
  }

  testfw->BeginWork();
  OBJECT testfolder := testsite->rootobject->CreateFolder([ name := "testfolder" ]);
  ListenToId(testfolder->id);
  testfw->CommitWork();

  TestEQ([
    [
      folder := testsite->root,
      events := STRING[],
      files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "create" ] ] ]
    ]
  ], ExpectWHFSEvents(1));

  testfw->BeginWork();
  testfolder->UpdateMetadata([ title := "updated title" ]);
  testfw->CommitWork();

  TestEQ([
    [
      folder := testsite->root,
      events := STRING[],
      files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "update" ] ] ]
    ]
  ], ExpectWHFSEvents(1));

  testfw->BeginWork();
  testfolder->UpdateMetadata([ name := "testfolder2" ]);
  testfw->CommitWork();

  TestEQ([
    [
      folder := testsite->root,
      events := STRING[],
      files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "rename", "update" ] ] ]
    ]
  ], ExpectWHFSEvents(1));

    testfw->BeginWork();
    testfolder->MoveTo(movetarget, "testfile3.txt");
    testfw->CommitWork();

    TestEQ([
      [
        folder := testsite->root,
        events := STRING[],
        files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "move" ] ] ]
      ], [
        folder := movetarget->id,
        events := STRING[],
        files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "moved" ] ] ]
      ]
    ], ExpectWHFSEvents(2));

  testfw->BeginWork();
  OBJECT subfile := testfolder->CreateFile([
    name := "published.txt",
    typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile",
    publish := TRUE
  ]);
  testfw->CommitWork();

  WaitForOutputAnalyzer(INTEGER[ subfile->parent ], AddTimeToDate(60000, GetCurrentDatetime()));
  WaitForPublishCompletion(subfile->parent);

  // The output analyzer might also have scheduled a republish
  {
    RECORD ARRAY events := ExpectWHFSEvents(2, [ minmsgcount := 1 ]);

    TestEQ([
      [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "create", "rep" ] ] ]
      ],
      ...(LENGTH(events) = 2 ? [
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ] : RECORD[])
    ], events);
  }

  testfw->BeginWork();
  subfile->UpdateMetadata([ title := "updated title" ]);
  testfw->CommitWork();

  TestEQ([
    [
      folder := testfolder->id,
      events := STRING[],
      files := [ [ file := subfile->id, isfolder := FALSE, events := [ "update" ] ] ]
    ]
  ], ExpectWHFSEvents(1, [ timeout := 30000 ]));

  testfw->BeginWork();
  subfile->UpdateMetadata([ name := "published2.txt" ]);
  testfw->CommitWork();

  WaitForOutputAnalyzer(INTEGER[ subfile->parent ], AddTimeToDate(60000, GetCurrentDatetime()));
  WaitForPublishCompletion(subfile->parent);

  {
    RECORD ARRAY events := ExpectWHFSEvents(2, [ minmsgcount := 1 ]);

    TestEQ([
      [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rename", "rep", "update" ] ] ]
      ],
      ...(LENGTH(events) = 2 ? [
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ] : RECORD[])
    ], events);
  }

  testfw->BeginWork();
  ScheduleFolderRepublish(testfolder->id, TRUE, TRUE);
  testfw->CommitWork();

  {
    RECORD ARRAY events := ExpectWHFSEvents(2, [ minmsgcount := 1 ]);
    TestEQ([
      [
        folder := testfolder->id,
        events := [ "fullrep" ],
        files := RECORD[]
      ],
        ...(LENGTH(events) >= 2 ? [
          [
            folder := testfolder->id,
            events := STRING[],
            files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
          ]
        ] : RECORD[])
    ], events);
  }

  WaitForPublishCompletion(subfile->parent);

  // STORY: change indexdoc of testfolder
  testfw->BeginWork();
  testfolder->UpdateMetadata([ indexdoc := subfile->id ]);
  testfw->CommitWork();

  WaitForOutputAnalyzer(INTEGER[ subfile->parent ], AddTimeToDate(60000, GetCurrentDatetime()));
  WaitForPublishCompletion(subfile->parent);

  {
    RECORD ARRAY events := ExpectWHFSEvents(4, [ minmsgcount := 2 ]);
    TestEQ([
      [
        folder := testfolder->parent,
        events := STRING[],
        files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "update" ] ] ]
      ],
      [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep", "update" ] ] ]
      ],
      ...(LENGTH(events) >= 3 ? [ // output analyzer might have run and produce 1-2 reps
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ] : RECORD[]),
      ...(LENGTH(events) >= 4 ? [
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ] : RECORD[])
    ], events);
  }

  // STORY: reset indexdoc of testfolder
  testfw->BeginWork();
  testfolder->UpdateMetadata([ indexdoc := 0 ]);
  testfw->CommitWork();

  WaitForOutputAnalyzer(INTEGER[ subfile->parent ], AddTimeToDate(60000, GetCurrentDatetime()));
  WaitForPublishCompletion(subfile->parent);

  {
    RECORD ARRAY events := ExpectWHFSEvents(4, [ minmsgcount := 2 ]);
    TestEQ([
      [
        folder := testfolder->parent,
        events := STRING[],
        files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "update" ] ] ]
      ],
      [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep", "update" ] ] ]
      ],
      ...(LENGTH(events) >= 3 ? [ // output analyzer might have run again and produce 1-2 reps
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ]: RECORD[]),
      ...(LENGTH(events) >= 4 ? [ // output analyzer might have run again and produce 1-2 reps
        [
          folder := testfolder->id,
          events := STRING[],
          files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
        ]
      ] : RECORD[])
    ], events);
  }

  testfw->BeginWork();

  OBJECT metamgr := NEW WorkflowManager(subfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
  metamgr->UpdateData(StringToBlob("Draft data 1"));
  OBJECT draft := metamgr->__GetAutosavedObject();

  metamgr->DiscardAutosavedChanges(); //FIXME this isn't generating events as CleanupMyPrivateDrafts isn't caring
  metamgr->UpdateData(StringToBlob("Draft data 2"));
  OBJECT draft2 := metamgr->__GetAutosavedObject();

  testfw->CommitWork();

  TestEQ([
    [
      folder := whconstant_whfsid_autosaves,
      events := STRING[],
      files := [
        [ file := draft->id, isfolder := FALSE, events := [ "create", "update" ] ],
        [ file := draft2->id, isfolder := FALSE, events := [ "create", "update" ] ]
      ]
    ]
  ], ExpectWHFSEvents(1));

  testfw->BeginWork();
  metamgr->UpdateMetadata([ title := "updated title" ]);
  testfw->CommitWork();

  TestEQ([
    [
      folder := whconstant_whfsid_autosaves,
      events := STRING[],
      files := [ [ file := draft2->id, isfolder := FALSE, events := [ "update" ] ] ]
    ]
  ], ExpectWHFSEvents(1));

  testfw->BeginWork();
  metamgr->Save();
  testfw->CommitWork();

  INTEGER snapshotfolder;
  {
    RECORD ARRAY events := ExpectWHFSEvents(3);
    INTEGER finalsnapshot := events[0].files[1].file;

    snapshotfolder := events[1].files[0].file; // The snapshot parent folder

    // Draft is moved from autosaves to drafts, so we get a moved event
    // Draft2 is deleted (autosaeve cleanup)
    TestEQ([
      [
        folder := whconstant_whfsid_autosaves,
        events := STRING[],
        files := [
          [ file := draft2->id, isfolder := FALSE, events := [ "moved" ] ],
          [ file := finalsnapshot, isfolder := FALSE, events := [ "create" ] ]
        ]
      ], [
        folder := whconstant_whfsid_whfs_snapshots,
        events := STRING[],
        files := [
          [ file := snapshotfolder, isfolder := TRUE, events := [ "create" ] ]
        ]
      ], [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "history" ] ] ]
      ]
    ], events);
  }

  // Also listen to the snapshot folder
  ListenToId(snapshotfolder);

  testfw->BeginWork();
  metamgr := NEW WorkflowManager(subfile->id, [ user := GetEffectiveUser(), useworkflow := TRUE ]);
  metamgr->UpdateData(StringToBlob("Draft data 3"));
  OBJECT draft3 := metamgr->__GetAutosavedObject();
  testfw->CommitWork();

  TestEQ([
    [
      folder := whconstant_whfsid_autosaves,
      events := STRING[],
      files := [
                [ file := draft3->id, isfolder := FALSE, events := [ "create", "update" ] ]
              ]
    ]
  ], ExpectWHFSEvents(1));

  testfw->BeginWork();
  metamgr->Save([ publish := TRUE]);
  testfw->CommitWork();

  WaitForOutputAnalyzer(INTEGER[ subfile->parent ], AddTimeToDate(60000, GetCurrentDatetime()));
  WaitForPublishCompletion(subfile->parent);

  {
    RECORD ARRAY events := ExpectWHFSEvents(4, [ minmsgcount := 3 ]);
    TestEQ([
      [
        folder := whconstant_whfsid_autosaves,
        events := STRING[],
        files := [
          [ file := draft3->id, isfolder := FALSE, events := [ "moved" ] ]
        ]
      ], [
        folder := testfolder->id,
        events := STRING[],
        files := [ [ file := subfile->id, isfolder := FALSE, events := [ "history" ] ] ]
      ],
      ...(LENGTH(events) = 4 ? [ // if output analyzer runs after publish no 'rep' is issued
          [
            folder := testfolder->id,
            events := STRING[],
            files := [ [ file := subfile->id, isfolder := FALSE, events := [ "rep" ] ] ]
          ]
        ] : RECORD[]), [
        folder := snapshotfolder,
        events := STRING[],
        files := [
          [ file := draft3->id, isfolder := FALSE, events := [ "move","update" ] ]
        ]
      ]
    ], events);
  }

  testfw->BeginWork();
  testfolder->RecycleSelf();
  testfw->CommitWork();

  TestEQ([
    [
      folder := movetarget->id,
      events := STRING[],
      files := [ [ file := testfolder->id, isfolder := TRUE, events := [ "del" ] ] ]
    ]
  ], ExpectWHFSEvents(1));
}

RunTestframework([
  PTR Prepare,
  PTR TestEvents
], [
  testusers := [ [ login := "sysop", grantrights := ["system:sysop"] ] ]
]);
