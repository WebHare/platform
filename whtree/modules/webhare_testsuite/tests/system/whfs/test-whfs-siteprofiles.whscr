<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::system/lib/whfs.whlib";

STRING basesiteprofile :=
  '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" xmlns:xyz="urn:xyz">'
  || '<languagefolder fullpath="/design/baselanguage/" />'
  || '<apply>'
  || '<to type="all" />'
  || '<xyz:test test="2" />'
  || '</apply>'
  || '</siteprofile>';

STRING includingsiteprofile :=
  '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" xmlns:xyz="urn:xyz">'
  || '<languagefolder fullpath="/design/language/" />'
  || '<apply>'
  || '<to type="all" />'
  || '<xyz:test test="1" />'
  || '</apply>'
  || '<applysiteprofile path="/base.siteprl" />'
  || '</siteprofile>';

STRING including_nolangsiteprofile :=
  '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" xmlns:xyz="urn:xyz">'
  || '<applysiteprofile path="/base.siteprl" />'
  || '</siteprofile>';

STRING baselang_default :=
  '<?xml version="1.0" encoding="UTF-8" ?>'
  || '<language xmlns="http://www.webhare.net/xmlns/tollium/screens" xml:lang="nl" xmlns:h="http://www.w3.org/1999/xhtml">'
  || '  <textgroup gid="global">'
  || '    <text tid="text0">Text Zero</text>'
  || '  </textgroup>'
  || '</language>';

STRING lang_default :=
  '<?xml version="1.0" encoding="UTF-8" ?>'
  || '<language xmlns="http://www.webhare.net/xmlns/tollium/screens" xml:lang="nl" xmlns:h="http://www.w3.org/1999/xhtml">'
  || '  <textgroup gid="global">'
  || '    <text tid="text1">Text <h:b>1</h:b> &amp;NL</text>'
  || '  </textgroup>'
  || '</language>';

STRING lang_en :=
  '<?xml version="1.0" encoding="UTF-8" ?>'
  || '<language xmlns="http://www.webhare.net/xmlns/tollium/screens" xml:lang="en" xmlns:h="http://www.w3.org/1999/xhtml">'
  || '  <textgroup gid="global">'
  || '    <text tid="text1">Text 1 EN</text>'
  || '  </textgroup>'
  || '</language>';

STRING dummysiteprofile :=
     '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" />';

STRING embeddingsiteprofile :=
     '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile" xmlns:xyz="urn:xyz"  xmlns:h="http://www.w3.org/1999/xhtml">'
  || '  <language xmlns="http://www.webhare.net/xmlns/tollium/screens">'
  || '    <textgroup gid="global">'
  || '      <text tid="text1">Text <h:b>1</h:b> &amp;embedded NL</text>'
  || '      <text tid="paramtext">text1-<param p="1"/>-<ifparam p="1" value="1"><param p="2"/><else /><param p="3"/></ifparam>X</text>'
  || '    </textgroup>'
  || '  </language>'
  || '  <language xmlns="http://www.webhare.net/xmlns/tollium/screens" xml:lang="en">'
  || '    <textgroup gid="global">'
  || '      <text tid="text1">Text <h:b>1</h:b> &amp;embedded<h:br/> EN</text>'
  || '      <text tid="paramtext">detext1-<param p="1"/>-<ifparam p="1" value="1"><param p="2"/><else /><param p="3"/></ifparam>X</text>'
  || '    </textgroup>'
  || '  </language>'
  || '</siteprofile>';

OBJECT site;

MACRO Prepare()
{
  testfw->BeginWork();

  STRING sitename := "webhare_testsuite.inclusion";
  OBJECT siteroot := testfw->GetWHFSTestRoot()->CreateFolder( [ name := sitename ] );
  site := CreateSiteFromFolder(siteroot->id);

  OBJECT dummysiteprof := site->rootobject->CreateFile([name:="dummy.siteprl", data := StringToBlob(dummysiteprofile)]);
  OBJECT basesiteprof := site->rootobject->CreateFile([name:="base.siteprl", data := StringToBlob(basesiteprofile)]);
  OBJECT includingsiteprof := site->rootobject->CreateFile([name:="including.siteprl", data := StringToBlob(includingsiteprofile)]);
  OBJECT including_nolangsiteprof := site->rootobject->CreateFile([name:="including_nolang.siteprl", data := StringToBlob(including_nolangsiteprofile)]);
  OBJECT embeddingsiteprof := site->rootobject->CreateFile([name:="embedding.siteprl", data := StringToBlob(embeddingsiteprofile)]);

  OBJECT designfolder := siteroot->CreateFolder([name := "design"]);
  OBJECT baselanguagefolder := designfolder->CreateFolder([name := "baselanguage"]);
  OBJECT file_baselang_default := baselanguagefolder->CreateFile( [ name := "default.xml"
                                                                  , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/languagefile")->id
                                                                  , data := StringToBlob(baselang_default)
                                                                  ]);

  OBJECT languagefolder := designfolder->CreateFolder([name := "language"]);
  OBJECT file_lang_default := languagefolder->CreateFile( [ name := "default.xml"
                                                          , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/languagefile")->id
                                                          , data := StringToBlob(lang_default)
                                                          ]);
  OBJECT file_lang_en := languagefolder->CreateFile( [ name := "en.xml"
                                                     , type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/languagefile")->id
                                                     , data := StringToBlob(lang_en)
                                                     ]);
  TestEq(TRUE, site->AssignSiteProfile(dummysiteprof->id)); //dummy to make sure we don't get stuck on the 'first' siteprofile
  TestEq(TRUE, site->AssignSiteProfile(includingsiteprof->id));
  testfw->CommitWork();
}

MACRO TestInclusion()
{
  RECORD ARRAY custom := GetCustomSiteProfileSettings("urn:xyz", "test", site->id);
  TestEq(2, Length(custom));
  TestEq("1", custom[0].node->GetAttribute("test"));
  TestEq("2", custom[1].node->GetAttribute("test"));

  //However, it should not apply to random other sites
  custom := GetCustomSiteProfileSettings("urn:xyz", "test", 1); //test against repository
  TestEq(0,Length(custom));
}

MACRO TestLanguage()
{
  TestEq("Text Zero", GetSiteprofileTID(site->id, "nl", "global.text0"));
  TestEq("Text 1 &NL", GetSiteprofileTID(site->id, "nl", "global.text1"));
  TestEq("Text <b>1</b> &#38;NL", GetSiteprofileHTMLTID(site->id, "nl", "global.text1"));
}

MACRO TestLanguageEmbedded()
{
  TestEq("Text 1 &NL", GetSiteprofileTID(site->id, "nl", "global.text1")); //work that into our cache...

  // Need to remove dummy site profile here, we'll use its (auto) reference to the site language files otherwise
  testfw->BeginWork();
  TestEq(TRUE, site->UnassignSiteProfile(site->rootobject->OpenByPath("dummy.siteprl")->id));
  TestEq(TRUE, site->UnassignSiteProfile(site->rootobject->OpenByPath("including.siteprl")->id));
  TestEq(TRUE, site->AssignSiteProfile(site->rootobject->OpenByPath("embedding.siteprl")->id));
  testfw->CommitWork();

  TestEq("Text 1 &embedded NL", GetSiteprofileTID(site->id, "nl", "global.text1"), "If we still have 'Test 1 &NL', siteprofile tid caches did not flush");
  TestEq("Text <b>1</b> &#38;embedded NL", GetSiteprofileHTMLTID(site->id, "nl", "global.text1"));
  TestEq("Text 1 &embedded\n EN", GetSiteprofileTID(site->id, "en", "global.text1"));
  TestEq("Text <b>1</b> &#38;embedded<br/> EN", GetSiteprofileHTMLTID(site->id, "en", "global.text1"));

  TestEq("text1-1-2X",   GetSiteProfileTID(site->id, "", "global.paramtext", "1", "2", "3"));
  TestEq("text1-2-3X",   GetSiteProfileTID(site->id, "", "global.paramtext", "2", "1", "3"));
  TestEq("detext1-2-3X", GetSiteProfileTID(site->id, "en", "global.paramtext", "2", "1", "3"));
  TestEq("text1-2-3X",   GetSiteProfileTID(site->id, "nl", "global.paramtext", "2", "1", "3"));

  testfw->BeginWork();
  TestEq(TRUE, site->AssignSiteProfile(site->rootobject->OpenByPath("dummy.siteprl")->id));
  testfw->CommitWork();
}

RunTestFramework( [ PTR Prepare
                  , PTR TestInclusion
                  , PTR TestLanguage
                  , PTR TestLanguageEmbedded
                  ]);
