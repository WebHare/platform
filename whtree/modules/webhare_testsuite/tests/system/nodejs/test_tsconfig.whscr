<?wh

LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

STRING FUNCTION ResolveToAbsolute(STRING base, STRING target)
{
  IF (IsPathAbsolute(target))
    RETURN target;
  RETURN CollapsePath(MergePath(base, target));
}


MACRO TSConfigTest()
{
  OBJECT whdata_tsconfig := DecodeJSONBlob(GetDiskResource(MergePath(GetWebhareConfiguration().basedataroot, "tsconfig.json")), DEFAULT RECORD, [ wrapobjects := TRUE, allowcomments := TRUE ]);

  // Test paths from whtree/tsconfig.json
  {
    OBJECT whtree_tsconfig := DecodeJSONBlob(GetDiskResource(MergePath(GetWebhareConfiguration().installationroot, "tsconfig.json")), DEFAULT RECORD, [ wrapobjects := TRUE, allowcomments := TRUE ]);
    RECORD whdata_paths := whdata_tsconfig->GetProp("compilerOptions")->GetProp("paths")->GetValue();
    STRING baseurl := whdata_tsconfig->GetProp("compilerOptions")->GetProp("baseUrl");
    baseurl := ResolveToAbsolute(GetWebhareConfiguration().basedataroot, baseurl);

    RECORD expect_paths;
    FOREVERY (RECORD rec FROM UnpackRecord(whdata_paths))
    {
      STRING abspath := ResolveToAbsolute(baseurl, rec.value[0]);

      IF (abspath NOT LIKE `${GetWebhareConfiguration().installationroot}*`)
        CONTINUE;
      IF (abspath LIKE `${GetWebhareConfiguration().installationroot}node_modules/*`)
        CONTINUE;
      IF (rec.name = "@MOD-WEBHARE_TESTSUITE" OR rec.name LIKE "@MOD-WEBHARE_TESTSUITE/*")
        CONTINUE;

      STRING relpath := ResolveToRelativePath(GetWebhareConfiguration().installationroot, abspath);
      expect_paths := CellInsert(expect_paths, rec.name, VARIANT[ relpath ]);
    }

    RECORD got_paths := whtree_tsconfig->GetProp("compilerOptions")->GetProp("paths")->GetValue();
    TestEQ(expect_paths, got_paths, "whtree/tsconfig.json should have all paths from whdata/tsconfig.json, minus whtree/node_modules/* and whdata/* paths");
  }

  // Test paths from whtree/tsconfig.json
  {
    STRING baseurl := whdata_tsconfig->GetProp("compilerOptions")->GetProp("baseUrl");
    baseurl := ResolveToAbsolute(GetWebhareConfiguration().basedataroot, baseurl);
    STRING ext := whdata_tsconfig->GetProp("extends");

    TestEQ(MergePath(GetWebhareConfiguration().installationroot, "tsconfig.json"), ResolveToAbsolute(baseurl, ext), "whdata/tsconfig.json should extend whtree/tsconfig.json");
  }
}


RunTestFramework([ PTR TSConfigTest
                 ],
                 [ usedatabase := FALSE
                 ]);
