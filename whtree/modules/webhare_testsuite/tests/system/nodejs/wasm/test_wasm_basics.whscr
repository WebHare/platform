<?wh
LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internal/wasm.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

OBJECTTYPE TObject <
  PUBLIC OBJECT val;
>;

OBJECTTYPE TToCreate <
  PUBLIC INTEGER v;
>;

PUBLIC MACRO RunWasmtest()
{
  TestEq("81DC9BDB52D04DC20036DBD8313ED055", EncodeBase16(GetHashForBlob(StringToBlob("1234"),"MD5")));

  TestEqLike("*/modules/system/", GetModuleInstallationRoot("system"));
  TestEq("", GetModuleInstallationRoot("__nosuchmodule__"), "should NOT throw");
  TestEq("Cancel", GetTid("~cancel"));

  TestAssert(GetWebHareVersionNumber() >= 50300);

  STRING ARRAY mods := GetInstalledModuleNames();
  TestEq(TRUE, "system" IN mods);
  TestEq(TRUE, "webhare_testsuite" IN mods);

  RECORD config := GetWebHareConfiguration();
  TestEqLike("/?*", config.basedataroot);
  TestEqLike("/?*", config.ephemeralroot);

  //Do wasm-specific tests *last* so it's easier to manually test us under native
  TestEq(TRUE, IsWASM(), "We should be tested under wasm!");
  TestThrowsLike("*no such *syscall*notthere*", PTR EM_Syscall("notthere"));

  DATETIME now := GetCurrentDateTime();
  RECORD testrec :=
      [ i := 1
      , i64 := 1i64
      , d := now
      , ia := [ 2 ]
      , i64a := [ 2i64 ]
      , da := [ now ]
      , r := [ i := 1 ]
      , ra := [ [ i := 1 ] ]
      ];

  TestEQ(CELL[ ...testrec, called := TRUE ], EM_Syscall("executeInline", [ func := "return { ...param, called: true };", param := testrec ]));

  TestEQ(CELL[ ...testrec, called := TRUE ], WaitForPromise(EM_Syscall("executeInline",
      [ func := "return await new Promise(r => setTimeout(() => r({ ...param, called: true }), 1));"
      , param := testrec
      ])));

  TestEQ(CELL[ ...testrec, called := TRUE ], EM_SyncSyscall("executeInline",
      [ func := "return await new Promise(r => setTimeout(() => r({ ...param, called: true }), 1));"
      , param := testrec
      ]));

  //verify a promise not returning anything doesn't break the VM
  TestEq(FALSE, WaitForPromise(EM_Syscall("executeInline",
      [ func := "return await new Promise(r => setTimeout(() => r(), 1));"
      ])));

  //test buffers over EM_Syscall
  TestEq("010203", EM_Syscall("executeInline", [ func := "return param instanceof Buffer ? param.toString('hex') : 'NOT A BUFFER'", param := NEW Buffer("\x01\x02\x03")]));
  TestEq("040506", EM_Syscall("executeInline", [ func := "return param[1] instanceof Buffer ? param[1].toString('hex') : 'NOT A BUFFER ARRAY'", param := [NEW Buffer("\x01\x02\x03"),NEW Buffer("\x04\x05\x06")]]));

  TestThrowsLike("sync inline exception", PTR EM_Syscall("executeInline", [ func := "throw new Error(`sync inline exception`)"]));
  TestRejectedLike("ASYNC inline exception", EM_Syscall("executeInline", [ func := "return (async function(){ throw new Error(`ASYNC inline exception`); })()"]));

  //Test reentrant syscalls
  PRINT(`start reentrant syscall test\n`);
  TestEq(2, WaitForPromise(EM_Syscall("executeInline", [ func := `return 1 + await vm.loadlib(${EncodeJSON(Resolve("testwasmlib.whlib"))}).ReentrantSyscall();`, param := DEFAULT RECORD ])));
  PRINT(`done reentrant syscall test\n`);

  // Not allowed to reenter the HSVM while running a synchronous syscall
  TestThrowsLike("Not allowed *SyncSyscall", PTR EM_SyncSyscall("executeInline", [ func := `return 1 + await vm.loadlib(${EncodeJSON(Resolve("testwasmlib.whlib"))}).ReentrantSyscall();`, param := DEFAULT RECORD ]));

  // Test extending empty objects
  OBJECT t := NEW TObject;
  VARIANT res := EM_Syscall("executeInline", [ func :=
  `
    const r = vm.wasmmodule._HSVM_ObjectMemberRef(vm.hsvm, param.$obj.id, vm.getColumnId("VAL"), 1);
    vm.wasmmodule._HSVM_ObjectInitializeEmpty(vm.hsvm, r);
    const libbuf = vm.wasmmodule.stringToNewUTF8("mod::webhare_testsuite/tests/system/nodejs/wasm/test_wasm_basics.whscr");
    const namebuf = vm.wasmmodule.stringToNewUTF8("TTOCREATE");
    try {
      return vm.wasmmodule._HSVM_ObjectExtend(vm.hsvm, r, libbuf, namebuf);
    } finally {
      vm.wasmmodule._free(libbuf);
      vm.wasmmodule._free(namebuf);
    }
  `, param := t ]);
  TestEQ(1, res);
  TestEQ(TRUE, t->val EXTENDSFROM TToCreate);
  TestEQ(0, t->val->v);
}

RunWasmTest(); //TODO RunTestFramework isn't supported yet
