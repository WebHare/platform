<?wh
LOADLIB "wh::internal/testfuncs.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::promise.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::webhare_testsuite/lib/database.whlib";

PUBLIC INTEGER FUNCTION Add(INTEGER lhs, INTEGER rhs)
{
  RETURN lhs + rhs;
}
PUBLIC ASYNC FUNCTION MultiplyPromise(INTEGER lhs, INTEGER rhs)
{
  AWAIT CreateSleepPromise(20);
  RETURN lhs * rhs;
}

OBJECTTYPE ObjType
<
  PUBLIC STRING FUNCTION Test()
  {
    RETURN "ok";
  }
>;

OBJECT obj;
RECORD globalrec;

PUBLIC OBJECT FUNCTION GetObject(INTEGER sleepmsecs)
{
  IF (NOT ObjectExists(obj))
    obj := NEW ObjType;
  Sleep(sleepmsecs);
  RETURN obj;
}

PUBLIC MACRO SetGlobal(RECORD rec)
{
  globalrec := rec;
}
PUBLIC RECORD FUNCTION GetGlobal()
{
  RETURN globalrec;
}

PUBLIC RECORD ARRAY FUNCTION GetExportTest()
{
  RETURN SELECT *, harescript := TRUE FROM webhare_testsuite.exporttest;
}
PUBLIC MACRO UpdateExportTest()
{
  UPDATE webhare_testsuite.exporttest SET text := text || " (updated)";
}
PUBLIC INTEGER FUNCTION testTypes(INTEGER expectid)
{
  //Runs the HS part of the testTypes
  TestEq(expectid, (SELECT AS INTEGER id FROM webhare_testsuite.consilio_index WHERE text="row1" AND grouprequiredindexdate = DEFAULT DATETIME));
  TestEq(expectid, (SELECT AS INTEGER id FROM webhare_testsuite.consilio_index WHERE text="row1" AND objectrequiredindexdate = MAX_DATETIME));

  INTEGER myrow := MakeAutonumber(webhare_testsuite.consilio_index, "id");
  INSERT INTO webhare_testsuite.consilio_index(id,text,grouprequiredindexdate,objectrequiredindexdate) VALUES(myrow,"hs-wasm-row1",DEFAULT DATETIME,MAX_DATETIME);
  RETURN myrow;
}

MACRO DoSetGlobal(RECORD newrec, BOOLEAN iscommit)
{
  //ensure we're in a proper state, after the original commit
  GetPrimary()->BeginWork();
  GetPrimary()->CommitWork();
  SetGlobal(CELL[...newrec, iscommit]);
}
PUBLIC MACRO SetGobalOnCommit(RECORD newrec)
{
  GetPrimary()->RegisterCommitHandler("", PTR DoSetGlobal(newrec, #1));
}
INTEGER FUNCTION DoTheInsert()
{
  GetPrimary()->BeginWork();
  INTEGER nextid := InsertImmediately();
  GetPrimary()->CommitWork();
  RETURN nextid;
}
PUBLIC INTEGER FUNCTION InsertUsingSeparateTrans()
{
  RETURN RunInSeparatePrimary(PTR DoTheInsert);
}
PUBLIC INTEGER FUNCTION InsertImmediately()
{
  INTEGER nextid := MakeAutonumber(webhare_testsuite.exporttest, "id");
  INSERT INTO webhare_testsuite.exporttest(id, text) VALUES(nextid, "Record #" || nextid);
  RETURN nextid;
}

PUBLIC MACRO Crash() {
  ABORT("jscall aborted");
}
