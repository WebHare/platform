<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internet/webdav.whlib";

LOADLIB "mod::publisher/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::wrd/lib/api.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/networking/antivirus.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::webhare_testsuite/lib/internal/testsite.whlib";


STRING todaysvirus := GenerateUFS128BitId();
STRING virushash := ToLowercase(EncodeBase16(GetHashForString(todaysvirus, "SHA-256")));

RECORD ARRAY FUNCTION GetAntiVirusEvents(DATETIME since)
{
  // Should be flushed immediately
  OBJECT stream := OpenWebHareLogStream("audit", since, GetCurrentDatetime());

  RECORD ARRAY events;
  WHILE (TRUE)
  {
    RECORD rec := stream->ReadRecord();
    IF (NOT RecordExists(rec))
      BREAK;

    IF (rec.parts[0] LIKE "system:antivirusscan.*")
      INSERT rec INTO events AT END;
  }
  stream->Close();
  RETURN events;
}

MACRO PrepIt()
{
  testfw->BeginWork();
  testfw->GetUserObject("lisa")->UpdateGrant("grant", "system:fs_fullaccess", GetTestsuiteTempFolder()->id, testfw->GetUserObject("lisa"), [ allowselfassignment := TRUE ]);
  testfw->CommitWork();
}

MACRO SetupScanner(BOOLEAN add_it)
{
  testfw->BeginWork();

  OBJECT externalservicetype := OpenWRDSchema("system:config")->^externalservice;
  INTEGER existing_test_scanner := externalservicetype->Search("WRD_TAG", "TESTFW_ANTIVIRUS_SCANNER", [ historymode := "all" ]);
  IF(existing_test_scanner != 0)
    externalservicetype->DeleteEntity(existing_test_scanner);

  IF(add_it)
  {
    externalservicetype->CreateEntity( [ wrd_tag := "TESTFW_ANTIVIRUS_SCANNER"
                                       , url  := "x-antivirus-mock:" || todaysvirus
                                       , wrd_limitdate := AddTimeToDate(60000, GetCurrentDatetime())
                                       , priority := -2000000000
                                       , servicetype := "system:clamav"
                                       ]);
  }

  testfw->CommitWork();

}

ASYNC MACRO TestTolliumVirus()
{
  TestEq(TRUE, IsAntiVirusScanEnabled());

  DATETIME start := GetCurrentDatetime();

  testfw->SetTestUser("lisa");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ ToString(GetTestsuiteTempFolder()->id) ] ]));
  TT("upload")->ExecuteUpload([[ filename := "test-av1.txt", data := StringToBlob("Not a virus, honest!") ]]);

  TestEq(["test-av1.txt"], SELECT AS STRING ARRAY name FROM system.fs_objects WHERE parent = GetTestsuiteTempFolder()->id ORDER BY name);

  AWAIT ExpectAndAnswerMessageBox("ok"
                                  , PTR TT("upload")->ExecuteUpload([[ filename := "test-av2.txt", data := StringToBlob(todaysvirus) ]])
                                  , [ messagemask := "*Virus!*" ]
                                  );

  TestEq(["test-av1.txt"], SELECT AS STRING ARRAY name FROM system.fs_objects WHERE parent = GetTestsuiteTempFolder()->id ORDER BY name);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ ToString(GetTestsuiteTempFolder()->id) ] ]));
  AWAIT ExpectAndAnswerMessageBox("no"
                                  , PTR TT("upload")->ExecuteUpload([[ filename := "test-av3.txt", data := StringToBlob(todaysvirus) ]])
                                  , [ messagemask := "*Virus!*" ]
                                  );
  AWAIT ExpectAndAnswerMessageBox("yes"
                                  , PTR TT("upload")->ExecuteUpload([[ filename := "test-av4.txt", data := StringToBlob(todaysvirus) ]])
                                  , [ messagemask := "*Virus!*" ]
                                  );
  TestEq(["test-av1.txt", "test-av4.txt"], SELECT AS STRING ARRAY name FROM system.fs_objects WHERE parent = GetTestsuiteTempFolder()->id ORDER BY name);

  RECORD ARRAY events := GetAntiVirusEvents(start);
  TestEq( ["system:antivirusscan.ok", "system:antivirusscan.fail", "system:antivirusscan.fail", "system:antivirusscan.fail", "system:antivirusscan.override"]
        , SELECT AS STRING ARRAY parts[0] FROM events);
  TestEqMembers([ [ client_filename := "test-av1.txt", errorcode := "", client_mimetype := "text/plain", filehash := "91b5aec93e2f71b3a91c556dc60dedd3d17e7a18f1311809d2c47013af5be27c", detail := "", message := "" ]
                , [ client_filename := "test-av2.txt", errorcode := "suspicious", client_mimetype := "text/plain", filehash := virushash, detail := "Virus!", message := "Suspicious file: Virus!" ]
                , [ client_filename := "test-av3.txt", errorcode := "suspicious", client_mimetype := "text/plain", filehash := virushash, detail := "Virus!", message := "Suspicious file: Virus!" ]
                , [ client_filename := "test-av4.txt", errorcode := "suspicious", client_mimetype := "text/plain", filehash := virushash, detail := "Virus!", message := "Suspicious file: Virus!" ]
                , [ client_filename := "test-av4.txt", errorcode := "suspicious", client_mimetype := "text/plain", filehash := virushash, detail := "Virus!", message := "Suspicious file: Virus!" ]
                ], (SELECT AS RECORD ARRAY DecodeHSON(parts[3]) FROM events), "*");

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestWebdavVirus()
{
  DATETIME start := GetCurrentDatetime();

  testfw->SetTestUser("lisa");
  STRING webdavroot := testfw->GetTestSite()->webroot || "webdav/";
  OBJECT webdavserver := NEW WebdavConnection(webdavroot);
  webdavserver->webbrowser->SetPassword(webdavroot, testfw->GetUserLogin("lisa"), testfw->GetUserPassword("lisa"));

  webdavserver->Put("/publisher/tmp/test-webdav1.txt", StringToBlob("Not a virus, honest!"), FALSE);
  TestThrowsLike('Failed*',PTR webdavserver->Put("/publisher/tmp/test-webdav2.txt", StringToBlob(todaysvirus), FALSE));

  TestEq(["test-av1.txt", "test-av4.txt", "test-webdav1.txt"]
         , SELECT AS STRING ARRAY name FROM webdavserver->ListDirectory("/publisher/tmp/") ORDER BY name);

  RECORD ARRAY events := GetAntiVirusEvents(start);
  TestEq( ["system:antivirusscan.ok", "system:antivirusscan.fail" ]
        , SELECT AS STRING ARRAY parts[0] FROM events);
  TestEqMembers([ [ client_filename := "test-webdav1.txt", errorcode := "", client_mimetype := "", filehash := "91b5aec93e2f71b3a91c556dc60dedd3d17e7a18f1311809d2c47013af5be27c", detail := "", message := "", uploadpath := "/publisher/tmp/test-webdav1.txt" ]
                , [ client_filename := "test-webdav2.txt", errorcode := "suspicious", client_mimetype := "", filehash := virushash, detail := "Virus!", message := "Suspicious file: Virus!", uploadpath := "/publisher/tmp/test-webdav2.txt" ]
                ], (SELECT AS RECORD ARRAY DecodeHSON(parts[3]) FROM events), "*");
}

MACRO TestPublisherFormVirus()
{
  DATETIME start := GetCurrentDatetime();
  BuildWebtoolForm( [ emailfieldname := "email" ]);

  BLOB gifvirus := StringToBlob(DecodeBase64("R0lGODlhAQABAPAAAP///wAAACH5BAEAAAAALAAAAAABAAEAAAICRAEAOw==") || todaysvirus);
  STRING gifvirushash := ToLowercase(EncodeBase16(GetHashForBlob(gifvirus, "SHA-256")));
  TestEq("image/gif", ScanBlob(gifvirus).mimetype);

  OBJECT formfile := OpenTestsuiteSite()->OpenByPath("webtools/form");
  TestEq(TRUE, testfw->browser->GotoWebPage(formfile->url));

  RECORD submitrec := [ firstname := "Corona"
                      , upload := [[ filename := "novirus.txt"
                                  , link := GetDataUrl(StringToBlob("Not a virus, honest!"), "text/plain")
                                  ]]
                      , imgedit := [[ filename := "goudvis.png"
                                   , link :=  GetDataUrl(gifvirus,"image/gif")
                                   ]]
                      , email := "testfw+virus1@beta.webhare.net"
                      ];

  OBJECT tester := OpenFormsapiFormTester(testfw->browser);
  RECORD res := tester->SubmitForm(submitrec);
  TestEq(FALSE, res.success);
  TestEqMembers([[ name := "imgedit", message := "This file looks suspicious and can unfortunately not be uploaded" ]], res.errors, "*");

  submitrec.imgedit := DEFAULT RECORD ARRAY;
  res := tester->SubmitForm(submitrec);
  TestEq(TRUE, res.success);

  submitrec.upload := [[ filename := "virus.txt"
                       , link := GetDataUrl(StringToBlob(todaysvirus), "text/plain")
                      ]];
  res := tester->SubmitForm(submitrec);
  TestEq(FALSE, res.success);

  RECORD ARRAY events := GetAntiVirusEvents(start);
  TestEq( ["system:antivirusscan.ok", "system:antivirusscan.fail", "system:antivirusscan.ok", "system:antivirusscan.fail" ]
        , SELECT AS STRING ARRAY parts[0] FROM events);
  TestEqMembers([ [ url := formfile->url, client_filename := "novirus.txt", errorcode := "", client_mimetype := "text/plain", filehash := "91b5aec93e2f71b3a91c556dc60dedd3d17e7a18f1311809d2c47013af5be27c", field := "upload", detail := "", message := "" ]
                , [ url := formfile->url, client_filename := "goudvis.png", errorcode := "suspicious", client_mimetype := "image/gif", filehash := gifvirushash, detail := "Virus!", field := "imgedit", message := "Suspicious file: Virus!" ]
                , [ url := formfile->url, client_filename := "novirus.txt", errorcode := "", client_mimetype := "text/plain", filehash := "91b5aec93e2f71b3a91c556dc60dedd3d17e7a18f1311809d2c47013af5be27c", field := "upload", detail := "", message := "" ]
                , [ url := formfile->url, client_filename := "virus.txt", errorcode := "suspicious", client_mimetype := "text/plain", filehash := virushash, detail := "Virus!", field := "upload", message := "Suspicious file: Virus!" ]
                ], (SELECT AS RECORD ARRAY DecodeHSON(parts[3]) FROM events), "*");
}

RunTestframework([ PTR SetupScanner(TRUE)
                 , PTR PrepIt
                 , PTR TestTolliumVirus
                 , PTR PrepareTestModuleWebDesignWebsite("webhare_testsuite:basetest", [istemplate := FALSE, withcontent := FALSE ])
                 , PTR TestWebdavVirus
                 , PTR TestPublisherFormVirus
                 , PTR SetupScanner(FALSE)
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa", grantrights := ["system:webdav"] ]
                                   ]]);
