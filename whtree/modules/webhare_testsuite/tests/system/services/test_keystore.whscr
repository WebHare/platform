<?wh

LOADLIB "mod::system/lib/keystore.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


MACRO TestAPIKeyLookup() {
  testfw->BeginWork();
  testfw->CleanupWRDSchema(OpenWRDSchema("system:config"), [ [ type := "API_SECRET", tagmasks := [ "TESTFW_*" ] ]]);

  RECORD ARRAY testkeys :=
    [ [ api_type := "platform:google.cloud.backend"
      , masks := "https://backend.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key1" ]
      ]
    , [ api_type := "platform:google.cloud.frontend"
      , masks := "https://any.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key2" ]
      ]
    , [ api_type := "platform:google.cloud.backend"
      , masks := "https://any.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key2" ]
      ]
    , [ api_type := "platform:google.cloud.frontend"
      , masks := "https://client.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key3" ]
      ]
    , [ api_type := "platform:google.cloud.frontend"
      , masks := "https://*.wild.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key4-client" ]
      ]
    , [ api_type := "platform:google.cloud.backend"
      , masks := "https://*.wild.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key4-server" ]
      ]
    , [ api_type := "platform:google.cloud.frontend"
      , masks := "https://*.bla.wild.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key5-client" ]
      ]
    , [ api_type := "platform:google.cloud.backend"
      , masks := "https://*.bla.wild.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key5-server" ]
      ]
    , [ api_type := "platform:google.cloud.backend"
      , masks := "https://wild.testframework.beta.webhare.net/*"
      , secret := [ api_key := "Key6-server" ]
      ]

    , [ api_type := "platform:friendlycaptcha"
      , masks := "https://*.testframework.beta.webhare.net/*"
      , secret := [ api_key := "friendlyapi" ]
      ]
    , [ api_type := "platform:google.recaptcha"
      , masks := "https://*.rec.testframework.beta.webhare.net/*"
      , secret := [ api_key := "recaptcha" ]
     ]
  ];

  OBJECT keytype := OpenWRDSchema("system:config")->^api_secret;
  FOREVERY(RECORD keyrec FROM testkeys)
    keytype->CreateEntity(CELL[ wrd_tag := "TESTFW_KEY_" || #keyrec, comment := "Added by test_keystore", ...keyrec]);

  testfw->CommitWork();

  TestEqMembers([ secret := [ api_key := "Key2" ]], GetAPIKey(["platform:google.cloud.backend"],"https://any.testframework.beta.webhare.net/myrequest"), "*");
  TestEqMembers([ secret := [ api_key := "Key2" ]], GetAPIKey(["platform:google.cloud.frontend"],"https://any.testframework.beta.webhare.net/myrequest"), "*");
  TestEqMembers([ secret := [ api_key := "Key2" ]], GetAPIKey(["platform:google.cloud.frontend","platform:google.cloud.backend"],"https://any.testframework.beta.webhare.net/myrequest"), "*");
  TestThrowsLike("No API key found for*", PTR GetAPIKey(["platform:unknownkeytype"],"https://any.testframework.beta.webhare.net/myrequest"));

  TestEq("Key2", LookupAPIKey("google","any.testframework.beta.webhare.net", [scope:="server"]).apikey);
  TestEq("Key2", LookupAPIKey("google","any.testframework.beta.webhare.net", [scope:="client"]).apikey);
  TestEq("Key2", LookupAPIKey("google","https://any.testframework.beta.webhare.net/", [scope:="server"]).apikey);
  TestThrowsLike("Unable to find*", PTR LookupAPIKey("google","x.testframework.beta.webhare.net", [scope:="server"]));
  TestEq(DEFAULT RECORD, LookupAPIKey("google","x.testframework.beta.webhare.net", [scope:="server", allowmissing   := TRUE ]));
  TestEq("Key4-server", LookupAPIKey("google","x.wild.testframework.beta.webhare.net", [scope:="server"]).apikey);
  TestEq("Key5-server", LookupAPIKey("google","x.bla.wild.testframework.beta.webhare.net", [scope:="server"]).apikey);
  TestEq("Key4-server", LookupAPIKey("google","https://x.wild.testframework.beta.webhare.net/", [scope:="server"]).apikey);
  TestEq("Key4-client", LookupAPIKey("google","https://x.wild.testframework.beta.webhare.net/", [scope:="client"]).apikey);

  TestEqMembers([ api_type := "platform:google.recaptcha", secret := [ api_key := "recaptcha" ]], GetAPIKey(["platform:google.recaptcha"],"https://any.rec.testframework.beta.webhare.net/myrequest"), "*");
  TestEqMembers([ api_type := "platform:friendlycaptcha", secret := [ api_key := "friendlyapi" ]], GetAPIKey(["platform:google.recaptcha","platform:friendlycaptcha"],"https://any.testframework.beta.webhare.net/myrequest"), "*", "looking for both types find closest match");
  TestEqMembers([ api_type := "platform:google.recaptcha", secret := [ api_key := "recaptcha" ]], GetAPIKey(["platform:google.recaptcha","platform:friendlycaptcha"],"https://any.rec.testframework.beta.webhare.net/myrequest"), "*", "looking for both types find closest match");
}

RunTestframework([ PTR TestAPIKeyLookup
                 ]);
