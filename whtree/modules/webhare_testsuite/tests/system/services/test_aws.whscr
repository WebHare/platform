<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/webapi/aws/s3.whlib";
LOADLIB "mod::system/lib/webapi/aws/awsbase.whlib";
LOADLIB "mod::system/lib/webapi/aws/sns.whlib";

/* This test requires various environment variables to be set
   In Sublime, set these under User preferences > build_env, eg

  "build_env":
  {
    "TESTSECRET_AWS_APIKEY": "AK...."
  },

  We need something like the following policy on the testbucket

  {
      "Version": "2012-10-17",
      "Statement": [
          {
              "Effect": "Allow",
              "Principal": {
                  "AWS": "arn:aws:iam::079021186127:user/webhare-ci-test-aws"
              },
              "Action": "s3:*",
              "Resource": [
                  "arn:aws:s3:::webhare-ci-test-aws/*",
                  "arn:aws:s3:::webhare-ci-test-aws"
              ]
          }
      ]
  }

*/

OBJECT aws;
OBJECT s3;
STRING apizone := GetEnvironmentVariable("TESTSECRET_AWS_APIZONE") ?? "eu-west-1";
STRING apikey := GetEnvironmentVariable("TESTSECRET_AWS_APIKEY");
STRING apisecret := GetEnvironmentVariable("TESTSECRET_AWS_APISECRET");
STRING bucket := GetEnvironmentVariable("TESTSECRET_AWS_APIBUCKET") ?? "webhare-ci-test-aws";

MACRO Prepare_S3()
{
  IF(apikey="" OR apisecret="")
    THROW NEW Exception("TESTSECRET_AWS_APIKEY or TESTSECRET_AWS_APISECRET not set, cannot test S3!");

  aws := NEW AmazonAWSConfig(apikey, apisecret);
  s3 := NEW AmazonAWSS3Service(aws, apizone);
}

MACRO Test_S3()
{
  //for our CI, apikeys associated with webhare-ci-test-aws
  //DumpValue(s3->listBuckets());

  STRING testcontent := "This is a test - " || GenerateUFS128BitId();
  s3->PutObject(bucket, "/testfile", StringToBlob(testcontent), [ contenttype := "text/x-plainer", cachecontrol := "max-age=1" ]);
  s3->PutObject(bucket, "testfile2", StringToBlob(testcontent));
  s3->PutObject(bucket, "/testdir/testfile", StringToBlob(testcontent));
  s3->PutObject(bucket, "testdir/testfile2", StringToBlob(testcontent));

  //a better test would be with two different buckets. but oh well..
  s3->CopyObject(bucket, "/testfile", bucket, "/testdir/testfile2");
  s3->CopyObject(bucket, "testdir/testfile2", bucket, "/testdir/testdeeper/testfile", [contenttype := "text/x-extra-plain" ]);
  TestThrowsLike('*without*setting*Content-Type*',PTR s3->CopyObject(bucket, "testdir/testfile2", bucket, "/testdir/testdeeper/testfile", [ cachecontrol := "max-age=2" ]));

  RECORD ARRAY dir := s3->GetDirectory(bucket,"/");
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testfile" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(TRUE, RecordExists(SELECT size FROM dir WHERE name="testfile2" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir" AND isfolder AND size=0));
  TestEq(3, Length(SELECT FROM dir WHERE name != "testmultipart")); //delete might not be visible due to eventual consistency..
  TestEq((SELECT * FROM dir ORDER BY name), (SELECT * FROM s3->GetDirectory(bucket,"") ORDER BY name), "The slash was implicit, so these should return the same");

  dir := s3->GetDirectory(bucket,"/testdir");
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testfile" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testfile2" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testdeeper" AND isfolder AND size=0));
  TestEq(3, Length(dir));

  TestEq((SELECT * FROM dir ORDER BY name), (SELECT * FROM s3->GetDirectory(bucket,"/testdir/") ORDER BY name), "The last slash is implicitly added, so these should return the same");
  TestEq((SELECT * FROM dir ORDER BY name), (SELECT * FROM s3->GetDirectory(bucket,"testdir/") ORDER BY name), "The first slash is also implicitly added, so these should return the same");

  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testfile" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testdeeper" AND isfolder AND size=0));
  TestEq(3, Length(dir));

  dir := s3->GetDirectory(bucket,"/testdir/testdeeper/");
  TestEq(TRUE, RecordExists(SELECT FROM dir WHERE name="testdir/testdeeper/testfile" AND isfolder = FALSE AND size=Length(testcontent)));
  TestEq(1, Length(dir));

  //test delimiter trickery. this is sometimes needed to avoid excessive recursion (eg IVS files). ignore 'testmult(ipart)' which may linger from earlier tests
  TestEq([[ name := "testd", isfolder := TRUE ]
         ,[ name := "testf", isfolder := TRUE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"", [ delimiter := "i" ]) WHERE name != "testmult" ORDER BY name);

  //once you explicitly specify a delimiter other than /, we won't do any of our usual workarounds that might only confuse users that are explcitly trying to control AWS
  TestEq([[ name := "testd", isfolder := TRUE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"testd", [ delimiter := "i" ]) ORDER BY name);
  TestEq([[ name := "testdir/testdeeper/testf", isfolder := TRUE ]
         ,[ name := "testdir/testf", isfolder := TRUE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"testdi", [ delimiter := "i" ]) ORDER BY name);

  TestEq([[ name := "testdir/testdeeper/testf", isfolder := TRUE ]
         ,[ name := "testdir/testf", isfolder := TRUE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"testdi", [ delimiter := "i" ]) ORDER BY name);

  TestEq([[ name := "testdir/testf", isfolder := TRUE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"testdir/testf", [ delimiter := "i" ]) ORDER BY name);
  TestEq([[ name := "testdir/testfile", isfolder := FALSE ]
         ,[ name := "testdir/testfile2", isfolder := FALSE ]
         ], SELECT name, isfolder FROM s3->GetDirectory(bucket,"testdir/testfi", [ delimiter := "i" ]) ORDER BY name);


  RECORD obj := s3->GetObject(bucket, "/testfile");
  TestEq(testcontent, BlobToString(obj.body));
  TestEq("text/x-plainer", obj.contenttype);
  TestEq("max-age=1", obj.cachecontrol);

  obj := s3->GetObject(bucket, "/testdir/testfile2");
  TestEq(testcontent, BlobToString(obj.body));
  TestEq("text/x-plainer", obj.contenttype);
  TestEq("max-age=1", obj.cachecontrol);

  obj := s3->GetObject(bucket, "testdir/testdeeper/testfile");
  TestEq("text/x-extra-plain", obj.contenttype);
  TestEq("", obj.cachecontrol, "Cachecontrol is lost in the copy when setting content-type, this is an AWS restriction, x-amz-metadata-directive lacks granularity");

  obj := s3->GetObject(bucket, "/testfile2");
  TestEq("text/plain", obj.contenttype);

  RECORD objhead := s3->HeadObject(bucket, "/testfile");
  TestEQ(39i64, objhead.contentlength);

  s3->DeleteObject(bucket, "/testfile");

  dir := s3->GetDirectory(bucket,"/");
  TestEq(FALSE, RecordExists(SELECT FROM dir WHERE name="testfile" AND size=Length(testcontent)));
  TestEq(FALSE, RecordExists(s3->GetObject(bucket, "/testfile")));
}

MACRO Test_S3_MultipartUploads()
{
  //Clear any old uploads
  FOREVERY(RECORD oldupload FROM s3->ListMultipartUploads(bucket).uploads)
    s3->AbortMultipartUpload(bucket, oldupload.uploadkey, oldupload.uploadid);

  INTEGER uploadsize := 5*1024*1024 + 5;
  BLOB toupload := StringToBlob(`[${RepeatText(".", 5*1024*1024 - 2)}(123)]`);
  TestEq(uploadsize, Length(toupload));

  RECORD upload := s3->CreateMultipartUpload(bucket,"/testmultipart-cancelled");
  TestEq(TRUE, RecordExists(SELECT FROM s3->ListMultipartUploads(bucket).uploads WHERE uploadkey = "testmultipart-cancelled" AND uploadid = upload.uploadid));
  s3->AbortMultipartUpload(bucket, upload.uploadkey, upload.uploadid);

  RECORD putpartsresult := s3->PutObjectInParts(bucket, "/testmultipart", toupload);
  RECORD ARRAY uploadurls := putpartsresult.uploadurls; //generated URLs for 5MB + 5 bytes
  TestEq(2, Length(uploadurls));
  TestEq(5*1024*1024, uploadurls[0].size);
  TestEq(0i64, uploadurls[0].off);
  TestEq(5, uploadurls[1].size);
  TestEq(5*1024*1024i64, uploadurls[1].off);

  TestEqLike("https://*", uploadurls[0].url);
  TestEqLike("https://*", uploadurls[1].url);

  RECORD obj := s3->GetObject(bucket, "/testmultipart");
  TestEq(obj.body, toupload);

  RECORD uploadhead := s3->HeadObject(bucket, putpartsresult.objectkey);
  TestEQ(INTEGER64(uploadsize), uploadhead.contentlength);

  TestEq(RECORD[], s3->ListMultipartUploads(bucket).uploads);
  s3->DeleteObject(bucket, "/testmultipart");
}

MACRO Test_S3ListBuckets()
{
  /* We can only run this test if we -own- the test bucket. The standard
     credentials can't

     To test this call, uncomment the test and

     export TESTSECRET_AWS_APIKEY="<API KEY>"
     export TESTSECRET_AWS_APISECRET="<API SECRET>"
     wh run mod::webhare_testsuite/tests/system/services/test_aws.whscr
     */
  RECORD ARRAY buckets := s3->listAllBuckets();
  RECORD mytestbucket := SELECT * FROM buckets WHERE name = VAR bucket;
  TestEq(TRUE, RecordExists(mytestbucket));
  TestEq(TRUE, mytestbucket.creationdate > MakeDate(2017,11,1));
}

MACRO Test_SNS()
{
  IF(apikey="" OR apisecret="")
    THROW NEW Exception("TESTSECRET_AWS_APIKEY or TESTSECRET_AWS_APISECRET not set, cannot test SNS!");

  OBJECT sns := NEW AmazonAWSSNSInterface(apizone, apikey, apisecret);
  TestEQ(TRUE, Length(sns->ListTopics()) > 10);
}

                 // , PTR Test_S3ListBuckets //this requires higher privileges!
RunTestframework([ PTR Prepare_S3
                 , PTR Test_S3
                 , PTR Test_S3_MultipartUploads
                 , PTR Test_SNS
                 ]);
