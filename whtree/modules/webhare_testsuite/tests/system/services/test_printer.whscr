<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/dialogs.whlib";
LOADLIB "mod::system/lib/printer.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";


STRING doc := `<!DOCTYPE html>
<html>
  <head>
    <meta name="wh-chromepdf-margin-top" value="2cm" />
    <meta name="wh-chromepdf-margin-left" value="2.3cm" />
    <meta name="wh-chromepdf-margin-right" value="2.8cm" />
    <meta name="wh-chromepdf-margin-bottom" value="2cm" />
    <meta name="wh-chromepdf-format" value="a4" /></head>
    <style>
      html,body { background-color: transparent; }
      html { font: 45px Arial; color: #000000; }
    </style>
  </head>
  <body>
    NOT A TEAPOT.
    <script src="${GetprimaryWebHareInterfaceURL()}.webhare_testsuite/tests/system/printertest.js"></script>
  </body>
</html>`;

STRING multipagedoc := `<!DOCTYPE html>
<html>
  <head>
    <style>
      html,body { background-color: transparent; }
      html { font: 45px Arial; color: #000000; }
    </style>
  </head>
  <body>
    <p>Page 1</p>
    <p style="page-break-before: always">Page 2</p>
    <p style="page-break-before: always">Page 3</p>
  </body>
</html>`;

STRING bgdoc := `<!DOCTYPE html>
<html>
  <head>
    <style>
      html,body { background-color: transparent; }
      img { position: absolute; top:0;bottom:0;left:0;right:0; opacity:0.5; }
    </style>
  </head>
  <body>
    <img src="mod::tollium/web/img/objects/webhare.24x24.b.svg" />
  </body>
</html>`;

STRING doc2 := `<!DOCTYPE html>
<html>
  <head>
    <meta name="wh-chromepdf-margin-top" value="1cm" />
    <meta name="wh-chromepdf-margin-left" value="2.3cm" />
    <meta name="wh-chromepdf-margin-right" value="2.8cm" />
    <meta name="wh-chromepdf-margin-bottom" value="2cm" />
    <meta name="wh-chromepdf-width" value="10cm" /></head>
    <meta name="wh-chromepdf-height" value="10cm" /></head>
    <template name="wh-chromepdf-headertemplate"><div style="width:100%; text-align:right; font-size:12px;">HEADER: <span class='pageNumber'></span>/<span class='totalPages'></span></div></template>
    <template name="wh-chromepdf-footertemplate"><div style="width:100%; text-align:right; font-size:12px;">FOOTER: <span class='pageNumber'></span>/<span class='totalPages'></span></div></template>
    <style>
      html,body { background-color: transparent; }
      html { font: 45px Arial; color: #000000; }
    </style>
  </head>
  <body>
    NOT A TEAPOT.
    <script src="${GetprimaryWebHareInterfaceURL()}.webhare_testsuite/tests/system/printertest.js"></script>
  </body>
</html>`;


MACRO TestPDFs()
{
  RECORD result := GeneratePDF( [ body := PTR Print(doc)
                                , wittypath := "inline::[body]"
                                ], [ printbackground := FALSE
                                   ]);

  RECORD ARRAY images := ExportPDFToImages(result.data);

  TestEq(1,Length(images));
  Testeq("page-1.jpg", images[0].filename); //pdfbox upgrade from 2.0.8/.11 to 20.0.21 slightly changed screenshot sizes  TODO drop resizeimgifneeded again
  testfw->CompareWithImage(images[0].data, Resolve("testdata/fullscreenshot.png"), [ maxmse := 500, resizeimgifneeded := TRUE ]); //TODO lower maxmse but we'll probably need a webfont

  RECORD result2 := GeneratePDF( [ body := PTR Print(bgdoc)
                                 , wittypath := "inline::[body]"
                                 ]);

  images := ExportPDFToImages(result2.data);
  testfw->CompareWithImage(images[0].data, Resolve("testdata/background.png"), [ maxmse := 25, resizeimgifneeded := TRUE ]);

  BLOB final := ApplyOverlayToPDF(result.data, result2.data);
  images := ExportPDFToImages(final);
  testfw->CompareWithImage(images[0].data, Resolve("testdata/merged.png"), [ maxmse := 500, resizeimgifneeded := TRUE ]); //TODO lower maxmse but we'll probably need a webfont

  //test empty concat
  TestThrowsLike("Empty PDF*", PTR ConcatenatePDFs(BLOB[]));

  //test identity concat
  BLOB collection := ConcatenatePDFs(BLOB[result.data]);
  Testeq(collection, result.data);

  collection := ConcatenatePDFs(BLOB[result.data,result2.data,result.data,result2.data,result.data,result2.data,result.data,result2.data,result.data,result2.data,result.data,result2.data]);
  images := ExportPDFToImages(collection);

  Testeq(12,Length(images));
  FOR(INTEGER x := 0; x < 12; x := x + 2)     //also test their ordering
  {
    testfw->CompareWithImage(images[x].data, Resolve("testdata/fullscreenshot.png"), [ maxmse := 500, resizeimgifneeded := TRUE ]);
    testfw->CompareWithImage(images[x+1].data, Resolve("testdata/background.png"), [ maxmse := 25, resizeimgifneeded := TRUE ]);
  }

  result := GeneratePDF( [ body := PTR Print(multipagedoc)
                         , wittypath := "inline::[body]"
                         ], [ printbackground := FALSE
                            , displayheaderfooter := TRUE
                            , margin := [ top := "0.5cm", left := "0.5cm", right := "0.5cm", bottom := "0.5cm" ]
                            , headertemplate := `<div style="width:100%; text-align:right; font-size:12px;">HEADER: <span class='pageNumber'></span>/<span class='totalPages'></span></div>`
                            , footertemplate := `<div style="width:100%; text-align:right; font-size:12px;">FOOTER: <span class='pageNumber'></span>/<span class='totalPages'></span></div>`
                            ]);
  images := ExportPDFToImages(result.data);
  TestEq(3,Length(images));
  Testeq("page-2.jpg", images[1].filename);
  testfw->CompareWithImage(images[1].data, Resolve("testdata/pagenumberedpage.png"), [ maxmse := 500, resizeimgifneeded := TRUE ]); //TODO lower maxmse but we'll probably need a webfont
}

ASYNC MACRO TestWittyDialog()
{
  AWAIT ExpectScreenChange(+1, PTR RunWittyTestDialog(GetTestController(),
                                [ body := PTR Print(doc)
                                , wittypath := "inline::[body]"
                                ], [ pdf := TRUE ]));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

MACRO TestPDFFromURL()
{
  STRING testurl := GetPrimaryWebHareInterfaceURL() || ".webhare_testsuite/tests/system/pdf.html";
  Print("Generate pdf from: " || testurl || "\n");
  RECORD result := GeneratePDFFromURL(testurl, [ printbackground := FALSE ]);
  RECORD ARRAY images := ExportPDFToImages(result.data);

  TestEq(1,Length(images));
  Testeq("page-1.jpg", images[0].filename);
  testfw->CompareWithImage(images[0].data, Resolve("testdata/webpage.png"), [ maxmse := 500, resizeimgifneeded := TRUE ]); //TODO lower maxmse but we'll probably need a webfont
}

MACRO TestScreenshots()
{
  testfw->BeginWork();
  testfw->SetupTestWebsite(DEFAULT BLOB);
  OBJECT testsite := testfw->GetTestSite();
  testfw->CommitWork();

  STRING url := ResolveToAbsoluteURL(testsite->webroot, "/tollium_todd.res/webhare_testsuite/tests/screenshot.html");
  Print("screenshotting " || url || "\n");

  RECORD test := GenerateBrowserScreenshot(url);
  testfw->CompareWithImage(test.screenshot.data, Resolve("testdata/fullscreenshot2.png"), [ annotation := url ]);

  test := GenerateBrowserScreenshot(url,[ withalpha :=  TRUE]);

  testfw->CompareWithImage(test.screenshot.data, Resolve("testdata/alphascreenshot.png"), [ annotation := url ]);

  test := GenerateBrowserScreenshot(url,
        [ screenwidth :=    500
        , screenheight :=   100
        ]);

  testfw->CompareWithImage(test.screenshot.data, Resolve("testdata/smallscreenshot.png"), [ annotation := url ]);

  url := ResolveToAbsoluteURL(testsite->webroot, "/tollium_todd.res/webhare_testsuite/tests/fontfaceload/");
  Print("screenshotting " || url || "\n");
  test := GenerateBrowserScreenshot(url);
  testfw->CompareWithImage(test.screenshot.data, Resolve("testdata/fontfaceload.png"), [ maxmse := 50, annotation := url ]);
}

RunTestframework([ PTR TestPDFs
                 , PTR TestWittyDialog
                 , PTR TestPDFFromURL
                 , PTR TestScreenshots
                 ]);
