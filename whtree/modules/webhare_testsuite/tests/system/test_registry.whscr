<?wh
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/registry.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::system/lib/internal/modules/moduleregistry.whlib";

LOADLIB "wh::files.whlib";

MACRO DoKeyTests(STRING basename, STRING nodebasename)
{
  //Read&Write have different responses to non existing keys whether is system or user registry, as one requires values to exist and the other requires fallback values to be provided
  TestThrowsLike("*", PTR ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue"));
  TestEq(43, ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43 ]));
  TestThrowsLike("*", PTR WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", 43));
  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", 44, [createifneeded := TRUE]);
  TestEq(44, ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43 ]));
  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", 45, [createifneeded := TRUE]);
  TestEq(45, ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43 ]));
  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", 46, [initialcreate := TRUE]);
  TestEq(45, ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43 ]));

  TestEq([[ name := basename || ".__webhare_testsuite_base_node__.stupidvalue", value := 45 ]], ReadRegistryKeysByMask(basename || ".__webhare_testsuite_base_node__.stupidvalue"));

  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.blobvalue", StringToBlob("Hello, World!"), [createifneeded:=TRUE]);
  TestEq("Hello, World!", BlobToString(ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.blobvalue", [fallback:=DEFAULT BLOB])));
  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.blobvalue", StringToBlob(RepeatText("Hello, World!",1000)), [createifneeded:=TRUE]);
  TestEq(RepeatText("Hello, World!",1000), BlobToString(ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.blobvalue", [fallback:=DEFAULT BLOB])));

  TestEq([[ data := StringToBlob(RepeatText("Hello, World!",1000)), fullname := nodebasename || ".__webhare_testsuite_base_node__.blobvalue", subkey := "blobvalue" ]
         ,[ data := 45, fullname := nodebasename || ".__webhare_testsuite_base_node__.stupidvalue", subkey := "stupidvalue" ]
         ]
         , SELECT * FROM ReadRegistryNode(basename || ".__webhare_testsuite_base_node__") ORDER BY subkey);

  DeleteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue");
  TestThrowsLike("*", PTR ReadRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue"));
  TestEq([[ data := StringToBlob(RepeatText("Hello, World!",1000)), fullname := nodebasename || ".__webhare_testsuite_base_node__.blobvalue", subkey := "blobvalue" ]
         ], ReadRegistryNode(basename || ".__webhare_testsuite_base_node__"));

  WriteRegistryKey(basename || ".__webhare_testsuite_base_node__.stupidvalue", 46, [initialcreate := TRUE]);
  DeleteRegistryNode(basename || ".__webhare_testsuite_base_node__");
  TestEq(RECORD[], ReadRegistryNode(basename || ".__webhare_testsuite_base_node__"));
}

MACRO TestRegistry()
{
  TestEq(['system:registry.webhare_testsuite.x3', 'system:registry.webhare_testsuite.y1'], GetRegistryKeyEventMasks(["webhare_testsuite.y1.testkey","webhare_testsuite.y1.testkey2","webhare_testsuite.x3.testkey"]));

  testfw->BeginWork();

  //cleanup. we'll use a special name so we can safely destroy it from the registry unseen
  DELETE FROM system.flatregistry WHERE name LIKE "*__webhare_testsuite_base_node__*";

  DoKeyTests("webhare_testsuite","webhare_testsuite");
  DoKeyTests("<wrd:00001111222233334444555566667a7a>","<wrd:00001111222233334444555566667a7a>");

  TestThrowsLike("Invalid registry key name *", PTR DoKeyTests("modules.webhare_testsuite","webhare_testsuite"));
  TestThrowsLike("Invalid registry key name *", PTR DoKeyTests("system.modules.webhare_testsuite","webhare_testsuite"));
  TestThrowsLike("Invalid userprefix *", PTR DoKeyTests("<someuser>","<someuser>"));
  //registry APIs require lowercase wrd guids
  TestThrowsLike("Invalid userprefix *", PTR DoKeyTests("<wrd:00001111222233334444555566667A7A>","<wrd:00001111222233334444555566667A7A>"));

  TestThrowsLike("No such registry*", PTR ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));
  WriteRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", 43, [createifneeded:=TRUE]);
  TestEq(43, ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));
  TestEq(43, ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));
  TestEq(43, ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43 ]));
  TestThrowsLike("Invalid type*", PTR ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", [ fallback := 43m ]));


  TestEq( [[ fullname := "webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", subkey := "stupidvalue", data := 43 ]]
    , ReadRegistryNode("webhare_testsuite.__webhare_testsuite_base_node__"));
  TestEq( [[ fullname := "webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", subkey := "stupidvalue", data := 43 ]]
    , ReadRegistryNode("webhare_testsuite.__webhare_testsuite_base_node__"));

  DeleteRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue");
  TestThrowsLike("No such registry key '*'", PTR ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));


  testfw->CommitWork();

  testfw->MockRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", 45);
  TestEq(45, ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));
  testfw->MockRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue", 47);
  TestEq(47, ReadRegistryKey("webhare_testsuite.__webhare_testsuite_base_node__.stupidvalue"));

}

MACRO TestAnonymousRegistry()
{
  testfw->BeginWork();

  TestEq(0, Length(SELECT FROM system.flatregistry WHERE name LIKE "<anonymous>*"));
  DoKeyTests("<anonymous>","<anonymous>");
  TestEq(0, Length(SELECT FROM system.flatregistry WHERE name LIKE "<anonymous>*"));

  testfw->RollbackWork();
}

MACRO TestModuleDefs()
{
  testfw->BeginWork();
  WriteRegistryKey("webhare_testsuite.registrytests.removekey", 42, [ initialcreate := TRUE ]);
  WriteRegistryKey("webhare_testsuite.registrytests.removenode.subkey", 43, [ initialcreate := TRUE ]);
  testfw->CommitWork();

  TestEq(42, ReadRegistryKey("webhare_testsuite.registrytests.removekey"));
  TestEq(43, ReadRegistryKey("webhare_testsuite.registrytests.removenode.subkey"));

  TestEq(RECORD[], InitModuleRegistryKeys([ "webhare_testsuite" ]).commitmessages);

  TestEq(0, ReadRegistryKey("webhare_testsuite.registrytests.removekey", [fallback := 0 ]));
  TestEq(0, ReadRegistryKey("webhare_testsuite.registrytests.removenode.subkey", [ fallback := 0 ]));
}

RunTestFramework( [ PTR TestRegistry
                  , PTR TestAnonymousRegistry
                  , PTR TestModuleDefs
                  ] );
