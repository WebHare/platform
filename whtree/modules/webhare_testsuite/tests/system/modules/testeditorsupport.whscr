<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/remoting/client.whlib";

RECORD FUNCTION AuthHandler(STRING realm, STRING url)
{
  dumpvalue(cell[realm,url]);
  RETURN
      [ username := testfw->GetUserLogin("sysop")
      , password := testfw->GetUserPassword("sysop")
      ];
}

INTEGER FUNCTION SearchSourceElementLine(STRING resource, STRING ARRAY searchparts)
{
  STRING data := BlobToString(GetWebhareResource(resource));
  INTEGER pos := 0;
  FOREVERY (STRING part FROM searchparts)
  {
    pos := SearchSubString(data, part, pos);
    IF (pos = -1)
      THROW NEW Exception(`Could not find part '${part} in file ${resource}`);
    pos := pos + LENGTH(part);
  }
  RETURN LENGTH(Tokenize(Left(data, pos), "\n"));
}

MACRO TestAdminService()
{
  RECORD port := testfw->GetLocalhostWebinterface(); //to ensure our wrdschema applies for webhare account lookups
  STRING adminservice := ResolveToAbsoluteURL(port.baseurl, "/wh_services/system/admin");

  testfw->browser->onauth := PTR AuthHandler;

  RECORD searchres := InvokeRemoteFunctionWithBrowser(testfw->browser, `${adminservice}/GetWebdavOpenInfo`, 'wh::system.whlib', DEFAULT RECORD);
  //FIXME should be able to get 'local' paths
  TestEq("/system/modules/system/whlibs/system.whlib", searchres.item);
  TestEq(testfw->GetUserLogin("sysop"), searchres.login);
  TestEqLike("????-????-????-????", searchres.password);

  searchres := InvokeRemoteFunctionWithBrowser(testfw->browser, `${adminservice}/GetWebdavOpenInfo`, 'mod::system/screens/userrights/userrights.xml#main',
                                               [ componentpath := [ "todd0000000001:xxunknownelement", "todd0000000001:unitcontents!userandrolelist"] ]);
  //dumpvalue(searchres);
  TestEq('/system/modules/system/screens/userrights/parts/unitcontents.xml', searchres.item); // should give the <list>
  Testeq(SearchSourceElementLine("mod::system/screens/userrights/parts/unitcontents.xml", [ '<list name="userandrolelist"', '>']), searchres.data.line);

  searchres := InvokeRemoteFunctionWithBrowser(testfw->browser, `${adminservice}/GetWebdavOpenInfo`, 'mod::system/screens/userrights/userrights.xml#main',
                                               [ componentpath := [ "todd0000000001:xxunknownelement" ] ]);
  //dumpvalue(searchres);
  TestEq('/system/modules/system/screens/userrights/userrights.xml', searchres.item); //should give us the <screen>
  Testeq(SearchSourceElementLine("mod::system/screens/userrights/userrights.xml", [ '<screen name="main"', '>']), searchres.data.line);
}

ASYNC MACRO TestNoticeLog()
{
  DATETIME starttest := GetCurrentDatetime();
  TRY
  {
    THROW NEW Exception("test-notice-log-hs");
  }
  CATCH(OBJECT e)
  {
    LogHareScriptException(e, [ info := [ context := "webhare_testsuite:testeditorsupport" ]]);
  }

  CallJS(Resolve("testeditorsupport_helper.ts#testLogError"));

  RECORD ARRAY errors := ReadJSONLogLines("system:notice", starttest);
  errors := SELECT * FROM errors WHERE type='script-error' AND CellExists(errors,'info') AND info.context='webhare_testsuite:testeditorsupport';

  TestEQMembers(
      [ [ message := "Exception: test-notice-log-hs"
        , errors := [ [ message := "Exception: test-notice-log-hs" ] ]
        ]
      , [ message := "test-notice-log-js"
        , errors := [ [ message := "test-notice-log-js" ] ]
        , causes := [ [ message := "cause1"
                      ]
                    , [ message := "cause2"
                      ]
                    ]
        ]
      ], errors, "*");
}

RunTestFramework([ PTR TestAdminService
                 , PTR TestNoticeLog
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "homer" ]
                                   ]
                    ]);
