<?wh

LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/moduleimexport.whlib";

MACRO TestImExport()
{
  testfw->SetupTestModule();

  STRING moduleroot := GetModuleInstallationRoot("webhare_testsuite_temp");
  CreateDiskDirectoryRecursive(MergePath(moduleroot, "lib"), TRUE);

  StoreDiskFile(MergePath(moduleroot, "lib/test.whlib"), StringToBlob(
`<?wh

LOADLIB "wh::devsupport.whlib";

PUBLIC RECORD FUNCTION GetScriptInfo()
{
  RETURN [ libs := GetHarescriptLoadedLibraries() ];
}
`));

  StoreDiskFile(MergePath(moduleroot, "lib/test2.whlib"), StringToBlob(
`<?wh

LOADLIB "mod::webhare_testsuite_temp/lib/test.whlib";

PUBLIC MACRO GetScriptInfo2()
{
}

PUBLIC OBJECTTYPE TestObject2
<
>;
`));


  // Download the archive
  OBJECT archiver := NEW ModuleArchiver;
  archiver->SetUserData("loginname", "realname");
  RECORD moduledata := archiver->CreateArchive("webhare_testsuite_temp");

  // ADDME: test archive contents

  // Delete the module
  DeleteModule("webhare_testsuite_temp");

  // Import as new module
  RECORD feedback := ImportModule(moduledata.data);
  // ADDME: test result

  // Should be available
  FUNCTION PTR test := MakeFunctionPtr("mod::webhare_testsuite_temp/lib/test.whlib", "GetScriptInfo");

  // Defeat the file up-to-date cache
  Sleep(1000);

  // Import again (overwriting existing module)
  RECORD feedback2 := ImportModule(moduledata.data);

  // Should have out of date message
  RECORD libinfo := test();

  // Should have at least one out of date library
  TestEQ(TRUE, SELECT AS BOOLEAN outofdate FROM libinfo.libs WHERE outofdate);

  TRY
  {
    MakeFunctionPtr("mod::webhare_testsuite_temp/lib/test2.whlib", "GetScriptInfo2");
    TestEQ(TRUE, FALSE, "MakeFunctionPtr should have thrown");
  }
  CATCH (OBJECT e)
  {
    TestEQ(TRUE, e EXTENDSFROM LibraryVersionConflictException);
  }

  TRY
  {
    MakeObject("mod::webhare_testsuite_temp/lib/test2.whlib", "TestObject2");
    TestEQ(TRUE, FALSE, "MakeObject should have thrown");
  }
  CATCH (OBJECT e)
  {
    TestEQ(TRUE, e EXTENDSFROM LibraryVersionConflictException);
  }
}

RunTestFramework([ PTR TestImExport
                 ]);
