<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/moduleimexport.whlib";
LOADLIB "mod::system/lib/internal/userrights/resync.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO Prepare()
{
  testfw->BeginWork();
  testfw->userapi->ResyncToSystemTables(); //prevent spurious errors from earlier tests
  testfw->CommitWork();
}

MACRO AddRight(RECORD modoptions)
{
  STRING moduledefpath := modoptions.moduleroot || "moduledefinition.xml";
  OBJECT moduledef := MakeXMLDocument(GetDiskResource(moduledefpath));
  OBJECT rightsnode := moduledef->CreateElementNS("http://www.webhare.net/xmlns/system/moduledefinition", "rights");
  moduledef->documentelement->AppendChild(rightsnode);

  OBJECT right := moduledef->CreateElementNS("http://www.webhare.net/xmlns/system/moduledefinition", "right");
  right->SetAttribute("name","danglingright");
  rightsnode->AppendChild(right);

  OBJECT impliedby := moduledef->CreateElementNS("http://www.webhare.net/xmlns/system/moduledefinition", "impliedby");
  impliedby->SetAttribute("right","system:sysop");
  right->AppendChild(impliedby);

  StoreDiskFile(moduledefpath, moduledef->GetDocumentBlob(FALSE), [ overwrite := TRUE ]);
}

MACRO CreateProblemRight()
{
  testfw->SetupTestModule( [ aftermodulecreation := PTR AddRight ]);

  testfw->BeginWork();
  testfw->GetUserObject("sysop")->GrantRightTo("webhare_testsuite_temp:danglingright", testfw->GetUserObject("lisa"), TRUE);
  testfw->CommitWork();
}

ASYNC MACRO TestProblemRights()
{
  DeleteModule(testfw_testmodule);

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  //Try to grant the user a right
  TT("unittree->unitlist")->selection := SELECT * FROM TT("unittree->unitlist")->rows WHERE title="webhare_testsuite.unit";
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE name="lisa@beta.webhare.net";
  TestEq(TRUE, REcordExists(TT("unitcontents->userandrolelist")->selection), 'user must be selected');
  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->addrighttousers"));
  RECORD problemrights := SELECT * FROM TT("objecttree->objectlist")->rows WHERE TITLE = "Obsolete rights";
  TestEq(TRUE, RecordExists(problemrights));

  //Test that this doesn't crash:
  INSERT problemrights.rowkey INTO TT("objecttree->objectlist")->expanded AT END;

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

MACRO TestFixUnconnectedItems()
{
  EnsureUserMgmtStateClean(); //make sure all is okay before we start. also verifies no loose ends left above
  testfw->BeginWork();

  OBJECT deletedunit :=      testfw->GetWRDSchema()->^whuser_unit->CreateEntity([ wrd_title := "Deletable unit" ]);
  OBJECT expiredunit :=      testfw->GetWRDSchema()->^whuser_unit->CreateEntity([ wrd_title := "Expirable unit" ]);

  OBJECT deletedchildunit := testfw->GetWRDSchema()->^whuser_unit->CreateEntity([ wrd_leftentity := deletedunit->id, wrd_title := "Deletable child unit" ]); //note, this unit is okay as units may live in the root!
  OBJECT expiredchildunit := testfw->GetWRDSchema()->^whuser_unit->CreateEntity([ wrd_leftentity := expiredunit->id, wrd_title := "Expirable child unit" ]);

  OBJECT deletedchildrole := testfw->GetWRDSchema()->^whuser_role->CreateEntity([ wrd_leftentity := deletedunit->id, wrd_title := "Deletable child role" ]);
  OBJECT expiredchildrole := testfw->GetWRDSchema()->^whuser_role->CreateEntity([ wrd_leftentity := expiredunit->id, wrd_title := "Expirable child role" ]);

  OBJECT deletedchilduser := testfw->GetWRDSchema()->^wrd_person->CreateEntity([ whuser_unit := deletedunit->id, wrd_contact_email := "deletable@beta.webhare.net" ]);
  OBJECT expiredchilduser := testfw->GetWRDSchema()->^wrd_person->CreateEntity([ whuser_unit := expiredunit->id, wrd_contact_email := "expirable@beta.webhare.net" ]);

  RECORD ARRAY resync := testfw->userapi->ResyncToSystemTables();
  IF(Length(resync) != 8)
  {
    DumpValue(resync,'boxed');
    TestEq(8, Length(resync), "Ensure all newobjects are created like real users/units would");
  }

  deletedunit->DeleteEntity();
  expiredunit->CloseEntity();

  RECORD ARRAY fixes := FixUnconnectedItems(testfw->GetWRDSchema());
  IF(Length(fixes) != 4)
  {
    DumpValue(fixes,'boxed');
    TestEq(4, Length(fixes), "Expecting 5 fixes (2 users + 1 role + 1 unit)");
  }

  INTEGER lostandfound := testfw->GetWRDSchema()->^whuser_unit->Search("WRD_TAG", "WH_LOSTANDFOUND");
  TestEq(FALSE, deletedchildunit->IsLive());
  TestEq(lostandfound, expiredchildunit->GetField("WRD_LEFTENTITY"));
  TestEq(FALSE, deletedchildrole->IsLive());
  TestEq(lostandfound, expiredchildrole->GetField("WRD_LEFTENTITY"));
  TestEq(lostandfound, deletedchilduser->GetField("WHUSER_UNIT"));
  TestEq(lostandfound, expiredchilduser->GetField("WHUSER_UNIT"));

  testfw->CommitWork();

  //FIXME: not checking for loose ends anymore, we _know_ the actions above have generated loose ends (deletions) BUT we need incremental changes to prevent that
}

RunTestFramework([ PTR Prepare
                 , PTR CreateProblemRight
                 , PTR TestProblemRights
                 , PTR TestFixUnconnectedItems
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa"]
                                   ]
                    , testroles := [[ role := "powerplantmanagers" ]]
                    ]
                 );
