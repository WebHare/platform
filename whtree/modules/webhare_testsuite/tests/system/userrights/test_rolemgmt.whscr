<?wh
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/userrights.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

LOADLIB "mod::wrd/lib/internal/auth/exchange.whlib";

ASYNC MACRO TestAddRoleToUsers()
{
  OBJECT unit := testfw->GetUnit();

  OBJECT usr := testfw->GetUserObject("user1");
  TestEQ(TRUE, usr->ExistsInDatabase());

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  TT("unittree->unitlist")->selection := SELECT * FROM TT("unittree->unitlist")->rows WHERE title="webhare_testsuite.unit";
  topscreen->frame->focused := TT("unitcontents->userandrolelist");

  // Grant to a single user (selecting role)
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE name="role1";
  TestEq(INTEGER[], GetTestController()->userapi->GetRoleMembers(testfw->GetRoleObject("role1")->entityid));
  TestEq(INTEGER[], GetTestController()->userapi->GetUserRoles(testfw->GetUserObject("user1")->entityid));

  //Grant a right to the role
  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->addrighttousers"));
  TT("objecttree->objectlist->thelist")->selection := SELECT * FROM TT("objecttree->objectlist->thelist")->rows WHERE title = "Miscellaneous";
  TT("rightslist")->value := "webhare_testsuite:globalright";
  TT("comment")->Value := "I have my reasons";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //caching in userobjects require us to reopen this
  TestEq(FALSE, testfw->userapi->GetUser(testfw->GetUserObject("user1")->entityid)->HasRight("webhare_testsuite:globalright"));

  AWAIT ExpectScreenChange(+1, PTR TTClick("userrolerightslist->adduserstorole"));

  // Verify the rolegrant in the export
  TestEq([[ login := "role1", rightname := "webhare_testsuite:globalright", comment := "I have my reasons" ]],
         SELECT login, rightname,comment FROM ListRightsAndRolesForExport(testfw->userapi) WHERE login="role1");

  // Test search
  TT("selectuserrole->searchfor")->value := "user1";
  TTClick(":Search");
  TestEq(["user1@beta.webhare.net"],SELECT AS STRING ARRAY email FROM TT("selectuserrole->unitcontents->userandrolelist")->rows);
  TT("selectuserrole->unitcontents->userandrolelist")->selection := TT("selectuserrole->unitcontents->userandrolelist")->rows;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq(INTEGER[testfw->GetUserObject("user1")->entityid], GetTestController()->userapi->GetRoleMembers(testfw->GetRoleObject("role1")->entityid));
  TestEq(INTEGER[testfw->GetRoleObject("role1")->entityid], GetTestController()->userapi->GetUserRoles(testfw->GetUserObject("user1")->entityid));
  TestEq(TRUE, testfw->userapi->GetUser(testfw->GetUserObject("user1")->entityid)->HasRight("webhare_testsuite:globalright"));
  TestEQ(TRUE, testfw->GetRoleAuthobjectid("role1") IN GetGrantedAuthObjects("webhare_testsuite:globalright", 0));
  TestEQ(TRUE, testfw->GetUserAuthobjectid("user1") IN GetGrantedAuthObjects("webhare_testsuite:globalright", 0));

  TT("userrolerightslist->rightstabs")->selectedtab := TT("userrolerightslist->userstab");
  TestEq(1, Length(TT("userrolerightslist->userlistbyrole")->rows));
  topscreen->frame->focused := TT("userrolerightslist->userlistbyrole");
  TT("userrolerightslist->userlistbyrole")->selection :=TT("userrolerightslist->userlistbyrole")->rows;
  AWAIT ExpectAndAnswerMessagebox("YES", PTR TTClick("userrolerightslist->removeusersfromrole"));
  TestEq(0, Length(TT("userrolerightslist->userlistbyrole")->rows));

  // Grant to a single user (selecting user)
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE email="user1@beta.webhare.net";
  AWAIT ExpectScreenChange(+1, PTR TTClick("userrolerightslist->addroletousers"));

  TT("selectuserrole->unittree->unitlist")->value := unit->entityid;
  TT("selectuserrole->unitcontents->userandrolelist")->value := testfw->GetRoleObject("role1")->entityid;
  TT(":Comments")->value := "WebHareTestSuite is testing this";

  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq([[ login := "user1@beta.webhare.net", rightname := "", comment := "WebHareTestSuite is testing this", objectpath := ["role1"] ]],
         SELECT login, rightname,comment,objectpath FROM ListRightsAndRolesForExport(testfw->userapi) WHERE type="ROLEGRANT");

  TestEQ(TRUE, testfw->GetUserObject("user1")->HasRole(testfw->GetRoleObject("role1")));
  TestEQ(FALSE, testfw->userapi->GetUser(testfw->GetUserObject("user1")->entityid)->HasRightOn("system:manageroles", testfw->GetRoleObject("role1")->authobjectid));

  topscreen->frame->focused := TT("userrolerightslist->directpermissions");
  TestEq(2, Length(TT("userrolerightslist->directpermissions")->rows));
  TT("userrolerightslist->directpermissions")->selection := SELECT * FROM TT("userrolerightslist->directpermissions")->rows WHERE name="role1";

  AWAIT ExpectScreenChange(+1, PTR TTClick("userrolerightslist->edit"));
  TestEq("WebHareTestSuite is testing this", TT("comment")->value);
  TestEqLike("*sysop@beta.webhare.net*", TT("grantor")->value);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Remove the rolegrant again
  AWAIT ExpectAndAnswerMessagebox("YES", PTR TTClick("userrolerightslist->revokefromusers"));
  TestEq(0, Length(TT("userrolerightslist->directpermissions")->rows));

  // Grant to multiple users
  topscreen->frame->focused := TT("unitcontents->userandrolelist");
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE email IN ["user1@beta.webhare.net","user2@beta.webhare.net"];

  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->addroletousers"));
  TT("selectuserrole->unittree->unitlist")->value := unit->entityid;
  TT("selectuserrole->unitcontents->userandrolelist")->value := testfw->GetRoleObject("role1")->entityid;

  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEQ(TRUE, testfw->GetUserObject("user1")->HasRole(testfw->GetRoleObject("role1")));
  TestEQ(TRUE, testfw->GetUserObject("user2")->HasRole(testfw->GetRoleObject("role1")));
  TestEQ(FALSE, testfw->userapi->GetUser(testfw->GetUserObject("user1")->entityid)->HasRightOn("system:manageroles", testfw->GetRoleObject("role1")->authobjectid));
  TestEQ(FALSE, testfw->GetUserObject("user2")->HasRightOn("system:manageroles", testfw->GetRoleObject("role1")->authobjectid));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestEditDeleteRole()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));

  TT("unittree->unitlist")->selection := SELECT * FROM TT("unittree->unitlist")->rows WHERE title="webhare_testsuite.unit";

  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->newrole"));
  TT(":Name")->value := "role1-new";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  TestEq("role1-new", TT("unitcontents->userandrolelist")->selection[0].name);

  topscreen->frame->focused := TT("unitcontents->userandrolelist");
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE name="role1";

  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->edit"));
  TT(":Name")->value := "role1-renamed";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  TestEq("role1-renamed", TT("unitcontents->userandrolelist")->selection[0].name);

  INTEGER role1_id := GetTestController()->userapi->wrdschema->^whuser_role->Search("WRD_TITLE","role1-renamed");

  AWAIT ExpectAndAnswerMessagebox("YES", PTR TTClick("unitcontents->delete"));
  TestEQ(FALSE, testfw->GetRoleAuthobjectid("role1") IN GetGrantedAuthObjects("webhare_testsuite:globalright", 0));
  TestEQ(FALSE, testfw->GetUserAuthobjectid("user1") IN GetGrantedAuthObjects("webhare_testsuite:globalright", 0));

  TestEq(2, Length(GetTestController()->userapi->GetRoleMembers(role1_id)), "Directly accessing the role and requesting its members is still allowed when deleted (so we can someday build 'undelete' in usermgmt and pre-show the role)");
  TestEq(TRUE, RecordExists(SELECT FROM system_internal.authobjects WHERE id = GetTestController()->userapi->GetRole(role1_id)->authobjectid
                                                                          AND deactivated));

  testfw->BeginWork();
  testfw->GetUserObject("sysop")->GrantRoleTo(testfw->GetRoleObject("role2"), testfw->GetUserObject("user1")); //add an extra role for more useful copy test
  testfw->CommitWork();

  TestEq(1, Length(GetTestController()->userapi->GetRoleMembers(testfw->GetRoleObject("role2")->entityid)), "Check role2 1 member");

  TestEq(FALSE, testfw->userapi->GetUser(testfw->GetUserObject("user1")->entityid)->HasRight("webhare_testsuite:globalright"));

  //Copying a user with a deleted role crashed earlier (because existence wasn't verified). See that it properly works now
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE name="user1@beta.webhare.net";
  AWAIT ExpectScreenChange(+1, PTR TTClick("copypermissions"));
  TT("selectuserrole->unitcontents->userandrolelist")->selection := SELECT * FROM TT("selectuserrole->unitcontents->userandrolelist")->rows WHERE name="user3@beta.webhare.net";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  TestEq(2, Length(GetTestController()->userapi->GetRoleMembers(role1_id)), "Verify user didn't receive the deleted role");
  TestEq(2, Length(GetTestController()->userapi->GetRoleMembers(testfw->GetRoleObject("role2")->entityid)), "User not added to role2");

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

MACRO TestExport()
{
  //Test the export
  RECORD ARRAY userexport := ListRightsAndRolesForExport(testfw->userapi);
  RECORD ARRAY grants := SELECT * FROM userexport WHERE type = "GRANT" ORDER BY comment;

  TestEq(1,Length(grants)); //just one grant. 'role1' should not be shown as having gotten anything granted
  TestEq("Granted by RunTestframework", grants[0].comment);
  TestEq("", grants[0].grantor);
  TestEq(TRUE, grants[0].withgrantoption);

  RECORD ARRAY rolegrants := SELECT * FROM userexport WHERE type = "ROLEGRANT";
  TestEq(2,Length(rolegrants)); //just 2 rolegrants, those that match 'role2'. role1-renamed is deleted and should be invisible
  TestEq(TRUE, RecordExists(rolegrants[0].grantordata));
  TestEq("sysop@beta.webhare.net", rolegrants[0].grantor);
  TestEq(FALSE, rolegrants[0].withgrantoption);
  TestEq(["role2"], rolegrants[0].objectpath);

  TestEq(["role2"], rolegrants[1].objectpath);
}

RunTestframework( [ PTR TestAddRoleToUsers
                  , PTR TestEditDeleteRole
                  , PTR EnsureUserMgmtStateClean
                  , PTR TestExport
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ,[ login := "user1"]
                                    ,[ login := "user2"]
                                    ,[ login := "user3"]
                                    ]
                     , testroles := [[ role := "role1" ]
                                    ,[ role := "role2" ]
                                    ]
                     ]
                  );
