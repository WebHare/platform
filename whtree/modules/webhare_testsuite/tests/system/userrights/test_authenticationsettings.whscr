<?wh

LOADLIB "wh::util/otp.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

ASYNC MACRO Setup()
{
  GetTestController()->userapi->CreateUnit([wrd_title := "Lisa's unit"]);
}

ASYNC MACRO ManageAuthenticationSettings()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD, message := DEFAULT RECORD ]));

  TTFillByTitle("unittree->unitlist", "Lisa's unit");

  AWAIT ExpectScreenChange(+1, PTR TTClick("unitcontents->newuser"));
  TT("username")->value := "homer@allow2fa.test.webhare.net";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  topscreen->frame->focused := TT("unitcontents->userandrolelist");
  TTFillByTitle("unitcontents->userandrolelist", "homer@allow2fa.test.webhare.net");

  AWAIT ExpectScreenChange(+1, PTR TTClick("edit"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->btn_managesecondfactor"));
  TestEQ("Not configured", TT(":One-time access code")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  // setup TOTP for homer
  testfw->BeginWork();
  STRING newtotpsecret := GenerateOTPSecret();
  STRING totpurl := GetTOTPUrl(newtotpsecret, "homer@allow2fa.test.webhare.net", "Webhare testsuite");

  INTEGER homer := testfw->GetWRDschema()->^wrd_person->Search("wrd_contact_email", "homer@allow2fa.test.webhare.net");
  testfw->GetWRDschema()->^wrd_person->UpdateEntity(homer,
      [ whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      DEFAULT DATETIME
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            , totp :=         [ url :=          totpurl
                              , backupcodes :=  [ [ code := "" ] ]
                              ]
            ]
      ]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTClick("edit"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->btn_managesecondfactor"));
  TestEQ("Configured", TT(":One-time access code")->value);

  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick(":Disable two-factor authentication"));

  TestAuditEvent(
      [ entity :=           homer
      , login :=            "homer@allow2fa.test.webhare.net"
      , byentity :=         testfw->GetUserObject("sysop")->entityid
      , bylogin :=          "sysop@beta.webhare.net"
      , browsertriplet :=   ""
      , ip :=               ""
      , type :=             "wrd:authsettings.disabletotp"
      //HS always sets impersonator to the editing user even when he's not using login-as
      , impersonator_login  := "sysop@beta.webhare.net"
      , impersonator_entity := testfw->GetUserObject("sysop")->entityid
      , data :=             DEFAULT RECORD
      ]);

  TestEQ("Not configured", TT(":One-time access code")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));

  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->btn_changepassword"));
  TestEq(TRUE, TT("newpassword", [ findinvisible := TRUE ])->visible);
  TT("newpassword")->value := "a";
  TT("confirmpassword")->value := "a";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestAuditEvent(
      [ entity :=           homer
      , login :=            "homer@allow2fa.test.webhare.net"
      , byentity :=         testfw->GetUserObject("sysop")->entityid
      , bylogin :=          "sysop@beta.webhare.net"
      , browsertriplet :=   ""
      , ip :=               ""
      , type :=             "wrd:authsettings.setpassword"
      , impersonator_login  := "sysop@beta.webhare.net"
      , impersonator_entity := testfw->GetUserObject("sysop")->entityid
      , data :=             DEFAULT RECORD
      ]);

  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //STORY: Try to edit self, shouldn't see mangagement mode but normal password flow
  TTFillByTitle("unittree->unitlist", "webhare_testsuite.unit");
  TTFillByTitle("unitcontents->userandrolelist", "sysop@beta.webhare.net");

  AWAIT ExpectScreenChange(+1, PTR TTClick("edit"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->btn_changepassword"));
  TestEq(TRUE, TT("enterpasswordforauthorization")->visible);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestAuditLog()
{
  // STORY: ensure the audit log of lisa has an event
  testfw->SetTestUser("lisa");

  // Change lisa's password
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings"));
  AWAIT ExpectScreenChange(+1, PTR TTClick(":Change[1/2]"));
  TTFillByTitle(":Password", testfw->GetUserPassword("lisa"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  // grant necessary rights to manage lisa's unit to supervisor and unitmanager
  testfw->BeginWork();
  testfw->GetUserObject("sysop")->UpdateGrant("grant", "system:manageunit", 0, testfw->GetUserObject("supervisor"), [ withgrantoption := TRUE ]);
  testfw->GetUserObject("sysop")->UpdateGrant("grant", "system:manageunit", 0, testfw->GetUserObject("unitmanager"), [ withgrantoption := TRUE ]);
  testfw->CommitWork();

  // STORY: non-supervisor can see audit log
  testfw->SetTestUser("supervisor");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  {
    TTFillByTitle("unittree->unitlist", "webhare_testsuite.unit");
    TTFillByTitle("unitcontents->userandrolelist", "lisa@beta.webhare.net");
    TT("frame")->focused := TT("unitcontents->userandrolelist");

    // global show audit log
    AWAIT ExpectScreenChange(+1, PTR TTClick(":Show audit log[1/2]", [ menuitem := TRUE ]));
    {
      TestEQMembers(
          [ [ bylogin :=              "sysop@beta.webhare.net"
            , wrdaudit_type :=        "Disabled two factor authentication"
            ]
          , [ bylogin :=              "sysop@beta.webhare.net"
            , wrdaudit_type :=        "Set password"
            ]
          , [ byentity :=             testfw->GetUserWRDId("lisa")
            , bylogin :=              "lisa@beta.webhare.net"
            , user_account :=         ""
            , impersonator_account := ""
            , wrdaudit_type :=        "Set password"
            ]
          ], (SELECT * FROM TT("events")->rows ORDER BY wrd_creationdate), "*");

      AWAIT ExpectScreenChange(-1, PTR TTEscape);
    }

    // per-user show audit log
    AWAIT ExpectScreenChange(+1, PTR TTClick(":Show audit log[2/2]", [ menuitem := TRUE ]));
    {
      TestEQMembers(
          [ [ byentity :=             testfw->GetUserWRDId("lisa")
            , bylogin :=              "lisa@beta.webhare.net"
            , user_account :=         ""
            , impersonator_account := ""
            , wrdaudit_type :=        "Set password"
            ]
          ], TT("events")->rows, "*");

      AWAIT ExpectScreenChange(-1, PTR TTEscape);
    }
    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }

  // STORY: non-supervisor can't see audit log
  testfw->SetTestUser("unitmanager");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  {
    TTFillByTitle("unittree->unitlist", "webhare_testsuite.unit");
    TTFillByTitle("unitcontents->userandrolelist", "lisa@beta.webhare.net");
    TT("frame")->focused := TT("unitcontents->userandrolelist");

    TestEQ(FALSE, TTClick(":Show audit log[1/2]", [ allowfailure := TRUE, menuitem := TRUE ]));
    TestEQ(FALSE, TTClick(":Show audit log[2/2]", [ allowfailure := TRUE, menuitem := TRUE ]));
    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }
}



RunTestframework( [ PTR Setup
                  , PTR ManageAuthenticationSettings
                  , PTR TestAuditLog
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ,[ login := "supervisor", grantrights := ["system:supervisor"] ]
                                    ,[ login := "unitmanager" ]
                                    ,[ login := "lisa"]
                                    ]
                     ]
                  );
