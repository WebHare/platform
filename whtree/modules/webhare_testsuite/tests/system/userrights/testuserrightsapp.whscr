<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ooxml/spreadsheet.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/otp.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::wrd/lib/internal/dbschema.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


DATETIME lastcheck;

ASYNC MACRO Setup()
{
  INTEGER lisaunit := GetTestController()->userapi->CreateUnit([wrd_title := "Lisa's unit"]);

  testfw->SetTestUser("lisa");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));
  TestEqLike("Rights problem", topscreen->title);

  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  testfw->BeginWork();
  testfw->GetUserObject("sysop")->GrantRightToOn("system:manageunit", testfw->GetUserObject("lisa"), GetTestController()->userapi->GetUnit(lisaunit)->authobjectid, TRUE);
  testfw->CommitWork();
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD, message := DEFAULT RECORD ]));

  //Create a unit
  TT("unittree->unitlist")->selection := SELECT * FROM TT("unittree->unitlist")->rows WHERE title="Lisa's unit";
  AWAIT ExpectScreenChange(+1, PTR TTClick("unittree->newunit"));
  TT("wrd_title")->value := "Lisa's new unit";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  //Select the new unit
  TestEq("Lisa's new unit", TT("unittree->unitlist")->selection.title);

  //Verify edit works
  AWAIT ExpectScreenChange(+1, PTR TT("unittree->unitlist")->openaction->TolliumClick());
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));


  //Add a user
  AWAIT ExpectScreenChange(+1, PTR TT("unitcontents->newuser")->TolliumClick);

  TT("username")->value := "bart@example.net";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  //Select the new user
  TestEq(TRUE, RecordExists(SELECT FROM TT("unitcontents->userandrolelist")->rows WHERE email="bart@example.net"));
  TT("unitcontents->userandrolelist")->selection := SELECT * FROM TT("unitcontents->userandrolelist")->rows WHERE email="bart@example.net";
  AWAIT ExpectScreenChange(+1, PTR topscreen->unitcontents->move->TolliumClick);

  TT("unittree->unitlist")->selection := SELECT * FROM TT("unittree->unitlist")->rows WHERE title="Lisa's unit";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  OBJECT bartuser := GetTestController()->userapi->GetTolliumUserByLogin("bart@example.net");
  TestEq(TRUE, ObjectExists(bartuser));

  OBJECT lisaunitobject := GetTestController()->userapi->GetUnit(lisaunit);
  //test that authobjects was updated properly
  TestEq(lisaunitobject->authobjectid, SELECT AS INTEGER parent FROM system.authobjects WHERE id=bartuser->authobjectid);

  //Add a user with the same name. Should give an error, not crash
  AWAIT ExpectScreenChange(+1, PTR TT("unitcontents->newuser")->TolliumClick);
  TT("username")->value := "bart@example.net";
  AWAIT ExpectAndAnswerMessageBox("OK", PTR TTClick(":OK"), [ messagemask := "*not unique*" ]);
  TT("username")->value := "bart2@example.net";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //Get its ID for later use
  INTEGER bart2id := testfw->GetWRDschema()->^wrd_person->Search("WRD_CONTACT_EMAIL", "bart2@example.net");
  TestEq(TRUE, bart2id != 0);

  //Delete the user
  topscreen->frame->focused := TT("unitcontents->userandrolelist");
  AWAIT ExpectAndAnswerMessageBox("Yes", PTR TTClick("delete"));
  TestEq(TRUE, testfw->GetWRDschema()->^wrd_person->GetEntityField(bart2id,"WRD_LIMITDATE") < GetCurrentDatetime());

  //Delete his unit
  topscreen->frame->focused := TT("unittree->unitlist");
  AWAIT ExpectAndAnswerMessageBox("Yes", PTR TTClick("delete"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO ManageAuthenticationSettings()
{
  STRING ARRAY allowsecondfactormasks := Tokenize(ReadRegistryKey("system.userrights.allowsecondfactorloginmasks"), " ");
  IF ("*@allow2fa.test.webhare.net" NOT IN allowsecondfactormasks)
  {
    allowsecondfactormasks := STRING[ ...ArrayDelete(allowsecondfactormasks, [ "" ]), "*@allow2fa.test.webhare.net" ];
    GetPrimary()->BeginWork();
    WriteRegistryKey("system.userrights.allowsecondfactorloginmasks", Detokenize(allowsecondfactormasks, " "));
    GetPrimary()->CommitWork();
  }

  testfw->SetTestUser("sysop");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD, message := DEFAULT RECORD ]));

  TTFillByTitle("unittree->unitlist", "Lisa's unit");

  AWAIT ExpectScreenChange(+1, PTR TT("unitcontents->newuser")->TolliumClick);
  TT("username")->value := "homer@allow2fa.test.webhare.net";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  topscreen->frame->focused := TT("unitcontents->userandrolelist");
  TTFillByTitle("unitcontents->userandrolelist", "homer@allow2fa.test.webhare.net");

  AWAIT ExpectScreenChange(+1, PTR TTClick("edit"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->:Change"));
  TestEQ("Not configured", TT(":One-time access code")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  // setup TOTP for homer
  testfw->BeginWork();
  STRING newtotpsecret := GenerateOTPSecret();
  STRING totpurl := GetTOTPUrl(newtotpsecret, "homer@allow2fa.test.webhare.net", "Webhare testsuite");

  INTEGER homer := testfw->GetWRDschema()->^wrd_person->Search("wrd_contact_email", "homer@allow2fa.test.webhare.net");
  testfw->GetWRDschema()->^wrd_person->UpdateEntity(homer,
      [ whuser_password :=
            [ version :=      1
            , passwords :=    [ [ validfrom :=      DEFAULT DATETIME
                                , passwordhash :=   testfw->hashedpasswords.secret
                                ]
                              ]
            , totp :=         [ url :=          totpurl
                              , backupcodes :=  [ [ code := "" ] ]
                              ]
            ]
      ]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTClick("edit"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("authenticationsettings->:Change"));
  TestEQ("Configured", TT(":One-time access code")->value);

  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick(":Disable two-factor authentication"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":Password", testfw->GetUserPassword("sysop"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestAuditEvent(
      [ entity :=           homer
      , login :=            "homer@allow2fa.test.webhare.net"
      , byentity :=         testfw->GetUserObject("sysop")->entityid
      , bylogin :=          "sysop@beta.webhare.net"
      , browsertriplet :=   ""
      , ip :=               ""
      , type :=             "wrd:authsettings.disabletotp"
      , impersonated :=     FALSE
      , data :=             ""
      ]);


  TestEQ("Not configured", TT(":One-time access code")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Close"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestExport()
{
  testfw->BeginWork();
  testfw->SetupTestWebsite(DEFAULT BLOB);

  OBJECT subfolder1 := testfw->GetTestSite()->rootobject->CreateFolder([ name := "visiblefolder" ]);
  OBJECT subfolder2 := testfw->GetTestSite()->rootobject->CreateFolder([ name := "invisiblefolder" ]);

  OBJECT visibleunit := testfw->userapi->GetUnit(testfw->userapi->CreateUnit([ wrd_title := "visibleunit" ]));
  OBJECT invisibleunit := testfw->userapi->GetUnit(testfw->userapi->CreateUnit([ wrd_title := "invisibleunit" ]));

  OBJECT testuser := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := visibleunit->entityid, wrd_contact_email := "testuser@beta.webhare.net", wrd_lastname := "testuser" ]));
  OBJECT visibleuser1 := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := visibleunit->entityid, wrd_contact_email := "visibleuser1@beta.webhare.net", wrd_lastname := "visibleuser1" ]));
  OBJECT visibleuser2 := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := visibleunit->entityid, wrd_contact_email := "visibleuser2@beta.webhare.net", wrd_lastname := "visibleuser2" ]));
  OBJECT visibleuser3 := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := visibleunit->entityid, wrd_contact_email := "visibleuser3@beta.webhare.net", wrd_lastname := "visibleuser3" ]));
  OBJECT invisibleuser1 := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := invisibleunit->entityid, wrd_contact_email := "invisibleuser1@beta.webhare.net", wrd_lastname := "invisibleuser1" ]));
  OBJECT invisibleuser2 := testfw->userapi->GetUser(testfw->userapi->CreateUser([ whuser_unit := invisibleunit->entityid, wrd_contact_email := "invisibleuser2@beta.webhare.net", wrd_lastname := "invisibleuser2" ]));
  OBJECT visiblerole := testfw->userapi->GetRole(testfw->userapi->CreateRole([ wrd_leftentity := visibleunit->entityid, wrd_title := "visiblerole" ]));
  OBJECT invisiblerole := testfw->userapi->GetRole(testfw->userapi->CreateRole([ wrd_leftentity := invisibleunit->entityid, wrd_title := "invisiblerole" ]));

  testfw->GetUserObject("sysop")->GrantRightToOn("system:manageunit", testuser, visibleunit->authobjectid, TRUE);
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", testuser, subfolder1->id, FALSE);

  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", visibleuser1, subfolder1->id, FALSE); // visible right, visible user, visible object
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", visibleuser1, subfolder2->id, FALSE); // visible right, visible user, invisible object
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_fullaccess", visibleuser1, subfolder1->id, FALSE); // invisible right, visible user, visible object
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", invisibleuser1, subfolder1->id, FALSE); // visible right, invisible user, visible object
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", visiblerole, subfolder1->id, FALSE); // visible right, visible user, visible object, via visible role
  testfw->GetUserObject("sysop")->GrantRoleTo(visiblerole, visibleuser2);
  testfw->GetUserObject("sysop")->GrantRightToOn("system:fs_browse", invisiblerole, subfolder1->id, FALSE); // visible right, visible user, visible object, via invisible role
  testfw->GetUserObject("sysop")->GrantRoleTo(invisiblerole, visibleuser3);

  testfw->CommitWork();

  __SetEffectiveUser(testuser);
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));

  // Go to the 'Module and object rights' tab
  TTFillByTitle("viewmode", "Module and object rights");

  // Find the 'visiblefolder' object
  TTFillByTitle("objecttree->objectlist->thelist", "visiblefolder", [ expandtreelevels := 3 ]);

  // Export the object rights on this folder
  AWAIT ExpectScreenChange(+1, PTR TTClick("objecttree->exportobjectrights"));
  RECORD rec := AWAIT ExpectSentWebFile(PTR TTClick("download"));
  AWAIT ExpectScreenChange(-1, DEFAULT FUNCTION PTR);

  RECORD ARRAY rows := GetOOXMLSpreadsheetRows(rec.file.data);
  TestEQ(
      [ [ path   :=       "/visiblefolder"
        , username :=     "visibleunit/testuser@beta.webhare.net"
        , "full name" :=  "testuser"
        , right :=        "Browse"
        , "via role" :=   ""
        ]
      , [ path   :=       "/visiblefolder"
        , username :=     "visibleunit/visibleuser1@beta.webhare.net"
        , "full name" :=  "visibleuser1"
        , right :=        "Browse"
        , "via role" :=   ""
        ]
      , [ path   :=       "/visiblefolder"
        , username :=     "visibleunit/visibleuser2@beta.webhare.net"
        , "full name" :=  "visibleuser2"
        , right :=        "Browse"
        , "via role" :=   "visibleunit/visiblerole"
        ]
      ], SELECT * FROM rows ORDER BY username);

  //Delete 'visiblerole'. This should eliminate it from the exportobjectrights
  testfw->BeginWork();
  testfw->userapi->wrdschema->^whuser_role->CloseEntity(visiblerole->entityid);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTClick("objecttree->exportobjectrights"));
  rec := AWAIT ExpectSentWebFile(PTR TTClick("download"));
  AWAIT ExpectScreenChange(-1, DEFAULT FUNCTION PTR);

  rows := GetOOXMLSpreadsheetRows(rec.file.data);
  TestEQ(
      [ [ path   :=       "/visiblefolder"
        , username :=     "visibleunit/testuser@beta.webhare.net"
        , "full name" :=  "testuser"
        , right :=        "Browse"
        , "via role" :=   ""
        ]
      , [ path   :=       "/visiblefolder"
        , username :=     "visibleunit/visibleuser1@beta.webhare.net"
        , "full name" :=  "visibleuser1"
        , right :=        "Browse"
        , "via role" :=   ""
        ]
      ], SELECT * FROM rows ORDER BY username);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Undelete 'visiblerole' for the next test to work
  testfw->BeginWork();
  testfw->userapi->wrdschema->^whuser_role->UpdateEntity(visiblerole->entityid, [ wrd_limitdate := MAX_DATETIME ]);
  testfw->CommitWork();
}

ASYNC MACRO TestExportSysop()
{
  testfw->SetTestUser("sysop");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("exportallgrants"));
  RECORD rec := AWAIT ExpectSentWebFile(PTR TTClick("download"));
  AWAIT ExpectScreenChange(-1, DEFAULT FUNCTION PTR);

  RECORD ARRAY rows := GetOOXMLSpreadsheetRows(rec.file.data);
  //Test that ROLEGRANTs exist, they were a recent addition
  TestEq(TRUE, RecordExists(SELECT FROM rows WHERE type="ROLEGRANT" AND username="visibleuser2@beta.webhare.net" AND rows."object path" = "visiblerole"));
  //Test that we see the ROLE's grant
  TestEq(TRUE, RecordExists(SELECT FROM rows WHERE type="GRANT" AND username="visiblerole" AND rows."object path" = "webhare_testsuite.testfolder; webhare_testsuite.site; visiblefolder"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestAuditLog()
{
  // STORY: ensure the audit log of lisa has an event
  testfw->SetTestUser("lisa");

  STRING ARRAY allowsecondfactormasks := Tokenize(ReadRegistryKey("system.userrights.allowsecondfactorloginmasks"), " ");
  BOOLEAN lisawith2fa := __MatchesAny("lisa@beta.webhare.net", allowsecondfactormasks)

  // Change lisa's password
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:personalsettings"));
  AWAIT ExpectScreenChange(+1, lisawith2fa ? PTR TTClick(":Change[1/2]") : PTR TTClick(":Change"));
  TTFillByTitle(":Password", testfw->GetUserPassword("lisa"));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(+1, DEFAULT FUNCTION PTR);
  TTFillByTitle(":New password", "secret2");
  TTFillByTitle(":Confirm new password", "secret2");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  // grant necessary rights to manage lisa's unit to supervisor and unitmanager
  testfw->BeginWork();
  testfw->GetUserObject("sysop")->GrantRightToOn("system:manageunit", testfw->GetUserObject("supervisor"), 0, TRUE);
  testfw->GetUserObject("sysop")->GrantRightToOn("system:manageunit", testfw->GetUserObject("unitmanager"), 0, TRUE);
  testfw->CommitWork();

  // STORY: non-supervisor can see audit log
  testfw->SetTestUser("supervisor");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  {

    TTFillByTitle("unittree->unitlist", "webhare_testsuite.unit");
    TTFillByTitle("unitcontents->userandrolelist", "lisa@beta.webhare.net");
    TT("frame")->focused := TT("unitcontents->userandrolelist");

    AWAIT ExpectScreenChange(+1, PTR TTClick(":Show audit log", [ menuitem := TRUE ]));
    {
      TestEQMembers(
          [ [ byentity :=             testfw->GetUserWRDId("lisa")
            , bylogin :=              "lisa@beta.webhare.net"
            , user_account :=         ""
            , impersonator_account := ""
            , wrdaudit_type :=        "Set password"
            ]
          ], TT("events")->rows, "*");

      AWAIT ExpectScreenChange(-1, PTR TTEscape);
    }
    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }

  // STORY: non-supervisor can't see audit log
  testfw->SetTestUser("unitmanager");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  {
    TTFillByTitle("unittree->unitlist", "webhare_testsuite.unit");
    TTFillByTitle("unitcontents->userandrolelist", "lisa@beta.webhare.net");
    TT("frame")->focused := TT("unitcontents->userandrolelist");

    TestEQ(FALSE, TTClick(":Show audit log", [ allowfailure := TRUE, menuitem := TRUE ]));
    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }
}


RunTestframework( [ PTR Setup
                  , PTR ManageAuthenticationSettings
                  , PTR TestExport
                  , PTR TestExportSysop
                  , PTR TestAuditLog
                  , PTR EnsureUserMgmtStateClean
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ,[ login := "supervisor", grantrights := ["system:supervisor"] ]
                                    ,[ login := "unitmanager" ]
                                    ,[ login := "lisa"]
                                    ]
                     , testroles := [[ role := "powerplantmanagers" ]]
                     ]
                  );
