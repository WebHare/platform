<?wh

LOADLIB "wh::ipc.whlib";
LOADLIB "wh::promise.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/internal/services.whlib";


ASYNC MACRO WebHareServiceTest()
{
  OBJECT exc := TestThrowsLike("*Service*nosuchservice*unavailable*", PTR WaitForPromise(OpenWebHareService("webhare_testsuite:nosuchservice","x")));
  TestEQ(TRUE, exc EXTENDSFROM ServiceUnavailableException);

  TestThrowsLike("*Invalid parameters*",PTR WaitForPromise(OpenWebHareService("webhare_testsuite:webhareservicetest")));
  TestThrowsLike("abort",PTR WaitForPromise(OpenWebHareService("webhare_testsuite:webhareservicetest", "abort")));

  OBJECT serverinstance := WaitForPromise(OpenWebHareService("webhare_testsuite:webhareservicetest","x"));
  TestEq(42,WaitForPromise(serverinstance->GetLUE()));

  OBJECT promise := serverinstance->GetAsyncLUE();
  TestEq(42,WaitForPromise(serverinstance->GetLUE()));
  TestEq(42, WaitForPromise(promise));

  TestThrowsLike("Crash()", PTR WaitForPromise(serverinstance->Crash()));
  promise := serverinstance->GetAsyncLUE();

  OBJECT promise2 := serverinstance->GetAsyncCrash();
  TestThrowsLike("Async crash()", PTR WaitForPromise(promise2));

  RECORD deferred := CreateDeferredPromise();
  serverinstance->AddListener("testevent", PTR deferred.resolve(#1));
  serverinstance->EmitTestEvent([ value := 42 ]);
  RECORD testdata := AWAIT deferred.promise;
  TestEq([ value := 42 ], testdata);
}

ASYNC MACRO SyncWebHareServiceTest()
{
  OBJECT exc:=TestThrowsLike("*Service*nosuchservice*unavailable*", PTR __OpenSynchronousWebHareService("webhare_testsuite:nosuchservice","x"));
  TestEQ(TRUE, exc EXTENDSFROM ServiceUnavailableException);

  TestThrowsLike("*Invalid parameters*",PTR __OpenSynchronousWebHareService("webhare_testsuite:webhareservicetest"));
  TestThrowsLike("abort",PTR __OpenSynchronousWebHareService("webhare_testsuite:webhareservicetest", "abort"));

  OBJECT serverinstance := __OpenSynchronousWebHareService("webhare_testsuite:webhareservicetest","x");
  TestEq(42, serverinstance->GetLUE());

  TestEq(42, serverinstance->GetLUE());
  TestEq(42, serverinstance->GetAsyncLUE());

  TestThrowsLike("Crash()", PTR (serverinstance->Crash()));

  TestThrowsLike("Async crash()", PTR serverinstance->GetAsyncCrash());

  RECORD deferred := CreateDeferredPromise();
  serverinstance->AddListener("testevent", PTR deferred.resolve(#1));
  serverinstance->EmitTestEvent([ value := 42 ]);
  RECORD testdata := AWAIT deferred.promise;
  TestEq([ value := 42 ], testdata);
}

RunTestFramework([ PTR WebHareServiceTest
                 , PTR SyncWebHareServiceTest
                 ],
                 [ usedatabase := FALSE
                 ]);
