<?wh
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";
LOADLIB "mod::wrd/lib/api.whlib";

LOADLIB "mod::webhare_testsuite/lib/internal/testsite.whlib";

RECORD testsetup;

MACRO PrepFeedbackAppTests()
{
  //There's no way to create feedback *in* the feedback app directly, and there might never be because we want our feedback to have url+screenshot
  testfw->BeginWork();
  testsetup := SetupTestFeedbackTopics();

  OBJECT feedbackschema := OpenWRDSchema("publisher:feedback");
  RECORD feedback1 := StoreFeedback([ browser := testfw_mockbrowser, userdata := testfw->GetUserObject("sysop")->GetUserDataForLogging() ]);
  RECORD topic_testtopic := SELECT * FROM feedback1.topics WHERE title = "Test Topic";

  STRING feedback1id := StoreFeedbackExtraData(feedback1.guid, CELL[ topic := topic_testtopic.guid, remarks := testfw_mockremark ]);
  //dumpvalue(feedback1id,'tree');

  INTEGER feedback1wrdid := feedbackschema->^feedback->Search("FEEDBACKID", feedback1id);
  TestEq(testsetup.testtopic->id, feedbackschema->^feedback->GetEntityField(feedback1wrdid, "topic"));
  TestEq(testsetup.testrole1->id, feedbackschema->^feedback->GetEntityField(feedback1wrdid, "role"));
  TestEq(testsetup.testprio1->id, feedbackschema->^feedback->GetEntityField(feedback1wrdid, "priority"));

  testfw->GetUserObject("sysop")->UpdateGrant("grant", "publisher:managefeedbackrole", testsetup.testrole2->id, testfw->GetUserObject("marge"));

  testfw->CommitWork();
}

ASYNC MACRO TestFeedbackApp()
{
  testfw->SetTestUser("marge");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:feedback"));

  RECORD mytestissue := SELECT * FROM TT("feedback")->rows WHERE remarks = testfw_mockremark;
  TestEq(testsetup.testrole1->GetField("WRD_TITLE"), mytestissue.role);

  TT("feedback")->selection := mytestissue;

  RECORD totestrole2 := SELECT * FROM TT("forwardtomenu")->items WHERE menuitem->title = "Test Role 2";
  TestAssert(RecordExists(totestrole2), "No forwardto: testrole2");
  totestrole2.menuitem->action->TolliumClick();

  mytestissue := SELECT * FROM TT("feedback")->rows WHERE remarks = testfw_mockremark;
  TestEq(testsetup.testrole2->GetField("WRD_TITLE"), mytestissue.role);

  AWAIT ExpectScreenChange(-1, PTR TTEScape);
}

RunTestFramework( [ PTR PrepFeedbackAppTests
                  , PTR TestFeedbackApp
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ,[ login := "marge" ]
                                    ]
                     ]);
