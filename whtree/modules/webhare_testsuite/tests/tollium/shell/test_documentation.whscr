<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/internal/embeddeddocs.whlib";

LOADLIB "mod::publisher/lib/testfw/sites.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


ASYNC MACRO TestDocumentationSite()
{
  //Setup docroot URL. note that there should be some way to ensure/verify that the Help system can prescan/preload URLs as to not break the UI waiting for remote assets
  testfw->BeginWork();

  STRING randomdocroot := GenerateUFS128BitId();
  OBJECT testfolder := GetTestsuiteTempFolder()->CreateFolder([name := "docs." || randomdocroot]);
  OBJECT subpath := testfolder->CreateFolder([name := "docsubpath"]); //from webhare_testsuite moduledefinition.xml
  OBJECT docindex := subpath->CreateFile(
     [ name := "whdocs-v1.json"
     , publish := TRUE
     , data := EncodeJSONBlob([ languages := [[ code := "en"
                                              , topics := [[ topic := "exists", link := "existingdocs" ]]
                                             ]]
                              , editfallback := "docs-missing/"
                              ])
     ]);

  testfw->CommitWork();
  testfw->WaitForPublishCompletion(testfolder->id);

  testfw->BeginWork();
  WriteRegistryKey("webhare_testsuite.tests.docroot", GetTestsuiteTempFolder()->objecturl || "docs." || randomdocroot); //NOTE: no final slash... we expect the docscode to be robust against that
  RefreshEmbeddedDocumentation("webhare_testsuite"); //do this inside the transaction to ensure noone's racing us
  testfw->CommitWork();


  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:testdocs"));
  TestEq(TRUE, TTIsVisible("alwayshelp->helpicon->^helpicon"));
  TestEq("https://www.example.com/", TT("alwayshelp")->GetDocumentationURL());

  TestEq(FALSE, TTIsVisible("neverhelp->helpicon->^helpicon"));
  TestEq("", TT("neverhelp")->GetDocumentationURL());

  TestEq(TRUE, TTIsVisible("relativehelp->helpicon->^helpicon"));
  TestEq("https://www.example.com/docs/testapp/subpage", TT("relativehelp")->GetDocumentationURL());

  GetTestController()->GrabInstructions();

  TT("relativehelp->helpicon->^helpicon")->onclick([nativex := 0, nativey := 0]);

  RECORD openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));
  TestEqLike("https://www.example.com/docs/testapp/subpage?ts=*", openhelp.params[0]);
  TestEq("", openhelp.params[1]);

  // directhelp is enabled and visible by default
  TT("directhelp->^helpicon")->onclick([nativex := 0, nativey := 0]);
  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));

  // Disable it, it should no longer have an active onclick handler
  TT("directhelp_enabled")->value := FALSE;
  TestEq(DEFAULT MACRO PTR, TT("directhelp->^helpicon")->onclick);

  // Enable it again
  TT("directhelp_enabled")->value := TRUE;
  TT("directhelp->^helpicon")->onclick([nativex := 0, nativey := 0]);
  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));

  // Hide it
  TT("directhelp_visible")->value := FALSE;
  TestEq(FALSE, TTIsVisible("directhelp"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  testfw->GetUserObject("sysop")->SetRegistryKey("system.prefs.showdeveloperapps", FALSE);

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:testdocs", [ params := [ "" ]]));
  TestEq(TRUE, TTIsVisible("alwayshelp->helpicon->^helpicon"));
  TestEq(FALSE, TTIsVisible("relativehelp->helpicon->^helpicon"));
  TestEq(TRUE, TTIsVisible("remotehelp_exists->helpicon->^helpicon"));
  TestEq(1f, TT("remotehelp_exists->helpicon->^helpicon")->opacity);
  TestEq(FALSE, TTIsVisible("remotehelp_doesnotexist->helpicon->^helpicon"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  testfw->GetUserObject("sysop")->SetRegistryKey("system.prefs.showdeveloperapps", TRUE);

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:testdocs", [ params := [ "" ]]));
  TestEq(TRUE, TTIsVisible("alwayshelp->helpicon->^helpicon"));
  TestEq(FALSE, TTIsVisible("relativehelp->helpicon->^helpicon"));
  TestEq(TRUE, TTIsVisible("remotehelp_exists->helpicon->^helpicon"));
  TestEq(TRUE, TTIsVisible("remotehelp_doesnotexist->helpicon->^helpicon")); //should now offer editing
  TestEq(`${subpath->objecturl}existingdocs`, TT("remotehelp_exists")->GetDocumentationURL());;
  TestEqLike(`${subpath->objecturl}docs-missing?*`, TT("remotehelp_doesnotexist")->GetDocumentationURL());
  TestEq("doesnotexist", GetVariableFromURL(TT("remotehelp_doesnotexist")->GetDocumentationURL(), "topic"));
  TestEq(0.5f, TT("remotehelp_doesnotexist->helpicon->^helpicon")->opacity);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Let's build a documentation testsite
  OBJECT testsite := SetupTestWebsite("tollium:webinterface");

  testfw->BeginWork();
  OBJECT docfolder := testsite->rootobject->CreateFolder([name := "doc", typens := "http://www.webhare.net/xmlns/tollium/manual"]);
  OBJECT firstdoc := docfolder->EnsureFile([name := "testfile", typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile", publish := TRUE]);
  testfw->CommitWork();

  //STORY: Open app in readonly mode. 'relativehelp' points to a nonexisting page and shouldn't initially appear
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:testdocs", [ params := [ `site::${testsite->name}/doc` ]]));
  TestEq("", TT("relativehelp")->GetDocumentationURL());
  TestEq(FALSE, TTIsVisible("relativehelp->helpicon->^helpicon")); //as this doc is not in place

  TT("relativehelp")->doclink := "testfile";
  TestEq(testsite->webroot || "doc/testfile/" , TT("relativehelp")->GetDocumentationURL());
  TestEq(TRUE, TTIsVisible("relativehelp->helpicon->^helpicon")); //and now its available!

  GetTestController()->GrabInstructions();

  TT("relativehelp->helpicon->^helpicon")->onclick([nativex := 0, nativey := 0]);

  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));
  TestEqLike(testsite->webroot || "doc/testfile/?ts=*", openhelp.params[0]);
  TestEq("", openhelp.params[1]);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);


  //STORY: Open app in with write access to docs. 'relativehelp' points to a nonexisting page but should still appear!
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:testdocs", [ params := [ `site::${testsite->name}/doc`, "edit" ]]));
  TestEq("", TT("relativehelp")->GetDocumentationURL());
  TestEq(TRUE, TTIsVisible("relativehelp->helpicon->^helpicon"));

  GetTestController()->GrabInstructions();

  TT("relativehelp->helpicon->^helpicon")->onclick([nativex := 0, nativey := 0]);

  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));
  TestEq("", openhelp.params[0]);
  TestEq(TRUE, openhelp.params[1] != "", "Expecting an edit token");

  //STORY: Start the editor. we'll put some text there but NOT publish it yet...
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("tollium:editdocumentation", [ target := [ edittoken := openhelp.params[1] ]]));
  TT("doceditor")->contents->rte->value := [htmltext := StringToBlob(`<body><p class="normal">help text!</p></body>`)];
  TT("doceditor")->contents->rte->__debug_simulatedirty();
  TTClick("saveasdraftbutton");
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //That should still keep the help button without an url but available to editors
  TT("relativehelp->helpicon->^helpicon")->onclick([nativex := 0, nativey := 0]);

  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));
  TestEq("", openhelp.params[0]);
  TestEq(TRUE, openhelp.params[1] != "", "Expecting an edit token");

  //STORY: Now publish the rtd
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("tollium:editdocumentation", [ target := [ edittoken := openhelp.params[1] ]]));
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick("publishbutton"), [ messagemask := "*sure*save*publish*" ]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //That should activate the help button without an url but available to editors
  TT("relativehelp->helpicon->^helpicon")->onclick([nativex := 0, nativey := 0]);

  openhelp := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr = "appcall" AND type = "OpenDocumentation";
  TestEq(TRUE, RecordExists(openhelp));
  TestEqLike(testsite->webroot || "doc/subpage/?ts=*", openhelp.params[0]);

  AWAIT ExpectScreenChange(-1, PTR TTEscape); //close our testapp

  //Clear the config key after a succesful test so the updateremotedocumentation task doesn't make noise about us
  testfw->BeginWork();
  WriteRegistryKey("webhare_testsuite.tests.docroot", "");
  RefreshEmbeddedDocumentation("webhare_testsuite"); //should clear the on disk file
  testfw->CommitWork();
}

RunTestFramework( [ PTR TestDocumentationSite
                  ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ]
                     ]);
