<?wh
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

ASYNC MACRO MultilineComponent()
{
  //attempt to insert a block container into a <select><option>. currently throws immediately. should either throw or generate lastloaderrors
  TestThrows(PTR GetTestController()->LoadScreen("webhare_testsuite:tests/addons.block_multilinecomponent"));

  //attempt to insert a inline container into a <select><option> which actually contains multiple lines
  AWAIT ExpectScreenChange(+1, PTR GetTestController()->LoadScreen("webhare_testsuite:tests/addons.inline_multilinecomponent"));
  TestEq("Fragment \'mod::webhare_testsuite/screens/tests/components.xml#multilinecomponent\' was expected to be inline, but expanded to multiple lines", TT("errors")->rows[0].message);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

/* Test add-on components */
OBJECT ASYNC FUNCTION YoutubeComponent()
{
  OBJECT scr := GetTestController()->LoadScreen("webhare_testsuite:tests/addons.youtubecomponent");
  TestEq(TRUE, ObjectExists(scr));
  /* this caused a: The component \'youtubecomponent$textedit\' you are trying to insert as a child of tollium_s$1 has already been inserted into selectcomponent */
  AWAIT ExpectScreenChange(+1, PTR scr->RunModal);
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  //verify the titles used
  TestEq("Youtube component", scr->youtubecomponent->title);

  RETURN TRUE;
}

ASYNC MACRO TestCustomImplementation()
{
  OBJECT screen := GetTestController()->LoadScreen("webhare_testsuite:tests/customparser.testscreen");

  AWAIT ExpectScreenChange(+1, PTR screen->RunModal());

  // The 'Hello, World!' text is created by the custom tabs extension implementation, which is loaded by the custom fragment
  // implementation, which in turn is loaded by the custom screen implementation
  TestEq("Hello, World!", TT(":hello_world")->value);

  // The rows are filled by the <test:child> elements within the <test:extraparsercomponent> component
  TestEq(
      [ [ testattr := "Custom parser attribute value"
        , compref := ""
        ]
      , [ testattr := ""
        , compref := "Referenced component"
        ]
      , [ testattr := "Last child"
        , compref := ""
        ]
      ], SELECT testattr, compref FROM TT("children")->rows);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestFramework([ PTR MultilineComponent
                 , PTR YoutubeComponent
                 , PTR TestCustomImplementation
                 ]);
