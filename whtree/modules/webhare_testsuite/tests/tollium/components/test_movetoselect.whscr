<?wh
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/tests/tollium/components/screens/helpers.whlib";


ASYNC MACRO TestMoveToSelectParse()
{
  OBJECT scr := GetTestController()->LoadScreen(InlineScreens(`
  <screen name="test" implementation="none">
    <body>
      <movetoselect name="comp">
        <column name="rowkey" title="Rowkey" type="integer" />
        <column name="title" title="Title" type="text" />
      </movetoselect>
    </body>
  </screen>`, "#test"));

  AWAIT ExpectScreenChange(+1, PTR scr->RunModal);
  TT("comp")->rows := [[ rowkey := 1, title := "Item 1" ]
                      ,[ rowkey := 2, title := "Item 2" ]
                      ,[ rowkey := 3, title := "Item 3" ]
                      ,[ rowkey := 4, title := "Item 4" ]
                      ];

  //test sync between rows, selection and value
  TestEq(4, Length(TT("comp")->rows));
  TestEq(0, Length(TT("comp")->selection));
  TestEq(INTEGER[], TT("comp")->value);

  TestEq(4, Length(TT("comp->sourcelist")->rows));
  TestEq("rowkey", TT("comp->sourcelist")->sortcolumn);
  TestEq(0, Length(TT("comp->selectionlist")->rows));

  TT("comp")->value := [2];
  TestEq(1, Length(TT("comp")->selection));

  TestEq(3, Length(TT("comp->sourcelist")->rows));
  TestEq(1, Length(TT("comp->selectionlist")->rows));
  TestEq(0, Length(TT("comp->selectionlist")->selection), "should not initially select rows moved by changing ->value");

  TT("comp")->selection := SELECT * FROM TT("comp")->rows WHERE rowkey%2 = 1;
  TestEq([1,3], TT("comp")->value);

  TT("comp->sourcelist")->selection := TT("comp->sourcelist")->rows;
  TTClick("comp->addtoselection");
  TestEq(0, Length(TT("comp->sourcelist")->selection));
  TestEq([2,4], TT("comp->selectionlist")->value, "we expect the rows we just moved to still be selected in the selectionlist");
  TT("comp->selectionlist")->value := [1,3];
  TTClick("comp->removefromselection");
  TestEq([1,3], TT("comp->sourcelist")->value, "we expect the rows we just moved back to still be selected in the sourcelist");
  TestEq(INTEGER[], TT("comp->selectionlist")->value);

  TestEq(FALSE, TT("comp->sourcefilterfield", [ findinvisible := TRUE ])->visible);

  TT("comp")->value := INTEGER[];
  TT("comp")->onsourcefilter := PTR MySourceFilter(topscreen->contexts, #1, #2);
  TestEq(TRUE , TT("comp->sourcefilterfield", [ findinvisible := TRUE ])->visible);
  TestEq(4, Length(TT("comp")->rows));
  TestEq([ "Item 4" ], SELECT AS STRING ARRAY title FROM TT("comp->sourcelist")->rows);

  TT("comp->sourcefilterfield")->value := "2";
  TestEq([ "Item 2", "Item 3", "Item 4" ], SELECT AS STRING ARRAY title FROM TT("comp->sourcelist")->rows);

  TT("comp->sourcefilterfield")->value := "2a";
  TestEq([ "Item 1", "Item 2", "Item 3", "Item 4" ], SELECT AS STRING ARRAY title FROM TT("comp->sourcelist")->rows);

  //changing columns kills everything, just like a list
  TT("comp")->columns := SELECT * FROM TT("comp")->columns WHERE name="title";
  TestEq(0, Length(TT("comp")->rows));
  TestEq(0, Length(TT("comp")->selection));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  scr := GetTestController()->LoadScreen(InlineScreens(`
  <screen name="test" implementation="none">
    <body>
      <movetoselect name="comp" onsourcefilter="${Resolve("screens/helpers.whlib#MySourceFilter")}" rowkeytype="integer">
        <column name="rowkey" title="Rowkey" type="integer" />
        <column name="title" title="Title" type="text" />
      </movetoselect>
    </body>
  </screen>`, "#test"));

  AWAIT ExpectScreenChange(+1, PTR scr->RunModal);
  TestEq(INTEGER[], TT("comp")->value);

  TT("comp")->rows := [[ rowkey := 1, title := "Item 1" ]
                      ,[ rowkey := 2, title := "Item 2" ]
                      ,[ rowkey := 3, title := "Item 3" ]
                      ,[ rowkey := 4, title := "Item 4" ]
                      ];
  TestEq([ "Item 4" ], SELECT AS STRING ARRAY title FROM TT("comp->sourcelist")->rows);
  TestEq(TRUE, TT("comp->sourcefilterfield", [ findinvisible := TRUE ])->visible);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

}

ASYNC MACRO TestMoveToSelectComponent()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:anycomponent", [ params := ["movetoselect"]]));

  //We expect some basic initialization to have been done
  TestEq(2, Length(topscreen->comp->value));
  TestEq(5, Length(topscreen->comp->rows));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestframework([ PTR TestMoveToSelectParse
                 , PTR TestMoveToSelectComponent
                 ], [ testusers := [[ login := "mysysop", grantrights := ["system:sysop"] ]
                                   ]
                    ]);
