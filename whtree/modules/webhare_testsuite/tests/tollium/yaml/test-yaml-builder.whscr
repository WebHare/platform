<?wh

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/yamlcomponents.whlib";

ASYNC MACRO TestYamlInlineScreens()
{
  AWAIT ExpectScreenChange(+1, PTR RunYamlInlineScreen(GetTestController(),
     CELL[ body_lines :=
           [[ textedit :=
               [ title := "My textedit", value := "Hello, world!" ]]]
         ]));

  TestEq("Hello, world!", TT(":My textedit")->value);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //value_constraints is an 'initial setting;'
  AWAIT ExpectScreenChange(+1, PTR RunYamlInlineScreen(GetTestController(),
     CELL[ body_lines :=
           [[ "datetime" := [ title := "DATE1", type := "date", value_constraints := [ required := TRUE ] ]]
           ,[ "datetime" := [ title := "DATE2", type := "date", value_constraints := [ required := TRUE ], required := FALSE ]]
           ]
          ]));

  TestEq("date", TT(":DATE1")->type);
  TestEq(TRUE, TT(":DATE1")->required, "default valueConstraints sets required");
  TestEq(FALSE, TT(":DATE2")->required, "required explicitly disabled");

  //TODO verify interaction when WRD/WHFSInstance fields are used in combination with fields - whether they can forward their value_constraints in time to still interact with explicit required true/false
  //TODO WHFS should actually apply the original value_constraints even when overridden in tollium
  //TODO how useful *is* tightening constraints at the editProps level anyway? why not just modify the component props?

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestConditions()
{
  AWAIT ExpectScreenChange(+1, PTR RunYamlScreen(GetTestController(), Resolve("data/basics.screens.yml#conditions")));

  TestEq(TRUE, TT(":Text edit")->AllowExternalUpdates());
  TestEq("green", TT("pulldown")->value);
  TestEq(TRUE, TT("cb_text_edit")->value);
  TT("cb_text_edit")->value := FALSE;
  TestEq(FALSE, TT(":Text edit")->AllowExternalUpdates());

  //Verify events
  TestEq("showtime-init, color-init, showtime-set", TT(":Events")->value, "showtime-set should have fired for screen init, even if no explicit value was set");
  TestEq(FALSE, TT("c_box")->value);
  TT("c_box")->value := TRUE;

  TestEq("showtime-init, color-init, showtime-set, showtime-set", TT(":Events")->value);

  //TOOD test corner cases where a field, during init, sets a future or past field. ensure that enough OnSets get fired

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RuntestFramework([
  PTR TestYamlInlineScreens,
  PTR TestConditions
  ]);
