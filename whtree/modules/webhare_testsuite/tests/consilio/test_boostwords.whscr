<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::promise.whlib";
LOADLIB "wh::filetypes/richdocument.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/search.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestBoostWords()
{
  OBJECT boostwords_type := OpenWHFSType("http://www.webhare.net/xmlns/consilio/boostwords");

  // Create and fill the test site
  testfw->BeginWork();

  OBJECT siteroot := GetTestsuiteTempFolder();

  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT catalog := CreateConsilioCatalog("consilio:testfw_boostwords", [ fieldgroups := ["webhare_testsuite:pagelistfields" ]]);
  Print(catalog->GetStorageInfo() || "\n");

  OBJECT csource := catalog->AddPublisherContentSource(siteroot->id);

  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT file_1 := siteroot->CreateFile([name:="testfile1.rtd", title := "important", type := richdoctype->id, publish := TRUE]);
  OBJECT file_2 := siteroot->CreateFile([name:="testfile2.rtd", title := "blabla", type := richdoctype->id, publish := TRUE]);

  //explicitly set a headertitle or the 'important' word will be re-boosted by being in a H1
  file_1->SetInstanceData("http://www.webhare.net/xmlns/publisher/seosettings", [ headertitle := "file_1 headertitle" ]);
  file_2->SetInstanceData("http://www.webhare.net/xmlns/publisher/seosettings", [ headertitle := "file_2 headertitle" ]);

  OBJECT myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob(
`<html><head></head><body>
<p class="normal">blabla</p>
</body></html>`)
                 ]);
  richdoctype->SetInstanceData(file_1->id, [data := myrichdoc->ExportAsRecord()]);

  myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob(
`<html><head></head><body>
<p class="normal">blabla schanulleke</p>
</body></html>`)
                 ]);
  richdoctype->SetInstanceData(file_2->id, [data := myrichdoc->ExportAsRecord()]);

  testfw->CommitWork();

  Print("waiting for changes indexed...\n");
  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]);
  Print("it's indexed\n");

  OBJECT search_obj := OpenSearchObject("en", "consilio:testfw_boostwords");
  search_obj->empty_result_record := DEFAULT RECORD;
  search_obj->summary_length := 0;

  // Search for 'blabla', which will return file 2 as first result, as it has 'blabla' in its title
  RECORD results := search_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount := 2
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            , [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      ], results);

  // Search for 'important', which will only return file 1
  results := search_obj->Search("important", 0, 10);
  TestEq(
      [ totalcount := 1
      , results :=
            [ [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      ], results);

  testfw->BeginWork();
  boostwords_type->SetInstanceData(file_2->id, [ words := [ "important" ] ]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]); //implies catalog->refresh

  // Search for 'important', which will now return both files, starting with file 2, as it has 'important' in its keywords
  results := search_obj->Search("important", 0, 10);
  dumpvalue(results,'tree');
  TestEq(
      [ totalcount := 2
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            , [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      ], results);

  testfw->BeginWork();
  boostwords_type->SetInstanceData(file_1->id, [ words := [ "blabla" ] ]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]);

  // Search for 'blabla', which will return file 1 as first result, as it has 'blabla' in its keywords
  results := search_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount := 2
      , results :=
            [ [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            , [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            ]
      ], results);

  // Allow terms by default, returns file 2, which has 'schanulleke' but doesn't have 'nothing' (We use schanulleke instead of test as /webhare-tests/ in the printed output normalized 'test')
  results := search_obj->Search("schanulleke nothing", 0, 10);
  TestEq(
      [ totalcount := 1
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            ]
      ], results);

  // Require terms explicitly, returns nothing, because file 2 only has 'schanulleke' but doesn't have 'nothing'
  results := search_obj->Search("+schanulleke +nothing", 0, 10);
  TestEq(
      [ totalcount := 0
      , results := DEFAULT RECORD ARRAY
      ], results);

  // Require terms by default, returns nothing, because file 2 only has 'schanulleke' but doesn't have 'nothing'
  search_obj->and_search := TRUE;
  results := search_obj->Search("schanulleke nothing", 0, 10);
  TestEq(
      [ totalcount := 0
      , results := DEFAULT RECORD ARRAY
      ], results);
}

RunTestframework(
    [ PTR TestBoostWords ],
    [ testusers :=
        [ [ login := "sysop", grantrights := [ "system:sysop" ] ]
        ]
    ]);
