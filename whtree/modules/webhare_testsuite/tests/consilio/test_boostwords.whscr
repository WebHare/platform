<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/richdocument.whlib";

LOADLIB "mod::consilio/lib/api.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestBoostWords()
{
  OBJECT boostwords_type := OpenWHFSType("http://www.webhare.net/xmlns/consilio/boostwords");

  // Create and fill the test site
  testfw->BeginWork();

  OBJECT siteroot := GetTestsuiteTempFolder();

  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT catalog := CreateConsilioCatalog("consilio:testfw_boostwords", [ fieldgroups := [ "webhare_testsuite:pagelistfields" ]
                                                                        , priority := 9
                                                                        ]);
  Print(catalog->GetStorageInfo() || "\n");

  OBJECT csource := catalog->AddFolderToCatalog(siteroot->id);

  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT file_1 := siteroot->CreateFile([name:="testfile1.rtd", title := "important", type := richdoctype->id, publish := TRUE]);
  OBJECT file_2 := siteroot->CreateFile([name:="testfile2.rtd", title := "blabla", type := richdoctype->id, publish := TRUE]);

  //explicitly set a headertitle or the 'important' word will be re-boosted by being in a H1
  file_1->SetInstanceData("http://www.webhare.net/xmlns/publisher/seosettings", [ headertitle := "file_1 headertitle" ]);
  file_2->SetInstanceData("http://www.webhare.net/xmlns/publisher/seosettings", [ headertitle := "file_2 headertitle" ]);

  OBJECT myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob(
`<html><head></head><body>
<p class="normal">blabla</p>
</body></html>`)
                 ]);
  richdoctype->SetInstanceData(file_1->id, [data := myrichdoc->ExportAsRecord()]);

  myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob(
`<html><head></head><body>
<p class="normal">blabla schanulleke</p>
</body></html>`)
                 ]);
  richdoctype->SetInstanceData(file_2->id, [data := myrichdoc->ExportAsRecord()]);

  testfw->CommitWork();

  Print("waiting for changes indexed...\n");
  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]);
  Print("it's indexed\n");

  // Search for 'blabla', which will return file 2 as first result, as it has 'blabla' in its title
  RECORD results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("blabla"), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 2
      , totalexact := TRUE
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            , [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  // Search for 'important', which will only return file 1
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("important"), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 1
      , totalexact := TRUE
      , results :=
            [ [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  testfw->BeginWork();
  boostwords_type->SetInstanceData(file_2->id, [ words := [ "important" ] ]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]); //implies catalog->refresh

  // Search for 'important', which will now return both files, starting with file 2, as it has 'important' in its (higher scoring) keywords instead of its title
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("important"), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 2
      , totalexact := TRUE
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            , [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            ]
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  testfw->BeginWork();
  boostwords_type->SetInstanceData(file_1->id, [ words := [ "blabla" ] ]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  csource->ReindexContentSource([ rebuild := TRUE ]);
  csource->WaitForIndexingDone([ verbose := TRUE ]);

  // Search for 'blabla', which will return file 1 as first result, as it has 'blabla' in its keywords
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("blabla"), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 2
      , totalexact := TRUE
      , results :=
            [ [ objectid := file_1->objecturl
              , groupid := ToString(file_1->id)
              ]
            , [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            ]
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  // AND search is default, returns nothing, because file 2 only has 'schanulleke' but doesn't have 'nothing'
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("schanulleke nothing"), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 0
      , totalexact := TRUE
      , results := DEFAULT RECORD ARRAY
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  // Use OR search, returns file 2, which has 'schanulleke' but doesn't have 'nothing' (We use schanulleke instead of test as /webhare-tests/ in the printed output normalized 'test')
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("schanulleke nothing", [ querymode := "OR" ]), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 1
      , totalexact := TRUE
      , results :=
            [ [ objectid := file_2->objecturl
              , groupid := ToString(file_2->id)
              ]
            ]
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);

  // Use OR search, require terms explicitly, returns nothing, because file 2 only has 'schanulleke' but doesn't have 'nothing'
  results := RunConsilioSearch("consilio:testfw_boostwords", CQParseUserQuery("+schanulleke +nothing", [ querymode := "OR" ]), [ first := 0, count := 10, mapping := CELL[], summaryfield := "" ]);
  TestEq(
      [ totalcount := 0
      , totalexact := TRUE
      , results := DEFAULT RECORD ARRAY
      , eventmasks := STRING[ `consilio:index.${catalog->id}` ]
      ], results);
}

RunTestframework(
    [ PTR TestBoostWords ],
    [ testusers :=
        [ [ login := "sysop", grantrights := [ "system:sysop" ] ]
        ]
    ]);
