<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/search.whlib";

LOADLIB "mod::webhare_testsuite/lib/database.whlib";



MACRO RunCleanupTest()
{
  // Prepare the content to index
  testfw->BeginWork();

  OBJECT catalog := CreateConsilioCatalog("consilio:testfw_cleanup");
  OBJECT search_obj := OpenSearchObject("en", "consilio:testfw_cleanup");
  search_obj->empty_result_record :=
      [ groupid :=        ""
      , objectid :=       ""
      , _indexdate := DEFAULT DATETIME
      , _contentsource :=  0
      ];

  DELETE FROM webhare_testsuite.consilio_index;
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, adate, text)
      VALUES("a", "a1", MakeDate(1970, 1, 10), MakeDate(2010, 1, 1), "a a1");
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, adate, text)
      VALUES("a", "a2", MakeDate(1970, 1, 10), MakeDate(2011, 1, 1), "a a2");
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES("b", "b1", MakeDate(1970, 1, 10), "b b1");
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES("c", "c1", MakeDate(1970, 1, 10), "c c1");
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES("c", "c2", MakeDate(1970, 1, 10), "c c2");
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES("c", "c3", MakeDate(1970, 1, 10), "c c3");

  testfw->CommitWork();

  // Build an index
  testfw->BeginWork();

  OBJECT contentsource_1 := catalog->AddCustomContentSource("webhare_testsuite:contentsource_1", "mod::webhare_testsuite/lib/consilio/dbindex.whlib#DBContent",
      [ title :=            "BETA_CUSTOMSOURCE"
      , maxgroupobjects :=  1000
      , discardsummaries := TRUE
      ]);

  testfw->CommitWork();

  // Build the index
  catalog->ReindexCatalog();
  contentsource_1->WaitForIndexingDone();

  TestEQ((SELECT groupid, objectid, _indexdate := indexdate, _contentsource := contentsource_1->id FROM webhare_testsuite.consilio_index ORDER BY groupid, objectid),
         (SELECT groupid, objectid, _indexdate, _contentsource FROM search_obj->SearchQuery(CQAll(), 0, 1000).results ORDER BY groupid, objectid), "Initial index build didn't work");

  // Clean the contentsource from the DB without cleaning the index, add another contentsource
  // clean the list of index stuff
  testfw->BeginWork();
  DELETE FROM consilio.contentsources WHERE id = contentsource_1->id;

  OBJECT contentsource_2 := catalog->AddCustomContentSource("webhare_testsuite:contentsource_1", "mod::webhare_testsuite/lib/consilio/dbindex.whlib#DBContent",
      [ title :=            "BETA_CUSTOMSOURCE"
      , maxgroupobjects :=  1000
      , discardsummaries := TRUE
      ]);

  DELETE FROM webhare_testsuite.consilio_index;

  testfw->CommitWork();

  // Rebuild the index
  catalog->ReindexCatalog([ rebuild := TRUE ]);
  contentsource_2->WaitForIndexingDone();

  TestEQ(RECORD[],
         (SELECT groupid, objectid, _indexdate, _contentsource FROM search_obj->SearchQuery(CQAll(), 0, 1000).results ORDER BY groupid, objectid), "Cleanup wasn't executed");
}

RunTestframework([ PTR RunCleanupTest
                 ]);
