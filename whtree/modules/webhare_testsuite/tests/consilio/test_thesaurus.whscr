<?wh

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/search.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/database.whlib";


OBJECT search_obj;

MACRO TestThesaurus()
{

  // Prefix for object/group ids, just to stress the itf
  STRING idprefix := "\n\r\t()[]{}^$+-%!\"";//" \n\r\t()[]{}^$+-%!\"";

  testfw->BeginWork();

  // Clear the current contents
  DELETE FROM webhare_testsuite.consilio_index;

  OBJECT testcatalog := CreateConsilioCatalog("consilio:testfw_thesaurus");
  //having indexnames makes log analysis easier
  Print(`${testcatalog->tag} (${testcatalog->id}) => ${testcatalog->indexname}\n`);

  OBJECT contentsource := testcatalog->AddCustomContentSource("webhare_testsuite:thesaurus", "mod::webhare_testsuite/lib/consilio/dbindex.whlib#DBContent",
      [ maxgroupobjects :=      1000
      , discardsummaries :=     TRUE
      ]);

  testfw->CommitWork();
  search_obj := OpenSearchObject("en", "consilio:testfw_thesaurus");

  // Clear the index
  contentsource->ReindexContentSource([rebuild := TRUE]);
  contentsource->WaitForIndexingDone();

  testfw->BeginWork();
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, text)
      VALUES(idprefix||"a", idprefix||"a1", "a");
  testfw->CommitWork();

  // Rebuild the index
  contentsource->ReindexContentSource([rebuild := TRUE]);
  contentsource->WaitForIndexingDone();

  // Search
  RECORD res := search_obj->Search("+contentsource:"||contentsource->id||" +a", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +b", 0, 1000);
  TestEq(0, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:a", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:b", 0, 1000);
  TestEq(0, Length(res.results));


  // Add some thesaurus words
  testfw->BeginWork();
  INTEGER wordgroupid := (SELECT AS INTEGER Max(wordgroup) FROM consilio.thesaurus) + 1;
  INSERT INTO consilio.thesaurus(indexid, wordgroup, word)
       VALUES(testcatalog->id, wordgroupid, "a");
  INSERT INTO consilio.thesaurus(indexid, wordgroup, word)
       VALUES(testcatalog->id, wordgroupid, "b");
  testfw->CommitWork();

  // Search again
  res := search_obj->Search("+contentsource:"||contentsource->id||" +a", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +b", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:a", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:b", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:\"a\"", 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->Search("+contentsource:"||contentsource->id||" +body:\"b\"", 0, 1000);
  TestEq(0, Length(res.results)); // Phrase queries are treated as exact match queries

  res := search_obj->SearchQuery(CQAnd([ CQMatch("contentsource", "=", contentsource->id), CQParseUserQuery("a") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("contentsource", "=", contentsource->id), CQParseUserQuery("b") ]), 0, 1000);
  TestEq(0, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("contentsource", "=", contentsource->id), CQParseUserQuery("b", [ thesaurusid := testcatalog->id ]) ]), 0, 1000);
  TestEq(1, Length(res.results));

  //Ensure toplevel CQParseUserQuery doesn't crash
  res := search_obj->SearchQuery(CQParseUserQuery("a"), 0, 1000);

  testfw->BeginWork();
  DELETE FROM webhare_testsuite.consilio_index;
  testfw->CommitWork();

  contentsource->ReindexContentSource();
  contentsource->WaitForIndexingDone();
}


RunTestframework(
    [ PTR TestThesaurus ],
    [ testusers :=
        [ [ login := "sysop", grantrights := [ "system:sysop" ] ]
        ]
    ]);
