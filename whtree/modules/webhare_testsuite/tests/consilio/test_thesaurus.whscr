<?wh
LOADLIB "wh::datetime.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/search.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/database.whlib";


OBJECT search_obj;

MACRO TestThesaurus()
{
  // Prefix for object/group ids, just to stress the itf
  STRING idprefix := "\n\r\t()[]{}^$+-%!\"";//" \n\r\t()[]{}^$+-%!\"";

  testfw->BeginWork();

  // Clear the current contents
  DELETE FROM webhare_testsuite.consilio_index;

  OBJECT testcatalog := CreateConsilioCatalog("consilio:testfw_thesaurus", [ priority := 9 ]);


  OBJECT contentsource := testcatalog->AddCustomContentSource("webhare_testsuite:thesaurus", "mod::webhare_testsuite/lib/consilio/dbindex.whlib#DBContent");

  testfw->CommitWork();
  TestEq(TRUE, testcatalog->WaitReady(MAX_DATETIME, [ forconfiguration := TRUE ]));

  Print(testcatalog->GetStorageInfo() || "\n");

  search_obj := OpenSearchObject("en", "consilio:testfw_thesaurus");

  // Clear the index
  contentsource->ReindexContentSource([rebuild := TRUE]);
  contentsource->WaitForIndexingDone();

  testfw->BeginWork();
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, text)
      VALUES(idprefix||"a", idprefix||"a1", "a c");
  testfw->CommitWork();

  // Rebuild the index
  contentsource->ReindexContentSource([rebuild := TRUE]);
  contentsource->WaitForIndexingDone();

  // Search
  RECORD res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("a") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("b") ]), 0, 1000);
  TestEq(0, Length(res.results));


  // Add some thesaurus words
  testfw->BeginWork();
  testcatalog->SetSynonyms([ CELL[ words := [ "a", "b" ] ] ]);
  testfw->CommitWork();
  // Regression: New word groups (without wordgroupid) wouldn't be actually created
  INTEGER wordgroupid := SELECT AS INTEGER COLUMN wordgroupid FROM testcatalog->GetSynonyms(); // There is only one word group
  TestAssert(wordgroupid > 0);
  TestEq(TRUE, testcatalog->WaitReady(MAX_DATETIME, [ forconfiguration := TRUE, forstorage := TRUE ]));

  // Search again
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("a") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("b") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("\"b\"") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("\"a c\"") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("\"b c\"") ]), 0, 1000);
  TestEq(1, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("b", [ synonyms := FALSE ]) ]), 0, 1000);
  TestEq(0, Length(res.results));

  // Update the thesaurus words
  testfw->BeginWork();
  testcatalog->SetSynonyms([ CELL[ wordgroupid, words := [ "a", "c" ] ] ]);
  testfw->CommitWork();
  TestEq(TRUE, testcatalog->WaitReady(MAX_DATETIME, [ forconfiguration := TRUE ]));

  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("b") ]), 0, 1000);
  TestEq(0, Length(res.results));
  res := search_obj->SearchQuery(CQAnd([ CQMatch("_contentsource", "=", contentsource->id), CQParseUserQuery("c") ]), 0, 1000);
  TestEq(1, Length(res.results));

  //Ensure toplevel CQParseUserQuery doesn't crash
  res := search_obj->SearchQuery(CQParseUserQuery("a"), 0, 1000);

  testfw->BeginWork();
  DELETE FROM webhare_testsuite.consilio_index;
  testfw->CommitWork();

  contentsource->ReindexContentSource();
  contentsource->WaitForIndexingDone();
}


RunTestframework(
    [ PTR TestThesaurus ],
    [ testusers :=
        [ [ login := "sysop", grantrights := [ "system:sysop" ] ]
        ]
    ]);
