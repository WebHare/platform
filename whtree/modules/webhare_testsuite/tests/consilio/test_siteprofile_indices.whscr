<?wh

LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/testfw/indexactions.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


PUBLIC MACRO TestModernSiteProfileIndices()
{
  FixConsilioIndices([ reportmissing := TRUE ]);

  OBJECT maincatalog := OpenConsilioCatalog("webhare_testsuite:testsitecatalog");
  Testeq(TRUE, ObjectExists(maincatalog));

  RECORD ARRAY contentsources := maincatalog->ListContentSources();
  contentsources := SELECT *, whfspath := (SELECT AS STRING whfspath FROM system.fs_objects WHERE fs_objects.id = contentsources.fsobject) FROM contentsources WHERE NOT isorphan;
  TestEq([ '/WebHare testsuite site/portal1/' //portal1 is added through ongetsources=
         , '/WebHare testsuite site/TestPages/'
         , '/WebHare testsuite site/webtools/'
         ], SELECT AS STRING ARRAY whfspath FROM contentsources WHERE whfspath != "" ORDER BY ToUppercase(whfspath));
  TestEq([ 'webhare_testsuite:dbindex'
         , 'webhare_testsuite:loremindex'
         ], SELECT AS STRING ARRAY tag FROM contentsources WHERE tag != "" ORDER BY tag);

  WaitForCatalogDone(maincatalog);

  RECORD result := RunConsilioSearch("webhare_testsuite:testsitecatalog", CQParseUserQuery("simpletest"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("webhare_testsuite:testsitecatalog", CQParseUserQuery("Pellentesque"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("webhare_testsuite:testsitecatalog", CQParseUserQuery("Welcome"));
  TestEq(RECORD[], result.results);
}

PUBLIC MACRO TestLegacySiteProfileIndices()
{
  testfw->BeginWork();
  OBJECT site := testfw->SetupTestWebdesignWebsite([ sitename := "betatestindexsite", webfeatures := ["webhare_testsuite:siteprofileindices"] ]);
  INTEGER siteroot := site->root;
  INTEGER index_1 := testfw->GetCatalogIdByName(testfw->GetCatalogName_1());
  IF (index_1 != 0)
  {
    // If the index exists, it should be empty
    INTEGER contentsource_1 := testfw->GetPublisherContentSource(testfw->GetCatalogName_1(), siteroot);
    TestEq(0, contentsource_1);
  }
  INTEGER index_2 := testfw->GetCatalogIdByName(testfw->GetCatalogName_2());
  IF (index_2 != 0)
  {
    // If the index exists, it should be empty
    INTEGER contentsource_2 := testfw->GetPublisherContentSource(testfw->GetCatalogName_2(), siteroot);
    TestEq(0, contentsource_2);
  }
  // Index 3 should not exist
  INTEGER index_3 := testfw->GetCatalogIdByName(testfw->GetCatalogName_3());
  TestEq(0, index_3);

  testfw->CommitWork();

  FixConsilioIndices([ reportmissing := TRUE ]); //indices are created in the background so explicitly enforce them

  // The indices 1 and 2 must be present now, 3 doesn't match the site name
  index_1 := testfw->GetCatalogIdByName(testfw->GetCatalogName_1());
  TestEq(TRUE, index_1 != 0);
  index_2 := testfw->GetCatalogIdByName(testfw->GetCatalogName_2());
  TestEq(TRUE, index_2 != 0);
  index_3 := testfw->GetCatalogIdByName(testfw->GetCatalogName_3());
  TestEq(0, index_3, "catalog #3 has a sitename filter which shouldn't match anything");

  INTEGER contentsource_1 := testfw->GetPublisherContentSource(testfw->GetCatalogName_1(), siteroot);
  RECORD contentsource := testfw->GetContentSource(contentsource_1);
  TestEq(TRUE, RecordExists(contentsource));
  TestEq("SOMETHING", contentsource.tag);
  TestEq(FALSE, contentsource.discardsummaries);
  TestEq(3, contentsource.maxgroupobjects);

  INTEGER contentsource_2 := testfw->GetPublisherContentSource(testfw->GetCatalogName_2(), siteroot);
  contentsource := testfw->GetContentSource(contentsource_2);
  TestEq(TRUE, RecordExists(contentsource), `Expected ${testfw->GetCatalogName_2()} to have a content source`);
  TestEq("", contentsource.tag);
  TestEq(TRUE, contentsource.discardsummaries);
  TestEq(0, contentsource.maxgroupobjects);
}

ASYNC MACRO TestCatalogApp()
{
  testfw->BeginWork();
  OBJECT toindexfolder1 := GetTestsuiteTempFolder()->CreateFolder([name := "index-me-1"]);
  OBJECT toindexfolder2 := GetTestsuiteTempFolder()->CreateFolder([name := "index-me-2"]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("consilio"));
  TestEq(1, Length(SELECT * FROM TT("catalogs")->rows WHERE tag = "webhare_testsuite:testsitecatalog"));
  TT("catalogs")->selection := SELECT * FROM TT("catalogs")->rows WHERE tag = "webhare_testsuite:testsitecatalog";

  AWAIT ExpectScreenChange(+1,  PTR TTClick("addcontentsource"));
  TestEq("WebHare folder", TT("contentsourcetype")->value);
  TT("folder")->value := toindexfolder1->id;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  TestEqMembers([[ tag := "/WebHare testsuite site/tmp/index-me-1/" ]], TT("catalogs")->selection, "*");

  AWAIT ExpectScreenChange(+1,  PTR TTClick("edititem"));
  TestEq(toindexfolder1->id, TT("folder")->value);
  TT("folder")->value := toindexfolder2->id;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  TestEqMembers([[ tag := "/WebHare testsuite site/tmp/index-me-2/" ]], TT("catalogs")->selection, "*");

  //TODO check if the content source is indeed restarting, cleaning out old stuff and indexing new!

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestFramework( [ PTR TestModernSiteProfileIndices
                  , PTR TestLegacySiteProfileIndices
                  , PTR TestCatalogApp
                  ], [ consilio := TRUE
                     , testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                    ]
                     ]);
