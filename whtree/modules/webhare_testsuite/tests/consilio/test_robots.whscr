<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::consilio/lib/api.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO TestHTMLIndexing()
{
  // The testfile has two pages, with the first page linking to the second page

  // First page has robots "index, follow" -> both pages should be indexed
  testfw->BeginWork();
  OBJECT catalog := CreateConsilioCatalog("consilio:testfw_robots");
  Print(`${catalog->tag} (${catalog->id}) => ${catalog->indexname}\n`);

  OBJECT siteroot := GetTestsuiteTempFolder();
  OBJECT newsource := catalog->AddPublisherContentSource(siteroot->id, [ enabled := FALSE ]); //disable it so we can manually control the frontend (and can avoid WaitIndexDone etc) without risking conflicting/duplicate index actions
  OBJECT testfile := siteroot->CreateFile([ name := "robottest", typens := "http://www.webhare.net/xmlns/webhare_testsuite/consilio/robottest", publish := TRUE]);

  testfw->CommitWork();

  IF (testfw->debug)
    Print(testfile->url || "\n");

  testfw->WaitForPublishCompletion(siteroot->id);
  newsource->ReindexGroup(ToString(testfile->id), [ rebuild := TRUE, foreground := TRUE ]); //run in foreground, we're not testing queues

  RECORD res := RunConsilioSearch("consilio:testfw_robots", CQParseUserQuery("page"));
  TestEQ(2, res.totalcount);
  TestEq(TRUE, RecordExists(SELECT FROM res.results WHERE objectid = testfile->url));
  TestEq(TRUE, RecordExists(SELECT FROM res.results WHERE objectid = `${testfile->url}?page=1`));


  testfw->BeginWork();
  testfile->SetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/consilio/robottest", [ noindex := TRUE ]);
  // First page now has robots "noindex, follow" -> only second page should be indexed
  testfw->CommitWork();

  newsource->ReindexGroup(ToString(testfile->id), [ rebuild := TRUE, foreground := TRUE ]);

  res := RunConsilioSearch("consilio:testfw_robots", CQParseUserQuery("page"));
  TestEq(1, res.totalcount);
  TestEq(TRUE, RecordExists(SELECT FROM res.results WHERE objectid = `${testfile->url}?page=1`));


  testfw->BeginWork();
  testfile->SetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/consilio/robottest", [ noindex := FALSE, nofollow := TRUE ]);
  // First page has robots "index, nofollow" -> only first page should be indexed
  testfw->CommitWork();

  newsource->ReindexGroup(ToString(testfile->id), [ rebuild := TRUE, foreground := TRUE ]);

  res := RunConsilioSearch("consilio:testfw_robots", CQParseUserQuery("page"));
  TestEQ(1, res.totalcount);
  TestEq(TRUE, RecordExists(SELECT FROM res.results WHERE objectid = testfile->url));


  // First page has robots "noindex, nofollow" -> both pages should not be indexed
  testfw->BeginWork();
  testfile->SetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/consilio/robottest", [ noindex := TRUE, nofollow := TRUE ]);
  testfw->CommitWork();

  newsource->ReindexGroup(ToString(testfile->id), [ rebuild := TRUE, foreground := TRUE ]);

  res := RunConsilioSearch("consilio:testfw_robots", CQParseUserQuery("page"));
  TestEQ(0, res.totalcount);
}

RunTestframework(
    [ PTR TestHTMLIndexing ],
    [ testusers :=
        [ [ login := "sysop", grantrights := [ "system:sysop" ] ]
        ]
    ]);
