<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

RECORD testindex1, testindex2;
//Views straight to the two separate catalogs..
OBJECT cat_index1_view, cat_index2_view;

MACRO Prepare()
{
  FixConsilioIndices([ catalogmask := "webhare_testsuite:testindex" ]);
  OBJECT cat := OpenConsilioCatalog("webhare_testsuite:testindex");
  RECORD builtin_opensearch := SELECT * FROM ListIndexManagers() WHERE isbuiltin;

  testfw->BeginWork();

  //Verify and cleanup from any previously (aborted)? testrun
  FOREVERY(RECORD toremove FROM cat->ListAttachedIndices())
    cat->DetachIndex(toremove.id);
  TestEq(RECORD[], cat->ListAttachedIndices(), `Catalog "${cat->tag}" should have all attachments removed after initial cleanup`);

  INTEGER index1id := cat->AttachIndex(0);
  testindex1 := cat->ListAttachedIndices()[0];
  TestEqLike("c_??*", testindex1.indexname, "test for properly structured builtin indexname");

  STRING index2name := Left(testindex1.indexname, Length(testindex1.indexname) - 3) || "_b2";
  cat->AttachIndex(builtin_opensearch.id, [ indexname := index2name]); //add a second instance.. use initial indexname to still ensure uniqueness
  testindex2 := cat->ListAttachedIndices()[1];

  TestEqMembers([[ id := index1id
                 , searchpriority := 100
                 , indexname := testindex1.indexname
                 ]
                ,[ searchpriority := 0
                 , indexname := index2name
                 ]
                ], cat->ListAttachedIndices(), "*");

  cat_index1_view := CreateConsilioCatalog("consilio:testfw_index1_view", [ managed := FALSE ]);
  cat_index1_view->AttachIndex(testindex1.indexmanager, [ indexname := testindex1.indexname, readonly := TRUE ]);
  cat_index2_view := CreateConsilioCatalog("consilio:testfw_index2_view", [ managed := FALSE ]);
  cat_index2_view->AttachIndex(testindex2.indexmanager, [ indexname := testindex2.indexname, readonly := TRUE ]);
  testfw->CommitWork();

  cat->WaitReady(MAX_DATETIME, [ forstorage := TRUE, forconfiguration := TRUE ]);

  Print(`${cat->GetStorageInfo()}, ${cat_index1_view->GetStorageInfo()}, ${cat_index2_view->GetStorageInfo()}\n`);
  TestEq(testindex1.indexname, cat_index1_view->ListAttachedIndices()[0].indexname);
  TestEq(testindex2.indexname, cat_index2_view->ListAttachedIndices()[0].indexname);
  TestEq(index2name, cat_index2_view->ListAttachedIndices()[0].indexname);

  //FIXME Verify the actual objects appeared in both

  cat->AddObjects([[ objectid := "test0"
                   , documentfields := [ title := "Object 0" ]
                  ]],[ synchronous := TRUE ]);
  TestEqMembers([[ suffix := "" ]], cat->ListSuffixes(), "*");
}

RunTestframework(
    [ PTR Prepare
    ]);
