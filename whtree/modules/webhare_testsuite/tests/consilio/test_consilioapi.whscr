<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::publisher/lib/internal/consiliohandler.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::webhare_testsuite/lib/database.whlib";

MACRO TestConsilioAPI()
{
  testfw->BeginWork();

  //webhare_testsuite:testsitecatalog should always exist
  TestEq("webhare_testsuite:testsitecatalog", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages")->id));
  TestEq("webhare_testsuite:testsitecatalog", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id));

  OBJECT testcatalog1 := CreateConsilioCatalog("consilio:testfw_apitest1");
  OBJECT testcatalog2 := CreateConsilioCatalog("consilio:testfw_apitest2");
  TestEq(TRUE, testcatalog1->ismanaged);
  TestEq(TRUE, SELECT AS BOOLEAN ismanaged FROM ListConsilioCatalogs() WHERE tag = "consilio:testfw_apitest1");

  //having indexnames makes log analysis easier
  Print(`consilio:testfw_apitest1 => ${testcatalog1->indexname}, consilio:testfw_apitest2 => ${testcatalog2->indexname}\n`);

  OBJECT newsource := testcatalog1->AddPublisherContentSource(OpenTestsuiteSite()->OpenByPath("testpages")->id);

  //contentsourcefields need to exist so that we can find these fields when indexing. test_siteprofile_indices will test this at a 'higher' level (verifying title exists)
  //FIXME we're ignoring _score and _summary but those shouldn't exist in the database at all
  TestEq([ [ name := "DATE_WHMODIFICATION" ]
         , [ name := "FILETYPE" ]
         , [ name := "SIZE" ]
         , [ name := "TITLE" ]
         ], SELECT name FROM consilio.contentsourcefields WHERE contentsourceid = newsource->id AND name NOT LIKE "_*" ORDER BY name);

  TestEq(TRUE, RecordExists(SELECT FROM ListPublisherContentSourcesFor(OpenTestsuiteSite()->OpenByPath("testpages/formtest")->id) WHERE catalog = testcatalog1->id AND contentsource = newsource->id));

  OBJECT newsource2 := testcatalog2->AddPublisherContentSource(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id);
  OBJECT newsource3 := testcatalog2->AddPublisherContentSource(OpenTestsuiteSite()->OpenByPath("portal1")->id);
  TestEq("consilio:testfw_apitest2", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id)); //most specific
  TestEq("consilio:testfw_apitest1", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages")->id)); //less sources in catalog

  testfw->CommitWork();

  newsource->WaitForIndexingDone([ verbose := TRUE
                                 , deadline := AddTimeToDate(5 * 60 * 1000,GetCurrentDatetime())
                                 ]);

  RECORD result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("OneOfTheSimpleFiles"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("simpletest.rtd"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("simpletest"));
  TestEq(TRUE, Length(result.results) > 0);
}

MACRO TestForegroundIndexing()
{
  testfw->BeginWork();
  STRING testgroup := "tg_" || generateufs128bitid();
  DELETE FROM webhare_testsuite.consilio_index;
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES(testgroup, testgroup || "obj1", MakeDate(1970, 1, 10), "text__" || testgroup);
  testfw->CommitWork(); //TODO if we can get rid of the subjobs we may should be able to do this without Commitwork

  OBJECT dbindex := OpenConsilioCatalog("webhare_testsuite:testsitecatalog")->OpenContentSource("webhare_testsuite:dbindex");
  dbindex->ReindexGroup(testgroup, [ foreground := TRUE ]);
  RECORD result := RunConsilioSearch("webhare_testsuite:testsitecatalog", CQParseUserQuery("text__" || testgroup));
  TestEq(TRUE, Length(result.results) > 0);
  TestEq(testgroup,result.results[0].groupid);
}

RunTestframework(
    [ PTR TestConsilioAPI
    , PTR TestForegroundIndexing
    ]);
