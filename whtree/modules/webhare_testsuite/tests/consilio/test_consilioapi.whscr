<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";
LOADLIB "mod::publisher/lib/internal/consiliohandler.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::webhare_testsuite/lib/database.whlib";

MACRO TestConsilioAPI()
{
  FixConsilioIndices([ catalogmask := "webhare_testsuite:testsitecatalog" ]);

  testfw->BeginWork();

  //webhare_testsuite:testsitecatalog should always exist
  TestEq("webhare_testsuite:testsitecatalog", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages")->id));
  TestEq("webhare_testsuite:testsitecatalog", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id));

  OBJECT testcatalog1 := CreateConsilioCatalog("consilio:testfw_apitest1", [ priority := 9 ]);
  OBJECT testcatalog2 := CreateConsilioCatalog("consilio:testfw_apitest2", [ priority := 9 ]);
  TestEq(TRUE, testcatalog1->managed);
  TestEq(TRUE, SELECT AS BOOLEAN managed FROM ListConsilioCatalogs() WHERE tag = "consilio:testfw_apitest1");

  //having indexnames makes log analysis easier
  Print(testcatalog1->GetStorageInfo() || ", " || testcatalog2->GetStorageInfo() || "\n");
  TestThrowsLike("*Missing tag for*", PTR testcatalog1->OpenContentSource(""), "Explicitly block for ambiguity");
  TestEq(DEFAULT OBJECT, testcatalog1->OpenContentSource("nosuchsource"));

  OBJECT newsource := testcatalog1->AddFolderToCatalog(OpenTestsuiteSite()->OpenByPath("testpages")->id, [ enabled := FALSE ] );
  TestEq(0, newsource->getsettings().contentcheckinterval);

  //contentsourcefields need to exist so that we can find these fields when indexing. test_siteprofile_indices will test this at a 'higher' level (verifying title exists)
  //FIXME we're ignoring _score and _summary but those shouldn't exist in the database at all
  STRING ARRAY expectedfields := [ "description", "filetype", "keywords", "modificationdate", "size" , "title", "whfspath" ];
  TestEq(expectedfields, SELECT AS STRING ARRAY name FROM testcatalog1->GetExpectedMapping() WHERE name IN expectedfields ORDER BY name);

  TestEq(FALSE, newsource->enabled);
  TestEq(FALSE, RecordExists(SELECT FROM ListPublisherContentSourcesFor(OpenTestsuiteSite()->OpenByPath("testpages/formtest")->id) WHERE catalog = testcatalog1->id AND contentsource = newsource->id));
  newsource->UpdateContentSource([ enabled := TRUE, settings := [ contentcheckinterval := 123456 ]]);
  TestEq(TRUE, newsource->enabled);
  TestEq(TRUE, RecordExists(SELECT FROM ListPublisherContentSourcesFor(OpenTestsuiteSite()->OpenByPath("testpages/formtest")->id) WHERE catalog = testcatalog1->id AND contentsource = newsource->id));
  TestEq(123456, newsource->getsettings().contentcheckinterval);

  OBJECT newsource2 := testcatalog2->AddFolderToCatalog(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id);
  OBJECT newsource3 := testcatalog2->AddFolderToCatalog(OpenTestsuiteSite()->OpenByPath("portal1")->id);
  TestEq("consilio:testfw_apitest2", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages/foldertemplate")->id)); //most specific
  TestEq("consilio:testfw_apitest1", GetConsilioPublisherCatalog(OpenTestsuiteSite()->OpenByPath("testpages")->id)); //less sources in catalog

  TestEq(TRUE, newsource3->enabled);
  newsource3->UpdateContentSource([ enabled := FALSE ]);
  TestEq(FALSE, newsource3->enabled);

  testfw->CommitWork();

  newsource->WaitForIndexingDone([ verbose := TRUE
                                 , deadline := AddTimeToDate(5 * 60 * 1000,GetCurrentDatetime())
                                 ]);

  RECORD result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("OneOfTheSimpleFiles"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("simpletest"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("rtd"));
  TestEq(TRUE, Length(result.results) > 0);

  result := RunConsilioSearch("consilio:testfw_apitest1", CQParseUserQuery("simpletest.rtd"));
  TestEq(TRUE, Length(result.results) > 0);
}

MACRO TestForegroundIndexing()
{
  testfw->BeginWork();
  STRING testgroup := "tg_" || GenerateUFS128BitId();
  DELETE FROM webhare_testsuite.consilio_index;
  INSERT INTO webhare_testsuite.consilio_index(groupid, objectid, indexdate, text)
      VALUES(testgroup, testgroup || "obj1", MakeDate(1970, 1, 10), "text__" || testgroup);
  testfw->CommitWork(); //TODO if we can get rid of the subjobs we may should be able to do this without Commitwork

  OBJECT dbindex := OpenConsilioCatalog("webhare_testsuite:testsitecatalog")->OpenContentSource("webhare_testsuite:dbindex");
  dbindex->ReindexGroup(testgroup, [ foreground := TRUE ]);
  RECORD result := RunConsilioSearch("webhare_testsuite:testsitecatalog", CQParseUserQuery("text__" || testgroup));
  TestEq(TRUE, Length(result.results) > 0);
  TestEq(testgroup,result.results[0].groupid);
}

RunTestframework(
    [ PTR TestConsilioAPI
    , PTR TestForegroundIndexing
    ]);
