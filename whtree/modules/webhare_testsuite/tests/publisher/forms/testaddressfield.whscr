<?wh

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/testfw/forms.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

PUBLIC MACRO TestAddressField()
{
  OBJECT testfile := OpenTestsuiteSite()->OpenBypath("testpages/formtest");
  OBJECT webdesign := GetWebDesign(testfile->id);
  OBJECT addressform := webdesign->OpenForm("addressform");
  INSERT CELL city := "Residence" INTO addressform->^address->fieldtitles;

  RECORD ARRAY addressfields := addressform->^address->GetResultColumns(DEFAULT RECORD);
  TestEQ("Straat", SELECT AS STRING title FROM addressfields WHERE name = "street"); // From worldinfo.xml
  TestEQ("Number and Addition", SELECT AS STRING title FROM addressfields WHERE name = "nr_detail"); // XML title overwrite
  TestEQ("ZIP-code", SELECT AS STRING title FROM addressfields WHERE name = "zip"); // XML tid overwrite
  TestEQ("Residence", SELECT AS STRING title FROM addressfields WHERE name = "city"); // Dynamic title override

  // All address subfields should be exposed through ^<tagname>
  TestEQ(TRUE, MemberExists(addressform->^address, "^country"));
  TestEQ(TRUE, MemberExists(addressform->^address, "^state"));
  TestEQ(FALSE, MemberExists(addressform->^address, "^district"));// Not available in "NL" or "BE"
  TestEQ(TRUE, MemberExists(addressform->^address, "^city"));
  TestEQ("Residence", addressform->^address->^city->title);

  // This is also a great spot to test GetFormField
  TestEq(addressform->^address, addressform->GetFormField("address"));
  TestEq(addressform->^address, addressform->GetFormField("ADDRESS"));
  TestEq(DEFAULT OBJECT, addressform->GetFormField("^ADDRESS"));
  TestEq(addressform->^address->^country, addressform->GetFormField("address.country"));

  TestEq(TRUE, testfw->browser->GotoWebPage(GetPrimaryWebhareInterfaceURL() || "testoutput/webhare_testsuite.testsite/testpages/formtest/?address=1"));
  OBJECT tester := OpenFormsapiFormTester(testfw->browser);
  RECORD fields := DEFAULT RECORD;
  RECORD formresult := tester->SubmitForm(fields);

  TestEQMembers(
      [ errors :=     [ [ message :=    "Dit veld is verplicht."
                        , name :=       "address.country"
                        ]
                      , [ message :=    "Dit veld is verplicht."
                        , name :=       "address2.country"
                        ]
                      ]
      , result :=     [ address :=      DEFAULT RECORD ]
      ], formresult, "*");

  fields := [ "address.country" := "NL" ];
  formresult := tester->SubmitForm(fields);;

  TestEQMembers(
      [ [ name := "address.nr_detail",  message := "Dit veld is verplicht." ]
      , [ name := "address.zip",        message := "Dit veld is verplicht." ]
      , [ name := "address2.country",   message := "Dit veld is verplicht." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // Only NL fields?
  TestEQ([ country := "NL", city := "", nr_detail := "", street := "", zip := "", state := "" ], formresult.result.address);

  // no errors when invisible
  INSERT CELL visiblefields := VARIANT[] INTO fields;
  formresult := tester->SubmitForm(fields);;
  TestEQ([ [ name := "address2.country", metadata := DEFAULT RECORD, message := "Dit veld is verplicht." ] ], formresult.errors);
  fields.visiblefields := INTEGER[1];

  INSERT CELL visiblegroup := VARIANT[] INTO fields;
  formresult := tester->SubmitForm(fields);;
  TestEQ(VARIANT[], formresult.errors);
  fields.visiblegroup := INTEGER[1];

  // no errors when disabled
  INSERT CELL enablefields := VARIANT[] INTO fields;
  formresult := tester->SubmitForm(fields);;
  TestEQ([ [ name := "address2.country", metadata := DEFAULT RECORD, message := "Dit veld is verplicht." ] ], formresult.errors);

  fields.enablefields := INTEGER[1];
  INSERT CELL enablegroup := VARIANT[] INTO fields;
  formresult := tester->SubmitForm(fields);;
  TestEQ(VARIANT[], formresult.errors);
  fields.enablegroup := INTEGER[1];

  // still errors when all visible + enabled
  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.nr_detail",  message := "Dit veld is verplicht." ]
      , [ name := "address.zip",        message := "Dit veld is verplicht." ]
      , [ name := "address2.country",   message := "Dit veld is verplicht." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "296"
                      , "address.zip" :=        "7521AM"
                      , "address.city" :=       ""
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQ([ [ name := "address2.country", metadata := DEFAULT RECORD, message := "Dit veld is verplicht." ] ], formresult.errors);
  TestEQ(
      [ country :=    "NL"
      , street :=     "Hengelosestraat"
      , nr_detail :=  "296"
      , zip :=        "7521 AM"
      , city :=       "Enschede"
      , state :=      ""
      ], formresult.result.address);

  // Regression: The 'address' field has the 'nl-zip-force' option activated, so street and city are not editable. They
  // should be update however if zip and nr_detail have changed.
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     "Teststraat"
                      , "address.nr_detail" :=  "296"
                      , "address.zip" :=        "7521AM"
                      , "address.city" :=       "PLAATSNAAM"
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQ(
      [ country :=    "NL"
      , street :=     "Hengelosestraat"
      , nr_detail :=  "296"
      , zip :=        "7521 AM"
      , city :=       "Enschede"
      , state :=      ""
      ], formresult.result.address);

  // Regression: PL address missing nr_detail
  // should be update however if zip and nr_detail have changed.
  fields := [ "address2.country" :=    "PL"
            , "address2.street" :=     "In Polen"
            , "address2.zip" :=        "12-345"
            , "address2.city" :=       "Warsaw"
            ];

  formresult := tester->SubmitForm(fields);
  dumpvalue(formresult.result);
  TestEQ(
      [ country :=    "PL"
      , street :=     "In Polen"
      , nr_detail :=  ""
      , zip :=        "12-345"
      , city :=       "Warsaw"
      , state :=      ""
      ], formresult.result.value.address2);

  // zip not found error
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "2" // zip not found
                      , "address.zip" :=        "7500 OO"
                      , "address.city" :=       ""
                      , "address2.country" :=   "NL"
                      , "address2.street" :=    ""
                      , "address2.nr_detail" := "2" // zip not found
                      , "address2.zip" :=       "7500 OO"
                      , "address2.city" :=      ""
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.zip",        message := "Combinatie van postcode en huisnummer komt niet voor." ]
      , [ name := "address2.city",      message := "Dit veld is verplicht." ]
      , [ name := "address2.street",    message := "Dit veld is verplicht." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // zip not found error
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "2" // zip not found
                      , "address.zip" :=        "7500 OO"
                      , "address.city" :=       ""
                      , "address2.country" :=   "NL"
                      , "address2.street" :=    "Straat"
                      , "address2.nr_detail" := "2" // zip not found - but accepted as a suggestion may be ignored
                      , "address2.zip" :=       "7500 OO"
                      , "address2.city" :=      "Stad"
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.zip",        message := "Combinatie van postcode en huisnummer komt niet voor." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // address not found error
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "3" // address not found
                      , "address.zip" :=        "7500 OO"
                      , "address.city" :=       ""
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.zip",        message := "Het adres kon niet worden gevonden." ]
      , [ name := "address2.country",   message := "Dit veld is verplicht." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // invalid zip error
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "5" // simulates invalid zip code
                      , "address.zip" :=        "7500 OO"
                      , "address.city" :=       ""
                      , "address2.country" :=    "NL"
                      , "address2.street" :=     "Straat"
                      , "address2.nr_detail" :=  "5" // may still trigger zip code, 'somehow' the providers know even in offline mode. also allows you to test handling of this situation without switching to nl-zip-force
                      , "address2.zip" :=        "7500 OO"
                      , "address2.city" :=       "Stad"
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.zip",  message := "Ongeldige postcode." ]
      , [ name := "address2.zip", message := "Ongeldige postcode." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // real invalid zip (syntax, requires no lookups)
  fields :=  [ "address.country" :=    "NL"
                      , "address.street" :=     ""
                      , "address.nr_detail" :=  "100"
                      , "address.zip" :=        "7500" //syntax error
                      , "address.city" :=       ""
                      , "address2.country" :=    "NL"
                      , "address2.street" :=     ""
                      , "address2.nr_detail" :=  "100"
                      , "address2.zip" :=        "7500" //real invalid zipcode
                      , "address2.city" :=       ""
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.zip",        message := "Ongeldige postcode." ]
      , [ name := "address2.city",      message := "Dit veld is verplicht." ]
      , [ name := "address2.street",    message := "Dit veld is verplicht." ]
      , [ name := "address2.zip",       message := "Ongeldige postcode." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  fields."address.zip" := "7521 AM";
  //For some reason a lonely bad ZIP was't rejected if all other fields are good for a nl-zip-suggest
  fields."address2.city" := "Stad";
  fields."address2.street" := "Straat";

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address2.zip",       message := "Ongeldige postcode." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  fields."address2.city" := "Stad";
  fields."address2.street" := "Straat";

  //We also need to reject overlong nr_details because they point to someone pasting a phonenumber into a nr_detail
  fields."address2.city" := "Stad";
  fields."address2.street" := "Straat";

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address2.zip",       message := "Ongeldige postcode." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  //Some NL incorrect housenumber are also easy to reject
  fields."address2.zip" := "7521 AM";
  fields."address2.nr_detail" := "Eh";

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address2.nr_detail",       message := "Ongeldig huisnummer." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  fields."address2.zip" := "7521 AM";
  fields."address2.nr_detail" := "31533053003";
  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address2.nr_detail",       message := "Ongeldig huisnummer." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // real invalid zip - BE
  fields :=  [ "address2.country" :=    "BE"
                      , "address2.street" :=     "Brusselsestraat"
                      , "address2.nr_detail" :=  "296"
                      , "address2.zip" :=        "7521PA" // invalid zip code
                      , "address2.city" :=       "Brussel"
                      ];

  formresult := tester->SubmitForm(fields);;

  TestEQMembers(
      [ [ name := "address.country",   message := "Dit veld is verplicht." ]
      , [ name := "address2.zip",      message := "Ongeldige postcode." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");

  // incorrect city names
  fields :=  [ "address2.country" :=    "BE"
                      , "address2.street" :=     "Baron Opsomerlaan"
                      , "address2.nr_detail" :=  "Lier"
                      , "address2.zip" :=        "2500"
                      , "address2.city" :=       "1"
                      ];

  formresult := tester->SubmitForm(fields);;
  TestEQMembers(
      [ [ name := "address.country",   message := "Dit veld is verplicht." ]
      , [ name := "address2.city",     message := "Het adres kon niet worden gevonden." ]
      ], (SELECT * FROM formresult.errors ORDER BY name), "*");
}

RunTestframework([ PTR TestAddressField
                 ]);
