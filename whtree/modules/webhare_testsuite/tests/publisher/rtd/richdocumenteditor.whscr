<?wh

LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";


OBJECT FUNCTION Rte()
{
  RETURN TT("doceditor")->contents->rte;
}

BOOLEAN FUNCTION OnCanView(OBJECT fsobject)
{
  RETURN TRUE;
}

MACRO OnView(OBJECT openhandler, OBJECT fsobject, BOOLEAN autosave DEFAULTSTO FALSE)
{
  openhandler->SendUrl("https://example.org/");
}

MACRO Create()
{
  SetTidLanguage("debug");
  testfw->BeginWork();

  testfw->SetupTestWebdesignWebsite();

  // Grant read-only right on the test site to user 'reader'
  testfw->GetUserObject("writer")->UpdateGrant("grant", "system:fs_browse", testfw->GetTestSite()->id, testfw->GetUserObject("reader"));

  testfw->CommitWork();
}

OBJECT ASYNC FUNCTION TestReadOnly()
{
  BOOLEAN clicked;

  testfw->BeginWork();
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT testfile := siteroot->CreateFile(
      [ name :=   "readonly.rtd"
      , type :=   OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile")->id
      , title :=  "Readonly"
      ]);
  testfw->CommitWork();


  //////////////////////////////////////////////////////////////////////////////
  /// writer

  // Open rich document editor
  testfw->SetTestUser("writer");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // It should be writeable
  TestEq(FALSE, topscreen->contexts->editdocumentapi->readonly);

  // View should not be available
  RECORD openres := ExecuteWindowOpenAction(TT("viewpublished"));
  TestEq("cancel", openres.type);

  // Test if we can make view explicitly available
  topscreen->contexts->editdocumentapi->oncanview := PTR OnCanView;
  topscreen->contexts->editdocumentapi->onview := PTR OnView;
  openres := ExecuteWindowOpenAction(TT("viewpublished"));
  TestEq("url", openres.type);
  TestEq("https://example.org/", openres.url);

  // Reset custom view availability
  topscreen->contexts->editdocumentapi->oncanview := DEFAULT MACRO PTR;
  topscreen->contexts->editdocumentapi->onview := DEFAULT MACRO PTR;
  openres := ExecuteWindowOpenAction(TT("viewpublished"));
  TestEq("cancel", openres.type);

  // Add some text and save draft
  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is the document</p>');
  RTE()->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?
  clicked := AWAIT ExpectNoScreenChange(PTR topscreen->^save->TolliumClick());
  TestEq(TRUE, clicked);

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);

  testfile := OpenWHFSObject(testfile->id);
  TestEq(DEFAULT DATETIME, testfile->firstpublishdate);

  // TODO - test this with a dataFile: TestAssert(testfile->contentmodificationdate > testfile->firstpublishdate, "An unpublished draft save edits the file itself. So this should update contentModified");
  // Saving an unpublished RTD just creates a new draft now since WH5.9, just like saving against a published RTD would. so no change to testfile
  TestEq(DEFAULT DATETIME, testfile->contentmodificationdate);

  //////////////////////////////////////////////////////////////////////////////
  /// reader

  // Open rich document editor
  testfw->SetTestUser("reader");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // It should be read-only
  TestEq(TRUE, topscreen->contexts->editdocumentapi->readonly);
  //TestEQLike("*This is the document*", BlobToString(RTE()->value.htmltext));
  // A readonly user cannot see drafts
  TestEQ(FALSE, RecordExists(RTE()->value));

  // Save and publish should not be available
  clicked := AWAIT ExpectNoScreenChange(PTR TTClick("save", [ allowfailure := TRUE ]));
  TestEq(FALSE, clicked);
  clicked := AWAIT ExpectNoScreenChange(PTR TTClick("publish", [ allowfailure := TRUE ]));
  TestEq(FALSE, clicked);

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);


  //////////////////////////////////////////////////////////////////////////////
  /// writer

  // Open rich document editor
  testfw->SetTestUser("writer");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // Publish the document
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TT("publish")->TolliumClick(), [ messagemask := "*confirmsaveandpublish*" ]);

  testfile := OpenWHFSObject(testfile->id);
  TestEq(TRUE, testfile->publish);
  TestAssert(DEFAULT DATETIME != testfile->firstpublishdate);
  TestEq(testfile->firstpublishdate, testfile->contentmodificationdate);

  // Test custom preview handler
  topscreen->contexts->editdocumentapi->onpreview := PTR OnView;
  openres := ExecuteWindowOpenAction(TT("previewdraft"));
  TestEq("url", openres.type);
  TestEq("https://example.org/", openres.url);
  topscreen->contexts->editdocumentapi->onpreview := DEFAULT MACRO PTR;

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);


  //////////////////////////////////////////////////////////////////////////////
  /// reader

  // Open rich document editor
  testfw->SetTestUser("reader");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // We should now see the published document
  TestEQLike("*This is the document*", BlobToString(RTE()->value.htmltext));

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);


  //////////////////////////////////////////////////////////////////////////////
  /// writer

  // Open rich document editor
  testfw->SetTestUser("writer");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // Edit the text and save draft
  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is the draft</p>');
  RTE()->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?
  clicked := AWAIT ExpectNoScreenChange(PTR topscreen->^save->TolliumClick());
  TestEq(TRUE, clicked);

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);

  OBJECT testfile_state2 := OpenWHFSObject(testfile->id);
  //verify no change yet
  TestEq(testfile->firstpublishdate, testfile_state2->firstpublishdate);
  TestEq(testfile->contentmodificationdate, testfile_state2->contentmodificationdate);

  //////////////////////////////////////////////////////////////////////////////
  /// reader

  // Open rich document editor
  testfw->SetTestUser("reader");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // We should still see the published document
  TestEQLike("*This is the document*", BlobToString(RTE()->value.htmltext));

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);


  //////////////////////////////////////////////////////////////////////////////
  /// writer

  // Open rich document editor
  testfw->SetTestUser("writer");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ], target := DEFAULT RECORD ]));

  // Publish the document
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TT("publish")->TolliumClick(), [ messagemask := "*confirmsaveandpublish*" ]);

  // Close rich document editor
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);

  testfile_state2 := OpenWHFSObject(testfile->id);
  TestEq(testfile->firstpublishdate, testfile_state2->firstpublishdate);
  TestAssert(testfile_state2->contentmodificationdate > testfile_state2->firstpublishdate);

  RETURN DEFAULT RECORD;
}


RunTestFramework(
    [ PTR Create
    , PTR TestReadOnly
    ],
    [ testusers := [ [ login := "writer", grantrights := ["system:sysop"] ]
                   , [ login := "reader", grantrights := DEFAULT STRING ARRAY ]
                   ]
    , debug := TRUE
    ]);
