<?wh


LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";


LOADLIB "mod::tollium/lib/testframework.whlib";

OBJECT testfile;

OBJECT FUNCTION Rte()
{
  RETURN TT("doceditor")->contents->rte;
}

MACRO Create()
{
  testfw->BeginWork();
  //We need an unpublished site for this test
  testfw->SetupTestWebdesignWebsite("", [ enableoutput := FALSE ]);
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  testfile := siteroot->CreateFile(
      [ name :=   "testfile.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      ]);
  testfw->CommitWork();
}

ASYNC FUNCTION TestSave()
{
  testfw->SetTestUser("sysop");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ]  ]));

  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is the document</p>');
  RTE()->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?

  Testeq(FALSE, OpenWHFSObject(testfile->id)->publish);
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TT("publish")->TolliumClick());
  AWAIT ExpectAndAnswerMessageBox("ok", DEFAULT MACRO PTR, [ messagemask := "*has been prepared for publishing*"]);
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel);
  Testeq(TRUE, OpenWHFSObject(testfile->id)->publish);

  RETURN TRUE;
}


RunTestFramework(
    [ PTR Create
    , PTR TestSave
    ],
    [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                   ]
    ]);
