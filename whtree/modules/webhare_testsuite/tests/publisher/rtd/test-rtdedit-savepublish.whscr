<?wh


LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/dialogs.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


OBJECT FUNCTION Rte()
{
  RETURN TT("doceditor")->contents->rte;
}
RECORD ARRAY FUNCTION GetRTEInstructions()
{
  RETURN SELECT * FROM GetTestController()->GrabInstructions() WHERE instr IN ["update","component"] AND target = RTE()->toddname;
}

ASYNC FUNCTION TestMetaFields(INTEGER round, BOOLEAN readonly) //shared MetaTabs and ObjectProps stuff
{
  OBJECT anumberfield :=  TT(":A number field");
  TestAssert(ObjectExists(anumberfield));
  TestEq("integer", anumberfield->valuetype);
  TestEq(42 + round - 1, anumberfield->value);

  OBJECT selectuserfield := TT(":WH User"); //note that this finds the embedded textedit
  TestAssert(ObjectExists(selectuserfield));
  TestEq(FALSE, selectuserfield->enabled);
  TestEq('Sysop McTestsuite', selectuserfield->value);

  IF(readonly)
  {
    TestEq(FALSE, anumberfield->enabled);
    TestEq(FALSE, selectuserfield->enabled);
  }
  ELSE
  {
    // Update and save props
    anumberfield->value := anumberfield->value + 1;
    anumberfield->SetDirty(); //pretend the client did it
  }


  RETURN CELL [anumberfield];
}

ASYNC MACRO TestMetaTab()
{
  testfw->BeginWork();

  OBJECT testfile := GetTestsuiteJSTempFolder()->CreateFile(
      [ name :=   "testmetatab.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      , publish := TRUE
      ]);
  testfile->SetInstanceData("webhare_testsuite:baseTest.baseTestProps",
    [ number_field := 42,
      wh_user := testfw->GetUserObject("sysop")->entity->guid ]);

  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  TestEq(FALSE, topscreen->frame->flags.canpublish);

  AWAIT TestMetaFields(1, FALSE);

  TestEq(TRUE, topscreen->frame->flags.isdirty, "Verifies dirtylisteners are connected");
  TestEq(TRUE, topscreen->frame->flags.canpublish, "Should be allowed to publish now");

  TTClick(":Save");

  RECORD ARRAY autosaves := ListUserWHFSDrafts(GetEffectiveUserID());
  TestEq(1,Length(autosaves));
  TestEqMembers([ number_field := 43 ], OpenWHFSType("webhare_testsuite:baseTest.baseTestProps")->GetInstanceData(autosaves[0].id), "*");
  TestEqMembers([ number_field := 42 ], OpenWHFSType("webhare_testsuite:baseTest.baseTestProps")->GetInstanceData(testfile->id), "*");

  AWAIT ExpectAndAnswerMessageBox("YES", PTR TTClick("Publish"), [ awaitcall := TRUE ]); //confirm save and publish

  autosaves := ListUserWHFSDrafts(GetEffectiveUserID());
  TestEq(RECORD[], autosaves);

  TestEqMembers([ number_field := 43 ], OpenWHFSType("webhare_testsuite:baseTest.baseTestProps")->GetInstanceData(testfile->id), "*");

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Verify that objectprops works (not really out of scope - in the future objectprops will have to cooperate with the RTD drafts)
  AWAIT ExpectScreenChange(+1, PTR RunFSObjectPropertiesDialog(GetTestController(), testfile->id));
  AWAIT TestMetaFields(2, FALSE);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEqMembers([ number_field := 44 ], OpenWHFSType("webhare_testsuite:baseTest.baseTestProps")->GetInstanceData(testfile->id), "*");
}

ASYNC MACRO TestRevert()
{
  testfw->BeginWork();

  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile(
      [ name :=   "testrevert.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      , publish := TRUE
      ]);
  testfile->SetInstanceData("http://www.webhare.net/xmlns/publisher/richdocumentfile"
                           , [ data := [ htmltext := StringToBlob(`<html><body><p class="normal">Initial text</p></body></html>`)
                                       ]
                             ]);

  testfw->CommitWork();

  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ]  ]));

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is another text</p>');
  RTE()->__debug_simulatedirty();
  TestEq(TRUE, topscreen->frame->flags.isdirty);

  //Save as draft
  TTClick(":Save");
  TestEq(FALSE, topscreen->frame->flags.isdirty);

  //Revert back to original
  GetRTEInstructions(); //flush so far
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick(":Cancel draft"), [ messagemask := "*revert*last*"]); //Are you sure you want to revert the draft document to the last published version?
  AWAIT ExpectAndAnswerMessageBox("ok", DEFAULT MACRO PTR, [ messagemask := "*document*reverted*"]); //The draft version has been cancelled - the document is reverted to the latest published version.

  RECORD ARRAY rtes := GetRTEInstructions();
  TestEq(TRUE, Length(rtes) > 0, "RTE should have sent an update event reverting the content and not optimized it away");

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  TestEQLike("*Initial text*", BlobToString(RTE()->value.htmltext), "Original text should have returned");

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestSaveToUnpublishedSite()
{
  testfw->BeginWork();
  //We need an unpublished site for this test - you need to be able to 'prepare' documents without output so we cant use the testsuite-site
  testfw->SetupTestWebdesignWebsite([ enableoutput := FALSE ]);
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT testfile := siteroot->CreateFile(
      [ name :=   "testfile.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      ]);
  testfw->CommitWork();

  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ]  ]));

  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is the document</p>');
  RTE()->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?

  Testeq(FALSE, OpenWHFSObject(testfile->id)->publish);
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TT("publish")->TolliumClick());
  AWAIT ExpectAndAnswerMessageBox("ok", DEFAULT MACRO PTR, [ messagemask := "*has been prepared for publishing*"]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  Testeq(TRUE, OpenWHFSObject(testfile->id)->publish);
}

ASYNC MACRO TestReopenDeletedFile()
{
  //TODO also test History at some point
  testfw->BeginWork();
  OBJECT testfile := GetTestsuiteJSTempFolder()->OpenByNAme("testmetatab.rtd");
  testfile->RecycleSelf();
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));
  AWAIT TestMetaFields(3, TRUE);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //TODO also test Properties dialog, but that one currently doesn't render SP tabs at all in the recyclebin or for any readonly object
}

RunTestFramework(
    [ PTR TestMetaTab
    , PTR TestRevert
    , PTR TestSaveToUnpublishedSite
    , PTR TestReopenDeletedFile
    ],
    [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                   ]
    ]);
