<?wh


LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/preview.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

OBJECT FUNCTION Rte()
{
  RETURN TT("doceditor")->contents->rte;
}
RECORD ARRAY FUNCTION GetRTEInstructions()
{
  RETURN SELECT * FROM GetTestController()->GrabInstructions() WHERE instr IN ["update","component"] AND target = RTE()->toddname;
}

INTEGER round;
ASYNC FUNCTION TestMetaFields(BOOLEAN readonly, BOOLEAN isprops) //shared MetaTabs and ObjectProps stuff
{
  //naming is inconsistent here
  STRING metatitletitle := isprops ? ":SEO Title" : ":Meta title";
  TestEq(TRUE,  TTIsVisible(metatitletitle));

  // TestEq(FALSE,  TTIsVisible(":Description")); //FIXME TTISVISIBLE doesn't work? But if I don't make it invisible, it does find it.
  // TestEq(FALSE,  TTIsVisible(":Keywords"));
  TestEq("Se-o, se-e-e-e-o!", TT(metatitletitle)->value);

  OBJECT anumberfield :=  TT(":A number field");
  TestAssert(ObjectExists(anumberfield));
  TestEq("integer", anumberfield->valuetype);
  TestEq(42 + round, anumberfield->value);

  OBJECT selectuserfield := TT(":Username"); //note that this finds the embedded textedit
  TestAssert(ObjectExists(selectuserfield));
  TestEq(FALSE, selectuserfield->enabled);
  TestEq('Sysop McTestsuite', selectuserfield->value);

  IF(readonly)
  {
    TestEq(FALSE, anumberfield->enabled);
    TestEq(FALSE, selectuserfield->enabled);
  }
  ELSE
  {
    // Update and save props
    anumberfield->value := anumberfield->value + 1;
    anumberfield->SetDirty(); //pretend the client did it
  }

  RECORD multifieldline := SELECT * FROM anumberfield->parent->__ExplainLines() WHERE linetitle = "Multi field";
  TestAssert(RecordExists(multifieldline));
  TestEq(3, Length(multifieldline.items));
  TestEq(10 + round*2, multifieldline.items[0].obj->value);
  TestEq(round % 2 = 1, multifieldline.items[1].obj->value); //Cbox is initally off, then toggles each round
  TestEq(multifieldline.items[1].obj, multifieldline.items[2].obj->enabled_on_condition.field, 'textedit should point to cbox for enabling');
  TestEq(round % 2 = 1 AND NOT readonly, multifieldline.items[2].obj->AllowExternalUpdates(), 'allowexternal updates should match cbox state!');
  TestEq(20 + round*3, multifieldline.items[2].obj->value);

  IF(readonly)
  {
    TestEq(FALSE, multifieldline.items[0].obj->enabled);
    TestEq(FALSE, multifieldline.items[2].obj->enabled);
  }
  ELSE
  {
    // Update and save props
    multifieldline.items[0].obj->value := multifieldline.items[0].obj->value + 2;
    multifieldline.items[1].obj->value := NOT multifieldline.items[1].obj->value;
    multifieldline.items[2].obj->value := multifieldline.items[2].obj->value + 3;
    multifieldline.items[0].obj->SetDirty(); //pretend the client did it
    multifieldline.items[2].obj->SetDirty(); //pretend the client did it
  }

  TestEq(TT(":Check it"), TT(":extControlledField")->enabled_on_condition.field, 'extControlledField should point to Check it for enabling');

  IF(NOT readonly)
    round := round + 1;

  RETURN CELL [anumberfield];
}

ASYNC MACRO TestMetaTab()
{
  testfw->BeginWork();

  OBJECT testfile := GetTestsuiteJSTempFolder()->CreateFile(
      [ name :=   "testmetatab.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      , title := "My document"
      , publish := TRUE
      ]);
  testfile->SetInstanceData("webhare_testsuite:base_test.base_test_props",
    [ number_field := 42,
      wh_user := testfw->GetUserObject("sysop")->entity->guid,
      multi_field := [ num1 := 10, num2 := 20 ]
      ]);
  testfile->SetInstanceData("http://www.webhare.net/xmlns/publisher/seosettings", [ seotitle := "Se-o, se-e-e-e-o!" ]);

  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  TestEq(FALSE, topscreen->frame->flags.canpublish);
  TestEq(TRUE, TT(":Name")->enabled);
  TestEq("testmetatab.rtd", TT(":Name")->value);
  TestEq("My document", TT(":Title")->value);
  TT(":Name")->value := "testmetatab2.rtd";
  TT(":Title")->value := "My updated document";

  AWAIT TestMetaFields(FALSE, FALSE);

  TestEq(TRUE, topscreen->frame->flags.isdirty, "Verifies dirtylisteners are connected");
  TestEq(TRUE, topscreen->frame->flags.canpublish, "Should be allowed to publish now");

  TTClick(":Save");

  RECORD ARRAY autosaves := ListUserWHFSDrafts(GetEffectiveUserID());
  TestEq(1,Length(autosaves));

  INTEGER currentdraft := autosaves[0].id;
  TestEqMembers([ number_field := 43 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(currentdraft), "*");
  TestEqMembers([ number_field := 42 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");

  TestEq([ name := "testmetatab2.rtd", title := "My updated document", keywords := "", description := "", data := DEFAULT BLOB ], OpenWHFSDraft(autosaves[0].id)->GetMetadata());

  //Make a change, not saving or publishing but checking through My changes
  AWAIT TestMetaFields(FALSE, FALSE);
  { //click mychanges and test whether the change is there
    RECORD mychanges := ExecuteWindowOpenAction(TT("previewcurrentcontent"));
    RECORD previewinfo := ParsePreviewURL(mychanges.url);

    TestEqMembers([ number_field := 44 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(previewinfo.id), "*");
    TestEqMembers([ number_field := 43 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(currentdraft), "*");
    TestEqMembers([ number_field := 42 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");
  }

  //Make a second change, not saving or publishing but checking through My changes
  AWAIT TestMetaFields(FALSE, FALSE);
  { //click mychanges and test whether the change is there
    RECORD mychanges := ExecuteWindowOpenAction(TT("previewcurrentcontent"));
    RECORD previewinfo := ParsePreviewURL(mychanges.url);

    TestEqMembers([ number_field := 45 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(previewinfo.id), "*");
    TestEqMembers([ number_field := 43 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(currentdraft), "*");
    TestEqMembers([ number_field := 42 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");
  }

  TTClick(":Save"); //save again

  //Restart RTD
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));

  TestEq("testmetatab2.rtd", TT(":Name")->value);
  TestEq("My updated document", TT(":Title")->value);
  AWAIT TestMetaFields(FALSE, FALSE); //verifies the fields but also increases number_Field again

  AWAIT ExpectAndAnswerMessageBox("YES", PTR TTClick("Publish"), [ awaitcall := TRUE ]); //confirm save and publish

  autosaves := ListUserWHFSDrafts(GetEffectiveUserID());
  TestEq(RECORD[], autosaves);

  TestEqMembers([ number_field := 46 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");

  //Make yet ANOTHER change
  AWAIT TestMetaFields(FALSE, FALSE);

  { //click mychanges and test whether the change is there
    RECORD mychanges := ExecuteWindowOpenAction(TT("previewcurrentcontent"));
    RECORD previewinfo := ParsePreviewURL(mychanges.url);

    TestEqMembers([ number_field := 47 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(previewinfo.id), "*");
    TestEqMembers([ number_field := 46 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");
  }

  AWAIT ExpectAndAnswerMessageBox("YES", PTR TTClick("Publish"), [ awaitcall := TRUE ]); //confirm save and publish

  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Verify that objectprops works (not really out of scope - in the future objectprops will have to cooperate with the RTD drafts)
  AWAIT ExpectScreenChange(+1, PTR RunFSObjectPropertiesDialog(GetTestController(), testfile->id));
  AWAIT TestMetaFields(FALSE, TRUE);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEqMembers([ number_field := 48 ], OpenWHFSType("webhare_testsuite:base_test.base_test_props")->GetInstanceData(testfile->id), "*");
}

ASYNC MACRO TestRevert()
{
  testfw->BeginWork();

  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile(
      [ name :=   "testrevert.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      , publish := TRUE
      ]);
  testfile->SetInstanceData("http://www.webhare.net/xmlns/publisher/richdocumentfile"
                           , [ data := [ htmltext := StringToBlob(`<html><body><p class="normal">Initial text</p></body></html>`)
                                       ]
                             ]);

  testfw->CommitWork();

  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ]  ]));

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is another text</p>');
  RTE()->__debug_simulatedirty();
  TestEq(TRUE, topscreen->frame->flags.isdirty);

  //Save as draft
  TTClick(":Save");
  TestEq(FALSE, topscreen->frame->flags.isdirty);

  //Revert back to original
  GetRTEInstructions(); //flush so far
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick(":Cancel draft"), [ messagemask := "*revert*last*"]); //Are you sure you want to revert the draft document to the last published version?
  AWAIT ExpectAndAnswerMessageBox("ok", DEFAULT MACRO PTR, [ messagemask := "*document*reverted*"]); //The draft version has been cancelled - the document is reverted to the latest published version.

  RECORD ARRAY rtes := GetRTEInstructions();
  TestEq(TRUE, Length(rtes) > 0, "RTE should have sent an update event reverting the content and not optimized it away");

  TestEq(FALSE, topscreen->frame->flags.isdirty);
  TestEQLike("*Initial text*", BlobToString(RTE()->value.htmltext), "Original text should have returned");

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestPinned()
{
  testfw->BeginWork();
  OBJECT testfile := GetTestsuiteJSTempFolder()->OpenByNAme("testmetatab.rtd");
  testfile->UpdateMetadata([ispinned := TRUE]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));
  TestEq(FALSE, TT(":Name")->enabled);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestSaveToUnpublishedSite()
{
  testfw->BeginWork();
  //We need an unpublished site for this test - you need to be able to 'prepare' documents without output so we cant use the testsuite-site
  testfw->SetupTestWebdesignWebsite([ enableoutput := FALSE ]);
  OBJECT siteroot := testfw->GetTestSite()->rootobject;
  OBJECT testfile := siteroot->CreateFile(
      [ name :=   "testfile.rtd"
      , typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile"
      ]);
  testfw->CommitWork();

  testfw->SetTestUser("sysop");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ ToString(testfile->id) ]  ]));

  RTE()->TolliumWeb_FormUpdate('<p class="normal">This is the document</p>');
  RTE()->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?

  Testeq(FALSE, OpenWHFSObject(testfile->id)->publish);
  DATETIME publishedafter := GetCurrentDatetime();
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TT("publish")->TolliumClick());
  AWAIT ExpectAndAnswerMessageBox("ok", DEFAULT MACRO PTR, [ messagemask := "*has been prepared for publishing*"]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  TestEq(TRUE, OpenWHFSObject(testfile->id)->publish);

  testfile := OpenWHFSObject(testfile->id);
  TestAssert(testfile->firstpublishdate > publishedafter);
  TestEq(testfile->firstpublishdate, testfile->contentmodificationdate);

}

ASYNC MACRO TestReopenDeletedFile()
{
  //TODO also test History at some point
  testfw->BeginWork();
  OBJECT testfile := GetTestsuiteJSTempFolder()->OpenByNAme("testmetatab.rtd");
  testfile->RecycleSelf();
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(testfile->id) ]  ]));
  AWAIT TestMetaFields(TRUE, FALSE);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //TODO also test Properties dialog, but that one currently doesn't render SP tabs at all in the recyclebin or for any readonly object
}

RunTestFramework(
    [ PTR TestMetaTab
    , PTR TestRevert
    , PTR TestPinned
    , PTR TestSaveToUnpublishedSite
    , PTR TestReopenDeletedFile
    ],
    [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                   ]
    ]);
