<?wh


LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


OBJECT FUNCTION Rte()
{
  RETURN TT("doceditor")->contents->rte;
}
RECORD ARRAY FUNCTION GetRTEInstructions()
{
  RETURN SELECT * FROM GetTestController()->GrabInstructions() WHERE instr IN ["update","component"] AND target = RTE()->toddname;
}

ASYNC MACRO TestCreateNew()
{
  testfw->BeginWork();
  OBJECT temp1 := CreateNewTemporary(GetEffectiveUser(), GetTestsuiteJSTempFolder()->id, [ typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile" ]);
  TestEqLike("*autosaves*", temp1->whfspath);

  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ params := [ ToString(temp1->id) ]  ]));
  TestEq(FALSE, topscreen->frame->flags.isdirty);
  TestEq(TRUE, topscreen->frame->flags.canpublish);
  TestEq("New document", topscreen->frame->title);

  TestEq("http://www.webhare.dev/xmlns/basetestjs/defaultrtdtype", rte()->appliedsettings.namespace, "Verify RTD looks configured (HS applytester is looking at the right thing");

  TestEq("", TT(":Name")->value);
  TestEq("", TT(":Title")->value);

  OBJECT anumberfield := TT(":A number field");
  TestAssert(ObjectExists(anumberfield));
  anumberfield->value := 1403;
  anumberfield->SetDirty(); //pretend the client did it. this gives us something to save

  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Save"), [ messagemask := "*title or name*"]);

  TTFillByTitle(":Title", "Allrighty then");
  TestEq("allrighty-then", TT("name")->placeholder);
  TestEq("", TT("name")->value);
  TestEq(FALSE, TT("name")->required);

  TestEq(TRUE, topscreen->frame->flags.isdirty);
  TTClick(":Save");

  TestEq("allrighty-then", TT("name")->value);
  TestEq("", TT("name")->placeholder); //TODO or should it still follow the title?
  TestEq(TRUE, TT("name")->required);
  TestEq("allrighty-then", topscreen->frame->title);

  OBJECT finaldoc := GetTestsuiteJSTempFolder()->OpenByName("allrighty-then");
  TestEQ(TRUE, ObjectExists(finaldoc));
  TestEQ(FALSE, finaldoc->publish);
  TestEq("http://www.webhare.net/xmlns/publisher/richdocumentfile", finaldoc->typens);
  TestEq("Allrighty then", finaldoc->title);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestFramework(
    [ PTR TestCreateNew
    ],
    [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                   ]
    ]);
