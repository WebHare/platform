<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::regex.whlib";
LOADLIB "wh::filetypes/html.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::publisher/lib/internal/siteprofiles/structuredef.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/tests/publisher/rtd/restructuringtests.whlib";

MACRO DoSingleTest(OBJECT structure, STRING input_text, STRING expect_text, STRING title, BOOLEAN fix_input_hs)
{
  OBJECT inputdoc := MakeXMLDocumentFromHTML(StringToBlob(input_text));
  IF(fix_input_hs)
    PrepareInputHS(inputdoc);

  structure->RestructureRichDocument(inputdoc->body);

  OBJECT rewriter := NEW HtmlRewriterContext;
  OBJECT expect := MakeXMLDocumentFromHTML(StringToBlob(expect_text));
  TestEQ(BlobToString(rewriter->GenerateHTML(expect->body)), BlobToString(rewriter->GenerateHTML(inputdoc->body)), title);
}

MACRO PrepareInputHS(OBJECT doc)
{
  FOREVERY(OBJECT widgetnode FROM doc->GetElements("span.wh-rtd-embeddedobject"))
  {
    widgetnode->SetAttribute("data-instanceid", widgetnode->GetAttribute("data-instanceref"));
    widgetnode->RemoveAttribute("data-instanceref");
  }
}

STRING FUNCTION GenerateExpectHS(STRING expect_text)
{
  //Strip table widths and heights as WebHare can't calculate those
  OBJECT expectedhtml := MakeXMLDocumentFromHTML(StringToBlob(`<html><body>${expect_text}</body></html>`));
  FOREVERY(OBJECT stylednode FROM expectedhtml->GetElements("*[style]"))
  {
    STRING style := stylednode->GetAttribute("style");
    style := NEW RegEx("width: [0-9]+px;")->Replace(style, "");
    style := NEW RegEx("height: [0-9]+px;")->Replace(style, "");

    IF(style != "")
      stylednode->SetAttribute("style", style);
    ELSE
      stylednode->RemoveAttribute("style");
  }

  FOREVERY(OBJECT widgetnode FROM expectedhtml->GetElements("span.wh-rtd-embeddedobject"))
  {
    widgetnode->SetAttribute("data-instanceid", widgetnode->GetAttribute("data-instanceref"));
    widgetnode->RemoveAttribute("data-instanceref");
    widgetnode->SetAttribute("class", Substitute(widgetnode->GetAttribute("class"), " wh-rtd-embeddedobject--inline",""));
  }

  //Strip the colgroup, WebHare doesn't bother with those either
  FOREVERY(OBJECT stylednode FROM expectedhtml->GetElements("colgroup"))
    stylednode->parentnode->RemoveChild(stylednode);

  expect_text := expectedhtml->body->innerhtml;

  RETURN expect_text;
}

MACRO StructureTest()
{
  FOREVERY (RECORD test FROM restructuringtests)
  {
    OBJECT structure := NEW RichDocumentStructure;

    OBJECT doc;
    IF (test.structure LIKE "<*")
      doc := MakeXMLDocument(StringToBlob(test.structure));
    ELSE
      doc := MakeXMLDocument(GetWebhareResource(test.structure));

    structure->LoadFROMXML(doc->documentelement, doc->documentelement->namespaceuri);

    STRING expect_text := GenerateExpectHS(test.expect);

    DoSingleTest(structure, test.input, expect_text, test.title || " - input restructure", TRUE);

    // Double restructure should make no changes
    DoSingleTest(structure, expect_text, expect_text, test.title || " - second restructure", FALSE);
  }


  // Test LookupStyle

  OBJECT structure := NEW RichDocumentStructure;
  structure->LoadFromXML(MakeXMLDocument(GetWebhareResource("mod::webhare_testsuite/data/test/custompage.structure.xml"))->documentelement, "http://www.webhare.net/xmlns/tollium/richstructure");
  RECORD test := structure->LookupStyle("ol", "", TRUE);
  TestEq("p", test.containertag);

  structure->LoadFROMXML(MakeXMLDocument(StringToBlob(listtest_structure))->documentelement, "http://www.webhare.net/xmlns/tollium/richstructure");
  test := structure->LookupStyle("ol", "", TRUE);
  TestEq("ol", test.containertag);
}


RunTestframework([ PTR StructureTest ]);
