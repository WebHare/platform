<?wh
LOADLIB "wh::files.whlib";
LOADLIB "mod::publisher/lib/internal/rtd/editable.whlib";
LOADLIB "mod::publisher/lib/internal/rtd/publishable.whlib";
LOADLIB "wh::float.whlib";
LOADLIB "wh::filetypes/html.whlib";
LOADLIB "wh::graphics/canvas.whlib";
LOADLIB "wh::xml/dom.whlib";
LOADLIB "wh::internet/webbrowser.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::xml/xsd.whlib";

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/actions.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/structuredef.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";

LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::consilio/lib/internal/linkreports.whlib";
LOADLIB "mod::tollium/lib/rtd/doc.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";



MACRO RichDocumentTest()
{
  testfw->BeginWork();
  OBJECT site := OpenTestsuiteSite();
  testfw->CommitWork();

  testfw->BeginWork();
  OBJECT basefolder := GetTestsuiteTempFolder();
  OBJECT workfolder := basefolder->EnsureFolder([name := "richdocument"]);
  OBJECT testfile := workfolder->CreateFile([name:="richtest",publish:=TRUE], [ setindex := TRUE ]);

  OBJECT myrichdoc := NEW EditableRichDocument;
  TestEq(TRUE, ObjectExists(myrichdoc->body));

  myrichdoc->ImportFromHTML(DEFAULT BLOB);
  TestEq(TRUE, ObjectExists(myrichdoc->body));
  myrichdoc->ImportFromHTML(StringToBlob("   "));
  TestEq(TRUE, ObjectExists(myrichdoc->body));

  myrichdoc->ImportFromMHTML(testfw->GetModuleTestBlob("webhare_testsuite","system/testdata/b-lex.mht"));

  OBJECT betatype := OpenWHFSType("http://www.webhare.net/xmlns/beta/test");
  RECORD orgrec := myrichdoc->ExportAsRecord();
  TestEq(0, orgrec.embedded[0].rotation);
  TestEq("", orgrec.embedded[0].__blobsource);

  betatype->SetInstanceData(testfile->id, [ richtest := orgrec ]);

  // Rewrite the instance data, see if the ids are stable
  RECORD rec := betatype->GetInstanceData(testfile->id);
  betatype->SetInstanceData(testfile->id, rec);

  RECORD rec2 := betatype->GetInstanceData(testfile->id);
  TestEQ(rec, rec2);

  // See if the stuff is stable
  TestEQ( RecurseDeleteSettingIds(orgrec,DEFAULT STRING ARRAY),  RecurseDeleteSettingIds(rec2.richtest, ["HASH","LINK"]));

  testfw->CommitWork();
}

MACRO RichdocumentStrongEmphasis()
{
  testfw->BeginWork();
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile( [ name := "strong-em.rtd", type := richdoctype->id, publish := TRUE ]);
  richdoctype->SetInstanceData(testfile->id, [ data := [ htmltext := StringToBlob(
    `<html><body>
      <p class="normal">I am <b>bold</b> and <i>cursive</i></p>
    </body></html>`) ]]);

  testfw->CommitWork();

  RECORD publishres := RunFilePublish(testfile->id, FALSE);
  OBJECT doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");
  Testeq(`<p class="normal">I am <strong>bold</strong> and <em>cursive</em></p>`, doc->GetElementById("content")->innerhtml);
}

MACRO ParserApiTest()
{
  testfw->BeginWork();

  OBJECT basefolder := GetTestsuiteTempFolder();
  OBJECT workfolder := basefolder->OpenByPath("richdocument");
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT parserapitestfile := workfolder->CreateFile([name := "parserapitest", type := richdoctype->id, publish:=FALSE]);

  OBJECT myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob('<html><head></head><body>'
                                 || '<h1 class="heading1"><a href="">Ik</a> <a name="ben">ben</a>     \neen <b>heading 1&</b></h1>'
                                 || '<p class="normal"><br/></p>'
                                 || '<p class="normal">Ik ben £€n <b>&lt;normale&gt;</b> <a href="http://b-lex.nl/">b-lex</a> <i>paragraaf</i><br/>met\neen      soft-break en een afbeelding: <img src="cid:imagecid-81400" width="160" height="120" alt="I&amp;G" /></p>'
                                 || '<p class="normal">I\'m a merge field: <b><span data-merge="textContent:yourname"></span></b></p>'
                                 || '<table><colgroup><col style="width:25px"/><col style="width:45px"/></colgroup><tbody>' //FIXME should have class="tabletag" but we lack tabletyping
                                    || '<tr>'
                                      || '<th scope="col"><p data-rtd-anchor="bob" class="normal">Cell 1 para 1</p><p class="normal">Cell 1 para 2</p></th>'
                                      || '<th scope="col"><p class="normal">Cell 2 para 1</p></th>'
                                    || '</tr><tr>'
                                      || '<td><p class="normal">Cell 3</p></td>'
                                      || '<td><br/></td>' //Word->RTD conversion generated lonely BRs, we need to ignore them even if we fix this, as existing docs may still do this
                                    || '</tr>'
                                 || '</tbody></table>'
                                 || '<code class="language-harescript">&lt;?wh   Print("Hello, world")\n</code>'
                                 || '</body></html>')
                 , embedded := [ CELL[...WrapBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"), "bob.jpg", [ extractdominantcolor := TRUE ])
                                 , contentid := "imagecid-81400"
                                 ]
                               ]
                 ]);;

  richdoctype->SetInstanceData(parserapitestfile->id, [data := myrichdoc->ExportAsRecord()]);

  RECORD storeddata := richdoctype->GetInstanceData(parserapitestfile->id).data;
  TestEq(1, Length(storeddata.embedded));
  TestEQ(TRUE, CellExists(storeddata.embedded[0],"__blobsource"));
  TestEQ(0, storeddata.embedded[0].rotation);
  TestEQ(FALSE, storeddata.embedded[0].mirrored);
  TestEQ(DEFAULT RECORD, storeddata.embedded[0].refpoint);
  TestEQ("#EBEDF0", storeddata.embedded[0].dominantcolor);

  OBJECT structure := NEW RichDocumentStructure;
  structure->LoadFromRTDType(GetRTDSettingsForFile(parserapitestfile->id));
  OBJECT parseddoc := MakeParsedRichDocument(storeddata);
  parseddoc->ApplyStructureDefinition(structure);

  RECORD ARRAY outline := parseddoc->GetOutline();
  TestEq(6, Length(outline));
  TestEq("h1", outline[0].tagname);
  TestEq(0, Length(outline[0].rows));
  TestEq("block", outline[0].type);
  TestEq("normal", outline[1].classname);
  TestEq("table", outline[4].tagname);
  TestEq("table", outline[4].type);
  TestEq("", outline[4].classname);
  TestEq(2, Length(outline[4].rows));
  TestEq(2, Length(outline[4].rows[1].cells));

  TestEq(`Ik ben een heading 1&`, parseddoc->ExtractExcerpt("h1"));
  TestEqLike(`Ik ben £€n <normale> b-lex paragraaf\nmet een soft-break en een afbeelding:\nI'm a*`, parseddoc->ExtractExcerpt("p", [ maxlength := 150 ]));
  TestEq(`Ik ben £€n <norma…`, parseddoc->ExtractExcerpt("p", [ maxlength := 18 ]));
  TestEq(`Ik ben £€n <normal`, parseddoc->ExtractExcerpt("p", [ maxlength := 18, addiftruncated := "" ]));
  TestEq(`Ik ben £€n <…`, parseddoc->ExtractExcerpt("p.normal", [ limitutf8bytes := 18 ]));
  TestEq(`Ik ben £€n <nor`, parseddoc->ExtractExcerpt("p.normal", [ limitutf8bytes := 18, addiftruncated := "" ]));
  TestEq(18, Length(parseddoc->ExtractExcerpt("h1", [ limitutf8bytes := 18 ])));
  TestEqLike(`Ik ben een heading 1*world")`, parseddoc->ExtractExcerpt("*"));

  OBJECT parserobject, mergedobject;
  OBJECT myfilter := NEW RichDocumentFilter;
  parserobject := outline[0].richobject;
  //ADDME should probably export <a name=....
  TestEq('<h1 class=\"heading1\">Ik ben een <b>heading 1&amp;</b></h1>', myfilter->GetFiltered(parserobject));
  TestEq(TRUE, parserobject->HasContent());

  parserobject := outline[5].richobject;
  TestEq(TRUE, parserobject->HasContent());
  TestEqLike('<code class="language-harescript">&lt;?wh   Print("Hello, world")\n</code>', myfilter->GetFiltered(parserobject)); //linefeeds inside code blocks should stay

  parserobject := outline[1].richobject;
  TestEq(FALSE, parserobject->HasContent());

  parserobject := outline[2].richobject;
  mergedobject := outline[3].richobject;

  TestEq(TRUE, parserobject->HasContent());
  TestEq(TRUE, mergedobject->HasContent());
  TestEqLike('<p class="normal">Ik ben £€n <b>&lt;normale&gt;</b> <a href="http://b-lex.nl/">b-lex</a> <i>paragraaf</i><br />met een soft-break en een afbeelding: <img class="wh-rtd__img" *&amp;*/></p>', myfilter->GetFiltered(parserobject));
  TestEq('<p class="normal">I\'m a merge field: <b><span data-merge="textContent:yourname"></span></b></p>', myfilter->GetFiltered(mergedobject));

  //note, <br> doesn't really affect tagfilter, we'll ouput it either way
  myfilter->tagfilter := ["p","a-href","i"];
  TestEq('<p class="normal">Ik ben £€n &lt;normale&gt; <a href="http://b-lex.nl/">b-lex</a> <i>paragraaf</i><br />met een soft-break en een afbeelding: I&amp;G</p>', myfilter->GetFiltered(parserobject));
  TestEq('<p class="normal">I\'m a merge field: <span data-merge="textContent:yourname"></span></p>', myfilter->GetFiltered(mergedobject));

  myfilter->tagfilter := ["a-href","b","i"];
  TestEq('Ik ben £€n <b>&lt;normale&gt;</b> <a href="http://b-lex.nl/">b-lex</a> <i>paragraaf</i><br />met een soft-break en een afbeelding: I&amp;G', myfilter->GetFiltered(parserobject));
  TestEq('I\'m a merge field: <b><span data-merge="textContent:yourname"></span></b>', myfilter->GetFiltered(mergedobject));

  myfilter->tagfilter := ["p"];
  TestEq('<p class="normal">Ik ben £€n &lt;normale&gt; b-lex paragraaf<br />met een soft-break en een afbeelding: I&amp;G</p>', myfilter->GetFiltered(parserobject));
  TestEq('<p class="normal">I\'m a merge field: <span data-merge="textContent:yourname"></span></p>', myfilter->GetFiltered(mergedobject));

  myfilter->tagfilter := DEFAULT STRING ARRAY;
  TestEq('Ik ben £€n &lt;normale&gt; b-lex paragraaf<br />met een soft-break en een afbeelding: I&amp;G', myfilter->GetFiltered(parserobject));
  TestEq('I\'m a merge field: <span data-merge="textContent:yourname"></span>', myfilter->GetFiltered(mergedobject));

  TestEq('Ik ben £€n <normale> b-lex paragraaf\nmet een soft-break en een afbeelding: I&G', myfilter->GetRawText(parserobject));
  TestEq('I\'m a merge field: ', myfilter->GetRawText(mergedobject));

  OBJECT output := parseddoc->RenderAllAsHTMLDocument();

  OBJECT tablenode := output->GetElementsByTagName("table")->Item(0);
  OBJECT ARRAY tablerows := tablenode->GetElements("tr");
  TestEq(TRUE, "wh-rtd--hascolheader" IN ParseXSList(tablerows[0]->GetAttribute("class")));
  TestEq(FALSE, "wh-rtd--hascolheader" IN ParseXSList(tablerows[1]->GetAttribute("class")));

  OBJECT ARRAY tablecells := tablenode->GetElements("th,td");
  testEq(4, Length(tablecells));
  TestEq("th", tablecells[0]->nodename);
  TestEq(2, tablecells[0]->GetElementsByTagName("p")->length);
  TestEq(2, tablecells[0]->GetElementsByTagName("p")->length);
  TestEq(1, tablecells[1]->GetElementsByTagName("p")->length);

  testfw->CommitWork();
}

MACRO RichDocInstanceTest()
{
  //FIXME test dat niet geallowede instances weggefilterd worden
  OBJECT site := OpenTestsuiteSite();
  OBJECT workfolder := GetTestsuiteTempFolder();


  OBJECT myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromHTML(StringToBlob('<html><head></head><body>'
                                         || '<h1 class="heading1">Ik ben een <b>heading 1</b></h1>'
                                         || '<h3 class="heading2">Ik ben een <b>heading 2</b></h3>' //this should be coerced into a H2, as CLASS takes precedence over TAG
                                         || '</body></html>'));

  STRING instanceid := myrichdoc->CreateInstance([ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/rtdinstancetest"
                                                 , ignored := "xyz" //should be removed by normalization
                                                 , aref := site->id
                                                 , img := WrapBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"),"bob.jpg")
                                                 ]);

  TestEq([ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/rtdinstancetest"
         , stringdata := ""
         , aref := site->id
         ], CellDelete(myrichdoc->GetInstance(instanceid),'img'));
  TestEq(TRUE, RecordExists(myrichdoc->GetInstance(instanceid).img));

  myrichdoc->SetInstance(instanceid, [ stringdata := "MyString"
                                     ]);


  TestThrowsLike('No such*xyz*', PTR myrichdoc->SetInstance("xyz", [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/rtdinstancetest" ]));

  //Create a reference to the new block
  myrichdoc->body->AppendChild(myrichdoc->CreateInstanceBlockNode(instanceid));

  //Test serialization
  testfw->BeginWork();
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT testfile := workfolder->CreateFile( [ name := "serialized.rtd", type := richdoctype->id, publish := FALSE ]);
  myrichdoc->SaveToWHFS(testfile);
  testfw->CommitWork();

  OBJECT restoreddoc := NEW EditableRichDocument;
  restoreddoc->LoadFromWHFS(testfile);

  RECORD res := restoreddoc->GetInstance(instanceid);
  TestEqMembers([ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/rtdinstancetest"
                , stringdata := "MyString"
                , aref := site->id
                ], res, "*");
  TestEq(TRUE, RecordExists(res.img));
}

MACRO PublishRichDocTableTest()
{
  testfw->BeginWork();

  OBJECT workfolder := GetTestsuiteTempFolder();
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");

  OBJECT richdoc := NEW EditableRichDocument;

  RECORD publishres;

  OBJECT testfile := workfolder->CreateFile( [ name := "tabletest.rtd", type := richdoctype->id, publish := TRUE ]);

  richdoc->ImportFromRecord([ htmltext := StringToBlob(
    `<html>
      <body>
        <table class="mytable">
          <colgroup>
            <col style="width:100px"/>
            <col style="width:100px"/>
            <col style="width:100px"/>
            <col style="width:100px"/>
          </colgroup>
          <tbody>
            <tr>
              <td><p class="normal">This is a normal cell</p></td>
              <td class="wh-rtd__tablecell redpill"><p class="normal">This is a red cell</p></td>
              <td class="bluepill"><p class="normal">This is a blue cell</p></td>
              <td class="greenpill"><p class="normal">This is a green cell</p></td>
            </tr>
          </tbody>
        </table>
      </body>
    </html`)
                 ]);
  richdoc->SaveToWHFS(testfile);
  testfw->CommitWork();

  richdoc->LoadFromWHFS(testfile);
  publishres := RunFilePublish(testfile->id, FALSE);
  //dumpvalue(publishres,'tree');
  //sendblobto(0,publishres.files[1].data);

  TestEq(2, Length(publishres.files));
  OBJECT doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");

  OBJECT ARRAY cells := doc->GetElements("td");
  TestEq(4, Length(cells));
  TestEq("wh-rtd__tablecell", cells[0]->GetAttribute("class"));
  TestEq("wh-rtd__tablecell redpill", cells[1]->GetAttribute("class"));
  TestEq("wh-rtd__tablecell bluepill", cells[2]->GetAttribute("class"));
  TestEq("wh-rtd__tablecell", cells[3]->GetAttribute("class"));
}


MACRO PublishRichDocTest()
{
  testfw->BeginWork();

  OBJECT workfolder := GetTestsuiteTempFolder();
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT emptyfile := workfolder->CreateFile( [ name := "empty.rtd", type := richdoctype->id, publish := TRUE ]);
  testfw->CommitWork();

  RECORD publishres := RunFilePublish(emptyfile->id, FALSE);
  IF(NOT publishres.success) dumpvalue(publishres,'tree');
  TestEq(TRUE, publishres.success);

  OBJECT doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");
  OBJECT content := doc->GetElementById("content");
  //The content should be empty, as the document was empty
  TestEq(0,content->GetElementsByTagName("*")->length);

  /* Note: if we're supposed to have 'h2.heading2', but we encounter 'h3.heading2',
     we assume we have to coerce the H3 into a H2. does not make sense content-wise,
     but too many people think "Typography says H3 is 26px, so we'll make the
     FAQ section header a H3, because that one is defined as 26px" which will
     make it way too common for the ".faqsection" part to be more stable than "h2"

     So when they later change the containertag in the structure for faqsection,
     we want to follow the new tag, and not fall back to defaults */

  //Load up a document
  testfw->BeginWork();
  OBJECT serializedfile := workfolder->OpenByPath("serialized.rtd");
  serializedfile->UpdateMetadata([ publish := TRUE ]);
  testfw->CommitWork();

  publishres := RunFilePublish(serializedfile->id, FALSE);

  doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");
  content := doc->GetElementById("content");

  IF(NOT publishres.success) dumpvalue(publishres,'tree');
  //sendblobto(0,publishres.files[0].data);  dumpvalue(publishres,'tree');

  TestEq(TRUE, publishres.success);

  content := doc->GetElementById("content");
  TestEq(4,content->GetChildElementsByTagName("*")->length);

  OBJECT h1 := content->GetChildElementsByTagName("*")->item(0);
  TestEq("h1", h1->nodename);
  TestEq("Ik ben een heading 1", h1->textcontent);

  //Bold should be stripped, as we don't  permit <b> in a H2
  OBJECT h2 := content->GetChildElementsByTagName("*")->item(1);
  TestEq("h2", h2->nodename);
  TestEq("Ik ben een heading 2", h2->firstchild->nodevalue);

  OBJECT blockcomponent := content->GetChildElementsByTagName("*")->item(2);
  TestEq("blink", blockcomponent->nodename);
  TestEq("MyString", blockcomponent->firstchild->nodevalue);
}

MACRO PublishRichDocImageTest()
{
  testfw->BeginWork();

  OBJECT workfolder := GetTestsuiteTempFolder();
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");

  OBJECT richdoc := NEW EditableRichDocument;

  RECORD publishres;

  OBJECT testfile := workfolder->CreateFile( [ name := "imgtest.rtd", type := richdoctype->id, publish := TRUE ]);

  richdoc->ImportFromRecord([ htmltext := StringToBlob('<html><head></head><body>'
                                            || '<h1 class="heading1">Ik ben een <b>heading 1</b></h1>'
                                            || '<p class="normal">Afbeelding: <img src="cid:imagecid-81400" width="160" height="120"/></p>'
                                            || '<p class="normal">Grotere afbeelding: <img src="cid:imagecid-81400" width="320" height="240"/></p>'
                                            || '<p class="normal">Opgerekte afbeelding: <img src="cid:imagecid-81400" width="320" height="280"/></p>'
                                            || '<p class="normal">alleen verticaal, te groot: <img src="cid:imagecid-81400" height="280"/></p>'
                                            || '<p class="normal">alleen verticaal, binnen grenzen: <img src="cid:imagecid-81400" height="140"/></p>'
                                            || '<p class="normal">alleen horizontaal, te groot: <img src="cid:imagecid-81400" width="320"/></p>'
                                            || '<p class="normal">alleen horizontaal, binnen grenzen: <img src="cid:imagecid-81400" width="120"/></p>'
                                            || '<p class="normal">geen hints: <img src="cid:imagecid-81400" /></p>'
                                            || '<p class="normal">external link: <img src="https://www.webhare.com/media/globalassets/logo-bluecircle-120x120.png" width="600" height="300" /></p>')
                 , embedded := [ MakeMergedRecord(WrapBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"),"bob.jpg"), [ contentid := "imagecid-81400" ]) ]
                 ]);;
  richdoc->SaveToWHFS(testfile);
  testfw->CommitWork();


  richdoc->LoadFromWHFS(testfile);

  RECORD embeddedfile := richdoc->GetEmbeddedFile("cid:imagecid-81400");
  TestEq(TRUE, embeddedfile.__blobsource != "");
  TestEq(0, embeddedfile.rotation);

  publishres := RunFilePublish(testfile->id, FALSE);
//  dumpvalue(publishres,'tree');
//  sendblobto(0,publishres.files[0].data);

  TestEq(2, Length(publishres.files));
  OBJECT doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");

  OBJECT ARRAY imgtags := doc->GetElementsByTagName("img")->GetCurrentElements();
  TestEq(9, Length(imgtags));

  TestEq("160", imgtags[0]->GetAttribute("width"));
  TestEq("120", imgtags[0]->GetAttribute("height"));
  TestEq(TRUE, imgtags[0]->GetAttribute("src") LIKE "/*"); //site relative URL

  OBJECT browser := NEW WebBrowser;
  browser->GotoWebPage(ResolveToAbsoluteURL(testfile->url, imgtags[0]->GetAttribute("src")));

  TestEq("image/jpeg", browser->contenttype);
  OBJECT imgcanvas := CreateCanvasFromBlob(browser->content);
  TestEq(160, imgcanvas->width);
  TestEq(120, imgcanvas->height);

  OBJECT imgsrc := CreateResizedCanvasFromBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"), [ method := "scale", setwidth := 160, setheight := 120 ]);
  imgsrc->SetImageSize(160,120);
  TestEq(160, imgsrc->width);
  TestEq(120, imgsrc->height);


//  CreateDiskfileFromBlob("/tmp/imgcanvas.png", TRUE, FALSE, imgcanvas->ExportAsPNG(TRUE));
//  CreateDiskfileFromBlob("/tmp/imgsrc.png", TRUE, FALSE, imgsrc->ExportAsPNG(TRUE));
  FLOAT diff := imgsrc->CompareWithCanvas(imgcanvas);
  TestEq(TRUE, diff < 93f, "Diff: " || FormatFloat(diff,7)); //ADDME why don't we simply get MSE 0 ?


  //The second image should have bene reduced from 320x240 to 300x225
  TestEq("300", imgtags[1]->GetAttribute("width"));
  TestEq("225", imgtags[1]->GetAttribute("height"));

  //The third image should have bene reduced from 320x260 to 300x244 (broken aspect ratio should have been preserved)
  TestEq("300", imgtags[2]->GetAttribute("width"));
  TestEq("263", imgtags[2]->GetAttribute("height"));

  //The fourth image, had height 280. 373x280 is too big, so it should be reduced to 300x199
  TestEq("300", imgtags[3]->GetAttribute("width"));
  TestEq("199", imgtags[3]->GetAttribute("height"));
  //This one had height 140, that's okay. It should become 211x140
  TestEq("211", imgtags[4]->GetAttribute("width"));
  TestEq("140", imgtags[4]->GetAttribute("height"));

  //Width 320. 300x199 again
  TestEq("300", imgtags[5]->GetAttribute("width"));
  TestEq("199", imgtags[5]->GetAttribute("height"));
  //Width 120. should work out
  TestEq("120", imgtags[6]->GetAttribute("width"));
  TestEq("80", imgtags[6]->GetAttribute("height"));

  //Unspecified. Should reduced to 300x199
  TestEq("300", imgtags[7]->GetAttribute("width"));
  TestEq("199", imgtags[7]->GetAttribute("height"));

  //External image should still be reduced. We don't recommend external images and we don't support them in <richdocument>
  TestEq("300", imgtags[8]->GetAttribute("width"));
  TestEq("150", imgtags[8]->GetAttribute("height"));
  TestEq("https://www.webhare.com/media/globalassets/logo-bluecircle-120x120.png", imgtags[8]->GetAttribute("src"));
}

MACRO IntLinktest()
{
  testfw->BeginWork();

  OBJECT basefolder := GetTestsuiteTempFolder();
  OBJECT workfolder := basefolder->OpenByPath("richdocument");
  OBJECT testfile := workfolder->OpenByPath("richtest");
  OBJECT testextralink := basefolder->CreateFile( [ name := "testextralink", publish := TRUE ]);
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT parserapitestfile := workfolder->OpenByPath("parserapitest");

  TestEq(TRUE, ObjectExists(parserapitestfile));

  //FIXME test whether links are removed when the matching x-richdoclink is gone (who's problem is it? <rte> can't do it though, as it is not supposed to be publisher specific)

  //Create some simple links

  OBJECT myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob('<html><head></head><body>'
                                 || '<p><a href="x-richdoclink:1234-5678-001">Link naar site</a> <a href="x-richdoclink:1234-5678-002">Link naar richtest</a>'
                                 || ' afbeelding: <img src="cid:imagecid-81400" width="160" height="120" alt="I&amp;G" /></p>'
                                 || '<p><a id="boblink" href="x-richdoclink:EXT#goto-bob">Link naar Bob</a></p>'
                                 || '<p><a id="nowherelink" href="x-richdoclink:nowhere#goto-nowhere">Link naar Bob2</a></p>'
                                 || '</body></html>')
                 , embedded := [ MakeMergedRecord(WrapBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"),"bob.jpg"), [ contentid := "imagecid-81400" ]) ]
                 , links := [[ tag := "1234-5678-001", linkref := workfolder->id ]
                            ,[ tag := "nowhere", linkref := whconstant_whfsid_private_rootsettings ]
                            ]
                 ]);;

  myrichdoc->SetLinkRef("x-richdoclink:1234-5678-002", testfile->id);
  myrichdoc->SetLinkRef("x-richdoclink:1234-5678-003", testextralink->id);
  myrichdoc->SetLinkRef("x-richdoclink:EXT", parserapitestfile->id);

  TestEq(0,               myrichdoc->GetLinkRef("x-richdoclink:1234-5678-000"));
  TestEq(workfolder->id,  myrichdoc->GetLinkRef("x-richdoclink:1234-5678-001"));
  TestEq(testfile->id,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));
  TestEq(testextralink->id, myrichdoc->GetLinkRef("x-richdoclink:1234-5678-003"));
  TestEq(parserapitestfile->id, myrichdoc->GetLinkRef("x-richdoclink:EXT"));

  myrichdoc->SetLinkRef("x-richdoclink:1234-5678-002", 0);
  TestEq(0,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));

  myrichdoc->SetLinkRef("x-richdoclink:1234-5678-002", testfile->id);
  TestEq("x-richdoclink:1234-5678-002", myrichdoc->CreateLinkRef(testfile->id));
  TestEq(testfile->id,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));

  //Test simple turnaround
  RECORD exportrec := myrichdoc->ExportAsRecord();
  myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord(exportrec);

  TestEq(0,               myrichdoc->GetLinkRef("x-richdoclink:1234-5678-000"));
  TestEq(workfolder->id,  myrichdoc->GetLinkRef("x-richdoclink:1234-5678-001"));
  TestEq(testfile->id,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));
  TestEq(testextralink->id, myrichdoc->GetLinkRef("x-richdoclink:1234-5678-003"));
  TestEq(parserapitestfile->id, myrichdoc->GetLinkRef("x-richdoclink:EXT"));

  //Commit the document to the database  ADDME need a simpler api to manage richdocs
  OBJECT testfile2 := workfolder->CreateFile([name := "richtest2", type := richdoctype->id, publish:=FALSE]);
  richdoctype->SetInstanceData(testfile2->id, [ data := exportrec ]);

  OBJECT testfile3 := workfolder->CreateFile([name := "richtest3", type := richdoctype->id, publish := TRUE]);
  richdoctype->SetInstanceData(testfile3->id, [ data := exportrec ]);

  //Commit the database
  testfw->CommitWork();

  TestEQ(0,Length(GetLinksForFSObject(testfile2->id)), "Should not pick up any links, they're all internal"); //FIXME are we sure about this? it seems to be related to publish := FALSE actually..

  //Read it right back
  RECORD thedoc := richdoctype->GetInstanceData(testfile2->id).data;
  BLOB docblob := GetRichDocumentAsHtmlFragment(thedoc);
  OBJECT docblobdom := MakeXMLDocumentFromHTML(docblob);
  OBJECT ARRAY links := docblobdom->GetElementsByTagName("a")->GetCurrentElements();

  TestEq(4,Length(links));
  TestEq(workfolder->url,links[0]->GetAttribute("href"));
  TestEq(testfile->url,links[1]->GetAttribute("href")); //ADDME or should the <a> be stripped?
  TestEq(parserapitestfile->url || "#goto-bob",links[2]->GetAttribute("href")); //Note that it should be a link, even if the target file is unpublished!
  TestEq("#goto-nowhere",links[3]->GetAttribute("href"));

  OBJECT ARRAY images := docblobdom->GetElementsByTagName("img")->GetCurrentElements();
  TestEq(1, Length(images));
  TestEq(TRUE, images[0]->GetAttribute("src") LIKE "/.uc/*");

  myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord(thedoc);

  TestEq(0,               myrichdoc->GetLinkRef("x-richdoclink:1234-5678-000"));
  TestEq(workfolder->id,  myrichdoc->GetLinkRef("x-richdoclink:1234-5678-001"));
  TestEq(testfile->id,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));
  TestEq(testextralink->id, myrichdoc->GetLinkRef("x-richdoclink:1234-5678-003"));
  TestEq(parserapitestfile->id, myrichdoc->GetLinkRef("x-richdoclink:EXT"));

  //Test through publish code... there the webdesign has a chance to intercept the nowherelink
  RECORD publishres := RunFilePublish(testfile3->id, FALSE);
  TestEq(TRUE, publishres.success);

  OBJECT doc := MakeXMLDocumentFromHTML(SELECT AS BLOB data FROM publishres.files WHERE name="index.html");
  links := doc->GetElements("a");
  TestEq(3,Length(links), "Only parserapitest link should not exist");
  TestEq(workfolder->url,links[0]->GetAttribute("href"));
  TestEq(testfile->url,links[1]->GetAttribute("href"));
  TestEq("x-webdesign-takes-over-link:",links[2]->GetAttribute("href"));

  //Archive the testsite
  BLOB arc := workfolder->ExportFolder().files[0].data;

  //Delete testfile to prove it was a WHFSREF in the database (ie, SET DEFAULT was triggered)
  Sleep(100); // Wait a little for delayed publications
  testfw->WaitForPublishCompletion(basefolder->id); //make sure publication doesn't block our delete

  testfw->BeginWork();
  testfile->DeleteSelf(); // file needs to be deleted, so links to it are deleted from instance data

  myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord(richdoctype->GetInstanceData(testfile2->id).data);
  TestEq(0, myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));

  workfolder->RecycleSelf();
  testextralink->RecycleSelf();

  testfw->CommitWork();
  testfw->BeginWork();

  testextralink := basefolder->CreateFile( [ name := "testextralink", publish := FALSE ]);

  OBJECT imp := NEW ArchiveImporter;
  imp->AddInput(arc);
  imp->Import(basefolder->id);

  workfolder := basefolder->OpenByPath("richdocument");
  testfile := workfolder->OpenByPath("richtest");
  parserapitestfile := workfolder->OpenByPath("parserapitest");

  testfile2 := basefolder->OpenByPath("richdocument/richtest2");
  myrichdoc := NEW PublishableRichDocument;
  myrichdoc->ImportFromRecord(richdoctype->GetInstanceData(testfile2->id).data);

  TestEq(0,                 myrichdoc->GetLinkRef("x-richdoclink:1234-5678-000"));
  TestEq(workfolder->id,    myrichdoc->GetLinkRef("x-richdoclink:1234-5678-001"));
  TestEq(testfile->id,      myrichdoc->GetLinkRef("x-richdoclink:1234-5678-002"));
  TestEq(testextralink->id, myrichdoc->GetLinkRef("x-richdoclink:1234-5678-003"));
  TestEq(parserapitestfile->id, myrichdoc->GetLinkRef("x-richdoclink:EXT"));

  //Get a generated document
  OBJECT structure := NEW RichDocumentStructure;
  structure->LoadFromRTDType(GetRTDSettingsForFile(testfile2->id));
  myrichdoc->ApplyStructureDefinition(structure);

  OBJECT result := myrichdoc->RenderAllAsHTMLDocument();
 // print(result->outerxml||'\n');
  OBJECT ARRAY as_in_p := result->QuerySelectorAll("p > a")->GetCurrentElements();
  TestEq(2, Length(as_in_p));
  testfw->CommitWork();
}

RunTestframework([ PTR RichdocumentTest
                 , PTR ParserApiTest
                 , PTR RichDocInstanceTest
                 , PTR PublishRichDocTest
                 , PTR PublishRichDocTableTest
                 , PTR PublishRichDocImageTest
                 , PTR IntLinktest
                 , PTR RichdocumentStrongEmphasis
                 ]);
