<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// ObjectProps
//
ASYNC MACRO ObjectProps()
{
  testfw->BeginWork();

  OBJECT root := OpenTestsuiteSite()->rootobject;
  OBJECT propsfolder := GetTestsuiteTempFolder()->CreateFolder([name := "propstest"]);
  OBJECT imgfile := propsfolder->CreateFile([name:="img.jpg", data := GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"), publish := TRUE ]);
  OBJECT imglink := propsfolder->CreateFile([name:="imglink.jpg", type := 20, filelink := imgfile->id, publish := TRUE ]);
  imglink->SetInstanceData("http://www.webhare.net/xmlns/beta/test"
                          , [ arraytest := [[ textcell := "Row 1" ]]]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[imglink->whfspath] ]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  AWAIT ExpectScreenChange(+1, PTR TT("path")->__test_executebrowse());

  //the thumbnail view should have inited.
  TestEQ(TRUE, TT("filesthumbs")->__test_preinitdone, "The preinit for the thumbnail view was not invoked");
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq(FALSE, ObjectExists(TT(":SEO", [ allowmissing := TRUE ])), "The SEO tab isn't enabled in propstest so shouldn't appear");
  TestEq(FALSE, ObjectExists(topscreen->GetExtendedComponent("platform:web.config", "no_index")));

  { //test editing the existing file
    OBJECT betatab := TT(":Beta test tab");
    TestEQ(TRUE, ObjectExists(betatab));

    OBJECT arrayedit := SELECT AS OBJECT comp FROM ToRecordArray(betatab->GetTopDownAllComponents(), "comp") WHERE comp->name LIKE "*!arraytest";
    TestEq(1, Length(arrayedit->value));

    //Verify that Text 1 appears *before* Text 2 (inserts targetting a spacer should always be 'after') and Text 2 *before* Text 3 (atend converted to after)
    OBJECT t1 := TT(":Text #1");
    OBJECT t2 := TT(":Text #2");
    OBJECT t3 := TT(":Text #3");
    TestEq(t2->parent, t1->parent);
    TestEq(t3->parent, t1->parent);

    INTEGER t1_position := SearchElement(t1->parent->GetChildComponents(), t1);
    INTEGER t2_position := SearchElement(t2->parent->GetChildComponents(), t2);
    INTEGER t3_position := SearchElement(t3->parent->GetChildComponents(), t3);
    TestAssert(t1_position < t2_position, "T1 should appear before T2");
    TestAssert(t2_position < t3_position, "T2 should appear before T3");

    AWAIT ExpectScreenChange(+1, PTR arrayedit->addbutton->TolliumClick());
    TT("textcell")->value := "New row";
    INTEGER staticfile := SELECT AS INTEGER rowkey FROM TT("wh_fs_cell")->options WHERE title = "/webhare-tests/webhare_testsuite.testsite/index.rtd";
    TestEq(TRUE, staticfile != 0, "Where is my index.rtd?");

    TT("wh_fs_cell")->value := staticfile;
    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

    TestEq(2, Length(arrayedit->value));
    TestEq(1, Length(arrayedit->selection));
    TestEq(staticfile, arrayedit->selection[0].wh_fs_cell);

    //Now edit it
    AWAIT ExpectScreenChange(+1, PTR arrayedit->editbutton->TolliumClick());
    TestEq(staticfile, TT("wh_fs_cell")->value, "Selection not properly retained");
    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  }

  RECORD data := imglink->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(2, Length(data.arraytest));

  { //Create a new file. https://gitlab.com/webhare/platform/-/issues/22 reported site= being broken then

    AWAIT ExpectScreenChange(+1, PTR TTClick("newfile"));
    TTFillByTitle("types", "Testsuite: pagelists"); //just open something that won't launch a new document editor
    AWAIT ExpectScreenChange(0, PTR TTClick(":Ok")); // ok new file, immediately opens props

    OBJECT betatab := TT(":Beta test tab");
    OBJECT arrayedit := SELECT AS OBJECT comp FROM ToRecordArray(betatab->GetTopDownAllComponents(), "comp") WHERE comp->name LIKE "*!arraytest";
    AWAIT ExpectScreenChange(+1, PTR arrayedit->addbutton->TolliumClick());
    TT("textcell")->value := "New row";

    INTEGER staticfile := SELECT AS INTEGER rowkey FROM TT("wh_fs_cell")->options WHERE title = "/webhare-tests/webhare_testsuite.testsite/index.rtd";
    TestEq(TRUE, staticfile != 0, "Where is my index.rtd?");
    TT("wh_fs_cell")->value := staticfile;

    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok")); //adds the new row
    AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Ok"), [ messagemask := "*file name or a title*" ]);
    TT(":Title")->value := "My pagelist title";
    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok")); //adds the new file
  }

  { // STORY: Verifying that title changes in Publisher (objectprops) are not reverted by the RTD
    // - Create RTD
    AWAIT ExpectScreenChange(+1, PTR TTClick("newfile"));
    TTFillByTitle("types", "Rich text document");
    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok")); // close it

    // - Have it save a draft
    RECORD newfile := TT("filelist")->selection;
    AWAIT ExpectScreenChange(+1, PTR TTLaunchStartedApp());
    TT("doceditor")->contents->rte->TolliumWeb_FormUpdate('<p class="normal">This is the document</p>');
    TT("doceditor")->contents->rte->__debug_simulatedirty(); //ADDME why doesn't the above command imply this?
    TTClick(":Save");
    AWAIT ExpectScreenChange(-1, PTR TTEscape);

    // TT("richtexteditor")->value := "<p>Some content</p>";
    AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
    TestEq(FALSE, TT(":Title")->enabled);
    AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

    TT("filelist")->openaction->TolliumClick();

    AWAIT ExpectScreenChange(+1, PTR TTLaunchStartedApp());
    AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick("publish"));
    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }

  AWAIT ExpectScreenChange(-1, PTR TTescape);
}

ASYNC MACRO ObjectPropsWithYamlFields()
{
  //Test that we can round trip YAML fields through the object props dialog, and that they are properly applied to the instance data of the object
  testfw->BeginWork();
  OBJECT testfile := GetTestsuiteJSTempFolder()->CreateFile([ name := "allprops", publish := TRUE, typens := "platform:filetypes.html" ]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ testfile->whfspath ] ]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));

  TT(":str")->value := "Set a string!";

  //close and reopen to verify values are saved
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));

  TestEq("Set a string!", TT(":str")->value);

  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape); //close publisher
}

ASYNC MACRO TestRenameSiteConflict()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ OpenTestsuiteSite()->rootobject->whfspath ] ]));

  topscreen->frame->focused := TT("foldertree");
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  TestEq(OpenTestsuiteSite()->rootobject->name, TT(":Folder name")->value);
  TT(":Folder name")->value := "WebHare Backend";

  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Ok"), [ messagemask := "*site*WebHare Backend*exists*" ]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTescape);
}

ASYNC MACRO TestPinFlip()
{
  GetPrimary()->BeginWork();
  OBJECT testfolder := GetTestsuiteTempFolder()->CreateFolder([name := "testpinflip"]);
  GetPrimary()->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ testfolder->whfspath ] ]));
  TestEq("testpinflip", TT("foldertree")->selection.name);
  TestEq(TRUE, TT("foldertree")->selection.candelete);

  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  TT("pinobject")->value := TRUE;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq("testpinflip", TT("foldertree")->selection.name);
  TestEq(FALSE, TT("foldertree")->selection.candelete, "Pinning should have blocked delete - events missed?");

  AWAIT ExpectScreenChange(-1, PTR TTEscape); //close filemgr
}

ASYNC MACRO TestObjectPropsThroughObjectEditor()
{
  GetPrimary()->BeginWork();
  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile([name := "objectprops-propertydialog", title := "Testfile for property dialogs"]);
  GetPrimary()->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ testfile->whfspath ] ]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("open"));

  TestEq("Testfile for property dialogs", TT("title")->value);

  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok")); //close props
  AWAIT ExpectScreenChange(-1, PTR TTEscape); //close filemgr
}


RunTestframework([ PTR ObjectProps
                 , PTR ObjectPropsWithYamlFields
                 , PTR TestRenameSiteConflict
                 , PTR TestPinFlip
                 , PTR TestObjectPropsThroughObjectEditor
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ]]);
