<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO PrepIt()
{
  testfw->BeginWork();
  testfw->GetUserObject("lisa")->GrantRightToOn("system:fs_fullaccess", testfw->GetUserObject("lisa"), OpenTestsuiteSite()->rootobject->id, FALSE, TRUE);
  testfw->CommitWork();
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// ObjectProps
//
ASYNC MACRO ObjectProps()
{
  testfw->SetTestUser("lisa");

  testfw->BeginWork();

  OBJECT root := OpenTestsuiteSite()->rootobject;
  OBJECT propsfolder := GetTestsuiteTempFolder()->CreateFolder([name := "propstest"]);
  OBJECT imgfile := propsfolder->CreateFile([name:="img.jpg", data := GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"), publish := TRUE ]);
  OBJECT imglink := propsfolder->CreateFile([name:="imglink.jpg", type := 20, filelink := imgfile->id, publish := TRUE ]);
  imglink->SetInstanceData("http://www.webhare.net/xmlns/beta/test"
                          , [ arraytest := [[ textcell := "Row 1" ]]]);
  testfw->CommitWork();

  testfw->SetTestUser("sysop");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[imglink->whfspath] ]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  AWAIT ExpectScreenChange(+1, PTR TT("path")->__test_executebrowse());

  //the thumbnail view should have inited.
  TestEQ(TRUE, topscreen->filesthumbs->__test_preinitdone, "The preinit for the thumbnail view was not invoked");
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  OBJECT betatab := TT(":Beta test tab");
  TestEQ(TRUE, ObjectExists(betatab));

  OBJECT arrayedit := SELECT AS OBJECT comp FROM ToRecordArray(betatab->GetTopDownAllComponents(), "comp") WHERE comp->name LIKE "*!arraytest";
  TestEq(1, Length(arrayedit->value));

  AWAIT ExpectScreenChange(+1, PTR arrayedit->addbutton->TolliumClick());
  TT("textcell")->value := "New row";
  INTEGER staticfile := SELECT AS INTEGER rowkey FROM TT("_whfscell")->options WHERE title = "/WebHare testsuite site/index.rtd";
  TestEq(TRUE, staticfile != 0, "Where is my index.rtd?");

  TT("_whfscell")->value := staticfile;
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  TestEq(2, Length(arrayedit->value));
  TestEq(1, Length(arrayedit->selection));
  TestEq(staticfile, arrayedit->selection[0]._whfscell);

  //Nowe edit it
  AWAIT ExpectScreenChange(+1, PTR arrayedit->editbutton->TolliumClick());
  TestEq(staticfile, TT("_whfscell")->value, "Selection not properly retained");
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  RECORD data := imglink->GetInstanceData("http://www.webhare.net/xmlns/beta/test");
  TestEq(2, Length(data.arraytest));

  AWAIT ExpectScreenChange(-1, PTR TTescape);
}

ASYNC MACRO TestRenameSiteConflict()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ OpenTestsuiteSite()->rootobject->whfspath ] ]));

  topscreen->frame->focused := TT("foldertree");
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  TestEq(OpenTestsuiteSite()->rootobject->name, TT(":Name")->value);
  TT(":Name")->value := "WebHare Backend";

  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Ok"), [ messagemask := "*site*WebHare Backend*exists*" ]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTescape);
}

ASYNC MACRO TestPinFlip()
{
  GetPrimary()->BeginWork();
  OBJECT testfolder := GetTestsuiteTempFolder()->CreateFolder([name := "testpinflip"]);
  GetPrimary()->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := STRING[ testfolder->whfspath ] ]));
  TestEq("testpinflip", TT("foldertree")->selection.name);
  TestEq(TRUE, TT("foldertree")->selection.candelete);

  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));
  TT("pinobject")->value := TRUE;
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq("testpinflip", TT("foldertree")->selection.name);
  TestEq(FALSE, TT("foldertree")->selection.candelete, "Pinning should have blocked delete - events missed?");

  AWAIT ExpectScreenChange(-1, PTR TTEscape); //close filemgr
}

RunTestframework([ PTR ObjectProps
                 , PTR TestRenameSiteConflict
                 , PTR TestPinFlip
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa" ]
                                   ]]);
