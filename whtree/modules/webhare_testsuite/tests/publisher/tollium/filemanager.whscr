<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";
LOADLIB "mod::publisher/lib/commondialogs.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::publisher/lib/testframework.whlib";

MACRO PrepIt()
{
  SetTidLanguage("debug");
  testfw->BeginWork();
  testfw->GetUserObject("lisa")->GrantRightToOn("system:fs_browse", testfw->GetUserObject("lisa"), testfw->GetTestSite()->rootobject->id, FALSE, TRUE);
  testfw->CommitWork();
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Walkt the standard file manager tree, test interactions
//
BOOLEAN FUNCTION IsBrowserURLChanged(STRING currenturl, OBJECT previewbrowser, INTEGER fileid)
{
//  Print("IsBrowserURLChanged? last = " || currenturl || "\n"
//     || "                    , now = " || previewbrowser->GetPreviewState().browserurl || "\n\n");

  //DumpValue(previewbrowser->GetPreviewState(),'tree:1');
  //DumpValue( RECORD(SELECT * FROM system.fs_objects WHERE id=fileid),'tree:1');
  RETURN currenturl != previewbrowser->GetPreviewState().browserurl;
}

OBJECT ASYNC FUNCTION InitialGotoFolder() //used by eg Find.
{
  INTEGER headingsdoc := testfw->GetTestSite()->OpenByPath("headings.doc")->id;

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(headingsdoc)], target := DEFAULT RECORD ]));
  TestEq(TRUE, TT("filelistmulti")->pressed, 'Multirow is the default view');
  TestEq(headingsdoc, TT("filelist")->value[0]);
  TestEq(testfw->GetTestSite()->id, TT("foldertree")->value);

  //select singlerow layout. this broke jump-to-id
  TT("filelistsingle")->action->TolliumClick();
  TestEq(FALSE, TT("filelistmulti")->pressed);
  TestEq(TRUE, TT("filelistsingle")->pressed);

  TestEq(headingsdoc, TT("filelist")->value[0], 'selection should be retained after type switch');
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel());

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(headingsdoc)], target := DEFAULT RECORD ]));
  TestEq(TRUE, TT("filelistsingle")->pressed, 'Single row selection should be remembered');
  TestEq(FALSE, TT("filelistmulti")->pressed);
  TestEq(headingsdoc, TT("filelist")->value[0]);

  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel());
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app"));
  TestEq(testfw->GetTestSite()->id, TT("foldertree")->value);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  RETURN TRUE;
}

OBJECT ASYNC FUNCTION WalkTheTree()
{
  testfw->SetTestUser("lisa");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  RECORD pvs;
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(FALSE, pvs.showingmessages); //shouldn't have anything to say, no site should be selected

  //Set headless controller to https
  topscreen->tolliumcontroller->secure := TRUE;

  topscreen->frame->focused := TT("filelist");
  TT("filelist")->selection := SELECT * FROM TT("filelist")->rows WHERE name="webhare_testsuite.site";
  TestEq(TRUE, TT("filelist")->openaction->TolliumClick());
  TestEq("webhare_testsuite.site", TT("foldertree")->selection.name);

  //The displayed preview URL should match the site URL
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot, pvs.displayurl);

  IF(pvs.displayurl LIKE "http:*")
  {
    // The preview url should be empty and the secure vs unsecure message should be shown, as the headless controller is secure
    // and the test site is not
    TestEq("", pvs.browserurl);
    TestEq(TRUE, pvs.showingmessages);
    TestEqLike('*{publisher:components.previewbrowser.nonsecuresite}*', pvs.messages);
  }

  //Set headless controller to http
  topscreen->tolliumcontroller->secure := FALSE;

  //Find a simple file that should be published
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="headings.doc";
  topscreen->frame->focused := TT("filelist");

  //Check the previewed url
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "headings/", pvs.displayurl);
  TestEqLike(testfw->GetTestSite()->webroot || "headings/*__whpub_clock_=*", pvs.browserurl);
  TestEq(FALSE, pvs.showingmessages);

  //Check if the url changes after refresh (__whpub_clock_ should be updated)
  STRING currenturl := pvs.browserurl;
  Sleep(1);
  pvs.previewrefresh->TolliumClick();
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(TRUE, currenturl != pvs.browserurl);

  //Check if the url changes after republish
  currenturl := pvs.browserurl;
  Print("URL before clicking republish recursive: " || pvs.browserurl || "\n");
  TestEQ(TRUE, TT("republish_recursive")->TolliumClick());
  //Wait for the browser to refresh (we assume publishing the file takes longer than 1 ms, and thus __whpub_clock_ should have been updated)
  AWAIT testfw->WaitAndProcessEvents(PTR IsBrowserURLChanged(currenturl, TT("previewbrowser"), TT("filelist")->value[0]), "Preview is not refreshing #1");

  //Let's hit the errored file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="loadlib.html";

  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "loadlib.html", pvs.displayurl);
  TestEq("", pvs.browserurl); // File was never published, so nothing is shown
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*<a *errors*', pvs.messages);

  OBJECT link := MakeXMLDocumentFromHTML(StringToBlob(pvs.messages))->GetElementsByTagName('a')->Item(0);
  TestEq(TRUE, objectexists(link), "No hyperlink in the error message");

  //Open and close the error screen
  AWAIT(ExpectScreenChange(+1, PTR pvs.messagecomp->onclicklink(link->GetAttribute("href"))));
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel));

  // Select the internal link to the errored file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="internallink-errors.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "loadlib.html", pvs.displayurl);
  TestEq("", pvs.browserurl); // Target was never published
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.internallink|webhare_testsuite.site:/loadlib.html}*', pvs.messages);
  TestEqLike('*<a *errors*', pvs.messages);

  link := MakeXMLDocumentFromHTML(StringToBlob(pvs.messages))->GetElementsByTagName('a')->Item(0);
  TestEq(TRUE, objectexists(link), "No hyperlink in the error message");

  //Open and close the error screen
  AWAIT(ExpectScreenChange(+1, PTR pvs.messagecomp->onclicklink(link->GetAttribute("href"))));
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel));

  //Rename the file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="loadlib.html";
  pvs := TT("previewbrowser")->GetPreviewState();
  currenturl := pvs.browserurl;
  AWAIT(ExpectScreenChange(+1, PTR TT("props")->TolliumClick()));
  TT("name")->value := "loadlib-renamed.shtml";
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit));

  //ADDME test the state of the error while republishing
  //Wait for the browser to refresh
  AWAIT testfw->WaitAndProcessEvents(PTR IsBrowserURLChanged(currenturl, TT("previewbrowser"), TT("filelist")->value[0]), "Preview is not refreshing #2");

  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(FALSE, pvs.showingmessages);// Files of this type should be published as .shtml, but this one has the extension .html

  //Select the emptyfolder
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="emptyfolder";
  /// OK, somehow an index can exist; remove if it does:
  RECORD ARRAY files := SELECT id, name, type FROM system.fs_objects WHERE parent IN TT("filelist")->value;
  IF(Length(files) > 0)
  {
    testfw->BeginWork();
    FOREVERY(RECORD file FROM files)
      OpenWHFSObject(file.id)->DeleteSelf();
    testfw->CommitWork();
    TestEq(0, Length(SELECT AS INTEGER ARRAY id FROM system.fs_objects WHERE parent IN TT("filelist")->value));
    TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="emptyfolder";
  }
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("", pvs.browserurl); // No index document
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.folderwithoutindex|emptyfolder}*', pvs.messages);

  //Let's hit the content link
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="contentlink.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "contentlink.shtml", pvs.displayurl);
  TestEqLike(testfw->GetTestSite()->webroot || "contentlink.shtml*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.contentlink|webhare_testsuite.site:/dynamicpage.shtml}*', pvs.messages);

  TestEQ(FALSE, TT("filelist")->selection.canwrite);
  AWAIT ExpectAndAnswerMessageBox("ok", PTR TT("filelist")->openaction->TolliumClick(), [messagemask := "{publisher:commondialogs.messageboxes.norightstoeditfile}"]);

  //Let's hit the external link
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="externallink.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("http://www.b-lex.nl/", pvs.displayurl);
  TestEq("", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.externallink|http://www.b-lex.nl/*', pvs.messages);

  //Test internal links to various types of files
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="internallink-empty.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("", pvs.displayurl);
  TestEq("", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.internallink-broken}*', pvs.messages);

  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="internallink-published.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "dynamicpage.shtml", pvs.displayurl);
  TestEqLike(testfw->GetTestSite()->webroot || "dynamicpage.shtml*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.internallink|webhare_testsuite.site:/dynamicpage.shtml}*', pvs.messages);

  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="internallink-unpublished.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "dynamicpage.witty", pvs.displayurl);
  TestEqLike(testfw->GetTestSite()->webroot || "dynamicpage.witty*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*{publisher:components.previewbrowser.internallink-unpublished|webhare_testsuite.site:/dynamicpage.witty}*', pvs.messages);
  TestEqLike('*{publisher:components.previewbrowser.warnings}*', pvs.messages);

  //Let's hit the redirection
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="redirect.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "redirect.shtml", pvs.displayurl);
  TestEqLike(testfw->GetTestSite()->webroot || "redirect.shtml*__whpub_clock_=*", pvs.browserurl);
  TestEq(FALSE, pvs.showingmessages);
  //ADDME: We don't have access to the loaded content, so we cannot test clicking the 'follow redirection' link

  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="redirect-js.shtml";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(testfw->GetTestSite()->webroot || "redirect-js.shtml", pvs.displayurl);
  //ADDME: In the headless controller the page doesn't actually get redirected
  //TestEq(testfw->GetTestSite()->webroot || "nonexisting.html", pvs.browserurl);
  TestEq(FALSE, pvs.showingmessages);

  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  RETURN TRUE;
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PhotoAlbums
//
OBJECT ASYNC FUNCTION CreatePhotoAlbum()
{
  OBJECT dialog := CreateNewFolderDialog(GetTestController(), testfw->GetTestSite()->id, 3);
  AWAIT ExpectScreenChange(+1, PTR dialog->RunModal()); //simply should not crash
  TT("name")->value := "photoalbum";
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  TestEQ(TRUE, dialog->objectid != 0);

  OBJECT createfolder := OpenWHFSObject(dialog->objectid);
  TestEq(TRUE, ObjectExists(createfolder));
  TestEQ(3, createfolder->type);
  TestEq(TRUE, createfolder->indexdoc != 0);

  OBJECT indexdoc := OpenWHFSObject(createfolder->indexdoc);
  TestEq(TRUE, ObjectExists(indexdoc));
  TestEQ(24, indexdoc->type);
  RETURN TRUE;
}

RunTestframework([ PTR PrepareTestSiteDesignWebsite("", [ witherrors := TRUE ])
                 , PTR PrepIt
                 , PTR InitialGotoFolder //used by eg Find.
                 , PTR WalkTheTree
                 , PTR CreatePhotoAlbum
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa" ]
                                   ]]);

//select singlerow layout. this broke jump-to-id
