<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/dialogs.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO PrepIt()
{
  testfw->BeginWork();
  testfw->GetUserObject("lisa")->UpdateGrant("grant", "system:fs_browse",  OpenTestsuiteSite()->id, testfw->GetUserObject("lisa"), [ allowselfassignment := TRUE ]);
  testfw->CommitWork();
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Walkt the standard file manager tree, test interactions
//
BOOLEAN FUNCTION IsBrowserURLChanged(STRING currenturl, OBJECT previewbrowser, INTEGER fileid)
{
//  Print("IsBrowserURLChanged? last = " || currenturl || "\n"
//     || "                    , now = " || previewbrowser->GetPreviewState().browserurl || "\n\n");

  //DumpValue(previewbrowser->GetPreviewState(),'tree:1');
  //DumpValue( RECORD(SELECT * FROM system.fs_objects WHERE id=fileid),'tree:1');
  RETURN currenturl != previewbrowser->GetPreviewState().browserurl;
}

ASYNC MACRO InitialGotoFolder() //used by eg Find.
{
  INTEGER headingsdoc := OpenTestsuiteSite()->OpenByPath("index.rtd")->id;

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(headingsdoc)], target := DEFAULT RECORD ]));
  TestEq(TRUE, TT("filelistmulti")->pressed, 'Multirow is the default view');
  TestEq(headingsdoc, TT("filelist")->value[0]);
  TestEq(OpenTestsuiteSite()->id, TT("foldertree")->value);

  //select singlerow layout. this broke jump-to-id
  TT("filelistsingle")->action->TolliumClick();
  TestEq(FALSE, TT("filelistmulti")->pressed);
  TestEq(TRUE, TT("filelistsingle")->pressed);

  TestEq(headingsdoc, TT("filelist")->value[0], 'selection should be retained after type switch');
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel());

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(headingsdoc)], target := DEFAULT RECORD ]));
  TestEq(TRUE, TT("filelistsingle")->pressed, 'Single row selection should be remembered');
  TestEq(FALSE, TT("filelistmulti")->pressed);
  TestEq(headingsdoc, TT("filelist")->value[0]);

  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel());
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app"));
  TestEq(OpenTestsuiteSite()->id, TT("foldertree")->value);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //test jumping to /webhare-private/ - should fail initially as we haven't enabled that pref..
  testfw->BeginWork();
  OBJECT privatetestfwfolder := OpenWHFSPrivateFolder("webhare_testsuite")->EnsureFolder([name := "testfw"]);
  OBJECT targetfile := privatetestfwfolder->EnsureFile([name := "jumptarget"]);
  testfw->CommitWork();

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(targetfile->id)], target := DEFAULT RECORD ]));
  TestEq(0, TT("foldertree")->value);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  //Modify our preferences to show webhare-private
  testfw->GetUserObject("sysop")->SetRegistryKey("publisher.filemgr.showwhfsprivate", TRUE);

  //And now the jump should work
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := [ToString(targetfile->id)], target := DEFAULT RECORD ]));
  TestEq(privatetestfwfolder->id, TT("foldertree")->value);
  TestEq(targetfile->id, TT("filelist")->value[0]);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO WalkTheTree()
{
  testfw->SetTestUser("lisa");

  testfw->BeginWork();
  OBJECT crashfile := GetTestsuiteTempFolder()->CreateFile([ name := "crash.rtd", typens := "http://www.webhare.net/xmlns/publisher/richdocumentfile", publish := TRUE ]);
  OBJECT crashlink := GetTestsuiteTempFolder()->CreateFile([ name := "crash.link", typens := "http://www.webhare.net/xmlns/publisher/internallink", publish := TRUE, filelink := crashfile->id ]);
  OBJECT disconnectedlink := GetTestsuiteTempFolder()->CreateFile([ name := "disconnectedlink", typens := "http://www.webhare.net/xmlns/publisher/internallink", publish := TRUE ]);

  // to simulate the situation where a file/folder has a type that is no longer a valid file/folder type... (which the DB can't currently prevent) we'll force a file of type 'sitesettings'
  OBJECT invalidfile := GetTestsuiteTempFolder()->CreateFile( [ name := "invalidfiletype", type := 0, publish := TRUE ]);
  UPDATE system.fs_objects SET type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/sitesettings")->id WHERE id = invalidfile->id;

  testfw->CommitWork();
  WaitForPublishCompletion(OpenTestsuiteSite()->id, [ expecterrorsfor := INTEGER[ crashfile->id, disconnectedlink->id, invalidfile->id ]]); //the testsite better be published as tests will be using it!

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := DEFAULT STRING ARRAY, target := DEFAULT RECORD ]));

  RECORD pvs;
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(FALSE, pvs.showingmessages); //shouldn't have anything to say, no site should be selected

  //Set headless controller to https
  topscreen->tolliumcontroller->secure := TRUE;

  topscreen->frame->focused := TT("filelist");
  TT("filelist")->selection := SELECT * FROM TT("filelist")->rows WHERE name="webhare_testsuite.testsite";
  TestEq(TRUE, TT("filelist")->openaction->TolliumClick());
  TestEq("webhare_testsuite.testsite", TT("foldertree")->selection.name);

  //The displayed preview URL should match the site URL
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot, pvs.displayurl);

  IF(pvs.displayurl LIKE "http:*")
  {
    // The preview url should be empty and the secure vs unsecure message should be shown, as the headless controller is secure
    // and the test site is not
    TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
    TestEq(TRUE, pvs.showingmessages);
    TestEqLike(`*The site is unsecure*<a *`, pvs.messages);
  }

  //Verify the view button actually works
  RECORD urlaction := ExecuteWindowOpenAction(TT("viewbrowser"));
  TestEq(TRUE, testfw->browser->GotoWebPage(urlaction.url));

  //Set headless controller to http
  topscreen->tolliumcontroller->secure := FALSE;

  TT("filelist")->selection := SELECT * FROM TT("filelist")->rows WHERE name="TestPages";
  TestEq(TRUE, TT("filelist")->openaction->TolliumClick());
  TestEq("TestPages", TT("foldertree")->selection.name);

  topscreen->frame->focused := TT("filelist");

  //Select something that never has a preview
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="wittytest.witty";

  //Check the previewed url
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*has*no*preview*', pvs.messages);

  //This file should NOT be published
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="imgeditfile.jpeg";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*not*published*', pvs.messages);

  //This file should be published, but it's handled by a specific preview
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="rangetestfile.jpeg";
  pvs := TT("previewbrowser")->GetPreviewState();

  TestEq(FALSE, pvs.showingmessages, `got unexpected messages: ${pvs.messages}`);
  TestEqLike("*/rangetestfile.jpeg", pvs.displayurl);

  //verify the image.shtml is showing our image (quick test that all the preview APIs are functional)
  TestEq(TRUE, testfw->browser->GotoWebPage(pvs.browserurl));
  TestEqLike("*/rangetestfile.jpeg?hash=*", testfw->browser->QS("img.wh-imagepreview__mainimage")->GetAttribute("src"));

  //Verify an unknown file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="unknownfile";
  pvs := TT("previewbrowser")->GetPreviewState();

  TestEq(OpenTestsuiteSite()->webroot || "TestPages/unknownfile", pvs.displayurl);
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEq(TRUE, testfw->browser->GotoWebPage(pvs.browserurl));

  //Test multiselect
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name IN ["simpletest.rtd","rangetestfile.jpeg"];
  TestEq(2, Length(TT("filelist")->selection), "Confirm multipel select");

  //Check the previewed url
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("", pvs.displayurl);
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(FALSE, pvs.showingmessages);

  //Find a simple file that should be published
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="simpletest.rtd";

  //Check the previewed url
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "TestPages/simpletest/", pvs.displayurl);
  TestEqLike(OpenTestsuiteSite()->webroot || "TestPages/simpletest/*__whpub_clock_=*", pvs.browserurl);
  TestEq(FALSE, pvs.showingmessages);

  //Check if the url changes after refresh (__whpub_clock_ should be updated)
  STRING currenturl := pvs.browserurl;
  Sleep(1);
  pvs.previewrefresh->TolliumClick();
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(TRUE, currenturl != pvs.browserurl);

  //Check if the url changes after republish
  currenturl := pvs.browserurl;
  Print("URL before clicking republish recursive: " || pvs.browserurl || "\n");
  TestEQ(TRUE, TT("republish_recursive")->TolliumClick());
  //Wait for the browser to refresh (we assume publishing the file takes longer than 1 ms, and thus __whpub_clock_ should have been updated)
  AWAIT testfw->WaitAndProcessEvents(PTR IsBrowserURLChanged(currenturl, TT("previewbrowser"), TT("filelist")->value[0]), "Preview is not refreshing #1");

  //Go to the indexless folder
  TT("foldertree")->selection := SELECT * FROM TT("foldertree")->rows WHERE name = "tmp";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(OpenTestsuiteSite()->webroot || "tmp/", pvs.displayurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike(`The selected folder 'tmp' does not have an index document.`, pvs.messages);

  //Let's hit the errored file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="crash.rtd";

  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "tmp/crash/", pvs.displayurl);
  // TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl); //we now simply attempt to show what the output would show
  TestEqLike(OpenTestsuiteSite()->webroot || "tmp/crash/*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*<a *errors*', pvs.messages);

  OBJECT link := MakeXMLDocumentFromHTML(StringToBlob(pvs.messages))->GetElementsByTagName('a')->Item(0);
  TestEq(TRUE, objectexists(link), "No hyperlink in the error message");

  //Open and close the error screen
  AWAIT(ExpectScreenChange(+1, PTR pvs.messagecomp->onclicklink(link->GetAttribute("href"))));
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel));

  //Verify a file of an orphan type
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="invalidfiletype";
  TestEq(1, Length(TT("filelist")->value));
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEqLike("*/invalidfiletype", pvs.displayurl);
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);

  // Select the internal link to the errored file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="crash.link";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "tmp/crash/", pvs.displayurl);
  TestEqLike(OpenTestsuiteSite()->webroot || "tmp/crash/*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike('*internal link*crash.rtd*', pvs.messages);
  TestEqLike('*<a *errors*', pvs.messages);

  link := MakeXMLDocumentFromHTML(StringToBlob(pvs.messages))->GetElementsByTagName('a')->Item(0);
  TestEq(TRUE, objectexists(link), "No hyperlink in the error message");

  //Open and close the error screen
  AWAIT(ExpectScreenChange(+1, PTR pvs.messagecomp->onclicklink(link->GetAttribute("href"))));
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel));

  //Rename the file
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="crash.rtd";
  pvs := TT("previewbrowser")->GetPreviewState();
  currenturl := pvs.browserurl;
  AWAIT(ExpectScreenChange(+1, PTR TT("props")->TolliumClick()));
  TT("name")->value := "renamed.rtd";
  AWAIT(ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit));

  //ADDME test the state of the error while republishing
  //Wait for the browser to refresh
  AWAIT testfw->WaitAndProcessEvents(PTR IsBrowserURLChanged(currenturl, TT("previewbrowser"), TT("filelist")->value[0]), "Preview is not refreshing #2");

  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(FALSE, pvs.showingmessages);// Files of this type should be published as .shtml, but this one has the extension .html

  //Let's hit the content link
  TT("foldertree")->selection := SELECT * FROM TT("foldertree")->rows WHERE name = "TestPages";
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="staticpage-contentlink";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "TestPages/staticpage-contentlink/", pvs.displayurl);
  TestEqLike(OpenTestsuiteSite()->webroot || "TestPages/staticpage-contentlink/*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike(`This content link links to URL 'webhare_testsuite.testsite:/TestPages/StaticPage'.`, pvs.messages);

  TestEQ(FALSE, TT("filelist")->selection.canwrite);
  AWAIT ExpectAndAnswerMessageBox("ok", PTR TT("filelist")->openaction->TolliumClick(), [messagemask := "You don't have enough rights to edit this file."]);

  //Let's hit the external link
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="externallink";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("https://www.webhare.dev/", pvs.displayurl);
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike(`This external link links to URL 'https://www.webhare.dev/'. <a href="preview:showexternallink">Click here to open this link.</a>`, pvs.messages);

  //Test internal links to various types of files
  TT("foldertree")->selection := SELECT * FROM TT("foldertree")->rows WHERE name = "tmp";
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="disconnectedlink";
  TestEq(TRUE, RecordExists(TT("filelist")->selection));
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq("", pvs.displayurl);
  TestEq(GetPrimaryWebhareInterfaceURL() || ".publisher/backend/preview/nopreview.html", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike(`This external link does not seem to be linked to an existing file.<br />Errors have occurred during publication. <a href="preview:showerrors">Click here to view the errors.</a>`, pvs.messages);

  TT("foldertree")->selection := SELECT * FROM TT("foldertree")->rows WHERE name = "TestPages";
  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="staticpage-internallink";
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "TestPages/StaticPage/", pvs.displayurl);
  TestEqLike(OpenTestsuiteSite()->webroot || "TestPages/StaticPage/*__whpub_clock_=*", pvs.browserurl);
  TestEq(TRUE, pvs.showingmessages);
  TestEqLike(`This internal link links to file 'webhare_testsuite.testsite:/TestPages/StaticPage'.`, pvs.messages);

  TT("filelist")->value := SELECT AS INTEGER ARRAY id FROM TT("filelist")->rows WHERE name="unpublished-internallink";
  TestEq(TRUE, RecordExists(TT("filelist")->selection));
  pvs := TT("previewbrowser")->GetPreviewState();
  TestEq(OpenTestsuiteSite()->webroot || "TestPages/unpublished/", pvs.displayurl);
  TestEqLike(`*This internal link links to unpublished file 'webhare_testsuite.testsite:/TestPages/unpublished'*`, pvs.messages);
  TestEqLike(GetPrimaryWebhareInterfaceURL() || ".publisher/preview/*", pvs.browserurl, 'it should be previewed as an unpublished file');
  TestEq(TRUE, pvs.showingmessages);

  AWAIT ExpectScreenChange(+1, PTR TTClick("gotoanything"));
  TestEqMembers([[ title := "webhare_testsuite.testsite" ]], TT("obj")->options, "*");
  TT("obj")->value := "webhare_testsuite.testsite";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq("webhare_testsuite.testsite", TT("foldertree")->selection.name);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

MACRO CleanupRootFolder()
{
  OBJECT testfolder := OpenWHFSObjectByPath("/webhare_testsuite.test-publisher-tollium-filemanager");
  IF(ObjectExists(testfolder))
  {
    testfw->BeginWork();
    testfolder->RecycleSelf();
    testfw->CommitWork();
  }
}

ASYNC MACRO CreateRootObject()
{
  CleanupRootFolder();

  RECORD res := AWAIT ExpectScreenChange(+1, PTR RunNewFSObjectDialog(GetTestController(), 0, [ type := 0, isfolder := TRUE ]));
  TT("name")->value := "webhare_testsuite.test-publisher-tollium-filemanager";
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  CleanupRootFolder();
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//
// PhotoAlbums
//
ASYNC MACRO CreatePhotoAlbum()
{
  RECORD res := AWAIT ExpectScreenChange(+1, PTR RunNewFSObjectDialog(GetTestController(), GetTestsuiteTempFolder()->id, [ type := 3, isfolder := TRUE ]));
  TT("name")->value := "photoalbum";
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  TestEQ(TRUE, (AWAIT res.expectcallreturn()) != 0);

  OBJECT createfolder := OpenWHFSObject(AWAIT res.expectcallreturn());
  TestEq(TRUE, ObjectExists(createfolder));
  TestEQ(3, createfolder->type);
  TestEq(TRUE, createfolder->indexdoc != 0);

  OBJECT indexdoc := OpenWHFSObject(createfolder->indexdoc);
  TestEq(TRUE, ObjectExists(indexdoc));
  TestEQ(24, indexdoc->type);
}

RunTestframework([ PTR PrepIt
                 , PTR InitialGotoFolder //used by eg Find.
                 , PTR WalkTheTree
                 , PTR CreateRootObject
                 , PTR CreatePhotoAlbum
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa" ]
                                   ]]);
