<?wh

LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

ASYNC MACRO TestHookBasics() {
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("webhare_testsuite:runscreen", [ params := ["tests/extendablescreen.xml"]]));
  TestEq(["Tab 1", "Inserted tab 1", "Inserted tab 2", "Tab 2", "Tab 3"], SELECT AS STRING ARRAY tab->title FROM ToRecordArray(TT("extend_these_tabs")->pages,"tab"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestCreateSiteDialog()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ params := STRING[ testfw->GetWHFSTestRoot()->whfspath ]]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("newsite"));
  TT(":Name")->value := "testfw - My new site";
  TT(":StringTest")->value := "a string test";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TestEq("testfw - My new site", TT("filelist")->selection.name);

  //test our custom prop persisted
  AWAIT ExpectScreenChange(+1, PTR TTClick("siteproperties"));
  TestEq("a string test", TT(":StringTest")->value);
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestFilteredHooks()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("system:userrights"));
  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick("{item}:WH testsuite: Add export"), [ messagemask := "AddExport()"]);
  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick("{item}:WH testsuite: testschema"), [ messagemask := "AddExport2()"]);
  TestEq(FALSE, ObjectExists(TT("{item}:Should Not Be seen", [ allowmissing := TRUE])));
  TestEq(FALSE, ObjectExists(TT("{item}:WH testsuite: usermgmt schema", [ allowmissing := TRUE])));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestframework([ PTR TestHookBasics
                 , PTR TestCreateSiteDialog
                 , PTR TestFilteredHooks
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ]]);
