<?wh
LOADLIB "mod::publisher/lib/commondialogs.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

ASYNC MACRO TestThumbnailBrowse()
{
  OBJECT browse := CreateBrowseForWHFSObjectDialog(GetTestController());
  browse->acceptfiles := TRUE;
  browse->acceptfolders := FALSE;
  browse->acceptunpublished := TRUE;
  browse->accepttypes := [ "http://www.webhare.net/xmlns/publisher/imagefile" ];
  browse->type := "thumbnails";
  browse->selectmode := "single";
  browse->roots := [INTEGER(OpenTestsuiteSite()->id)];

  RECORD res := AWAIT ExpectScreenChange(+1, PTR browse->RunSelectfileDialog());

  OBJECT imgeditfile := OpenTestsuiteSite()->OpenByPath("/testpages/imgeditfile.jpeg");
  TT("folders")->selection := SELECT * FROM TT("folders")->rows WHERE name="testpages";
  topscreen->filesthumbs->value := imgeditfile->id;
  topscreen->frame->focused := topscreen->filesthumbs;
  TestEq("imgeditfile.jpeg", topscreen->filesthumbs->selection.name);

  AWAIT ExpectScreenChange(-1, PTR TT("submitaction")->TolliumClick());

  TestEq(imgeditfile->id, AWAIT res.expectcallreturn());

  browse := CreateBrowseForWHFSObjectDialog(GetTestController());
  browse->acceptfiles := TRUE;
  browse->folder := imgeditfile->parent;

  AWAIT ExpectScreenChange(+1, PTR browse->RunSelectfileDialog());
  TestEq("testpages", TT("folders")->selection.name);
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteCancel());
}

ASYNC MACRO TestBrowseForObject()
{
  testfw->BeginWork();
  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile([ name := "deleteme.txt" ]);
  testfw->CommitWork();

  OBJECT scr := GetTestController()->LoadScreen("webhare_testsuite:tests/anycomponent.anycomponent", [ component := "http://www.webhare.net/xmlns/publisher/components:browseforobject" ]);
  scr->comp->value := testfile->id;

  AWAIT ExpectScreenChange(+1, PTR scr->RunModal());
  TestEq("WebHare testsuite site:/tmp/deleteme.txt", scr->comp->^path->value);
  AWAIT ExpectScreenChange(-1, PTR TTEScape);

  testfw->BeginWork();
  testfile->RecycleSelf();
  testfw->CommitWork();

  scr := GetTestController()->LoadScreen("webhare_testsuite:tests/anycomponent.anycomponent", [ component := "http://www.webhare.net/xmlns/publisher/components:browseforobject" ]);
  scr->comp->value := testfile->id;

  AWAIT ExpectScreenChange(+1, PTR scr->RunModal());
  TestEq("deleteme.txt (deleted)", scr->comp->^path->value);
  AWAIT ExpectScreenChange(-1, PTR TTEScape);
}

ASYNC MACRO TestBrowseForObjectArray()
{
  OBJECT imgeditfile := OpenTestsuiteSite()->OpenByPath("/testpages/imgeditfile.jpeg");
  // ?app=webhare_testsuite:component(http://www.webhare.net/xmlns/publisher/components:browseforobjectarray)
  OBJECT scr := GetTestController()->LoadScreen("webhare_testsuite:tests/anycomponent.anycomponent", [ component := "http://www.webhare.net/xmlns/publisher/components:browseforobjectarray" ]);
  scr->comp->browsetype := "thumbnails";
  scr->comp->site := OpenTestsuiteSite()->name;
  scr->comp->fullpath := "/testpages/";
  scr->comp->accepttypes := [ "http://www.webhare.net/xmlns/publisher/imagefile" ];

  AWAIT ExpectScreenChange(+1, PTR scr->comp->__debug_addaction->TolliumClick());
  //INSERT TT("folders")->rows[0].rowkey INTO TT("folders")->expanded AT END;
  TestEq("testpages", TT("folders")->selection.name);
  topscreen->filesthumbs->value := [INTEGER(imgeditfile->id)];
  topscreen->frame->focused := topscreen->filesthumbs;

  AWAIT ExpectScreenChange(-1, PTR TT("submitaction")->TolliumClick());
  TestEq([INTEGER(imgeditfile->id)], scr->comp->value);
}

RunTestFramework(
    [ PTR TestThumbnailBrowse
    , PTR TestBrowseForObject
    , PTR TestBrowseForObjectArray
    ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"]]
                      ]
       ]);
