<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/worklists.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


OBJECT linksrtdfile, lisartdfile, lisamarkdownfile, sysoptestfolder, lisatestfolder;
STRING todaysuniqueword := GeneratePassword("Grosse", 22);

MACRO PrepIt()
{
  testfw->BeginWork();

  sysoptestfolder := GetTestsuiteTempFolder()->EnsureFolder([name:="forsysop"]);
  lisatestfolder := GetTestsuiteTempFolder()->EnsureFolder([name:="forlisa"]);

  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT markdowntype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/markdownfile");

  linksrtdfile := sysoptestfolder->CreateFile([name := "links.rtd", type := richdoctype->id, publish:=FALSE]);
  richdoctype->SetInstanceData(linksrtdfile->id,
      [ data := [ htmltext := StringToBlob('<html><head></head><body>'
                                     || `<p class="normal"><a href="http://www.example.net/link41/">Link 41</a> <a href="http://www.example.net/link42/">Link 42</a> XX${todaysuniqueword} ZZ${todaysuniqueword}</p>`
                                     || '</body></html>')
                ]
      ]);

  lisartdfile := lisatestfolder->CreateFile([name := "links-for-lisa.rtd", type := richdoctype->id, publish:=FALSE]);
  richdoctype->SetInstanceData(lisartdfile->id,
      [ data := [ htmltext := StringToBlob('<html><head></head><body>'
                                     || `<p class="normal"><a href="http://www.example.net/link42/">Link 42</a> YY${todaysuniqueword} ZZ${todaysuniqueword}</p>`
                                     || '</body></html>')
                ]
      ]);

  lisamarkdownfile := lisatestfolder->CreateFile([name := "markdown", type := markdowntype->id, publish:=FALSE ]);
  markdowntype->SetInstanceData(lisamarkdownfile->id,
      [ data := [ type := "publisher:markdown"
                , text := StringToBlob(`# Markdown\nThis is [link 43](http://www.example.net/link43) and word up: MD${todaysuniqueword}`)
                ]
      ]);

  OBJECT indexpromise := MakeChangesIndexedPromise();
  testfw->CommitWork();
  WaitForPromise(indexpromise);

  //Make sure the WHFS catalog is actually up-to-date before we start testing (TODO need to test if, given MakeChangesIndexedPromise, a Refresh wouldn't suffice)
  OBJECT catalog := OpenConsilioCatalog(whconstant_consilio_catalog_whfs);
  FOREVERY(RECORD csource FROM catalog->ListContentSources())
    catalog->OpenContentSourceById(csource.id)->WaitForIndexingDone([ verbose := TRUE ]);
}

ASYNC MACRO Search()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := ["Search"], target := DEFAULT RECORD ]));
  TestEq(TRUE, RecordExists(TT("foldertree")->selection));
  TestEq("Search", TT("foldertree")->selection.name);

  OBJECT searchscreen := TT("filelist")->folderfilter->contents;
  TestEq(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "" ]
            ]
          ]
      ], searchscreen->GetFilterData());

  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "XX" || todaysuniqueword ]
            ]
          ]
      ]);

  TT("filelist")->filter->TolliumClick();
  RECORD ARRAY rows := TT("filelist")->rows;
  TestEq(1,Length(rows));
  TestEq(linksrtdfile->id, rows[0].rowkey);

  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "MD" || todaysuniqueword ]
            ]
          ]
      ]);

  TT("filelist")->filter->TolliumClick();
  rows := TT("filelist")->rows;
  TestEq(1,Length(rows));
  TestEq(lisamarkdownfile->id, rows[0].rowkey);

  //=== Search by filetype
  searchscreen->filtercomps[0]->^filtertype->value := "publisher:whfstype";
  //Open the screen, ensure the list doesn't crash on eg tolliumicon...
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[0]->^selectqueryselectbutton->TolliumClick);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "consilio:linksto"
            , value := [ textquery := "example.net/link42" ]
            ]
          ]
      ]);

  TT("filelist")->filter->TolliumClick();

  rows := TT("filelist")->rows;
  //TODO 'and' queries to hit only one of the two
  TestEq(2,Length(rows));
  TestAssert(linksrtdfile->id IN SELECT AS INTEGER ARRAY rowkey FROM rows);
  TestAssert(lisartdfile->id IN SELECT AS INTEGER ARRAY rowkey FROM rows);

  //=== Search by widget type
  searchscreen->filtercomps[0]->^filtertype->value := "publisher:widgettype";
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[0]->^selectqueryselectbutton->TolliumClick);
  TT("groups")->selection := SELECT * FROM TT("groups")->rows WHERE title IN ["General","General (publisher)"];
  TT("contenttypes")->value := "http://www.webhare.net/xmlns/publisher/embedvideo";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //Search within test folder
  searchscreen->filtercomps[0]->^addfilterbutton->TolliumClick();
  searchscreen->filtercomps[1]->^filtertype->value := "publisher:whfsfolder";
  searchscreen->filtercomps[1]->^matchtype->value := "site";
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[1]->^selectqueryselectbutton->TolliumClick);
  TT("sites")->selection := SELECT * FROM TT("sites")->rows WHERE name = "webhare_testsuite.testsite";
  TestEq(TRUE, RecordExists(TT("sites")->selection));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TT("filelist")->filter->TolliumClick();
  TestEq([ "consenttest.rtd", "simpletest.rtd", "v2videotest.rtd" ], SELECT AS STRING ARRAY name FROM TT("filelist")->rows ORDER BY name);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO SearchDirectly()
{
  { //test direct searches used by dashboard
    AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", target := [ search := [ widgettype := "http://www.webhare.net/xmlns/webhare_testsuite/rtd/widgetblock" ]]]));

    RECORD ARRAY rows := TT("filelist")->rows;
    TestEq(TRUE, RecordExists(SELECT * FROM rows WHERE name = "widgetholder")); //should find the testuite widgetholder files

    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }

  { //test direct searches used by dashboard
    AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", target := [ search := [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/wrdauthtest-router" ]]]));

    RECORD ARRAY rows := TT("filelist")->rows;
    TestEq(TRUE, RecordExists(SELECT * FROM rows WHERE name = "wrdauthtest-router-broken"));

    AWAIT ExpectScreenChange(-1, PTR TTEscape);
  }
}

ASYNC MACRO SearchAsLisa()
{
  testfw->BeginWork();
  testfw->GetUserObject("sysop")->UpdateGrant("grant", "system:fs_browse", lisatestfolder->id, testfw->GetUserObject("lisa"));
  testfw->CommitWork();

  testfw->SetTestUser("lisa");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := ["Search"], target := DEFAULT RECORD ]));
  TestEq(TRUE, RecordExists(TT("foldertree")->selection));
  TestEq("Search", TT("foldertree")->selection.name);

  OBJECT searchscreen := TT("filelist")->folderfilter->contents;
  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "ZZ" || todaysuniqueword ]
            ]
          ]
      ]);

  TT("filelist")->filter->TolliumClick();
  RECORD ARRAY rows := TT("filelist")->rows;
  TestEq(1,Length(rows));
  TestEq(lisartdfile->id, rows[0].rowkey);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO SearchAsBart()
{
  //STORY: having worklists makes publisher & search visible ... but we saw publisher search returning results even without any browser access!
  testfw->BeginWork();
  OBJECT api_sysop := NEW WorkListAPI(testfw->GetUserObject("sysop"));

  FOREVERY(RECORD worklist FROM SELECT * FROM api_sysop->GetAllWorkLists() WHERE name LIKE "TESTFW - *")
    api_sysop->DeleteWorkList(worklist.id);

  INTEGER testworklistid := api_sysop->CreateWorkList([ name := "TESTFW - Test Work List", tag := "webhare_testsuite:testlist" ]);
  api_sysop->AddToWorkList(testworklistid,
      [ [ objectid := linksrtdfile->id, comments := "Work list item 1" ]
      , [ objectid := lisartdfile->id, comments := "Do some stuff for item 2" ]
      ]);
  api_sysop->AddWorkListUsers(testworklistid, OBJECT[ testfw->GetUserObject("bart") ]);
  testfw->CommitWork();

  testfw->SetTestUser("bart");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := ["Search"], target := DEFAULT RECORD ]));
  TestEq(TRUE, RecordExists(TT("foldertree")->selection));
  TestEq("Search", TT("foldertree")->selection.name);

  OBJECT searchscreen := TT("filelist")->folderfilter->contents;
  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "ZZ" || todaysuniqueword ]
            ]
          ]
      ]);

  //we should have no matches at all!
  TT("filelist")->filter->TolliumClick();
  TestEq(RECORD[], TT("filelist")->rows);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}


RunTestframework([ PTR PrepIt
                 , PTR Search
                 , PTR SearchDirectly
                 , PTR SearchAsLisa
                 , PTR SearchAsBart
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa" ]
                                   ,[ login := "bart" ]
                                   ]]);
