<?wh
LOADLIB "wh::files.whlib";

LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/internal/rtd/editable.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


OBJECT linksrtdfile;

MACRO PrepIt()
{
  testfw->BeginWork();

  OBJECT testroot := GetTestsuiteTempFolder();

  OBJECT myrichdoc := NEW EditableRichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob('<html><head></head><body>'
                                 || '<p class="normal"><a href="http://www.example.net/link42/">Link 42</a></p>'
                                 || '</body></html>')
                              ]);

  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  linksrtdfile := testroot->CreateFile([name := "links.rtd", type := richdoctype->id, publish:=FALSE]);

  richdoctype->SetInstanceData(linksrtdfile->id, [data := myrichdoc->ExportAsRecord()]);

  OBJECT indexpromise := MakeChangesIndexedPromise();
  testfw->CommitWork();
  WaitForPromise(indexpromise);

  //Make sure publisher:fsobjects is actually uptodate before we start testing (hopefully helps fix the CI race)
  OBJECT fsobjectssource := OpenConsilioCatalog(whconstant_consilio_catalog_whfs)->OpenContentSource("publisher:fsobjects");
  fsobjectssource->WaitForIndexingDone([ verbose := TRUE ]);
}

ASYNC MACRO Search()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:app", [ calltype := "direct", params := ["Search"], target := DEFAULT RECORD ]));
  TestEq(TRUE, RecordExists(TT("foldertree")->selection));
  TestEq("Search", TT("foldertree")->selection.name);

  OBJECT searchscreen := TT("filelist")->folderfilter->contents;
  TestEq(
      [ filters :=
          [ [ type := "publisher:text"
            , value := [ textquery := "" ]
            ]
          ]
      ], searchscreen->GetFilterData());
  
  //=== Search by filetype
  searchscreen->filtercomps[0]->^filtertype->value := "publisher:whfstype";
  //Open the screen, ensure the list doesn't crash on eg tolliumicon...
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[0]->selectqueryselectbutton->TolliumClick);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  searchscreen->SetFilterData(
      [ filters :=
          [ [ type := "consilio:linksto"
            , value := [ textquery := "example.net/link42" ]
            ]
          ]
      ]);

  TT("filelist")->filter->TolliumClick();

  RECORD ARRAY rows := TT("filelist")->rows;
  TestEq(1,Length(rows));
  TestEq(linksrtdfile->id, rows[0].rowkey);

  //=== Search by widget type
  searchscreen->filtercomps[0]->^filtertype->value := "publisher:widgettype";
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[0]->selectqueryselectbutton->TolliumClick);
  TT("groups")->selection := SELECT * FROM TT("groups")->rows WHERE title IN ["General","General (publisher)"];
  TT("contenttypes")->value := "http://www.webhare.net/xmlns/publisher/embedvideo";
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //Search within test folder
  searchscreen->filtercomps[0]->^addfilterbutton->TolliumClick();
  searchscreen->filtercomps[1]->^filtertype->value := "publisher:whfsfolder";
  searchscreen->filtercomps[1]->^matchtype->value := "site";
  AWAIT ExpectScreenChange(+1, PTR searchscreen->filtercomps[1]->^selectqueryselectbutton->TolliumClick);
  TT("sites")->selection := SELECT * FROM TT("sites")->rows WHERE name = "webhare_testsuite.testsite";
  TestEq(TRUE, RecordExists(TT("sites")->selection));
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  TT("filelist")->filter->TolliumClick();
  TestEq([ "consenttest.rtd", "simpletest.rtd", "v2videotest.rtd" ], SELECT AS STRING ARRAY name FROM TT("filelist")->rows ORDER BY name);

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestframework([ PTR PrepIt
                 , PTR Search
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ,[ login := "lisa" ]
                                   ]]);
