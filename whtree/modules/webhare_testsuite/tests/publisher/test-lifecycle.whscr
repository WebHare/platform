<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internal/testfuncs.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/webserver/management.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/database.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

MACRO EnsureTasksExecuted(DATETIME when)
{
  DATETIME deadline := AddTimeToDate(60000,GetCurrentDatetime());
  //Print("Waiting until " || formatdatetime("%H:%M:%S", deadline) || "\n");
  WHILE (TRUE)
  {
    RECORD ARRAY tasks_to_execute :=
        SELECT * FROM publisher.schedule
         WHERE COLUMN when <= VAR when;

    IF (NOT RecordExists(tasks_to_execute))
      BREAK;

    Sleep(100);
    IF(GetCurrentDatetime() >= deadline)
      ABORT("Task isn't completing");
  }

  testfw->WaitForPublishCompletion(GetTestsuiteTempFolder()->id);
  //PRINT("Tasks & publisher finished\n");
}

/** Wait for a file to have been published. Use this if you know that a file should be republish within a few seconds */
BOOLEAN FUNCTION WaitForBeenPublished(INTEGER fileid, DATETIME sincewhen)
{
  /* We loop because even if we EnsureTasksExecuted, there's no way to ensure
     that the publisher:executetasks post-commit completions have been sent
     to the completion handler (we could  see that the completions queue is empty,
     but is that because it hasn't received anything yet of because it's done ?) */

  DATETIME deadline := AddTimeToDate(15000,GetCurrentDatetime());
  WHILE (TRUE)
  {
    IF(IsBeingPublishedSince(fileid, sincewhen))
      RETURN TRUE;
    Sleep(100);
  }
  RETURN FALSE;
}

MACRO PublicationMoveFile()
{
  testfw->BeginWork();

  OBJECT tomovefile := GetTestsuiteTempFolder()->CreateFile(
      [ name:=        "tomove.html"
      , type :=       24
      , publish :=    TRUE
      ]);
  OBJECT destfolder := GetTestsuiteTempFolder()->CreateFolder([name := "destfolder"]);
  OBJECT deletedfolder := GetTestsuiteTempFolder()->CreateFolder([name := "deletedfolder"]);

  DATETIME now := GetCurrentDateTime();
  AddFileTask(tomovefile->id, now, 3, destfolder->id); // move

  testfw->CommitWork();
  EnsureTasksExecuted(now);

  tomovefile := OpenWHFSObject(tomovefile->id);
  TestEq(destfolder->id, tomovefile->parent);

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(tomovefile->id, now, 3, deletedfolder->id); //move file into a delted folder
  deletedfolder->RecycleSelf();
  testfw->CommitWork();
  EnsureTasksExecuted(now);

  tomovefile := OpenWHFSObject(tomovefile->id);
  TestEq(deletedfolder->id, tomovefile->parent);


}

MACRO PublicationDeleteFile()
{
  testfw->BeginWork();

  OBJECT tokillfile := GetTestsuiteTempFolder()->CreateFile(
      [ name:=        "todelete.html"
      , type :=       24
      , publish :=    TRUE
      ]);

  DATETIME now := GetCurrentDateTime();
  AddFileTask(tokillfile->id, now, 4, 0); // delete
  testfw->CommitWork();
  EnsureTasksExecuted(now);

  tokillfile := OpenWHFSObject(tokillfile->id);
  TestEq(FALSE, tokillfile->isactive);

}

MACRO PublicationDeleteFolder()
{
  testfw->BeginWork();

  OBJECT tokillfolder := GetTestsuiteTempFolder()->CreateFolder(
      [ name:=        "todelete"
      ]);
  OBJECT tokillfile := tokillfolder->CreateFile(
      [ name:=        "todelete.html"
      , type :=       24
      , publish :=    TRUE
      ]);

  DATETIME now := GetCurrentDateTime();
  AddFileTask(tokillfolder->id, now, 4, 0); // delete
  testfw->CommitWork();
  EnsureTasksExecuted(now);

  tokillfolder := OpenWHFSObject(tokillfolder->id);
  TestEq(FALSE, tokillfolder->isactive);

}

MACRO PublicationStartStopTest()
{
  testfw->BeginWork();

  OBJECT indexfile := GetTestsuiteTempFolder()->CreateFile(
      [ name:=        "index.html"
      , type :=       24
      , publish :=    TRUE
      ]);

  OBJECT testfile := GetTestsuiteTempFolder()->CreateFile(
      [ name:=        "test.html"
      , data :=       StringToBlob("Dit is tekst")
      , publish :=    FALSE
      ]);

  testfw->CommitWork();
  testfw->WaitForPublishCompletion(GetTestsuiteTempFolder()->id);

  testfw->BeginWork();
  DATETIME now := GetCurrentDateTime();
  AddFileTask(testfile->id, now, 1, 0); // Start publish NOW -> sets published to true, must republish all
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  //this tests that both testfile and indexfile have been (re)queued for republication since the last 'now'
  TestEQ(TRUE, IsBeingPublishedSince(testfile->id, now));
  TestEQ(TRUE, IsBeingPublishedSince(indexfile->id, now));

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->id, now, 1, 0); // Republish only testfile, now
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  //this tests that testfile has been (re)queued for republication since the last 'now', but not indexfile
  TestEQ(TRUE, IsBeingPublishedSince(testfile->id, now));
  TestEQ(FALSE, IsBeingPublishedSince(indexfile->id, now));

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->id, now, 2, 0); // Unpublish file
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  TestEQ(TRUE, WaitForBeenPublished(indexfile->id, now));
  testfile->Refresh();
  TestEQ(FALSE, testfile->publish);

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->parent, now, 1, 0); // Publish folder
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  TestEQ(TRUE, IsBeingPublishedSince(testfile->id, now));
  TestEQ(TRUE, IsBeingPublishedSince(indexfile->id, now));

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->parent, now, 1, 0); // Republish folder
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  TestEQ(TRUE, IsBeingPublishedSince(testfile->id, now));
  TestEQ(TRUE, IsBeingPublishedSince(indexfile->id, now));

  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->parent, now, 2, 0); // Unpublish folder
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  indexfile->Refresh();
  TestEQ(FALSE, indexfile->publish);
  testfile->Refresh();
  TestEQ(FALSE, testfile->publish);

  // Publish everything
  testfw->BeginWork();
  now := GetCurrentDateTime();
  AddFileTask(testfile->parent, now, 1, 0); // Publish folder
  testfw->CommitWork();

  EnsureTasksExecuted(now);
}

MACRO SetIndexDocTest()
{
  OBJECT indexfile := GetTestsuiteTempFolder()->OpenByPath("index.html");
  OBJECT testfile := GetTestsuiteTempFolder()->OpenByPath("test.html");

  testfw->BeginWork();
  indexfile->UpdateMetadata([ name := "test2.html" ]);
  testfw->CommitWork();

  TestEQ(indexfile->id, GetTestsuiteTempFolder()->indexdoc);

  // Make sure any republication is done
  testfw->WaitForPublishCompletion(GetTestsuiteTempFolder()->id);

  testfw->BeginWork();
  DATETIME now := GetCurrentDateTime();
  AddFileTask(testfile->id, now, 5, 0);
  testfw->CommitWork();

  EnsureTasksExecuted(now);

  GetTestsuiteTempFolder()->Refresh();
  TestEQ(testfile->id, GetTestsuiteTempFolder()->indexdoc);

  TestEQ(TRUE, IsBeingPublishedSince(testfile->id, now));
  TestEQ(TRUE, IsBeingPublishedSince(indexfile->id, now));
}

RunTestFramework([ PTR PublicationDeleteFile
                 , PTR PublicationDeleteFolder
                 , PTR PublicationMoveFile
                 , PTR PublicationStartStopTest
                 , PTR SetIndexDocTest
                 ]);
