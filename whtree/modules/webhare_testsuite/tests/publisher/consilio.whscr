<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/richdocument.whlib";
LOADLIB "wh::promise.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/search.whlib";
LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

MACRO ConsilioTest()
{
  testfw->BeginWork();

  OBJECT siteroot := GetTestsuiteTempFolder();
  OBJECT subfolder := siteroot->CreateFolder([ name := "subfolder" ]);

  OBJECT catalog_1 := CreateConsilioCatalog("consilio:testfw_frontend_1", [ fieldgroups := [ "webhare_testsuite:extrafield" ], priority := 9 ]);
  OBJECT catalog_2 := CreateConsilioCatalog("consilio:testfw_frontend_2", [ priority := 9 ]);
  OBJECT contentsource_1 := catalog_1->AddFolderToCatalog(siteroot->id);
  OBJECT contentsource_2 := catalog_2->AddFolderToCatalog(subfolder->id);
  testfw->CommitWork();

  Print(catalog_1->GetStorageInfo() || ", " || catalog_2->GetStorageInfo() || "\n");

  testfw->BeginWork();

  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT file_1 := siteroot->CreateFile([name:="testfile1.txt", data := StringToBlob("blabla check"), publish := TRUE]);
  OBJECT file_2 := siteroot->CreateFile([name:="testfile2.whlib", data := StringToBlob("<?wh PUBLIC MACRO RunDynamicPage() { PRINT('blabla2'); }") ]);
  OBJECT file_3 := siteroot->CreateFile([name:="testfile3.shtml", data := StringToBlob("<?wh LOADLIB 'relative::testfile2.whlib' EXPORT RunDynamicPage;"), publish := TRUE]);
  OBJECT file_4 := siteroot->CreateFile([name:="testfile4.txt", data := StringToBlob("blabla4"), publish := TRUE]);
  OBJECT file_6 := siteroot->CreateFile([name:="testfile6.rtd", type := richdoctype->id, publish := TRUE]);

  OBJECT myrichdoc := NEW RichDocument;
  myrichdoc->ImportFromRecord([ htmltext := StringToBlob('<html><head></head><body>'
                                 || '<h1 class="heading1"><a href="">Ik</a> <a name="ben">ben</a>     \neen <b>heading 1&</b></h1>'
                                 || '<p class="normal"><br/></p>'
                                 || '<p class="normal">Ik ben een <b>&lt;normale&gt;</b> <a href="http://b-lex.nl/">b-lex</a> <i>paragraaf</i><br/>met\neen      soft-break. Check deze afbeelding: <img src="cid:imagecid-81400" width="160" height="120" alt="I&#38;G" /></p>'
                                 || '<table><colgroup><col style="width:25px"/><col style="width:45px"/></colgroup><tbody>'
                                    || '<tr>'
                                      || '<td><p data-rtd-anchor="bob" class="normal">Cell 1 para 1</p><p class="normal">Cell 1 para 2</p></td>'
                                      || '<td><p class="normal">Cell 2 para 1</p></td>'
                                    || '</tr><tr>'
                                      || '<td><p class="normal">Cell 3</p></td>'
                                      || '<td><p class="normal">Cell 4</p></td>'
                                    || '</tr>'
                                 || '</tbody></table>'
                                 || '</body></html>')
                 , embedded := [ MakeMergedRecord(WrapBlob(GetHarescriptResource("mod::system/web/tests/snowbeagle.jpg"),"bob.jpg"), [ contentid := "imagecid-81400" ]) ]
                 ]);;

  richdoctype->SetInstanceData(file_6->id, [data := myrichdoc->ExportAsRecord()]);

  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQAll(), [refresh := TRUE ]).totalcount < 4)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_1 reached expected length >= 4\n`);

  OBJECT search_1_obj := OpenSearchObject("en", "consilio:testfw_frontend_1");
  search_1_obj->empty_result_record := DEFAULT RECORD;
  search_1_obj->summary_length := 0;
  search_1_obj->donthighlight := TRUE;
  OBJECT search_2_obj := OpenSearchObject("en", "consilio:testfw_frontend_2");
  search_2_obj->empty_result_record := DEFAULT RECORD;
  search_2_obj->summary_length := 0;
  RECORD results;

  results := search_1_obj->SearchQuery(CQMatch("body", "CONTAINS", "blabla"), 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);

  results := search_1_obj->Search("blabla2", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_3->objecturl
              , groupid :=    ToString(file_3->id)
              ]
            ]
      ], results);

  results := search_1_obj->Search("blabla4", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_4->objecturl
              , groupid :=    ToString(file_4->id)
              ]
            ]
      ], results);

  results := search_1_obj->Search("paragraaf", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_6->objecturl
              , groupid :=    ToString(file_6->id)
              ]
            ]
      ], results);

  testfw->BeginWork();
  file_4->RecycleSelf();
  testfw->CommitWork();

  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQAll(), [refresh := TRUE ]).totalcount != 3)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_1 reached expected length 3 (after recycleself)\n`);

  results := search_1_obj->Search("blabla4", 0, 10);
  TestEq(
      [ totalcount :=       0
      , results :=          DEFAULT RECORD ARRAY
      ], results);

  // Remove a group manually
  contentsource_1->DeleteGroup(ToString(file_1->id));
  contentsource_1->WaitForIndexingDone();

  results := search_1_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount :=       0
      , results :=          DEFAULT RECORD ARRAY
      ], results);

  // Update the index
  contentsource_1->ReindexContentSource([ rebuild := TRUE ]);
  contentsource_1->WaitForIndexingDone();

  results := search_1_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);

  TestEq(0, RunConsilioSearch("consilio:testfw_frontend_2", CQAll(), [refresh := TRUE ]).totalcount, "Verify the second index is still empty...");

  testfw->BeginWork();
  file_1->MoveTo(subfolder, "");
  testfw->CommitWork();

  file_1->Refresh();

  testfw->WaitForPublishCompletion(siteroot->id);

  WHILE(RunConsilioSearch("consilio:testfw_frontend_2", CQAll(), [refresh := TRUE ]).totalcount != 1)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_2 reached expected length 1 (after moveto)\n`);

  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQParseUserQUery("blabla"), [refresh := TRUE ]).results[0].objectid != file_1->objecturl)
    Sleep(50);

  Print(`.. consilio:testfw_frontend_1 finally saw the move\n`);

  results := search_2_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);

  // Test restrict url
  search_1_obj->restrict_url := siteroot->objecturl || "folder/";
  results := search_1_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount :=       0
      , results := DEFAULT RECORD ARRAY
      ], results);
  search_1_obj->restrict_url := Left(file_1->url, SearchLastSubstring(file_1->url, "/") + 1);
  results := search_1_obj->Search("blabla", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);
  results := search_1_obj->Search("blabla2", 0, 10);
  TestEq(
      [ totalcount :=       0
      , results := DEFAULT RECORD ARRAY
      ], results);
  search_1_obj->restrict_url := "";
  results := search_1_obj->Search("blabla2", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_3->objecturl
              , groupid :=    ToString(file_3->id)
              ]
            ]
      ], results);

  // Test exclude url's.
  // Note we MUST use date_whmodification as that's the field we told we'd use in the mapping
  RECORD query := CQAnd([ CQMatch("body", "CONTAINS", "check"), CQMatch("date_whmodification", "<=", AddDaysToDate(1, GetCurrentDateTime())) ]);
  results := search_1_obj->SearchQuery(query, 0, 10);
  TestEq(2, results.totalcount);
  TestEq(TRUE, RecordExists(SELECT FROM results.results WHERE objectid = file_1->objecturl));
  TestEq(TRUE, RecordExists(SELECT FROM results.results WHERE objectid = file_6->objecturl));
  search_1_obj->exclude_urls := [ Left(file_1->url, SearchLastSubstring(file_1->url, "/") + 1) ];
  results := search_1_obj->SearchQuery(query, 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_6->objecturl
              , groupid :=    ToString(file_6->id)
              ]
            ]
      ], results);
  search_1_obj->exclude_urls := STRING[];

  //Test the custom fields
  testfw->BeginWork();
  OBJECT file_5 := siteroot->CreateFile([name:="testfile5.html", data := StringToBlob('<html><head><meta name="consilio-extrafield" content="testextra5 and more stuff" /></head><body>blabla5</body></html>'), publish := TRUE, type := 21]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQMatch("groupid","=", ToString(file_5->id)), [refresh := TRUE ]).totalcount = 0)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_1 has indexed file_5\n`);

  search_1_obj->empty_result_record := [ extrafield := "" ];
  results := search_1_obj->SearchQuery(CQMatch("extrafield", "CONTAINS", "testextra5"), 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_5->objecturl
              , groupid :=    ToString(file_5->id)
              , extrafield := "testextra5 and more stuff"
              ]
            ]
      ], results);
  search_1_obj->empty_result_record := DEFAULT RECORD;

  // Test using Publisher titles instead of file titles
  testfw->BeginWork();
  OBJECT file_7 := siteroot->CreateFile([name:="testfile7.pdf", data := testfw->GetModuleTestBlob("webhare_testsuite","publisher/testdata/pdf-with-title.pdf"), title := "", publish := TRUE, type := 11]);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQMatch("groupid","=", ToString(file_7->id)), [refresh := TRUE ]).totalcount = 0)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_1 has indexed file_7\n`);

  // Search for a word in the content
  results := search_1_obj->Search("content", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_7->objecturl
              , groupid :=    ToString(file_7->id)
              ]
            ]
      ], results);
  // Search for a word in the PDF title
  results := search_1_obj->Search("certain", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_7->objecturl
              , groupid :=    ToString(file_7->id)
              ]
            ]
      ], results);

  // Update the file title, which should now be used for indexing instead of the PDF title
  testfw->BeginWork();
  file_7->UpdateMetaData([ title := "this is the overwritten titel" ]);
  ScheduleFileRepublish(file_7->id);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  WHILE(RunConsilioSearch("consilio:testfw_frontend_1", CQParseUserQUery("overwritten"), [refresh := TRUE ]).totalcount = 0)
    Sleep(50);
  Print(`.. consilio:testfw_frontend_1 has indexed new title of file_7\n`);

  // Search for a word in the content
  results := search_1_obj->Search("content", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid := file_7->objecturl
              , groupid :=  ToString(file_7->id)
              ]
            ]
      ], results);
  // Search for a word in the PDF title, should no longer be found
  results := search_1_obj->Search("certain", 0, 10);
  TestEq(
      [ totalcount :=       0
      , results :=          RECORD[]
      ], results);
  // Search for a word in the file title
  results := search_1_obj->Search("overwritten", 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid := file_7->objecturl
              , groupid :=  ToString(file_7->id)
              ]
            ]
      ], results);

  // Test not indexing internal links
  results := search_1_obj->SearchQuery(CQMatch("body", "CONTAINS", "blabla"), 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);

  // Add a link to file 1
  testfw->BeginWork();
  OBJECT linkfile := siteroot->CreateFile([name:="testlink.whlink", type := 19, filelink := file_1->id, publish := TRUE]);
  TestEq(file_1->link, linkfile->link);
  testfw->CommitWork();

  testfw->WaitForPublishCompletion(siteroot->id);
  catalog_1->Refresh();
  Sleep(500); //this is potentially a race unless we can somehow add a marker ?

  // The search should still (only) return file 1
  results := search_1_obj->SearchQuery(CQMatch("body", "CONTAINS", "blabla"), 0, 10);
  TestEq(
      [ totalcount :=       1
      , results :=
            [ [ objectid :=   file_1->objecturl
              , groupid :=    ToString(file_1->id)
              ]
            ]
      ], results);
}

RunTestframework([ PTR ConsilioTest ]);
