<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/testframework.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";


MACRO PrepContentSnippets()
{
  testfw->BeginWork();

  OBJECT snippetsfolder := GetTestsuiteTempFolder()->EnsureFolder([name := "snippets", typens := "http://www.webhare.net/xmlns/publisher/contentlibraries/contentsnippets" ]);
  OBJECT textsnippet := snippetsfolder->CreateFile([ name := "textsnippet", title := "Text Snippet", typens := "http://www.webhare.net/xmlns/publisher/widgets/contentsnippet" ]);
  textsnippet->SetInstanceData("http://www.webhare.net/xmlns/publisher/widgets/contentsnippet",
      [ snippettext :=  [ htmltext := StringToBlob(`
                           <html><body>
                             <p class="wh-normal">A simple text snippet</p>
                             <div class="wh-rtd-embeddedobject" data-instanceid="level1snippet" />
                           </body></html>`)
                        , instances :=
                          [ [ instanceid := "level1snippet"
                            , data :=
                              [ whfstype := "http://www.webhare.net/xmlns/webhare_testsuite/rtd/embedlvl1"
                              ]
                            ]
                          ]
                        ]
      ]);

  OBJECT includingsnippet := snippetsfolder->CreateFile([ name := "includingsnippet", title := "Including Snippet", typens := "http://www.webhare.net/xmlns/publisher/widgets/contentsnippet" ]);
  includingsnippet->SetInstanceData("http://www.webhare.net/xmlns/publisher/widgets/contentsnippet",
      [ snippettext :=  [ htmltext := StringToBlob(`
                           <html><body>
                             <p class="wh-normal">An including snippet</p>
                             <div class="wh-rtd-embeddedobject" data-instanceid="includetextsnippet" />
                           </body></html>`)
                        , instances :=
                          [ [ instanceid := "includetextsnippet"
                            , data :=
                              [ whfstype := "http://www.webhare.net/xmlns/publisher/widgets/insertsnippet"
                              , insertsnippet := textsnippet->id
                              ]
                            ]
                        ]
        ]
      ]);

  OBJECT richdocfolder := GetTestsuiteTempFolder()->EnsureFolder([name := "rtdtype"]);
  OBJECT richdoctype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  OBJECT snippets1doc := richdocfolder->CreateFile(
      [ name := "snippets-1"
      , type := richdoctype->id
      , publish := TRUE
    ]);

  richdoctype->SetInstanceData(snippets1doc->id,
      [ data :=
        [ htmltext := StringToBlob(`
             <html><body>
               <p class="wh-normal">Para 1</p>
               <div class="wh-rtd-embeddedobject" data-instanceid="snippet1" />
             </body></html>`)
        , instances :=
          [ [ instanceid := "snippet1"
            , data :=
              [ whfstype := "http://www.webhare.net/xmlns/publisher/widgets/insertsnippet"
              , insertsnippet := textsnippet->id
              ]
            ]
          ]
        ]
      ]);

  OBJECT snippets2doc := richdocfolder->CreateFile(
      [ name := "snippets-2"
      , type := richdoctype->id
      , publish := TRUE
    ]);

  richdoctype->SetInstanceData(snippets2doc->id,
      [ data :=
        [ htmltext := StringToBlob(`
             <html><body>
               <p class="wh-normal">Para 2</p>
               <div class="wh-rtd-embeddedobject" data-instanceid="snippet2" />
             </body></html>`)
        , instances :=
          [ [ instanceid := "snippet2"
            , data :=
              [ whfstype := "http://www.webhare.net/xmlns/publisher/widgets/insertsnippet"
              , insertsnippet := includingsnippet->id
              ]
            ]
          ]
        ]
      ]);

  testfw->CommitWork();
}

MACRO TestInclusions()
{
  OBJECT richdocfolder := GetTestsuiteTempFolder()->OpenByname("rtdtype");

  WaitForPublishCompletion(richdocfolder->id);

  TestEq(TRUE, testfw->browser->GotoWebPage(richdocfolder->objecturl || "snippets-1/"));
  TestEq(["p","p","div"], SELECT AS STRING ARRAY node->nodename FROM ToRecordArray(testfw->browser->QS("#content")->ListChildren("*","*"),"node"));
  TestEq("A simple text snippet", testfw->browser->QS("#content > p + p")->textcontent);

  TestEq(TRUE, testfw->browser->GotoWebPage(richdocfolder->objecturl || "snippets-2/"));
  TestEq(["p","p","p","div"], SELECT AS STRING ARRAY node->nodename FROM ToRecordArray(testfw->browser->QS("#content")->ListChildren("*","*"),"node"));
  TestEq("An including snippet", testfw->browser->QS("#content > p + p")->textcontent);
  TestEq("A simple text snippet", testfw->browser->QS("#content > p + p + p")->textcontent);
  TestEq("/webhare-tests/webhare_testsuite.testsite/tmp/", testfw->browser->QS("#content > div.level1widget")->GetAttribute("data-libraries-temp"));
}

ASYNC MACRO TestInsertContentSnippetWidget()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ GetTestsuiteTempFolder()->whfspath || "rtdtype/snippets-1"], target := DEFAULT RECORD ]));

  AWAIT ExpectScreenChange(+1, PTR TT("doceditor")->contents->rte->ProcessInboundMessage("buttonclick", [ button := "object-insert" ]));
  TT("contenttypes")->selection := SELECT * FROM TT("contenttypes")->rows WHERE rowkey = "http://www.webhare.net/xmlns/publisher/widgets/insertsnippet";
  TestEq(TRUE, RecordExists(TT("contenttypes")->selection));
  AWAIT ExpectScreenChange(0, PTR TTClick(":OK"));

  TestEq(TRUE, RecordExists(SELECT FROM TT(":Snippet")->options WHERE title="Text Snippet"));

  AWAIT ExpectAndAnswerMessageBox("ok", PTR TTClick(":Ok"), [ messagemask := "*Snippet*required*"]); //cannot submit, must select snippet

  TT(":Snippet")->selection := SELECT * FROM TT(":Snippet")->options WHERE title="Text Snippet";

  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));
  AWAIT ExpectAndAnswerMessageBox("yes", PTR TTClick("publishbutton")); //answer to dialog: are you sure you want to publish?
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestReferences()
{
  OBJECT snippetsfolder := GetTestsuiteTempFolder()->OpenByName("snippets");
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher", [ calltype := "direct", params := [ snippetsfolder->whfspath || "textsnippet"], target := DEFAULT RECORD ]));
  AWAIT ExpectScreenChange(+1, PTR TTClick("props"));

  TestEq(2, Length(TT("fragment1!documents")->rows)); //We lack a clean way to find Documents on the References tab, so for now fragment1 will work

  AWAIT ExpectScreenChange(-1, PTR TTEscape);
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

ASYNC MACRO TestModifySnippets()
{
  OBJECT snippetsfolder := GetTestsuiteTempFolder()->OpenByName("snippets");
  OBJECT richdocfolder := GetTestsuiteTempFolder()->OpenByname("rtdtype");

  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher:edit", [ calltype := "direct", params := [ snippetsfolder->whfspath || "textsnippet"], target := DEFAULT RECORD ]));
  TestEqLike("*A simple text snippet*", BlobToString(TT("component")->GetComponent("snippettext")->value.htmltext));

  TT("component")->GetComponent("snippettext")->value :=
                        [ htmltext := StringToBlob(`
                           <html><body>
                             <p class="wh-normal">We have altered this text. Pray we don't alter it any further.</p>
                           </body></html>`)
                        ];
  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));

  WaitForPublishCompletion(richdocfolder->id);
  TestEq(TRUE, testfw->browser->GotoWebPage(richdocfolder->objecturl || "snippets-1/"));
  TestEq("We have altered this text. Pray we don't alter it any further.", testfw->browser->QS("#content > p + p")->textcontent);
}

ASYNC MACRO TestManageSnippets()
{
  AWAIT ExpectScreenChange(+1, PTR TTLaunchApp("publisher", [ params := ["/webhare-tests/webhare_testsuite.testsite/tmp/snippets" ]]));
  TestEq(2, Length(TT("filelist")->rows));

  AWAIT ExpectScreenChange(+1, PTR TTClick("newfolder"));
  TestEq(RECORD[], TT("types")->rows, "All folder types should be blocked"); //and a new publisher UI should remove the need for an empty newfolder dialog
  AWAIT ExpectScreenChange(-1, PTR TTEscape);

  AWAIT ExpectScreenChange(+1, PTR TTClick("newfile"));
  TT(":Widget name")->value := "New Snippet";

  OBJECT rte := TT("component")->GetComponent("snippettext");
  AWAIT ExpectScreenChange(+1, PTR rte->ProcessInboundMessage("buttonclick", [ button := "object-insert" ]));

  GetTestController()->GrabInstructions(); //Flush all instructions

  TT("contenttypes")->selection := SELECT * FROM TT("contenttypes")->rows WHERE title = "http://www.webhare.net/xmlns/webhare_testsuite/rtd/embedlvl1";
  TestEq(TRUE, RecordExists(TT("contenttypes")->selection));
  AWAIT ExpectScreenChange(-1, PTR topscreen->TolliumExecuteSubmit());

  AWAIT ExpectScreenChange(+1, DEFAULT MACRO PTR); //widget editor for embedlvl1 automatically opens
  AWAIT ExpectScreenChange(-1, PTR TTClick(":Ok"));

  //Get the preview and ensure it had an applytester - <richdocument> had a bug where it didn't forward the applytester, breaking previews in "New widget" (fake applytester) flows
  RECORD insertedobjectinstr := SELECT * FROM GetTestController()->GrabInstructions() WHERE instr="update" AND type="messages" AND messages[0].type="InsertEmbeddedObject";
  TestAssert(RecordExists(insertedobjectinstr), "Should have an inserted object on the instructionstream");

  OBJECT inserteddom := MakeXMLDocumentFromHTML(StringToBlob(insertedobjectinstr.messages[0].data.widget.htmltext));
  TestEq("/webhare-tests/webhare_testsuite.testsite/tmp/", inserteddom->QuerySelector("div")->GetAttribute("data-libraries-temp"));

  AWAIT ExpectScreenChange(-1, PTR TTClick(":OK"));
  AWAIT ExpectScreenChange(-1, PTR TTEscape);
}

RunTestFramework([ PTR PrepContentSnippets
                 , PTR TestInclusions
                 , PTR TestReferences
                 , PTR TestInsertContentSnippetWidget
                 , PTR TestModifySnippets
                 , PTR TestManageSnippets
                 ], [ testusers := [[ login := "sysop", grantrights := ["system:sysop"] ]
                                   ]
                    ]);
