<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::webhare_testsuite/lib/system/tests.whlib";

MACRO TestWHFSType()
{
  RECORD htmlfile := DescribeContentTypeById(5);
  TestEq(TRUE, htmlfile.needstemplate);
  TestEq(TRUE, htmlfile.ispublishable);
  TestEQ(TRUE, htmlfile.isacceptableindex);
  TestEQ(TRUE, htmlfile.blobiscontent);
  TestEq("http://www.webhare.net/xmlns/publisher/htmlfile", htmlfile.namespace);

  OBJECT rtdtype := OpenWHFStype("http://www.webhare.net/xmlns/publisher/richdocumentfile");
  TestEQ([[name:="data",type:=15],[name :="original", type := 5]], SELECT name,type FROM rtdtype->members ORDER BY name);
  TestEq(TRUE, rtdtype->cloneoncopy);

  OBJECT dynfoldertype := OpenWHFStype("http://www.webhare.net/xmlns/beta/dynfolder");
  TestEQ([[name:="test",type:=2]], SELECT name,type FROM dynfoldertype->members ORDER BY name);

  RECORD wordtype := DescribeContentTypeById(4);
  TestEq(TRUE, wordtype.needstemplate);
  TestEq(TRUE, wordtype.ispublishable);
  TestEq("publisher:filetypes.msword", wordtype.title);
  TestEQ(TRUE, wordtype.isacceptableindex);
  TestEq(4,wordtype.id);
  TestEq("http://www.webhare.net/xmlns/publisher/mswordfile", wordtype.namespace);
  Testeq([".doc"], wordtype.extensions);
  TestEq(wordtype, DescribeContentTypeById(4, [isfolder := FALSE]));
  TestEQ(TRUE, wordtype.blobiscontent);
  TestEq("tollium:files/application_msword", wordtype.tolliumicon);
  TestEQ(FALSE, wordtype.ishidden);
  TestEq(DEFAULT RECORD, wordtype.dynamicexecution);
  TestEq(DEFAULT RECORD, DescribeContentTypeById(4, [isfolder := TRUE]));

  TestEq("mod::publisher/data/publisher.siteprl.xml", wordtype.siteprofile);
  TestEq(TRUE, wordtype.line > 20);

  TestEq(DEFAULT RECORD, DescribeContentTypeById(0));

  RECORD normalfolder := DescribeContentTypeById(0, [isfolder := TRUE]);
  TestEq(0, normalfolder.id);
  TestEQ(FALSE, normalfolder.blobiscontent);
  TestEq("http://www.webhare.net/xmlns/publisher/normalfolder", normalfolder.namespace);
  TestEq("tollium:folders/normal", normalfolder.tolliumicon);

  RECORD systemfolder := DescribeContentTypeById(2, [isfolder := TRUE]);
  TestEq(2, systemfolder.id);
  TestEQ(TRUE, systemfolder.ishidden);
  TestEq("http://www.webhare.net/xmlns/publisher/systemfolder", systemfolder.namespace);

  RECORD unknownfile := DescribeContentTypeById(0, [isfolder := FALSE]);
  TestEq(0, unknownfile.id);
  TestEQ(TRUE, unknownfile.blobiscontent);
  TestEq("http://www.webhare.net/xmlns/publisher/unknownfile", unknownfile.namespace);
  TestEq("tollium:files/application_x-webhare-unknown", unknownfile.tolliumicon);

  RECORD unrecognized := DescribeContentTypeById(999999999);
  TestEq(DEFAULT RECORD, unrecognized);

  TestThrowsLike('*mockifmissing*isfolder*', PTR DescribeContentTypeById(4, [ mockifmissing := TRUE]));
  TestThrowsLike('*mockifmissing*isfolder*', PTR DescribeContentTypeById(999999999, [ mockifmissing := TRUE ]));

  unrecognized := DescribeContentTypeById(999999999, [ isfolder := TRUE, mockifmissing := TRUE ]);
  TestEq(999999999, unrecognized.id);
  TestEq(":#999999999", unrecognized.title);
  TestEq("tollium:folders/normal", unrecognized.tolliumicon);
  TestEQ(FALSE, unrecognized.ishidden);
  TestEq("", unrecognized.siteprofile);
  TestEq(0, unrecognized.line);

  unrecognized := DescribeContentTypeById(999999999, [ isfolder := FALSE, mockifmissing := TRUE ]);
  TestEq(999999999, unrecognized.id);
  TestEq(":#999999999", unrecognized.title);
  TestEq("tollium:files/application_x-webhare-unknown", unrecognized.tolliumicon);

  RECORD appportaltype := DescribeContentTypeById(LookupContentTypeByName("http://www.webhare.net/xmlns/tollium/applicationportal").id);
  TestEQ(TRUE, RecordExists(appportaltype.dynamicexecution));
  TestEq(whconstant_publisher_fileiconfallback, appportaltype.tolliumicon); //no icon is (yet?) set
  //ADDME test title being set to namespace with orphan type

  RECORD photoalbum := DescribeContentTypeById(3);
  TestEq("tollium:folders/photoalbum", photoalbum.tolliumicon); //no icon is (yet?) set
  TestEq("contentlisting:http://www.webhare.net/xmlns/publisher/contentlisting", photoalbum.indexfile);
  TestEq(TRUE, photoalbum.protectindexfile);

  TestEq("http://www.webhare.net/xmlns/webhare_testsuite/basetestprops", OpenWHFSType("webhare_testsuite:base_test.base_test_props")->namespace);
  TestEq(OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/basetestprops")->id, OpenWHFSType("webhare_testsuite:base_test.base_test_props")->id);
}

MACRO TestApplyRuleTester()
{
  testfw->BeginWork();

  OBJECT tester := GetApplyTesterForFakeObject(OpenTestsuiteSite()->id, TRUE, 20); //contentlink
  TestEq(FALSE, tester->IsTypeNeedsTemplate()); //this crashed with fake objects at one point
  TestEq(TRUE, tester->IsNew());
  TestEq(FALSE, tester->IsSiteRoot());
  TestEq(FALSE, tester->IsIndexDoc());
  TestEq("http://www.webhare.net/xmlns/publisher/contentlink", tester->objtypens);

  OBJECT testindexfolder := GetTestsuiteTempFolder()->CreateFolder([name:="testindexfolder" ]);
  OBJECT testindex := testindexfolder->CreateFile([name := "index.html"]);

  tester := GetApplyTesterForObject(testindex->id);
  TestEq(FALSE, tester->IsNew());
  TestEq(TRUE, tester->IsIndexDoc());

  OBJECT siteroottester := GetApplyTesterForObject(OpenTestsuiteSite()->id);
  TestEq(TRUE, siteroottester->IsSiteRoot());
  TestEq("http://www.webhare.net/xmlns/publisher/normalfolder", siteroottester->objtypens);

  tester := GetApplyTesterForFakeObject(OpenTestsuiteSite()->id, TRUE, 0);
  TestEq("http://www.webhare.net/xmlns/publisher/unknownfile", tester->objtypens);

  tester := GetApplyTesterForFakeObject(OpenTestsuiteSite()->id, FALSE, 0);
  TestEq("http://www.webhare.net/xmlns/publisher/normalfolder", tester->objtypens);
  testfw->RollbackWork();

  testfw->BeginWork();
  OpenTestsuiteSite()->rootobject->SetInstanceData("webhare_testsuite:base_test.site_settings",  OpenWHFStype("webhare_testsuite:base_test.site_settings")->defaultinstance);
  testfw->CommitWork(); //to trigger flush of siteprofile apply caches

  testfw->BeginWork();
  tester := GetApplyTesterForObject(OpenTestsuiteSite()->OpenByPath("testpages")->id);
  TestEq(DEFAULT RECORD, tester->GetUserData("webhare_testsuite:setting"));

  OpenTestsuiteSite()->rootobject->SetInstanceData("webhare_testsuite:base_test.site_settings", [ mode := "blue" ]);
  testfw->CommitWork();  //to trigger flush of siteprofile apply caches

  testfw->BeginWork();
  tester := GetApplyTesterForObject(OpenTestsuiteSite()->OpenByPath("testpages")->id);
  TestEq([ dogname := "bluey", isdateset := FALSE ], tester->GetUserData("webhare_testsuite:setting"));
  OpenTestsuiteSite()->rootobject->SetInstanceData("webhare_testsuite:base_test.site_settings", [ setting := "red", when := MakeDate(2024,1,1) ]);
  testfw->CommitWork();  //to trigger flush of siteprofile apply caches

  testfw->BeginWork();
  tester := GetApplyTesterForObject(OpenTestsuiteSite()->OpenByPath("testpages")->id);
  TestEq([ dogname := "bluey", sistername := "bingo" ], tester->GetUserData("webhare_testsuite:setting"));
  testfw->RollbackWork();

  tester := GetApplyTesterForObject(OpenTestsuiteJSSite()->OpenByPath("testpages/staticpage")->id);
  //yaml baseProps is allowed to reset most settings
  TestEqMembers([ title := TRUE, description := FALSE, seotitle := TRUE ], GetBaseProperties(tester),'*');
}

MACRO SiteSettingsTester()
{
  STRING webdir := OpenTestsuiteSite()->webroot || "redirecttowebdir/";
  TestEq(TRUE, testfw->browser->GotoWebPage(webdir), webdir);
  TestEq("index.shtml", BlobToString(testfw->browser->content));

  // datastorage tryfile/tryfolder
  STRING datastorage_base := OpenTestsuiteSite()->webroot || "datastorage/";
  STRING basedatadir := GetWebHareConfiguration().basedataroot;

  // Delete the files we'll generate in this test
  DeleteDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/folderdirect/test/blue.html");
  DeleteDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha/72a/9b1ded08c2b7413bf64e2608e8cea03d5f8ff6b7c61382b11eb04a290d611.html");
  DeleteDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha_dir/9f8/6d081884c7d659a2feaa0c55ad015a3bf4f1b2b0b822cd15d6c15b0f00a08.html");
  DeleteDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/webhare_testsuite.testsite/file.shtml");

  TestEQ(TRUE, testfw->browser->GotoWebPage(datastorage_base || "test/blue.html"));
  TestEQLike("*I'm just a static page*", BlobToString(testfw->browser->content));

  CreateDiskDirectoryRecursive(GetModuleStorageRoot("webhare_testsuite") || "tmp/webhare_testsuite.testsite/", TRUE);
  StoreDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/webhare_testsuite.testsite/file.shtml", StringToBlob(`<?wh PRINT("I'm a"); PRINT(" dynamic page");`));
  TestEQ(TRUE, testfw->browser->GotoWebPage(datastorage_base || "test/blue.html"));
  TestEQLike("I'm a dynamic page*", BlobToString(testfw->browser->content));

  CreateDiskDirectoryRecursive(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha_dir/9f8/", TRUE);
  StoreDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha_dir/9f8/6d081884c7d659a2feaa0c55ad015a3bf4f1b2b0b822cd15d6c15b0f00a08.html", StringToBlob(`I'm a dir hash page`));
  TestEQ(TRUE, testfw->browser->GotoWebPage(datastorage_base || "test/blue.html"));
  TestEQLike("*I'm a dir hash page*", BlobToString(testfw->browser->content));

  CreateDiskDirectoryRecursive(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha/72a/", TRUE);
  StoreDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/foldersha/72a/9b1ded08c2b7413bf64e2608e8cea03d5f8ff6b7c61382b11eb04a290d611.html", StringToBlob(`I'm a hash page`));
  TestEQ(TRUE, testfw->browser->GotoWebPage(datastorage_base || "test/blue.html"));
  TestEQLike("*I'm a hash page*", BlobToString(testfw->browser->content));

  CreateDiskDirectoryRecursive(GetModuleStorageRoot("webhare_testsuite") || "tmp/folderdirect/test/", TRUE);
  StoreDiskFile(GetModuleStorageRoot("webhare_testsuite") || "tmp/folderdirect/test/blue.html", StringToBlob(`<body><body>I'm a blue page</body></html>");`));
  TestEQ(TRUE, testfw->browser->GotoWebPage(datastorage_base || "test/blue.html"));
  TestEQLike("*I'm a blue page*", BlobToString(testfw->browser->content));
}

MACRO GlobalDataTester()
{
  TestEq(FALSE, LookupContentTypeByName("http://www.webhare.net/xmlns/tollium/applicationportal").id IN GetDynamicExecuteFolderTypes());
  TestEq(TRUE, LookupContentTypeByName("http://www.webhare.net/xmlns/beta/dynfolder").id IN GetDynamicExecuteFolderTypes());

  TestEQ(FALSE, 4 IN GetDynamicExecuteFolderTypes()); //word doc
}

MACRO TestMailTemplates()
{
  RECORD ARRAY nowtemplates := GetApplyTesterForObject(GetTestsuiteTempFolder()->id)->GetMailTemplates();
  TestEq(1, Length(nowtemplates));
  TestEq(":mod::webhare_testsuite/tests/system/email/data/template.html.witty", nowtemplates[0].title); //defaulted title

  testfw->BeginWork();
  OBJECT templatefolder := GetTestsuiteTempFolder()->CreateFolder([name:="templates"]);
  OBJECT templatefolder2 := GetTestsuiteTempFolder()->CreateFolder([name:="templates2"]);
  OBJECT templatehtml := templatefolder->CreateFile([name := "template.html", title := "Cool template", typens := "http://www.webhare.net/xmlns/publisher/htmlfile", ordering := 4 ]);
  OBJECT templatehtmlwitty := templatefolder2->CreateFile([name := "template.html.witty", title := "template.html", typens := "http://www.webhare.net/xmlns/publisher/wittyfile", ordering := 2 ]);
  OBJECT untitledhtml := templatefolder->CreateFile([name := "untitled.html", typens := "http://www.webhare.net/xmlns/publisher/wittyfile" ]);
  OBJECT plaintexthtml := templatefolder->CreateFile([name := "plaintext.html", title := "plaintext", typens := "http://www.webhare.net/xmlns/publisher/plaintextfile", ordering := 1]);
  OBJECT plaintexttxt := templatefolder->CreateFile([name := "plaintext.txt", title := "plaintext.txt", typens := "http://www.webhare.net/xmlns/publisher/plaintextfile" ]);
  testfw->CommitWork();

  nowtemplates := GetApplyTesterForObject(GetTestsuiteTempFolder()->id)->GetMailTemplates();
  //plaintexttxt should not exist, 1untitled templates should not be shown (easy hiding)
  TestEq([ "mod::webhare_testsuite/tests/system/email/data/template.html.witty"
         , "site::webhare_testsuite.testsite/tmp/templates/plaintext.html"
         , "site::webhare_testsuite.testsite/tmp/templates2/template.html.witty"
         , "site::webhare_testsuite.testsite/tmp/templates/template.html"
         ], SELECT AS STRING ARRAY path FROM nowtemplates);

  TestEq(":Cool template", nowtemplates[3].title, "templatehtml should be named Cool template");
}

RunTestframework([ PTR TestWHFSType
                 , PTR TestApplyRuleTester
                 , PTR SiteSettingsTester
                 , PTR GlobalDataTester
                 , PTR TestMailTemplates
                 ]);
