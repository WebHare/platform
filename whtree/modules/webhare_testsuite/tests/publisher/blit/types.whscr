<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/whfs/contenttypes.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";


LOADLIB "mod::system/lib/testframework.whlib";

LOADLIB "mod::publisher/lib/internal/blit/api.whlib";
LOADLIB "mod::publisher/lib/internal/blit/hashapi.whlib";
LOADLIB "mod::publisher/lib/internal/blit/repositorytree.whlib";
LOADLIB "mod::publisher/lib/internal/blit/whfsintegration.whlib";

// Test storage of type info in folders

//__webbrowser_debugall := TRUE;

/*
RECORD args := ParseArguments(GetConsoleArguments(), [ [ name := "setup", type := "stringopt" ] ]);
IF (NOT RecordExists(args) OR args.setup NOT IN [ "", "updatetest" ])
{
  // Use setup to prepare a conflict to update (in /webhare_testsuite.testfolder/webhare_testsuite.site/wc_local_theirs)
  PRINT("Syntax: runscript testapi.whscr [ --setup=updatetest ]\n\n");
  RETURN;
}*/

OBJECTTYPE TestRepo
<
  PUBLIC RECORD ARRAY commits;
  RECORD ARRAY objects;

  MACRO NEW()
  {
    this->objects :=
        [ [ hash :=       GetBlitFolderHash(DEFAULT RECORD ARRAY)
          , isfolder :=   TRUE
          , data :=       DEFAULT BLOB
          , children :=   DEFAULT RECORD ARRAY
          , size :=       0i64
          ]
        ];

    this->commits :=
        [ [ hash :=     GetBlitFolderHash(DEFAULT RECORD ARRAY)
          , date :=     DEFAULT DATETIME
          ]
        ];
  }

  PUBLIC RECORD FUNCTION GetStatus()
  {
    RETURN
        [ repository_uuid :=  "uuid-1"
        , name :=             "testrepo"
        , path :=             ""
        , version :=          LENGTH(this->commits)
        , roothash :=         this->commits[END-1].hash
        , date :=             DEFAULT DATETIME
        ];
  }

  PUBLIC RECORD FUNCTION GetObjectTree(STRING hash, INTEGER depth DEFAULTSTO -1)
  {
    RECORD pos := RecordLowerBound(this->objects, [ hash := hash ],  [ "HASH" ]);
    IF (NOT pos.found)
      ABORT("Object not found: " || hash);
    RECORD obj := this->objects[pos.position];
    IF (obj.hash LIKE "d-*")
    {
      IF (depth != 0)
      {
        obj.children :=
            SELECT AS RECORD ARRAY CellUpdate(this->GetObjectTree(COLUMN hash, depth = -1 ? -1 : depth - 1), "name", name)
              FROM obj.children;
      }
      ELSE
        obj.children := DEFAULT RECORD ARRAY;
    }

    INSERT CELL name := "" INTO obj;
    RETURN obj;
  }

  PUBLIC RECORD ARRAY FUNCTION CheckObjectsPresence(RECORD ARRAY objects)
  {
    RETURN
        SELECT hash
             , isfolder
             , status := RecordLowerBound(this->objects, [ hash := hash ],  [ "HASH" ]).found ? "found" : "notfound"
          FROM objects;
  }

  PUBLIC RECORD FUNCTION AddFile(STRING hash, BLOB data)
  {
    RECORD rec :=
        [ hash :=       GetBlitFileHash(data)
        , isfolder :=   FALSE
        , data :=       data
        , children :=   DEFAULT RECORD ARRAY
        , size :=       LENGTH64(data)
        ];
    IF (rec.hash != hash)
      THROW NEW Exception("Wrong file hash");
    RECORD pos := RecordLowerBound(this->objects, rec,  [ "HASH" ]);
    IF (pos.found)
      RETURN [ status := "exists", hash := rec.hash ];
    INSERT rec INTO this->objects AT pos.position;
    RETURN [ status := "added", hash := rec.hash ];
  }

  PUBLIC RECORD FUNCTION AddFolder(STRING hash, RECORD ARRAY objects)
  {
    RECORD rec :=
        [ hash :=       GetBlitFolderHash(objects)
        , isfolder :=   TRUE
        , data :=       DEFAULT BLOB
        , children :=   (SELECT name, COLUMN hash FROM objects)
        , size :=       0
        ];
    IF (rec.hash != hash)
      THROW NEW Exception("Wrong file hash");
    RECORD pos := RecordLowerBound(this->objects, rec,  [ "HASH" ]);
    IF (pos.found)
      RETURN [ status := "exists", hash := rec.hash ];
    INSERT rec INTO this->objects AT pos.position;
    RETURN [ status := "added", hash := rec.hash ];
  }

  PUBLIC RECORD FUNCTION CommitTree(INTEGER oldversion, STRING oldtreehash, STRING newtreehash, STRING comment)
  {
    IF (oldversion != LENGTH(this->commits))
      RETURN [ status := "notuptodate", version := this->LENGTH(this->commits) ];
    IF (oldtreehash = newtreehash)
      RETURN [ status := "nochange", version := oldversion, roothash := newtreehash, date := this->commits[END-1].date ];
    IF (NOT RecordLowerBound(this->objects, [ hash := newtreehash ],  [ "HASH" ]).found)
      RETURN [ status := "rootnotavailable" ];
    INSERT [ hash := newtreehash, date := DEFAULT DATETIME ] INTO this->commits AT END;
    RETURN
        [ status := "ok"
        , version := LENGTH(this->commits)
        , roothash := newtreehash
        , date := DEFAULT DATETIME
        ];
  }


>;

STRING FUNCTION GetTreeHash(OBJECT folder, RECORD options)
{
  OBJECT tree := CreateWHFSDescriptionTree(folder, options);
  OBJECT rtree := tree->BuildRepositoryTree();
  RETURN rtree->root.hash;
}


STRING testsiteprofile :=
  '<siteprofile xmlns="http://www.webhare.net/xmlns/publisher/siteprofile">'
    || '<contenttype namespace="http://www.webhare.net/xmlns/beta/blit/typestest">'
      || '<member name="i1" type="integer" />'
      || '<member name="i2" type="integer" />'
    || '</contenttype>'
  || '</siteprofile>';

MACRO Setup()
{
  testfw->BeginWork();
  testfw->SetupTestWebsite(DEFAULT BLOB);
  testfw->CommitWork();
}

MACRO TestTypeUpdate()
{
  /* Test storing of whfstype descriptions in tree, so restoring will work without the
     types being known on the updated system
  */

  // Pain point: instances (MUST be enumerated too)
  //             folder types

  /* Test:

     Setup:
      common parent: no typedata
      remote: has type data for i1 & i2
      local: has no type data

     Action:
       Update

     Expect:
       remote folder differs only in type data from local folder
       Restore of i1 & i2 works without the site profile defs present
  */

  testfw->BeginWork();

  OBJECT testsite := testfw->GetTestSite();

  DELETE FROM system.fs_types WHERE namespace = "http://www.webhare.net/xmlns/beta/blit/typestest";
  GetWHFSCommitHandler()->FSTypesChanged();

  OBJECT siteprof := testsite->rootobject->CreateFile([name:="test.siteprl", data := StringToBlob(testsiteprofile)]);

  TestEq(TRUE, testsite->AssignSiteProfile(siteprof->id));

  // Use testfolder to avoid site fsdata to show up in trees
  OBJECT testfolder := testsite->rootobject->CreateFolder([ name := "testfolder" ]);

  testfw->CommitWork();

  // Construct parent
  testfw->BeginWork();
  OBJECT t1 := testfolder->CreateFile(
      [ name:="t1.html", data := StringToBlob("<html><title>Testfile</title></html>")

      , publish := true
      ]);

  OBJECT t2 := testfolder->CreateFile(
      [ name:="t2.html", data := StringToBlob("<html><title>Testfile</title></html>")
      , publish := true
      ]);

  testfw->CommitWork();

  OBJECT tree_parent := CreateWHFSDescriptionTree(testfolder);
  OBJECT rtree_parent := tree_parent->BuildRepositoryTree();

  // Construct remote
  testfw->BeginWork();
  t1->SetInstanceData("http://www.webhare.net/xmlns/beta/blit/typestest",
      [ i1 := 1
      ]);
  t2->SetInstanceData("http://www.webhare.net/xmlns/beta/blit/typestest",
      [ i1 := 1
      ]);
  testfw->CommitWork();

  OBJECT tree_remote := CreateWHFSDescriptionTree(testfolder);
  OBJECT rtree_remote := tree_remote->BuildRepositoryTree();

  // Remove the site profile assignment and the fstype (also kills the instancedata)
  testfw->BeginWork();
  TestEq(TRUE, testsite->UnassignSiteProfile(siteprof->id));
  DELETE FROM system.fs_types WHERE namespace = "http://www.webhare.net/xmlns/beta/blit/typestest";
  GetWHFSCommitHandler()->FSTypesChanged();
  testfw->CommitWork();

//  DumpValue(OpenWHFSTypeNoCheck("http://www.webhare.net/xmlns/blit/typestest"), "tree");
//  DumpValue(t1->GetInstanceDataNoCheck("http://www.webhare.net/xmlns/beta/blit/typestest"), "tree");

  OBJECT tree_local := CreateWHFSDescriptionTree(testfolder);
  OBJECT rtree_local := tree_local->BuildRepositoryTree();

  TestEQ(rtree_parent->root.hash, rtree_local->root.hash, "Parent tree should equal local tree");

  OBJECT rdifftree := RepositoryTreeDiff3(rtree_local, rtree_parent, rtree_remote);
  OBJECT difftree := GenerateWHFSDescriptionDiffTree(rdifftree);

  TestEQ("update-types", difftree->root.status);

  // Execute the update
  testfw->BeginWork();
  difftree->Apply(testfolder);
  testfw->CommitWork();

  OBJECT test_type := OpenWHFSType("http://www.webhare.net/xmlns/beta/blit/typestest", [ openorphans := TRUE ]);
  TestEQ([ i1 := 1
         , i2 := 0
         ], test_type->GetInstanceData(t1->id, [ orphans := TRUE ]));

  // Cleanup
  testfw->BeginWork();
  DELETE FROM system.fs_types WHERE namespace = "http://www.webhare.net/xmlns/beta/blit/typestest";
  GetWHFSCommitHandler()->FSTypesChanged();
  TestEq(TRUE, testsite->UnassignSiteProfile(siteprof->id));
  testfolder->RecycleSelf();
  siteprof->RecycleSelf();
  testfw->CommitWork();
}


MACRO TestPartialCommit()
{
  /* Tests if partial commit still updates the types descriptions relevant to the partially committed data
  */
  OBJECT testsite := testfw->GetTestSite();

  OBJECT repo := NEW TestRepo;

  testfw->BeginWork();

  DELETE FROM system.fs_types WHERE namespace = "http://www.webhare.net/xmlns/beta/blit/typestest";
  GetWHFSCommitHandler()->FSTypesChanged();

  OBJECT siteprof := testsite->rootobject->CreateFile([name:="test.siteprl", data := StringToBlob(testsiteprofile)]);

  TestEq(TRUE, testsite->AssignSiteProfile(siteprof->id));

  // Use testfolder to avoid site fsdata to show up in trees
  OBJECT testfolder := testsite->rootobject->CreateFolder([ name := "testfolder" ]);

  OBJECT repostype := OpenWHFSType("http://www.webhare.net/xmlns/blexdev_blit/v2/checkedoutfolder");
  repostype->SetInstanceData(testfolder->id,
        [ repository_localuuid := "local-uuid-1"
        , repository_roothash :=  repo->GetStatus().roothash
        , repository_version :=   repo->GetStatus().version
        , repository_uuid :=      "remote-uuid-1"
        , repository_path :=      "testrepos/testrepo"
        , repository_url :=       "http://example.com/"
        ]);

  testfolder->CopyTo(testsite->rootobject, "testfolder2");
  OBJECT testfolder2 := testsite->rootobject->OpenByPath("testfolder2");

  testfw->CommitWork();

  // Construct parent
  testfw->BeginWork();
  OBJECT t1 := testfolder->CreateFile(
      [ name:="t1.html", data := StringToBlob("<html><title>Testfile</title></html>")
      , publish := true
      ]);

  STRING no_idata_hash := GetTreeHash(testfolder, DEFAULT RECORD);
  STRING no_idata_cleaned_hash := GetTreeHash(testfolder, [ cleanwhitespace := TRUE ]);

  t1->SetInstanceData("http://www.webhare.net/xmlns/beta/blit/typestest",
      [ i1 := 1
      ]);
  testfw->CommitWork();

  OBJECT committer := NEW BlitCommitter(repo, testfolder);
  committer->Commit();

  // Test if update works
  OBJECT updater := NEW BlitUpdater(repo, testfolder2);
  updater->Execute();

  // Save hash
  STRING i1_hash := repo->GetStatus().roothash;

  testfw->BeginWork();
  t1->SetInstanceData("http://www.webhare.net/xmlns/beta/blit/typestest",
      [ i1 := 0
      ]);
  testfw->CommitWork();

  // After clearing instance data, rebuild of tree should be equal to original tree
  TestEQ(no_idata_hash, GetTreeHash(testfolder, DEFAULT RECORD));

  // Commit only file (should revert to empty tree, updating folder types)
  committer := NEW BlitCommitter(repo, testfolder);
  committer->CommitSelection(SELECT AS STRING ARRAY rowkey FROM committer->modificationlist WHERE Detokenize(path, "/") = "t1.html");

  // Should be equal to clean tree
  TestEQ(no_idata_cleaned_hash, repo->GetStatus().roothash);

  // Test update
  updater := NEW BlitUpdater(repo, testfolder2);
  updater->Execute();

  // Re-set i1
  testfw->BeginWork();
  t1->SetInstanceData("http://www.webhare.net/xmlns/beta/blit/typestest",
      [ i1 := 1
      ]);
  testfw->CommitWork();

  // Commit only file (should revert to empty tree, updating folder types)
  committer := NEW BlitCommitter(repo, testfolder);
  committer->CommitSelection(SELECT AS STRING ARRAY rowkey FROM committer->modificationlist WHERE Detokenize(path, "/") = "t1.html");

  TestEQ(i1_hash, repo->GetStatus().roothash);

  // Test update
  updater := NEW BlitUpdater(repo, testfolder2);
  updater->Execute();

  // See if updates worked
  TestEQ(i1_hash, GetTreeHash(testfolder2, DEFAULT RECORD));

  // Cleanup
  testfw->BeginWork();
  DELETE FROM system.fs_types WHERE namespace = "http://www.webhare.net/xmlns/beta/blit/typestest";
  testsite->UnassignSiteProfile(siteprof->id);
  testfolder->RecycleSelf();
  siteprof->RecycleSelf();
  testfw->CommitWork();
}

RunTestframework(
    [ PTR Setup
    , PTR TestTypeUpdate
    , PTR TestPartialCommit
    ],
    DEFAULT RECORD);
