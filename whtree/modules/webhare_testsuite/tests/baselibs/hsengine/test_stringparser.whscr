<?wh
/** @short String parser tests */

LOADLIB "wh::internal/hsselftests.whlib";
LOADLIB "wh::util/stringparser.whlib";


RECORD ARRAY errors;
RECORD result;

MACRO SetParserPositionEOF(OBJECT parser, INTEGER pos, BOOLEAN eof)
{
  __HS_SetPrivateMember(parser, "pvt_pos", pos);
  __HS_SetPrivateMember(parser, "pvt_eof", eof);
}


RECORD FUNCTION GenerateState(STRING data, INTEGER pos)
{
  RETURN
      [ pos             := pos
      , eof             := pos >= LENGTH(data)
      , current         := pos < 0 ? "" : SubString(data, pos, 1)
      , remaining_data  := SubString(data, pos)
      ];
}

RECORD FUNCTION GenerateFaultyState(STRING data, INTEGER pos, BOOLEAN eof)
{
  RETURN
      [ pos             := pos
      , eof             := eof
      , current         := pos < 0 OR eof ? "" : SubString(data, pos, 1)
      , remaining_data  := SubString(data, pos)
      ];
}

RECORD FUNCTION GetParserState(OBJECT parser)
{
  RETURN
      [ pos             := parser->position
      , eof             := parser->eof
      , current         := parser->current
      , remaining_data  := parser->remaining_data
      ];
}

MACRO ResetTest()
{
  OpenTest("TestStringParser: ResetTest");

  TestEq(GenerateState("abc", 0), GetParserState(NEW StringParser("abc")));

  OBJECT parser := NEW StringParser;
  TestEq(GenerateState("", 0), GetParserState(parser));

  parser->Reset("abc");
  TestEq(GenerateState("abc", 0), GetParserState(parser));

  parser->Next();
  TestEq(GenerateState("abc", 1), GetParserState(parser));

  parser->Reset("def");
  TestEq(GenerateState("def", 0), GetParserState(parser));

  CloseTest("TestStringParser: ResetTest");
}

MACRO NextTest()
{
  OpenTest("TestStringParser: NextTest");

  OBJECT parser := NEW StringParser("ab");
  TestEq(GenerateState("ab", 0), GetParserState(parser));

  parser->Next();
  TestEq(GenerateState("ab", 1), GetParserState(parser));

  parser->Next();
  TestEq(GenerateState("ab", 2), GetParserState(parser));

  parser->Next();
  TestEq(GenerateState("ab", 2), GetParserState(parser));

  // Test state inconsistency resilience:
  SetParserPositionEOF(parser, 3, TRUE);
  TestEq(3, parser->position);
  TestEq(TRUE, parser->eof);
  parser->Next();

  TestEq(GenerateState("ab", 3), GetParserState(parser));

  SetParserPositionEOF(parser, 1, TRUE);
  TestEq(1, parser->position);
  TestEq(TRUE, parser->eof);

  parser->Next();
  TestEq(GenerateFaultyState("ab", 1, TRUE), GetParserState(parser));

  SetParserPositionEOF(parser, 3, FALSE);
  TestEq(3, parser->position);
  TestEq(FALSE, parser->eof);

  parser->Next();
  TestEq(GenerateState("ab", 2), GetParserState(parser));

  SetParserPositionEOF(parser, -2, FALSE);
  parser->Next();
  TestEq(GenerateState("ab", 2), GetParserState(parser));

  CloseTest("TestStringParser: NextTest");
}

MACRO ParseWhileInSetTest()
{
  OpenTest("TestStringParser: ParseWhileInSetTest");

  OBJECT parser := NEW StringParser("abcd");
  TestEq(GenerateState("abcd", 0), GetParserState(parser));

  parser->ParseWhileInSet("a");
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  parser->ParseWhileInSet("B");
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  parser->ParseWhileInSet("bc");
  TestEq(GenerateState("abcd", 3), GetParserState(parser));

  parser->ParseWhileInSet("de");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  // Test state inconsistency resilience:
  SetParserPositionEOF(parser, 5, TRUE);
  TestEq(5, parser->position);
  TestEq(TRUE, parser->eof);
  parser->ParseWhileInSet("d");

  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, 3, TRUE);
  TestEq(3, parser->position);
  TestEq(TRUE, parser->eof);

  // ParseWhileInSet ignores eof
  parser->ParseWhileInSet("d");
  TestEq(GenerateFaultyState("abcd", 4, TRUE), GetParserState(parser));

  SetParserPositionEOF(parser, 5, FALSE);
  TestEq(5, parser->position);
  TestEq(FALSE, parser->eof);

  parser->ParseWhileInSet("d");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, -2, FALSE);
  parser->ParseWhileInSet("a");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  CloseTest("TestStringParser: ParseWhileInSetTest");
}

MACRO ParseWhileNotInSetTest()
{
  OpenTest("TestStringParser: ParseWhileNotInSetTest");

  OBJECT parser := NEW StringParser("abcd");
  TestEq(GenerateState("abcd", 0), GetParserState(parser));

  parser->ParseWhileNotInSet("a");
  TestEq(GenerateState("abcd", 0), GetParserState(parser));

  parser->ParseWhileNotInSet("b");
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  parser->ParseWhileNotInSet("bc");
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  parser->ParseWhileNotInSet("de");
  TestEq(GenerateState("abcd", 3), GetParserState(parser));

  parser->ParseWhileNotInSet("fg");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  // Test state inconsistency resilience:
  SetParserPositionEOF(parser, 5, TRUE);
  TestEq(5, parser->position);
  TestEq(TRUE, parser->eof);
  parser->ParseWhileNotInSet("e");

  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, 3, TRUE);
  TestEq(3, parser->position);
  TestEq(TRUE, parser->eof);

  // ParseWhileNotInSet ignores eof
  parser->ParseWhileNotInSet("e");
  TestEq(GenerateFaultyState("abcd", 4, TRUE), GetParserState(parser));

  SetParserPositionEOF(parser, 5, FALSE);
  TestEq(5, parser->position);
  TestEq(FALSE, parser->eof);

  parser->ParseWhileNotInSet("e");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, -2, FALSE);
  parser->ParseWhileNotInSet("z");
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  CloseTest("TestStringParser: ParseWhileNotInSetTest");
}

MACRO TryParseTest()
{
  OpenTest("TestStringParser: TryParseTest");

  OBJECT parser := NEW StringParser("abcd");
  TestEq(GenerateState("abcd", 0), GetParserState(parser));

  TestEq(FALSE, parser->TryParse(""));
  TestEq(GenerateState("abcd", 0), GetParserState(parser));

  TestEq(TRUE, parser->TryParse("a"));
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  TestEq(FALSE, parser->TryParse("bd"));
  TestEq(GenerateState("abcd", 1), GetParserState(parser));

  TestEq(TRUE, parser->TryParse("bc"));
  TestEq(GenerateState("abcd", 3), GetParserState(parser));

  TestEq(FALSE, parser->TryParse("de"));
  TestEq(GenerateState("abcd", 3), GetParserState(parser));

  TestEq(FALSE, parser->TryParse("D"));
  TestEq(GenerateState("abcd", 3), GetParserState(parser));

  TestEq(TRUE, parser->TryParse("d"));
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  // Test state inconsistency resilience:
  SetParserPositionEOF(parser, 5, TRUE);
  TestEq(5, parser->position);
  TestEq(TRUE, parser->eof);
  TestEq(FALSE, parser->TryParse("d"));

  TestEq(GenerateState("abcd", 5), GetParserState(parser));

  SetParserPositionEOF(parser, 3, TRUE);
  TestEq(3, parser->position);
  TestEq(TRUE, parser->eof);

  // TryParse ignores eof, uses only position
  TestEq(TRUE, parser->TryParse("d"));
  TestEq(GenerateState("abcd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, 5, FALSE);
  TestEq(5, parser->position);
  TestEq(FALSE, parser->eof);

  // TryParse doesn't care too much about correcting
  TestEq(FALSE, parser->TryParse("d"));
  TestEq(GenerateFaultyState("abcd", 5, FALSE), GetParserState(parser));

  SetParserPositionEOF(parser, -2, FALSE);
  parser->TryParse("a");
  TestEq(GenerateFaultyState("abcd", -2, FALSE), GetParserState(parser));

  CloseTest("TestStringParser: TryParseTest");
}

MACRO TryParseCaseTest()
{
  OpenTest("TestStringParser: TryParseCaseTest");

  OBJECT parser := NEW StringParser("AbCd");
  TestEq(GenerateState("AbCd", 0), GetParserState(parser));

  TestEq(FALSE, parser->TryParseCase(""));
  TestEq(GenerateState("AbCd", 0), GetParserState(parser));

  TestEq(TRUE, parser->TryParseCase("a"));
  TestEq(GenerateState("AbCd", 1), GetParserState(parser));

  TestEq(FALSE, parser->TryParseCase("bd"));
  TestEq(GenerateState("AbCd", 1), GetParserState(parser));

  TestEq(TRUE, parser->TryParseCase("Bc"));
  TestEq(GenerateState("AbCd", 3), GetParserState(parser));

  TestEq(FALSE, parser->TryParseCase("DE"));
  TestEq(GenerateState("AbCd", 3), GetParserState(parser));

  TestEq(TRUE, parser->TryParseCase("D"));
  TestEq(GenerateState("AbCd", 4), GetParserState(parser));

  // Test state inconsistency resilience:
  SetParserPositionEOF(parser, 5, TRUE);
  TestEq(5, parser->position);
  TestEq(TRUE, parser->eof);
  TestEq(FALSE, parser->TryParseCase("d"));

  TestEq(GenerateState("AbCd", 5), GetParserState(parser));

  SetParserPositionEOF(parser, 3, TRUE);
  TestEq(3, parser->position);
  TestEq(TRUE, parser->eof);

  // TryParse ignores eof, uses only position
  TestEq(TRUE, parser->TryParseCase("d"));
  TestEq(GenerateState("AbCd", 4), GetParserState(parser));

  SetParserPositionEOF(parser, 5, FALSE);
  TestEq(5, parser->position);
  TestEq(FALSE, parser->eof);

  // TryParseCase doesn't care too much about correcting
  TestEq(FALSE, parser->TryParseCase("d"));
  TestEq(GenerateFaultyState("AbCd", 5, FALSE), GetParserState(parser));

  SetParserPositionEOF(parser, -2, FALSE);
  parser->TryParseCase("a");
  TestEq(GenerateFaultyState("AbCd", -2, FALSE), GetParserState(parser));

  CloseTest("TestStringParser: TryParseCaseTest");
}

MACRO TestStates()
{
  OBJECT parser := NEW StringParser("123456");
  RECORD begin := parser->SaveState();
  TestEq(0, parser->GetDistanceFromState(begin));
  TestEQ("", parser->GetLastParsed(0));
  TestEQ("", parser->GetLastParsed(-1));
  TestEQ("", parser->GetLastParsed(1));
  TestEQ("12", parser->ParseN(2));
  RECORD two := parser->SaveState();
  TestEq(2, parser->GetDistanceFromState(begin));
  TestEQ("12", parser->GetLastParsed(2));
  parser->RestoreState(begin);
  TestEq(0, parser->GetDistanceFromState(begin));

  parser->Reset("123456");
  TestThrowsLike("Cannot compare states with different data", PTR parser->GetDistanceFromState(begin));
  parser->RestoreState(begin);
  TestEQ(-2, parser->GetDistanceFromState(two));
}

MACRO ParseNTest()
{
  OBJECT parser := NEW StringParser("123456");

  TestEQ("1", parser->ParseN(1));
  TestEQ("", parser->ParseN(-1));
  TestEQ("", parser->ParseN(0));
  TestEQ("2", parser->ParseN(1));
  TestEQ("34", parser->ParseN(2));
  TestEQ("5", parser->current);

  RECORD state := parser->SaveState();
  TestEQ(FALSE, parser->eof);
  TestEQ("56", parser->ParseN(2));
  TestEQ(TRUE, parser->eof);
  TestEQ("", parser->current);

  parser->RestoreState(state);
  TestEQ(FALSE, parser->eof);
  TestEQ("56", parser->ParseN(3));
  TestEQ(TRUE, parser->eof);
  TestEQ("", parser->current);
}

MACRO SkipNTest()
{
  OBJECT parser := NEW StringParser("123456");

  TestEQ(TRUE, parser->SkipN(1));
  TestEQ("2", parser->current);
  TestEQ(TRUE, parser->SkipN(-1));
  TestEQ(TRUE, parser->SkipN(0));
  TestEQ("2", parser->current);
  TestEQ(TRUE, parser->SkipN(1));
  TestEQ("3", parser->current);
  TestEQ(TRUE, parser->SkipN(2));
  TestEQ("5", parser->current);

  RECORD state := parser->SaveState();
  TestEQ(FALSE, parser->eof);
  TestEQ(FALSE, parser->SkipN(2));
  TestEQ(TRUE, parser->eof);
  TestEQ("", parser->current);

  parser->RestoreState(state);
  TestEQ(FALSE, parser->eof);
  TestEQ(FALSE, parser->SkipN(3));
  TestEQ(TRUE, parser->eof);
  TestEQ("", parser->current);
}


ResetTest();
NextTest();
ParseWhileInSetTest();
ParseWhileNotInSetTest();
TryParseTest();
TryParseCaseTest();
TestStates();
ParseNTest();
SkipNTest();
