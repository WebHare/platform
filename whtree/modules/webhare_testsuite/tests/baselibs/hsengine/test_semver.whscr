<?wh

LOADLIB "wh::internal/hsselftests.whlib";
LOADLIB "wh::util/semver.whlib";

MACRO TestSemVer_Increment()
{
  OpenTest("TestSemVer_Increment");

  STRING version := "0.1.0-release-candidate.1+2015-09-01-123456";
  TestEq(TRUE, IsValidVersion(version));
  RECORD decoded := DecodeVersion(version);
  TestEq(TRUE, RecordExists(decoded));
  TestEq(0, decoded.major);
  TestEq(1, decoded.minor);
  TestEq(0, decoded.patch);
  TestEq("release-candidate.1", decoded.prerelease);
  TestEq("2015-09-01-123456", decoded.build);
  TestEq(version, EncodeVersion(decoded));

  version := IncrementVersionPatch(version);
  TestEq(TRUE, IsValidVersion(version));
  decoded := DecodeVersion(version);
  TestEq(TRUE, RecordExists(decoded));
  TestEq(0, decoded.major);
  TestEq(1, decoded.minor);
  TestEq(1, decoded.patch);
  TestEq("release-candidate.1", decoded.prerelease);
  TestEq("2015-09-01-123456", decoded.build);
  TestEq(version, EncodeVersion(decoded));

  version := IncrementVersionMinor(version);
  TestEq(TRUE, IsValidVersion(version));
  decoded := DecodeVersion(version);
  TestEq(TRUE, RecordExists(decoded));
  TestEq(0, decoded.major);
  TestEq(2, decoded.minor);
  TestEq(0, decoded.patch);
  TestEq("release-candidate.1", decoded.prerelease);
  TestEq("2015-09-01-123456", decoded.build);
  TestEq(version, EncodeVersion(decoded));

  version := IncrementVersionMajor(version);
  TestEq(TRUE, IsValidVersion(version));
  decoded := DecodeVersion(version);
  TestEq(TRUE, RecordExists(decoded));
  TestEq(1, decoded.major);
  TestEq(0, decoded.minor);
  TestEq(0, decoded.patch);
  TestEq("release-candidate.1", decoded.prerelease);
  TestEq("2015-09-01-123456", decoded.build);
  TestEq(version, EncodeVersion(decoded));

  CloseTest("TestSemVer_Increment");
}

MACRO TestSemVer_Precedence()
{
  OpenTest("TestSemVer_Precedence");

  // 1.0.0 < 2.0.0 < 2.1.0 < 2.1.1
  TestEq(TRUE, IsVersionNewer("1.0.0", "2.0.0"));
  TestEq(FALSE, IsVersionNewer("2.0.0", "1.0.0"));
  TestEq(TRUE, IsVersionNewer("2.0.0", "2.1.0"));
  TestEq(FALSE, IsVersionNewer("2.1.0", "2.0.0"));
  TestEq(TRUE, IsVersionNewer("2.1.0", "2.1.1"));
  TestEq(FALSE, IsVersionNewer("2.1.1", "2.1.0"));

  // 1.0.0-alpha < 1.0.0
  TestEq(TRUE, IsVersionNewer("1.0.0-alpha", "1.0.0"));
  TestEq(FALSE, IsVersionNewer("1.0.0", "1.0.0-alpha"));

  // 1.0.0-alpha < 1.0.0-alpha.1 < 1.0.0-alpha.beta < 1.0.0-beta < 1.0.0-beta.2 < 1.0.0-beta.11 < 1.0.0-rc.1 < 1.0.0
  TestEq(TRUE, IsVersionNewer("1.0.0-alpha", "1.0.0-beta"));
  TestEq(TRUE, IsVersionNewer("1.0.0-beta", "1.0.0-gamma"));
  TestEq(FALSE, IsVersionNewer("1.0.0-gamma", "1.0.0-delta"));
  TestEq(TRUE, IsVersionNewer("1.0.0-delta", "1.0.0"));

  TestEq(TRUE, IsVersionNewer("1.0.0-alpha", "1.0.0-alpha.1"));
  TestEq(FALSE, IsVersionNewer("1.0.0-alpha.1", "1.0.0-alpha"));
  TestEq(TRUE, IsVersionNewer("1.0.0-alpha.1", "1.0.0-alpha.beta"));
  TestEq(FALSE, IsVersionNewer("1.0.0-alpha.beta", "1.0.0-alpha.1"));
  TestEq(TRUE, IsVersionNewer("1.0.0-alpha.beta", "1.0.0-beta"));
  TestEq(FALSE, IsVersionNewer("1.0.0-beta", "1.0.0-alpha.beta"));
  TestEq(TRUE, IsVersionNewer("1.0.0-beta", "1.0.0-beta.2"));
  TestEq(FALSE, IsVersionNewer("1.0.0-beta.2", "1.0.0-beta"));
  TestEq(TRUE, IsVersionNewer("1.0.0-beta.2", "1.0.0-beta.11"));
  TestEq(FALSE, IsVersionNewer("1.0.0-beta.11", "1.0.0-beta.2"));
  TestEq(TRUE, IsVersionNewer("1.0.0-beta.11", "1.0.0-rc.1"));
  TestEq(FALSE, IsVersionNewer("1.0.0-rc.1", "1.0.0-beta.11"));
  TestEq(TRUE, IsVersionNewer("1.0.0-rc.1", "1.0.0"));
  TestEq(FALSE, IsVersionNewer("1.0.0", "1.0.0-rc.1"));

  //ADDME: Test precedence with build identifiers

  CloseTest("TestSemVer_Precedence");
}

/** Satisfyability tests
*/
RECORD ARRAY satis_tests :=
    [ [ range := "",                  comparemode := "npm",          matches := [ "2.1.0" ], nonmatches := STRING[] ]
    , [ range := "*",                 comparemode := "npm",          matches := [ "2.1.0" ], nonmatches := STRING[] ]
    , [ range := ">=4.26.0",          comparemode := "npm",          matches := STRING[], nonmatches := [ "4.28.0-dev" ] ]
    , [ range := ">=4.26.0",          comparemode := "webhare",      matches := ["4.28.0-dev"], nonmatches := STRING[] ]
    , [ range := ">4.26.0",           comparemode := "npm",          matches := STRING[], nonmatches := [ "4.28.0-dev" ] ]
    , [ range := ">4.26.0",           comparemode := "webhare",      matches := [ "4.28.0-dev" ], nonmatches := STRING[] ]
    , [ range := "4.26.0-dev",        comparemode := "npm",          matches := STRING["4.26.0-dev", "4.26.0-dev+abc"], nonmatches := [ "4.26.0", "4.26.0-alpha", "4.26.0-epsilon", "4.26.0-epsilon+zzz" ] ]
    , [ range := "4.26.0-dev",        comparemode := "webhare",      matches := STRING["4.26.0-dev", "4.26.0-dev+abc"], nonmatches := [ "4.26.0", "4.26.0-alpha", "4.26.0-epsilon", "4.26.0-epsilon+zzz" ] ]
    , [ range := ">=2.1.0",           comparemode := "npm",          matches := [ "2.1.0", "2.1.1", "2.2.2", "3.0.0" ], nonmatches := [ "2.0.9", "2.1.0-prerelease", "2.2.0-prerelease" ] ]
    , [ range := ">=2.1.0",           comparemode := "webhare",      matches := [ "2.1.0", "2.1.1", "2.2.2", "3.0.0", "2.2.0-prerelease" ], nonmatches := [ "2.0.9", "2.1.0-prerelease" ] ]
    , [ range := ">2.1.0",            comparemode := "npm",          matches := [ "2.1.1", "2.2.2", "3.0.0" ], nonmatches := [ "2.1.0", "2.0.9", "2.1.0-prerelease", "2.2.0-prerelease" ] ]
    , [ range := ">2.1.0",            comparemode := "webhare",      matches := [ "2.1.1", "2.2.2", "3.0.0", "2.2.0-prerelease" ], nonmatches := [ "2.1.0", "2.0.9", "2.1.0-prerelease" ] ]
    , [ range := ">2.1.0-pre.1",      comparemode := "npm",          matches := [ "2.1.0-prerelease", "2.1.0", "2.1.1", "2.2.2", "3.0.0" ], nonmatches := [ "2.0.9", "2.2.0-prerelease" ] ]
    , [ range := ">2.1.0-pre.1",      comparemode := "webhare",      matches := [ "2.1.0", "2.1.1", "2.2.2", "3.0.0", "2.2.0-prerelease" ], nonmatches := [ "2.0.9", "2.1.0-prerelease" ] ]
    , [ range := "=2.1.0",            comparemode := "npm",          matches := [ "2.1.0" ], nonmatches := [ "2.0.9", "2.1.0-prerelease", "2.2.0-prerelease", "2.2.2", "3.0.0", "2.1.1" ] ]
    , [ range := "=2.1.0 || 2.1.1",   comparemode := "npm",          matches := [ "2.1.0", "2.1.1" ], nonmatches := [ "2.0.9", "2.1.0-prerelease", "2.2.0-prerelease", "2.2.2", "3.0.0" ] ]
    , [ range := "2.1.x",             comparemode := "npm",          matches := [ "2.1.0", "2.1.1" ], nonmatches := [ "2.0.9", "2.1.0-prerelease", "2.2.0-prerelease", "2.2.2", "3.0.0" ] ]
    , [ range := "2.x.x",             comparemode := "npm",          matches := [ "2.1.0", "2.1.1", "2.9.9" ], nonmatches := [ "1.9.9", "2.1.0-prerelease", "2.2.0-prerelease", "3.0.0" ] ]
    , [ range := "~2.1.1",            comparemode := "npm",          matches := [ "2.1.1", "2.1.2" ], nonmatches := [ "2.0.9", "2.1.0", "2.1.0-prerelease", "2.2.0-prerelease", "2.2.2", "3.0.0" ] ]
    , [ range := "~2.1.1-pre.1",      comparemode := "npm",          matches := [ "2.1.1-pre.1", "2.1.1-pre.2", "2.1.1", "2.1.2" ], nonmatches := [ "2.0.9", "2.1.0", "2.1.1-pre.0", "2.2.0-prerelease", "2.2.2", "3.0.0" ] ]
    , [ range := "~1.2.3-beta.2",     comparemode := "npm",          matches := [ "1.2.3-beta.2", "1.2.3-beta.3", "1.2.4" ], nonmatches := [ "1.2.2", "1.2.3-beta.1", "1.2.4-beta", "1.3.0-pre", "1.3.0" ] ]
    , [ range := ">=1.2.3-beta.2 <1.3.0", comparemode := "npm",      matches := [ "1.2.3-beta.2", "1.2.3-beta.3", "1.2.4" ], nonmatches := [ "1.2.2", "1.2.3-beta.1", "1.2.4-beta", "1.3.0-pre", "1.3.0" ] ]

    , [ range := ">=1.1.0-a >=1.0.0 || =1.2.0-a", comparemode := "npm", matches := [ "1.1.0" ], nonmatches := [ "1.2.0-gamma", "1.0.0" ] ]

    , [ range := ">=1.0.0 || >= 1.5.0-alpha <= 1.5.0-alpha.1", comparemode := "npm", matches := [ "1.5.0", "1.5.0-alpha" ], nonmatches := [ "1.5.0-beta" ] ]
    //
    , [ range := "~2",                comparemode := "npm",          matches := [ "2.0.0", "2.1.0", "2.2.2" ], nonmatches := [ "1.9.9", "2.1.0-prerelease", "2.2.0-prerelease", "3.0.0" ] ]
    , [ range := "^0.0.3",            comparemode := "npm",          matches := [ "0.0.3" ], nonmatches := [ "0.0.3-prerelease", "0.0.4-prerelease", "0.0.4" ] ]
    , [ range := "^0.2.3",            comparemode := "npm",          matches := [ "0.2.3", "0.2.4" ], nonmatches := [ "0.2.3-prerelease", "0.2.4-prerelease", "0.3.0" ] ]
    , [ range := "^1.2.3",            comparemode := "npm",          matches := [ "1.2.3", "1.2.4", "1.3.0" ], nonmatches := [ "0.2.3-prerelease", "0.2.4-prerelease", "2.0.0" ] ]
    , [ range := "1.0.1 - 1.1.3",     comparemode := "npm",          matches := [ "1.0.1", "1.0.2", "1.1.3", "1.1.2" ], nonmatches := [ "1.0.0", "1.1.4", "1.2.0" ] ]
    , [ range := "git://bla",         comparemode := "npm",          matches := [ "git://bla", "git://bla#0123456789012345678901234567890123456789" ], nonmatches := [ "git://blab.git" ] ]
    , [ range := "git://bla.git",     comparemode := "npm",          matches := [ "git://bla.git", "git://bla.git#0123456789012345678901234567890123456789" ], nonmatches := [ "git://blab.git" ] ]

    , [ range := ">=4.28.0 || 4.28.0-dev",   comparemode := "npm",          matches := [ "4.28.0-dev", "4.28.0","4.28.1" ]
                                                                          , nonmatches := [ "4.28.0-alpha", "4.28.0-dev.2", "4.28.0-eyo", "4.28.11-goodie" ] ]
    , [ range := ">=4.28.0 || >=4.28.0-dev", comparemode := "npm",          matches := [ "4.28.0-dev", "4.28.0-dev.2", "4.28.0-eyo", "4.28.0","4.28.1" ]
                                                                          , nonmatches := [ "4.28.0-alpha", "4.28.11-goodie" ] ]
    , [ range := ">=4.28.0 || 4.28.0-dev",   comparemode := "webhare",      matches := [ "4.28.0-dev", "4.28.0","4.28.1","4.28.11-goodie" ]
                                                                          , nonmatches := [ "4.28.0-alpha", "4.28.0-dev.2", "4.28.0-eyo" ] ]
    , [ range := ">=4.28.0 || >=4.28.0-dev", comparemode := "webhare"     , matches := [ "4.28.0-dev", "4.28.0","4.28.1","4.28.11-goodie" ]
                                                                          , nonmatches := [ "4.28.0-alpha", "4.28.0-dev.2", "4.28.0-eyo" ] ] //note, this version test doesnt really make sense, nothing is > 4.28.0-dev that doesnt also match >= 4.28.0
    ];


MACRO TestSemVer_Ranges()
{
  FOREVERY (RECORD r FROM satis_tests)
  {
    FOREVERY (STRING match FROM r.matches)
    {
      IF (NOT VersionSatisfiesRange(match, r.range, CELL[r.comparemode]))
        ABORT(CELL[ r.range, match, r.comparemode ]);
    }

    FOREVERY (STRING nonmatch FROM r.nonmatches)
    {
      IF (VersionSatisfiesRange(nonmatch, r.range, CELL[r.comparemode]))
        ABORT(CELL[ r.range, nonmatch, r.comparemode ]);
      }
  }
}


Print("\n === Running TestSemVer\n");

//ADDME: Test various valid and invalid version strings
TestSemVer_Increment();
TestSemVer_Precedence();
TestSemVer_Ranges();
