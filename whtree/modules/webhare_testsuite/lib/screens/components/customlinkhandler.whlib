<?wh
LOADLIB "mod::tollium/lib/componentbase.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";


PUBLIC STATIC OBJECTTYPE CustomLinkHandler EXTEND TolliumLinkHandlerBase
<
  OBJECT timestampobject;

  MACRO NEW()
  {
    this->title := "custom link handler";
  }

  UPDATE PUBLIC BOOLEAN FUNCTION SetLinkIfSupported(STRING inlink)
  {
    STRING ARRAY inlinktok := Tokenize(inlink,"x-webharetestsuite-customlink:");
    IF (Length(inlinktok) != 2)
      RETURN FALSE;

    INTEGER secsvalue := ToInteger(inlinktok[1],0);
    this->timestampobject->value := secsvalue;
    RETURN TRUE;
  }
  UPDATE PUBLIC OBJECT ARRAY FUNCTION CreateComponents(OBJECT screen)
  {
    this->timestampobject := screen->CreateCustomComponent("http://www.webhare.net/xmlns/tollium/screens", "textedit");
    this->timestampobject->required := TRUE;
    this->timestampobject->errorlabel := this->title;
    this->timestampobject->valuetype := "integer";
    this->timestampobject->width := "8x";
    this->timestampobject->minimumvalue := 1;

    RETURN [ this->timestampobject ];
  }
  UPDATE PUBLIC MACRO DestroyComponents()
  {
    this->timestampobject->DeleteComponent();
  }
  UPDATE PUBLIC STRING FUNCTION GetLink()
  {
    RETURN "x-webharetestsuite-customlink:" || this->timestampobject->value;
  }
>;


PUBLIC STATIC OBJECTTYPE CustomLinkHandler2 EXTEND TolliumLinkHandlerBase
<
  MACRO NEW()
  {
    this->title := "custom link handler2";
  }

  UPDATE PUBLIC BOOLEAN FUNCTION SetLinkRecordIfSupported(RECORD inlink)
  {
    RETURN inlink.internallink = 1 AND inlink.append="#test";
  }
  UPDATE PUBLIC RECORD FUNCTION GetLinkRecord()
  {
    RETURN MakeIntExtInternalLink(1,"#test");
  }
>;

PUBLIC STATIC OBJECTTYPE CustomLinkHandler3 EXTEND TolliumLinkHandlerBase
<
  OBJECT subobject;

  MACRO NEW()
  {
    this->title := "custom link handler3";
  }

  UPDATE PUBLIC OBJECT ARRAY FUNCTION CreateComponents(OBJECT screen)
  {
    //we need to retrigger the fragment loader to hit the unsaved-SetLocalRefs bug
    this->subobject := screen->CreateCustomComponent("http://www.webhare.net/xmlns/webhare_testsuite/testcomponents","fragcomp");
    RETURN [ this->subobject ];
  }
  UPDATE PUBLIC MACRO DestroyComponents()
  {
    this->subobject->DeleteComponent();
  }
  UPDATE PUBLIC BOOLEAN FUNCTION SetLinkRecordIfSupported(RECORD inlink)
  {
    RETURN inlink.internallink = 1 AND inlink.append="#test3";
  }
  UPDATE PUBLIC RECORD FUNCTION GetLinkRecord()
  {
    RETURN MakeIntExtInternalLink(1,"#test3");
  }
>;
