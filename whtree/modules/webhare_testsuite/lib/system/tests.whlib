<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/userrights/resync.whlib";

LOADLIB "mod::wrd/lib/internal/dbschema.whlib";

PUBLIC OBJECT FUNCTION OpenTestsuiteSite()
{
  RETURN OpenSiteByName("WebHare testsuite site");
}
PUBLIC OBJECT FUNCTION GetTestsuiteTempFolder()
{
  RETURN OpenSiteByName("WebHare testsuite site")->OpenByPath("tmp");
}

PUBLIC OBJECT FUNCTION OpenTestsuiteAltSite()
{
  RETURN OpenSiteByName("WebHare testsuite site - alt host");
}

PUBLIC MACRO EnsureUserMgmtStateClean()
{
  testfw->BeginWork();
  RECORD ARRAY resync := testfw->userapi->ResyncToSystemTables();
  IF(Length(resync)>0)
  {
    PRINT("UNEXPECTED RESYNC ACTIONS\n");
    DumpValue(resync,'tree');
    ABORT("There were unexpected changes made by ResyncToSystemTables - our incremental updates might be bad. (Once this state is triggered you may need to manually resync userrights)");
  }

  RECORD ARRAY reconnects := FixUnconnectedItems(testfw->userapi->wrdschema);
  IF(Length(reconnects)>0)
  {
    PRINT("UNEXPECTED RECONNECT ACTIONS\n");
    DumpValue(reconnects,'tree');
    ABORT("There were unexpected changes made by FixUnconnectedItems. (Once this state is triggered you may need to manually resync userrights)");
  }
  testfw->CommitWork();
}

DATETIME lastcheck;

PUBLIC RECORD FUNCTION TestAuditEvent(VARIANT expected)
{
  IF (TypeID(expected) = TypeID(RECORD))
    expected := RecordExists(expected) ? RECORD[ expected ] : RECORD[];

  RECORD ARRAY auditevents := SELECT *, DELETE id, DELETE creationdate, DELETE wrdschema, DELETE country
                                FROM wrd.auditevents
                               WHERE auditevents.wrdschema = testfw->GetWRDSchema()->id
                                     AND creationdate > lastcheck
                            ORDER BY creationdate, (SELECT AS INTEGER #expected FROM expected WHERE type = auditevents.type);

  // add defaults for impersonator
  FOREVERY (RECORD rec FROM expected)
  {
    IF (NOT rec.impersonated)
      rec := CELL[ impersonator_entity := rec.byentity, impersonator_login := rec.bylogin, ...rec ];
    IF (CellExists(rec, "__ignoredata"))
      rec := CELL[ ...rec, data := SELECT AS STRING data FROM auditevents WHERE type = rec.type, DELETE __ignoredata ];
    expected[#rec] := rec;
    IF(NOT CellExists(rec,'IP')) //some tests do not care about IP address (prevent connecting to webhareinterface from causing issues)
      auditevents := SELECT *, DELETE ip FROM auditevents;
  }

  TestEq(expected, auditevents);
  lastcheck := GetCurrentDatetime();
  Sleep(1);
  RETURN auditevents;
}
