<?wh

LOADLIB "wh::datetime.whlib";

LOADLIB "mod::publisher/lib/siteapi.whlib";

LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/internal/userrights/resync.whlib";
LOADLIB "mod::system/lib/internal/typecoder.whlib";
LOADLIB "mod::wrd/lib/internal/dbschema.whlib";


PUBLIC OBJECT FUNCTION OpenTestsuiteSite()
{
  RETURN OpenSiteByName("webhare_testsuite.testsite");
}
PUBLIC OBJECT FUNCTION OpenTestsuiteJSSite()
{
  RETURN OpenSiteByName("webhare_testsuite.testsitejs");
}
PUBLIC OBJECT FUNCTION GetTestsuiteTempFolder()
{
  RETURN OpenTestsuiteSite()->OpenByPath("tmp");
}
PUBLIC OBJECT FUNCTION GetTestsuiteJSTempFolder()
{
  RETURN OpenTestsuiteJSSite()->OpenByPath("tmp");
}

PUBLIC OBJECT FUNCTION OpenTestsuiteAltSite()
{
  RETURN OpenSiteByName("webhare_testsuite.althost");
}

PUBLIC MACRO EnsureUserMgmtStateClean(BOOLEAN expect_clean)
{
  testfw->BeginWork();
  RECORD ARRAY resync := testfw->userapi->ResyncToSystemTables();
  IF(Length(resync)>0 AND expect_clean)
  {
    PRINT("UNEXPECTED RESYNC ACTIONS\n");
    DumpValue(resync,'tree');
    ABORT("There were unexpected changes made by ResyncToSystemTables - our incremental updates might be bad. (Once this state is triggered you may need to manually resync userrights)");
  }

  RECORD ARRAY reconnects := FixUnconnectedItems(testfw->userapi->wrdschema);
  IF(Length(reconnects)>0 AND expect_clean)
  {
    PRINT("UNEXPECTED RECONNECT ACTIONS\n");
    DumpValue(reconnects,'tree');
    ABORT("There were unexpected changes made by FixUnconnectedItems. (Once this state is triggered you may need to manually resync userrights)");
  }
  testfw->CommitWork();
}

DATETIME lastcheck;

PUBLIC RECORD FUNCTION TestAuditEvent(VARIANT expected)
{
  IF (TypeID(expected) = TypeID(RECORD))
    expected := RecordExists(expected) ? RECORD[ expected ] : RECORD[];

  RECORD ARRAY auditevents := SELECT *, DELETE id, DELETE creationdate, DELETE wrdschema, DELETE country, data := DecodeHSONorJSONRecord(data)
                                FROM wrd.auditevents
                               WHERE auditevents.wrdschema = testfw->GetWRDSchema()->id
                                     AND creationdate > lastcheck
                            ORDER BY creationdate, (SELECT AS INTEGER #expected FROM expected WHERE type = auditevents.type);

  // add defaults for impersonator
  FOREVERY (RECORD rec FROM expected)
  {
    rec := CELL[ impersonated := FALSE, impersonator_entity := 0, impersonator_login := "", byentity := 0, bylogin := "", ... rec ];
    IF (CellExists(rec, "__ignoredata"))
      rec := CELL[ ...rec, data := SELECT AS RECORD data FROM auditevents WHERE type = rec.type, DELETE __ignoredata ];
    expected[#rec] := rec;
    IF(NOT CellExists(rec,'IP')) //some tests do not care about IP address (prevent connecting to webhareinterface from causing issues)
      auditevents := SELECT *, DELETE ip FROM auditevents;
  }

  TRY {
    TestEq(expected, auditevents);
  } CATCH(OBJECT e) {
    DumpValue(CELL[expected,auditevents], 'tree');
    THROW e;
  }
  lastcheck := GetCurrentDatetime();
  Sleep(1);
  RETURN auditevents;
}

PUBLIC MACRO TestUpdateGrant(STRING grantor, STRING action, STRING right, INTEGER obj, STRING grantee, RECORD options DEFAULTSTO DEFAULT RECORD) {
  TRY {
    testfw->BeginWork();
    testfw->GetUserObject(grantor)->UpdateGrant(action, right, obj, testfw->GetUserOrRoleObject(grantee), options);
  } CATCH(OBJECT e) {
    testfw->RollbackWork();
    THROW e;
  }
  testfw->CommitWork();
}

PUBLIC MACRO TestUpdateRoleGrant(STRING grantor, STRING action, STRING role, STRING user, RECORD options DEFAULTSTO DEFAULT RECORD) {
  TRY {
    testfw->BeginWork();
    testfw->GetUserObject(grantor)->UpdateRoleGrant(action, testfw->GetRoleObject(role)->authobjectid, testfw->GetUserObject(user), options);
  } CATCH(OBJECT e) {
    testfw->RollbackWork();
    THROW e;
  }
  testfw->CommitWork();
}
