<?wh

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::tollium/lib/yamlcomponents.whlib";

PUBLIC MACRO RunScreen(OBJECT controller, RECORD data)
{
  controller->user->timezone := "CET";
  STRING screenpath := data.params[0];
  IF(NOT IsAbsoluteResourcePath(screenpath))
    screenpath := screenpath LIKE "*.xml" ? "mod::webhare_testsuite/screens/" || screenpath : "webhare_testsuite:" || screenpath;
  controller->LoadScreen(screenpath, [ params := ArraySlice(data.params, 1) ])->RunModal();
}

PUBLIC MACRO RunYamlScreenApp(OBJECT controller, RECORD data)
{
  controller->user->timezone := "CET";
  STRING screenpath := data.params[0];
  IF(screenpath NOT LIKE "*/*") //just a name, not path at all!
    screenpath := "mod::webhare_testsuite/tests/tollium/yaml/data/basics.screens.yml#" || screenpath;

  VARIANT result := RunYamlScreen(controller, screenpath);
  REFLECT(result);
}

PUBLIC RECORD ARRAY FUNCTION UpdateDashboardMenu(RECORD ARRAY appmenu, RECORD context)
{
  INTEGER systemmenu := (SELECT AS INTEGER #appmenu + 1 FROM appmenu WHERE title = "System")-1;
  IF(systemmenu = -1) //menu not present yet? (eg Login!)
    RETURN appmenu;

  RECORD dashboardapp := SELECT * FROM appmenu[systemmenu].apps WHERE title = "Dashboard";
  IF(RecordExists(dashboardapp))
  {
    DELETE FROM appmenu[systemmenu].apps WHERE title = dashboardapp.title;

    INSERT [ groupname := "webhare_testsuite:testgroup"
           , title := "TEST GROUP"
           , apps := [dashboardapp]
           ] INTO appmenu AT END;
  }
  RETURN appmenu;
}
