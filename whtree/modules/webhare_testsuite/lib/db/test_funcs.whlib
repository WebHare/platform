<?wh
LOADLIB "wh::dbase/whdb.whlib";
LOADLIB "mod::system/lib/database.whlib";

PUBLIC TABLE <INTEGER id > beta_table0;

INTEGER FUNCTION GetWHDBRoleId(INTEGER transid, STRING rolename)
{
  RETURN GetWHDBTransaction(transid)->GetRoleId(rolename);
}

PUBLIC MACRO InsertTable0Record()
{
  INTEGER test_fp_trans := OpenTestTrans();
  dumpvalue((SELECT * FROM beta_table0),'boxed');
  INSERT DEFAULT RECORD INTO beta_table0;
  dumpvalue((SELECT * FROM beta_table0),'boxed');
  TestCommitSuccess(test_fp_trans);
}

PUBLIC MACRO TestCommitSuccess(INTEGER transid)
{
  RECORD ARRAY errors := CommitWHDBTransaction(transid);
  IF(Length(errors)>0)
  {
    FOREVERY(RECORD err FROM errors)
    {
      PRINT(err.message||"\n");
    }
    ABORT("Unexpected commit errors");
  }
}

PUBLIC INTEGER FUNCTION OpenTestTrans()
{
  INTEGER tr := __avoiddepwarning_StartWHDBTransaction([user := "~webhare", password := "", auto := FALSE]);
  SetPrimaryWebharetransaction(tr);
  beta_table0 := BindTransactionToTable(tr,"beta_dbtests.table0");
  RETURN tr;
}


PUBLIC INTEGER FUNCTION OpenTestTransUser(STRING username)
{
  INTEGER trans := OpenTestTrans();
  INTEGER roleid := GetWHDBRoleId(trans, "definition_schema.tests_" || username);

  INTEGER ARRAY roles;
  IF(roleid!=0)
    roles := [roleid];
  RETURN trans;
}

/** @short Compare two record arrays for equality
    @param recs1 Record array to compare
    @param recs2 Record array to compare against
    @return True if the record arrays match */
PUBLIC BOOLEAN FUNCTION RecordArraysEqual(RECORD ARRAY recs1, RECORD ARRAY recs2)
{
  IF (LENGTH(recs1) != LENGTH(recs2))
    RETURN FALSE;

  FOREVERY (RECORD nr FROM recs1)
    IF (NOT RecordsEqual(nr, recs2[#nr]))
      RETURN FALSE;

  RETURN TRUE;
}

PUBLIC BOOLEAN FUNCTION RecordsEqual(RECORD a, RECORD b)
{
  // Both not existing: Ok
  IF (NOT RecordExists(a) AND NOT RecordExists(b))
    RETURN TRUE;

  // One exists, the other doesn't: Fail
  IF (NOT RecordExists(a) OR NOT RecordExists(b))
    RETURN FALSE;

  RECORD ARRAY lista := UnpackRecord(a);
  RECORD ARRAY listb := UnpackRecord(b);

  IF (LENGTH(lista) != LENGTH(listb))
    RETURN FALSE;

  FOREVERY (RECORD field FROM lista)
  {
    /* Cell in a doesn't exist in b? Fail.
       (sufficient for cellnames contents equal, due to unique constraint and equal counts)
    */
    IF (NOT CellExists(b, field.name))
      RETURN FALSE;

    VARIANT value_a := field.value;
    VARIANT value_b := GetCell(b, field.name);

    IF (TypeId(value_a) != TypeId(value_b))
      RETURN FALSE;

    INTEGER type_a := TypeId(value_a);
    IF (IsTypeidArray(type_a))
    {
      IF (LENGTH(value_a) != LENGTH(value_b))
        RETURN FALSE;

      IF (type_a = TypeID(RECORD ARRAY))
      {
        IF (NOT RecordArraysEqual(value_a, value_b))
          RETURN FALSE;
      }
      ELSE IF (AnyToString(value_a, "tree") != AnyToString(value_b, "tree"))
        RETURN FALSE;

    }
    ELSE
    {
      IF (type_a = TypeID(RECORD))
      {
        IF (NOT RecordsEqual(value_a, value_b))
          RETURN FALSE;
      }
      ELSE IF (AnyToString(value_a, "tree") != AnyToString(value_b, "tree"))
        RETURN FALSE;
    }
  }
  RETURN TRUE;
}

PUBLIC STRING FUNCTION CreateUser(INTEGER transid, STRING name)
{
  SendWHDBCommand(transid,"CREATE USER TESTS_" || name);
  RETURN "TESTS_" || name;
}

PUBLIC MACRO DropUser(INTEGER transid, STRING name)
{
  IF (RecordExists(SELECT * FROM information_schema.users WHERE user_name = "TESTS_" || name))
    SendWHDBCommand(transid,"DROP USER TESTS_" || name);
}
