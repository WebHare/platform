<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::witty.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";
LOADLIB "mod::wrd/lib/pages.whlib";
LOADLIB "mod::wrd/lib/database.whlib";


PUBLIC OBJECTTYPE WRDAuthTestPage EXTEND DynamicPageBase
<
  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody(OBJECT webdesign)
  {
    OBJECT wrdauthplugin := webdesign->GetPlugin("http://www.webhare.net/xmlns/wrd", "wrdauth");
    RECORD data := [ feedbackurl := this->absolutebaseurl
                   , firstname := GetWebVariable("firstname")
                   , lastname := GetWebVariable("lastname")
                   , pwdresetresult := DEFAULT MACRO PTR
                   , multisite := webdesign->targetobject->name LIKE "*multisite*"
                   , allclaims := ""
                   , numsessions := 0
                   , userid := wrdauthplugin->GetLoggedinEntity()
                   , loginname := wrdauthplugin->GetLoggedinEntity() != 0 ? wrdauthplugin->wrdschema->accounttype->GetEntityField(wrdauthplugin->GetLoggedinEntity(), wrdauthplugin->wrdschema->accountlogintag) : ""
                   ];

    IF(GetWebVariable("tryoidc") != "") {
      STRING returnurl := UpdateURLVariables(GetRequestURL(), [ tryoidc := "", withprompt := "" ]);
      RECORD func := webdesign->GetWRDAuthPlugin()->GenerateLoginRequest("oidc", GetWebVariable("tryoidc"), returnurl, [ prompt := GetWebVariable("withprompt") ]);
      ExecuteSubmitInstruction(func);
    }

    IF(wrdauthplugin->IsLoggedIn()) {
      data.allclaims := EncodeJSON(wrdauthplugin->GetClaims());
      data.numsessions := LENGTH(SELECT FROM wrd.tokens WHERE entity = wrdauthplugin->GetLoggedinEntity());
    }

    IF(GetWebVariable('pwdreset')='1')
    {
      RECORD result := wrdauthplugin->ProcessVerificationLink(GetWebVariable("_ed"),GetWebVariable("verifier"));
      IF(result.success)
      {
        GetPrimary()->BeginWork();
        wrdauthplugin->accounttype->UpdateEntity(result.entityid, [ password := CreateWebharePasswordHash(GetWebVariable("password"))]);
        GetPrimary()->CommitWork();
        wrdauthplugin->MarkVerificationLinkUsed(result.entityid, result);
        wrdauthplugin->LoginByid(result.entityid, FALSE);
      }
      INSERT CELL back := this->absolutebaseurl INTO result;
      data.pwdresetresult := PTR EmbedWittyComponent("pwdresetdone", result);
    }
    IF(IsRequestPost() AND GetWebVariable("passwordresetbutton") != "")
    {
      INTEGER userid := wrdauthplugin->LookupLogin(GetWebVariable("resetlogin"));
      IF(userid=0)
      {
        RETURN PTR EmbedWittyComponent(this->pagefolder || "wrdauthtest.witty:error", [ error := "No such user", back := this->absolutebaseurl ]);
      }
      ELSE
      {
        RECORD result := wrdauthplugin->CreatePasswordResetLink("?pwdreset=1", userid);
        RETURN PTR EmbedWittyComponent(this->pagefolder || "wrdauthtest.witty:pwdreset", result);
      }
    }

    IF(IsRequestPost() AND GetWebVariable("detailsbutton") != "")
    {
      GetPrimary()->BeginWork();

      wrdauthplugin->wrdschema->GetType("WRD_PERSON")->UpdateEntity(wrdauthplugin->GetLoggedinEntity()
        , [ wrd_firstname := GetWebVariable("firstname")
        , wrd_lastname := GetWebVariable("lastname")
        ]);

      GetPrimary()->CommitWork();

      wrdauthplugin->UpdateUserInfo();
    }
    RETURN PTR EmbedWittyComponent(Resolve("wrdauthtest.witty:wrdauthtest"), data);
  }
>;

PUBLIC OBJECTTYPE WrdAuthStaticPage EXTEND StaticPageBase
<
  UPDATE PUBLIC MACRO RunBody(OBJECT webdeisgn)
  {
    EmbedWittyComponent(this->pagefolder || "wrdauthtest.witty:statictest");
  }
>;

PUBLIC OBJECTTYPE WRDAuthTestRequireLoginPage EXTEND DynamicPageBase
<
  UPDATE PUBLIC MACRO PrepareForPageConfig(OBJECT webdesign)
  {
    OBJECT wrdauthplugin := webdesign->GetWRDAuthPlugin();
    wrdauthplugin->RequireLoggedInUser();
  }

  UPDATE PUBLIC MACRO RunBody(OBJECT webdeisgn)
  {
    EmbedWittyComponent(this->pagefolder || "wrdauthtest.witty:requireloginpage");
  }

>;

PUBLIC OBJECT FUNCTION WRDAuthRouter(OBJECT webdesign)
{
  OBJECT page := GetWRDAuthPages(webdesign);
  RETURN page ?? NEW SimpleWebPage(PTR EmbedWittyComponent(Resolve("wrdauthtest.witty:wrdauthrouter"),
               [ wrdauthrouter := webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(webdesign->baseurl)
               ]));
}
