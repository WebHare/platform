<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "wh::internet/tcpip.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internet/webbrowser.whlib";
LOADLIB "wh::internet/webdriver.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/services.whlib";


LOADLIB "mod::system/lib/testfw/runtest.whlib";

OBJECT trans := OpenPrimary();

RECORD params := ParseArguments(GetConsoleArguments(),
    [ [ name := "skips",         type := "stringlist" ]
    , [ name := "site",          type := "stringopt" ]
    , [ name := "v",             type := "switch" ]
    , [ name := "nostart",       type := "switch" ]
    , [ name := "ci",            type := "switch" ]
    , [ name := "host",          type := "stringopt" ]
    , [ name := "browser",       type := "param" ]
    , [ name := "masks",         type := "paramlist" ]
    ]);

IF (NOT RecordExists(params))
{
  PRINT(`Run a browser window with js tests under selenium

Syntax: runscript hostjstest.whscr [ -v ] [ --skip test ]* [ --site sitename ] [ --external ] browser testmask*
Options:
  --ci        Run via the CI selenium
  --nostart   Don't autostart the test
  --host      Override host (defaults to localhost when --ci is not specified)
`);
  RETURN;
}

STRING s_capa;

// Check browser (for IE its unlily you have it running yourself, but meh).
IF (params.browser NOT IN [ "firefox", "chrome", "ie8", "ie9", "ie10", "ie11", "edge", "safari" ])
  THROW NEW Exception("Unsupported browser");

OBJECT runtestframework := NEW RunTests;
RECORD ARRAY tests := runtestframework->GatherAllTests(params.masks, params.skips);
DELETE FROM tests WHERE type != "jstest";

STRING ARRAY baseurls := SELECT AS STRING ARRAY DISTINCT baseurl FROM tests;

IF (LENGTH(baseurls) = 0)
  THROW NEW Exception("No matching tests\n");
ELSE IF (LENGTH(baseurls) != 1)
  THROW NEW Exception("Multiple matching baseurls: " || AnyToString(baseurls, "tree"));

STRING testurl := baseurls[0];

STRING webdriverurl;
IF (params.host != "" AND params.ci)
  THROW NEW Exception("--host and --ci are mutually exclusive");

OBJECT driver, session;

IF (params.ci)
  webdriverurl := "http://ci.b-lex.com:4444/wd/hub/";
ELSE
{
  IF (params.host LIKE "http*")
    params.host := UnpackURL(params.host).host;
  webdriverurl := "http://" || (params.host ?? "localhost") || ":4444/wd/hub/";
}

driver := NEW WebDriverConnection(webdriverurl);
STRING seleniumref;

RegisterEventCallback("webhare_testsuite:seleniumlog", PTR OnLog);

MACRO OnLog(STRING event, RECORD data)
{
  IF (data.seleniumref != seleniumref)
    RETURN;

  FOREVERY (RECORD line FROM data.lines)
  {
    SWITCH (line.method)
    {
      CASE "log"
      {
        STRING type := line.args.level;
        PRINT(FormatDateTime("%H:%M:%S.%Q", MakeDatetimeFromUnixTimestamp(MONEY(line.time) / 1000)) || " ");
        SWITCH (line.args.level)
        {
          CASE "log"    { PRINT(AnsiCmd("bold")); }
          CASE "warn"   { PRINT(AnsiCmd("bold,yellow")); }
          CASE "error"  { PRINT(AnsiCmd("bold,red")); }
        }
        PRINT(Detokenize(line.args.args, " ") || "\n");
        PRINT(AnsiCmd("reset,grey") || "  " || Substitute(TrimWhitespace(line.args.stack), "\n", "\n  ") || AnsiCmd("reset") || "\n");
      }
      CASE "trace"
      {
        PRINT(AnsiCmd("grey") || "  " || Substitute(TrimWhitespace(line.args), "\n", "\n  ") || AnsiCmd("reset") || "\n");
      }
    }
  }
}

RECORD FUNCTION TestSession(OBJECT session)
{
  OBJECT browser := NEW WebBrowser;
  STRING url := ResolveToAbsoluteUrl(webdriverurl, "/grid/api/testsession?session=" || session->sessionid);
  BOOLEAN res := browser->GotoWebPage(url); // ignore success, 500 response can also carry info

  IF (LENGTH(browser->content) = 0)
  {
    IF (NOT RecordExists(browser->GetHTTPStatus()))
      THROW NEW Exception("Could not connect to Selenium, is it running and reachable?");
    THROW NEW Exception("Selenium HTTP request problem " || AnyToString(browser->GetHTTPStatus(), "tree"));
  }

  RETURN DecodeJSONBlob(browser->content);
}

WHILE (TRUE)
{
  IF (params.v)
    PRINT("Starting webdriver session\n");

  OBJECT capa := NEW WebdriverCapabilities(params.browser);

  session := WaitForPromise(driver->CreateSession(capa));

  seleniumref := EncryptForCluster(EncodeHSON(
      [ h := webdriverurl
      , s := session->sessionid
      ]));

  testurl := ReplaceVariableInUrl(testurl, "seleniumref", seleniumref);
  testurl := ReplaceVariableInUrl(testurl, "autostart", "0");

  FOREVERY (STRING mask FROM params.masks)
    testurl := ReplaceVariableInUrl(testurl, "mask", mask);
  FOREVERY (STRING skip FROM params.skips)
    testurl := ReplaceVariableInUrl(testurl, "skip", skip);
  IF (params.site != "")
    testurl := ReplaceVariableInUrl(testurl, "site", params.site);

  IF (params.ci)
  {
    RECORD testresult := TestSession(session);
    IF (NOT testresult.success)
      THROW NEW Exception("Could not find session\n");

    STRING ip := UnpackURL(testresult.proxyid).host, hostname := ip;
    IF (IsValidIPAddress(ip))
      hostname := ResolveIPAddress(hostname);
    ELSE
      ip := ResolveHostname(hostname);

    PRINT("Test is running on " || hostname || " (" || ip || "), session " || testresult.session || "\n");
  }

  IF (params.v)
    PRINT("set URL\n");

  WaitForPromise(session->SetURL(testurl));

  // Start the testa automatically
  IF (NOT params.nostart)
  {
    IF (params.v)
      PRINT("Start tests\n");
    WaitForPromise(session->ClickElement(WaitForPromise(session->LookupElement("id", "starttests"))));
  }

  // Wait max 30 minutes to finish
  DATETIME wait_until := AddTimeToDate(30 * 60 * 1000, GetCurrentDateTime());

  STRING ARRAY finishedtests;

  DATETIME lastcheck := GetCurrentDateTime();

  PRINT("Session started\nPress enter to close it, or 'r' to restart\n");
  SetConsoleLineBuffered(FALSE);
  BOOLEAN stop := FALSE;

  WHILE (GetCurrentDateTime() < wait_until)
  {
    RECORD ARRAY handles := WaitUntil([ read := [ 0 ] ], AddTimeToDate(1000, GetCurrentDateTime()));
    IF (RecordExists(SELECT FROM handles WHERE type = "handle-read" and handle = 0))
    {
      STRING s := ReadFrom(0, 1);
      IF (s = "\n")
      {
        stop := TRUE;
        BREAK;
      }
      ELSE IF (s = "r")
        BREAK;
    }

    // Communicate every minute with the script, to keep the (external) session timeout of 90 seconds happy
    IF (params.ci AND GetDateTimeDifference(lastcheck, GetCurrentDateTime()).msecs > 60 * 1000)
    {
      RECORD testresult := TestSession(session);
      IF (NOT testresult.success)
      {
        session := DEFAULT OBJECT;
        PRINT("Session disappeared, quitting test\n");
        IF (params.v)
          PRINT(AnyToString(testresult, "tree"));
        BREAK;
      }

      IF (params.v)
        PRINT("get url to reset session & client timeout\n");
      session->GetURL();
      IF (params.v)
        PRINT("reset done\n");
      lastcheck := GetCurrentDateTime();
    }
  }

  IF (ObjectExists(session))
  {
    IF (params.v)
      PRINT("Shutting down session\n");

    WaitForPromise(session->Close());
  }

  IF (stop)
    BREAK;
}

SetConsoleLineBuffered(TRUE);
