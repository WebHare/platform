<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::promise.whlib";

LOADLIB "mod::system/lib/services.whlib";

OBJECT lock;

PUBLIC OBJECTTYPE ClusterTestLink EXTEND WebHareServiceProxyBase
<
  MACRO NEW(STRING testdata)
  {
    IF(testdata="abort")
      THROW NEW Exception("abort");
  }
  PUBLIC INTEGER FUNCTION GetLUE()
  {
    RETURN 42;
  }
  PUBLIC OBJECT ASYNC FUNCTION GetAsyncLUE()
  {
    RETURN CreateResolvedPromise(42);
  }
  PUBLIC MACRO Crash()
  {
    THROW NEW Exception("Crash()");
  }
  PUBLIC OBJECT FUNCTION GetAsyncCrash()
  {
    TRY
    {
      THROW NEW Exception("Async crash()");
    }
    CATCH (OBJECT e)
    {
      RETURN CreateRejectedPromise(e);
    }
  }
  PUBLIC MACRO EmitTestEvent(RECORD data)
  {
    this->EmitEvent("testevent", data);
  }
  PUBLIC RECORD FUNCTION Ping(VARIANT arg1, VARIANT arg2)
  {
    RETURN CELL[ arg1, arg2 ];
  }
  PUBLIC ASYNC FUNCTION AsyncPing(VARIANT arg1, VARIANT arg2)
  {
    AWAIT CreateSleepPromise(50);
    RETURN CELL[ arg1, arg2 ];
  }
  PUBLIC MACRO LockMutex(STRING name)
  {
    IF(ObjectExists(lock))
    {
      lock->Release();
      lock := DEFAULT OBJECT;
    }

    IF(name != "")
      lock := OpenLockManager()->LockMutex(name);
  }
>;

OBJECT FUNCTION Constructor(STRING testdata)
{
  RETURN NEW ClusterTestLink(testdata);
}


OBJECT extraport := CreateGlobalIPCPort("webhare_testsuite:globalport");
HandleExtraPort(extraport);
PRINT("Registered port\n");

ASYNC MACRO HandleExtraPort(OBJECT port)
{
  WHILE (TRUE)
  {
    OBJECT link := AWAIT port->AsyncAccept(MAX_DATETIME);
    PRINT(`New link on globalport\n`);
    HandleLink(link);
  }
}

ASYNC MACRO HandleLink(OBJECT link)
{
  WHILE (TRUE)
  {
    RECORD rec := AWAIT link->AsyncReceiveMessage(MAX_DATETIME);
    PRINT(`Data on globalport link\n` || AnyToString(rec, "tree"));
    IF (rec.status = "gone")
      RETURN;
    SWITCH (rec.msg.type)
    {
      CASE "sendreply"
      {
        link->SendReply([ type := "reply" ], rec.msgid);
        link->Close();
      }
      CASE "reflect"
      {
        link->SendReply([ ...rec.msg, type := "reflectreply" ], rec.msgid);
        link->Close();
      }
    }
  }
}

RunWebHareService("webhare_testsuite:webhareservicetest", PTR Constructor);
