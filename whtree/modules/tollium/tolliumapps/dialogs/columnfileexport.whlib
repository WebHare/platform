<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/csv.whlib";
LOADLIB "wh::ooxml/spreadsheet.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC STATIC OBJECTTYPE ColumnFileExport EXTEND TolliumScreenBase
<
  BOOLEAN headerrow;
  PUBLIC BOOLEAN autoaddcolumns;

  PUBLIC RECORD ARRAY columns;
  PUBLIC RECORD ARRAY rows;

  /// @short Overridable file name for the exported file (an extension is not needed; also, the file name will be used in conjunction with GetSafeFileName later)
  PUBLIC STRING filename;
  PUBLIC PROPERTY title(this->frame->title, this->frame->title);

  PUBLIC MACRO Init()
  {
    this->headerrow := TRUE;

    ^separator->SetvalueIfValid(this->tolliumuser->GetRegistryKey("tollium.commondialogs.csvexport.sep", "semicolon"));
    ^charset->SetValueIfValid(this->tolliumuser->GetRegistryKey("tollium.commondialogs.csvexport.charset","ISO-8859-1"));
    ^decimal->SetValueIfValid(this->tolliumuser->GetRegistryKey("tollium.commondialogs.csvexport.decimal","period"));
    ^outputformat->SetvalueIfValid(this->tolliumuser->GetRegistryKey("tollium.commondialogs.csvexport.outputformat", "xlsx"));
  }

  MACRO OnDownload(OBJECT filereceiver)
  {
    this->tolliumuser->SetRegistryKey("tollium.commondialogs.csvexport.sep", ^separator->value);
    this->tolliumuser->SetRegistryKey("tollium.commondialogs.csvexport.charset", ^charset->value);
    this->tolliumuser->SetRegistryKey("tollium.commondialogs.csvexport.decimal", ^decimal->value);
    this->tolliumuser->SetRegistryKey("tollium.commondialogs.csvexport.outputformat", ^outputformat->value);

    RECORD setupwriter;

    IF(^outputformat->value="csv")
      setupwriter := this->SetupCSVWriter();
    ELSE
      setupwriter := this->SetupXLSXWriter();

    setupwriter.writer->timezone := this->tolliumuser->timezone;
    IF(Length(this->columns)>0)
      setupwriter.writer->columns := this->columns;

    setupwriter.writer->WriteRows(this->rows);

    BLOB csvfile := setupwriter.writer->MakeOutputFile();

    STRING filename := this->filename != "" ? GetSafeFileName(this->filename) : "export";
    IF(filename LIKE "*.xlsx")
      filename := Left(filename, Length(filename) - 5); //strip extension added by __ValidateXLSXFileOptions, user can still go for CSV :(
    filename := filename || setupwriter.extension;

    filereceiver->SendFile(csvfile, setupwriter.mimetype, filename);
  }

  MACRO OnDownloadStarted()
  {
    this->tolliumresult := "ok";
  }

  RECORD FUNCTION SetupCSVWriter()
  {
    OBJECT csvwriter := NEW CSVColumnFileWriter;
    csvwriter->fieldseparator := ^separator->value="comma" ? "," : ^separator->value="semicolon" ? ";" : "\t";
    csvwriter->charset := ^charset->value;
    csvwriter->encode_excel_values := csvwriter->fieldseparator != "\t";
    csvwriter->booleans_to_yesno := TRUE; //for legacy reasons...
    csvwriter->timezone := this->tolliumuser->timezone;
    csvwriter->languagecode := this->tolliumuser->language;
    csvwriter->headerrow := this->headerrow;
    RETURN [ writer := csvwriter
           , extension := ".csv"
           , mimetype := "application/ms-excel"
           ];
  }

  RECORD FUNCTION SetupXLSXWriter()
  {
    OBJECT writer := NEW XLSXColumnFileWriter;
    RETURN [ writer := writer
           , extension := ".xlsx"
           , mimetype := "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet"
           ];
  }
>;
