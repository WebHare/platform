<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
// LOADLIB "mod::system/lib/webserver/filetransfer.whlib";

MACRO SendDownloadFile(BLOB data, STRING filename, STRING contenttype)
{
  AddHTTPHeader("Content-Type", contenttype, FALSE);
  AddHTTPHeader("Content-Disposition", "attachment; filename=\"" || EncodeJava(Filename) || "\"", FALSE);

  STRING uploadid := GetWebVariable("wh-download");
  IF(uploadid!="")
    UpdateWebCookie("wh-download-" || uploadid, "done", [ httponly := FALSE ]);
  SendWebFile(data);
}

MACRO SendDownloadError(RECORD errorinfo)
{
  Print('<!DOCTYPE html><script type="text/javascript">\n');
  Print('var f=window.frameElement;var d=f.ownerDocument;(d.defaultView).__wh_downloadfailurecallback(f,' || EncodeJSON(errorinfo) || ');\n');
  Print('</script>');
  RETURN;
}


BOOLEAN debug_logcontroller := IsDebugTagEnabled("tollium:logcontroller");
STRING portid := GetWebVariable("l");
STRING type := GetWebVariable("t");
IF (portid LIKE "*/*")
  ABORT("Can only connect to normal applications!");

IF(debug_logcontroller)
  LogDebug("filetransfer", `Incoming request of type '${type}' for application ${portid}'`);

//Connect to the controller. The request is handled by ExecuteDownloadRequest
OBJECT ipclink := ConnectToIPCPort("tollium:link." || portid);
IF(NOT ObjectExists(ipclink))
  ipclink := ConnectToGlobalIPCPort("tollium:link." || portid);

IF(NOT ObjectExists(ipclink))
{
  IF(debug_logcontroller)
    LogDebug("filetransfer", `IPC port ${portid}' not found`);

  ExecuteSubmitInstruction([ type := "close" ]);
  RETURN;
}

SWITCH (type)
{
  CASE "download" //'download' is still used by embedded files (eg RTE CSS)
  {
    RECORD rec := ipclink->DoRequest(
        [ type := GetWebVariable("t")
        , window := GetWebVariable("w")
        , component := GetWebVariable("n")
        , data := DecodeJSON(GetWebVariable("d"))
        ]);

    IF (rec.status != "ok" OR NOT RecordExists(rec.msg))
    {
      IF(debug_logcontroller)
       LogDebug("filetransfer", `Download request failed`, rec);

      AddHTTPHeader("Status", "404 Not found", FALSE);
      RETURN;
    }

    RECORD data := MakeUpdatedRecord(
        [ filename :=     ""
        , mimetype :=     "application/octet-stream"
        , data :=         DEFAULT BLOB
        , disposition :=  ""
        ], rec.msg);

    IF (data.disposition = "")
      data.disposition := "attachment";

    IF (data.filename!="")
      data.disposition := data.disposition || '; filename="' || EncodeJava(data.filename) || '"';

    AddHTTPHeader("Content-Type", data.mimetype,false);
    AddHTTPHeader("Content-Disposition", data.disposition,FALSE);

    //as this is used specifically for embedding nad we assume we have to trust our own tollium apps, strip WebHare's normal policy
    //TODO consider verifying the login cookies? that would prevent using this URL for rehosting
    AddHTTPHeader("Content-Security-Policy", "", FALSE);
    AddHTTPHeader("X-Frame-Options", "", FALSE);

    SendWebFile(data.data);
  }
  CASE "asyncdownload" //used by <downloadaction>
  {
    RECORD rec := ipclink->DoRequest(
        [ type := GetWebVariable("t")
        , window := GetWebVariable("w")
        , component := GetWebVariable("n")
        , data := DecodeJSON(GetWebVariable("d"))
        , id := GetWebVariable("s")
        ]);

    IF (rec.status != "ok" OR NOT RecordExists(rec.msg))
    {
      AddHTTPHeader("Status", "404 Not found", FALSE);
      RETURN;
    }

    IF (rec.msg.type = "cancel")
      SendDownloadError([ error := "Download cancelled" ]);
    ELSE
      SendDownloadFile(rec.msg.data, rec.msg.filename, rec.msg.mimetype);
  }
  CASE "asyncwindowopen"
  {
    RECORD rec := ipclink->DoRequest(
        [ type := GetWebVariable("t")
        , window := GetWebVariable("w")
        , component := GetWebVariable("n")
        , data := DecodeJSON(GetWebVariable("d"))
        , id := GetWebVariable("s")
        ]);

    IF (rec.status != "ok" OR NOT RecordExists(rec.msg))
    {
      ExecuteSubmitInstruction([ type := "close" ]);
      RETURN;
    }

    IF (rec.msg.type = "cancel")
    {
      ExecuteSubmitInstruction([ type := "close" ]);
      RETURN;
    }
    IF (rec.msg.type = "file")
    {
      AddHTTPHeader("Content-Type", rec.msg.mimetype, FALSE);
      SendWebFile(rec.msg.data);
    }
    ELSE IF (rec.msg.type = "url")
      Redirect(rec.msg.url);
    ELSE IF (rec.msg.type = "submitinstruction")
      ExecuteSubmitInstruction(rec.msg.instr);
  }
  CASE "asynccallback"
  {
    RECORD rec := ipclink->DoRequest(
        [ type := GetWebVariable("t")
        , window := GetWebVariable("w")
        , component := GetWebVariable("n")
        , data := DecodeJSON(GetWebVariable("d"))
        , id := GetWebVariable("s")
        ]);

    IF (rec.status != "ok" OR NOT RecordExists(rec.msg) OR rec.msg.type = "cancel")
    {
      AddHTTPHeader("Status", "410 Gone", FALSE);
      Print('This URL is no longer available');
      RETURN;
    }

    IF (rec.msg.type = "file")
    {
      AddHTTPHeader("Content-Type", rec.msg.mimetype, FALSE);
      SendWebFile(rec.msg.data);
    }
    ELSE IF (rec.msg.type = "url")
      Redirect(rec.msg.url);
  }
  DEFAULT
  {
    ABORT("Illegal request type");
  }
}
