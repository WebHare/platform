<?wh
LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::witty.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internal/debug.whlib";
LOADLIB "wh::util/stringparser.whlib";
LOADLIB "wh::xml/xsd.whlib";

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::system/lib/cache.whlib";
LOADLIB "mod::system/lib/checks.whlib";
LOADLIB "mod::system/lib/commonxml.whlib";
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/internal/backend/apps.whlib";
LOADLIB "mod::system/lib/internal/backend/newsapi.whlib";
LOADLIB "mod::system/lib/internal/backend/users.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/todd/internal/actions.whlib";
LOADLIB "mod::tollium/lib/todd/internal/service.whlib";
LOADLIB "mod::tollium/lib/internal/appmgmt.whlib";
LOADLIB "mod::tollium/lib/internal/controllerbase.whlib";
LOADLIB "mod::tollium/lib/internal/support.whlib";
LOADLIB "mod::tollium/lib/internal/gettid.whlib";
LOADLIB "mod::tollium/lib/internal/types.whlib";

LOADLIB "mod::tollium/lib/todd/internal/communication.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/pages.whlib";
LOADLIB "mod::wrd/lib/internal/auth/oidc.whlib";

///Expected protocol version. Bump if we break backwards compatible with JS code
CONSTANT INTEGER expecttoddprotocolversion := 1;

RECORD FUNCTION GetInstructionForAppRec(STRING manualbaseurl, RECORD app)
{
  IF(app.apprec.iswindowopen)
  {
    RETURN [ type := "windowopen"
           , link := app.apprec.link
           ];
  }
  IF(app.apprec.ismanual)
  {
    RETURN [ type := "windowopen"
           , link := UpdateURLVariables(manualbaseurl, [ app := app.apprec.name ])
           ];
  }

  RETURN GetAppLaunchInstruction(app.name);
}

DATETIME FUNCTION GetMaxModificationDate(STRING path)
{
  RECORD ARRAY items := ReadDiskDirectory(path,"*");
  UPDATE items SET modified := GetMaxModificationDate(path || "/" || name) WHERE type=1;
  RETURN SELECT AS DATETIME MAX(modified) FROM items;
}

RECORD FUNCTION GetCacheableUIFilesKey()
{
  STRING basedir := GetModuleInstallationRoot("tollium");
  DATETIME maxdate := GetMaxModificationDate(basedir || "web/ui");
  RETURN [ ttl := 60 * 60 * 1000
         , value := FormatDatetime("%Y%m%d%H%M", maxdate)
         ];
}

PUBLIC RECORD FUNCTION GetCacheableApplicationPortalConfigForApplyTester(OBJECT applytester)
{
  RECORD config := CELL
    [ allowinframe := FALSE
    , dashboard := TRUE

    , personalsettingsapp := "system:personalsettings"
    ///Use wrdauth's login mechanism instead of a login app (will usually redirect you to a wrdauth configured login page)
    , usewrdauthloginpage := FALSE
    , dashboardmenuupdaters := STRING[]
    , applicationmasks := STRING[]
    // temporary hack for testsuite TODO a real <inserthtml> siteprofile node might be useful ?
    , __inserts := STRING[]
    , forceapps := STRING[]
    , forceappfuncs := STRING[]
    , loginmethods := RECORD[]
    , passwordlogin := FALSE
    ];

  //TODO switch to proper plugin so settings can cache etc and just keep most boring settings inside a config record
  FOREVERY(RECORD setting FROM applytester->GetCustomSettings("http://www.webhare.net/xmlns/tollium", "portal"))
  {
    STRING resourcename := setting.siteprofile->name;
    IF(setting.node->HasAttribute("allowinframe"))
      config.allowinframe := ParseXSBoolean(setting.node->GetAttribute("allowinframe"));
    IF(setting.node->HasAttribute("dashboard"))
      config.dashboard := ParseXSBoolean(setting.node->GetAttribute("dashboard"));
    IF(setting.node->HasAttribute("usewrdauthloginpage"))
      config.usewrdauthloginpage := ParseXSBoolean(setting.node->GetAttribute("usewrdauthloginpage"));
    IF(setting.node->HasAttribute("personalsettingsapp"))
      config.personalsettingsapp := setting.node->GetAttribute("personalsettingsapp");
    IF(setting.node->HasAttribute("dashboardmenuupdater"))
      INSERT MakeAbsoluteResourcePath(resourcename, setting.node->GetAttribute("dashboardmenuupdater")) INTO config.dashboardmenuupdaters AT END;
    IF(setting.node->HasAttribute("applicationmasks"))
      config.applicationmasks := ParseXSList(setting.node->GetAttribute("applicationmasks"));
    FOREVERY(OBJECT forceapp FROM setting.node->GetChildElementsByTagNameNS("http://www.webhare.net/xmlns/tollium", "forceapp")->GetCurrentElements())
      INSERT forceapp->GetAttribute("name") INTO config.forceapps AT END;

    IF (setting.node->GetAttribute("forceappfunction") != "")
      INSERT MakeAbsoluteResourcePath(resourcename, setting.node->GetAttribute("forceappfunction")) INTO config.forceappfuncs AT END;

    FOREVERY(OBJECT loginmethod FROM setting.node->GetChildElementsByTagNameNS("http://www.webhare.net/xmlns/tollium", "loginmethod")->GetCurrentElements())
    {
      IF(NOT IsNodeApplicableToThisWebHare(loginmethod))
        CONTINUE;

      STRING gid := DetermineNodeGid(setting.siteprofile->name, loginmethod);
      STRING type := loginmethod->GetAttribute("type");

      SWITCH (type)
      {
        CASE "password"
        {
          config.passwordlogin := TRUE; //explicitly enabled
        }
        CASE "oidc", "saml"
        {
          STRING tag := loginmethod->GetAttribute("tag");
          IF(tag != "")
          {
            BOOLEAN isupdate;
            RECORD cursettings := SELECT * FROM config.loginmethods WHERE loginmethods.type = VAR type AND loginmethods.tag = VAR tag;
            IF(NOT RecordExists(cursettings))
              cursettings := CELL[ type, tag, ordering := 0, autologin := FALSE, tid := "", icon := "", visibility := "always" ];
            ELSE
              isupdate := TRUE;

            IF(loginmethod->HasAttribute("ordering"))
              cursettings.ordering := ToInteger(loginmethod->GetAttribute("ordering"), 99999);
            IF(loginmethod->HasAttribute("autologin"))
              cursettings.autologin := loginmethod->GetAttribute("autologin") IN [ "true", "1" ];
            IF(loginmethod->HasAttribute("title") OR loginmethod->HasAttribute("tid"))
              cursettings.tid := ParseXMLTIDPtr(resourcename, gid, loginmethod, "title");
            IF(loginmethod->HasAttribute("icon"))
              cursettings.icon := loginmethod->GetAttribute("icon");
            IF(loginmethod->HasAttribute("visibility"))
              cursettings.visibility := loginmethod->GetAttribute("visibility");

            IF(isupdate)
              UPDATE config.loginmethods SET RECORD cursettings WHERE loginmethods.type = VAR type AND loginmethods.tag = VAR tag;
            ELSE
              INSERT cursettings INTO config.loginmethods AT END;
          }
        }
      }
    }
  }

  config.loginmethods := SELECT * FROM config.loginmethods ORDER BY type != "saml", ordering;
  IF(Length(config.loginmethods) = 0) //if not explicitly configured through siteprofile, we enable the password login (TODO: then we also need to be able to shut off password logins when completely configured through WRD)
    config.passwordlogin := TRUE; //implicitly enabled (no methods at all)

  STRING ARRAY eventmasks := [ "publisher:internal.siteprofiles.recompiled" ];
  RECORD authplugin := applytester->GetPluginConfiguration("http://www.webhare.net/xmlns/wrd", "wrdauth");
  IF(RecordExists(authplugin) AND authplugin.wrdschema != "")
  {
    OBJECT targetschema := OpenWRDSchema(authplugin.wrdschema);
    OBJECT oidcclient := targetschema->GetType("WRDAUTH_OIDC_CLIENT");
    IF(ObjectExists(oidcclient))
    {
      eventmasks := eventmasks CONCAT oidcclient->GetEventMasks();
      RECORD ARRAY clients := oidcclient->RunQuery(CELL[ outputcolumns := [ "wrd_id", "wrd_title", "wrd_tag", "wrd_ordering", "icon", "metadataurl", "visibility" ]]);
      config.loginmethods := config.loginmethods CONCAT
                             (SELECT type := "oidc"
                                   , tag := wrd_tag ?? "#" || wrd_id
                                   , autologin := FALSE
                                   , tid := ":" || wrd_title
                                   , icon := ""
                                   , wrdicon := RecordExists(icon) ? WrapCachedImage(icon, [ method := "fit", setwidth := 24, setheight := 24 ]).link : GuessOpenIdIcon(metadataurl).frontend
                                   , visibility := visibility ?? "always"
                                FROM clients
                             ORDER BY wrd_ordering, ToUppercase(wrd_title));
    }
  }

  RETURN CELL[ ttl := 30 * 60 * 1000
             , value := config
             , eventmasks
             ];
}

RECORD FUNCTION GetCacheableApplicationPortalConfig(INTEGER fileid)
{
  RETURN GetCacheableApplicationPortalConfigForApplyTester(GetApplyTesterForObject(fileid));
}

PUBLIC RECORD FUNCTION GetApplicationPortalConfig(INTEGER fileid)
{
  RETURN GetAdhocCached(CELL[ applicationportalconfig := fileid ], PTR GetCacheableApplicationPortalConfig(fileid));
}


PUBLIC STATIC OBJECTTYPE TolliumInterfacePage EXTEND WebPageBase <
  PUBLIC STRING title;
  PUBLIC STRING toddroot;
  //Root for assets that should remain as static as possible, even without allow jscache
  PUBLIC STRING toddstaticroot;
  PUBLIC RECORD appserviceparams;

  PUBLIC BOOLEAN debugging;
  PUBLIC BOOLEAN profiling;
  PUBLIC BOOLEAN memoryprofiling;
  PUBLIC STRING addedheaders;

  PUBLIC STRING frontendid;

  INTEGER impersonatedentity;
  STRING impersonatedtitle;
  OBJECT wrdauthplugin;
  OBJECT currentuser;
  RECORD launchuserinfo;
  ///Are we overriding? Don't run unimportant paths (eg newsitems) if we're apparently doing tricky/lowlevel stuff
  BOOLEAN isoverriding;

  BOOLEAN disablenotifications;

  STRING ARRAY apps;
  RECORD rundata;
  STRING jsversion;

  RECORD config;

  MACRO NEW(OBJECT currentuser) {
    this->title := "WebHare Platform";
    this->currentuser := currentuser;
    this->frontendid := GenerateUFS128BitId();

    this->jsversion := GetAssetPackBuildVersion("platform:tollium");
  }

  RECORD FUNCTION GetRunData()
  {
    RECORD config := CELL
      [ toddroot         := this->toddroot
      , toddservice      := "/wh_services/tollium/todd"
      , appserviceparams := this->appserviceparams
      , shortunknowntids := IsWHDebugOptionSet('sut')
      , profiling        := this->profiling
      , memoryprofiling  := this->memoryprofiling
      , frontendid       := this->frontendid
      , checkered_background := checkered_background
      , this->config.allowinframe
      , jsversion        := this->jsversion
      ];

    RECORD data :=
      [ toddroot         := this->toddroot
      , title            := this->title
      , tolliumconfig    := EncodeJSON(config)
      , config := config

      , addedheaders     := this->addedheaders
      , version          := GetWebhareVersion()
    ];
    RETURN data;
  }

  MACRO SetupPortal()
  {
    this->config := GetApplicationPortalConfig(this->webdesign->targetobject->id);
    this->apps := this->apps CONCAT this->config.forceapps;

    IF(this->webdesign EXTENDSFROM WebdesignBase)
      FOREVERY(STRING insertion FROM this->config.__inserts)
        this->webdesign->InsertHTML(insertion, "dependencies-bottom");

    STRING overridetoken := GetWebVariable('overridetoken');
    STRING openas := GetWebVariable("openas");
    STRING language := GetWebVariable("lang") ?? GetWebVariable("language");

    DATETIME sessionexpires;
    this->wrdauthplugin := this->webdesign->GetWRDAuthPlugin();
    IF(ObjectExists(this->wrdauthplugin) AND this->wrdauthplugin->HasFailed())
      this->wrdauthplugin := DEFAULT OBJECT;
    IF(ObjectExists(this->wrdauthplugin))
    sessionexpires := this->wrdauthplugin->sessionexpires;

    INTEGER loggedin;
    STRING loggedin_name;

    this->isoverriding := overridetoken != "";
    IF(this->isoverriding AND NOT VerifyOverrideToken(overridetoken))
      AbortWithHTTPError(403, "The specified overridetoken is not, or no longer, valid", [ htmlbody := "<p>The specified overridetoken is not, or no longer, valid</p>" ]);

    IF(this->isoverriding)
    {
      loggedin := -1;
      loggedin_name := "<overrideuser>";
      this->currentuser := GetMagicSysopObject("<overrideuser>");
    }
    ELSE IF(ObjectExists(this->wrdauthplugin))
    {
      IF(this->wrdauthplugin->HasFailed())
        THROW NEW Exception("The authplugin could not load: " || this->wrdauthplugin->GetFailReason());

      IF(NOT this->isoverriding)
        loggedin := this->wrdauthplugin->GetLoggedinEntity();
    }

    IF(loggedin != 0 AND (loggedin != -1 OR openas != ""))
    {
      RECORD res := this->wrdauthplugin->userapi->GetTolliumUserByIdAndImpersonation(loggedin, openas);
      IF(NOT ObjectExists(res.userobject))
      {
        //never force a logout on development... we generally just want the actual failure reason when developing
        IF(GetDtapStage() = "development")
          THROW NEW Exception(res.error);

        this->wrdauthplugin->Logout();
        Redirect(GetRequestURL());
      }

      this->currentuser := res.userobject;
      this->impersonatedentity := res.impersonatedentity;
      this->impersonatedtitle := res.impersonatedtitle ?? "#" || this->impersonatedentity;
      loggedin_name := this->currentuser->login;
    }

    this->launchuserinfo := CELL
        [ loggedin
        , loggedin_name
        , loggedin_expires := sessionexpires
        , openas
        ];

    STRING lang := language ?? ObjectExists(this->currentuser) ? this->currentuser->language : GetSiteLanguage(this->webdesign->targetobject->id);

    SetTidLanguage(lang);
    this->webdesign->languagecode := lang;
  }

  RECORD ARRAY FUNCTION CalculateInitialInstructions()
  {
    RECORD ARRAY initialinstructions;

    RECORD ARRAY appinstructions;

    FOREVERY (STRING forceappfunc FROM this->config.forceappfuncs)
    {
      FUNCTION PTR fptr := MakeFunctionPTR(forceappfunc);
      TRY
      {
        STRING ARRAY forceapps := fptr([ user := this->currentuser ]);
        FOREVERY (STRING app FROM forceapps)
          INSERT GetAppLaunchInstruction(app) INTO appinstructions AT END;
      }
      CATCH (OBJECT e)
        LogHarescriptException(e);
    }


    FOREVERY(STRING app FROM this->apps)
      INSERT GetAppLaunchInstruction(app) INTO appinstructions AT END;

    FOREVERY(STRING app FROM SELECT AS STRING ARRAY value FROM GetAllWebVariables() WHERE ToUppercase(name) IN [ "APP", "MANUAL" ])
      INSERT GetAppLaunchInstruction(app) INTO appinstructions AT END;

    appinstructions := SELECT *, inbackground := #instr != Length(appinstructions)-1 FROM appinstructions AS instr;

    initialinstructions := initialinstructions CONCAT appinstructions;

    RETURN initialinstructions;
  }

  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody()
  {
    IF(this->subpath != "")
      AbortWithHTTPError(404);

    // Handle logincontrol when already logged in
    STRING logincontrol := GetWebVariable("wrdauth_logincontrol");
    IF (logincontrol != "")
      this->webdesign->GetWRDAuthPlugin()->ProcessReturnTo(logincontrol);

    IF(GetWebVariable("wrdauth_proof") != "" ) //Eg. UT login triggers this, wrdauth_proof isn't cleared because we're logging in on same host. wrdauth should be suppressing this, not us!
      Redirect(UpdateURLVariables(GetRequestURL(), [ wrdauth_proof := "" ]));

    //make sure people can't see "old" tollium interfaces after a recompile;
    this->webdesign->assetcachebuster := this->jsversion;
    this->webdesign->InsertHTML('<meta name="google" value="notranslate" />', 'dependencies-top');

    STRING ARRAY profiletoks := Tokenize(GetWebVariable("profile"),",");
    this->profiling := GetWebVariable("profile") IN ["1","true"] OR IsWHDebugOptionSet("apr");
    this->memoryprofiling := GetWebVariable("profile") = "memory";

    this->SetupPortal();

    IF (GetWebVariable("language") != "")
      this->webdesign->languagecode := GetWebVariable("language");

    // Starting with single ?app=manualname? Then redirect to that manual
    STRING ARRAY apps := SELECT AS STRING ARRAY value FROM GetAllWebVariables() WHERE ToUppercase(name) = "APP";
    IF (LENGTH(apps) = 1)
    {
      FOREVERY (RECORD grp FROM this->GetApplications())
      {
        RECORD app := SELECT * FROM grp.apps WHERE name = VAR apps[0] AND (apprec.ismanual OR apprec.iswindowopen);
        IF (RecordExists(app))
        {
          Redirect(app.apprec.iswindowopen
              ? app.apprec.link
              : GenerateProtectedManualURL(app.apprec.manual, app.apprec.accesstoken, this->webdesign->languagecode, app.apprec.supportedlanguages));
        }
      }
    }

    this->appserviceparams := [ overridetoken := ""
                              , openas := ""
                              , language := this->webdesign->languagecode
                              , debug := IsWHDebugOptionSet("tol")
                              ];

    this->appserviceparams.overridetoken := GetWebVariable('overridetoken');
    this->appserviceparams.openas := GetWebVariable("openas");

    STRING filekey := GetAdhocCached([type := "uifileskey"], PTR GetCacheableUIFilesKey());
    this->toddstaticroot := "/.tollium/ui/!" || filekey || "/";
    this->toddroot := this->toddstaticroot;

    this->rundata := this->GetRunData();
    this->webdesign->jsobjconfig := this->rundata.config;

    INSERT "wh-shell" INTO this->webdesign->htmlclasses AT END; //this is a trigger for templates who need to do extra work for our interface (TODO should indyshell apply it after succesful startup, and should that hide the 'webhare starting' root witty?)

    //do not allow us to be embedded into iframes
    IF(NOT this->config.allowinframe)
    {
      AddHTTPHeader("X-Frame-Options", "DENY", FALSE);
      AddHTTPHeader("X-WebharePreview-Frame-Options", "DENY", FALSE);
    }

    /* Google Maps: Content Security Policy Guide
       https://developers.google.com/maps/documentation/javascript/content-security-policy
    */
    RECORD csp :=
        [ "default-src" := "'self'"
        // blob: is needed for the image editor to process uploaded images client side
        // data: is needed to show inline images like tollium icons
        // https: is needed for eg the video browser to load thumbnails from youtube
        , "img-src" := "'self' data: blob: https:"
        // * is needed for the RTDs to load fonts.google.com and similar sites
        // 'unsafe-inline' is needed for the RTD which sets element styles through innerHTML
        , "style-src" := "* 'unsafe-inline'"
        // 'self' is required to allow websockets to connect to us
        // *.googleapis.com and *.gstatic.com are needed for Google Maps
        , "connect-src" := "'self' https://*.googleapis.com"
        // * is needed for tollium to be able to frame anything (eg publisher preview)
        , "frame-src" := "*"
        // * is needed for the RTDs to load fonts.google.com and similar sites
        , "font-src" := "*"
        // *.googleapis.com and *.gstatic.com are needed for Google Maps
        , "script-src" := "'self' https://*.googleapis.com"
        ];
    // This was a bug in Safari before 16 (https://bugs.webkit.org/show_bug.cgi?id=235873), but Firefox (which should support
    // this: https://bugzilla.mozilla.org/show_bug.cgi?id=1345615) now gives an error connecting to wss when it's not
    // explicitly added to connect-src
    IF (GetWebHeader("User-Agent") LIKE "*Firefox/*") //quick-n-dirty browser sniffing
      csp."connect-src" := csp."connect-src" || " ws: wss:"; //workaround, should be implicit
    AddHTTPHeader("Content-Security-Policy", Detokenize((SELECT AS STRING ARRAY `${ToLowercase(name)} ${value}` FROM UnpackRecord(csp)), "; "), FALSE);

    AddHTTPHeader("X-Robots-Tag", "noindex, nofollow", FALSE);

    RETURN PTR EmbedWittyComponent("mod::tollium/lib/todd/internal/webpage.witty:main", this->rundata);
  }

  STRING ARRAY FUNCTION ParseInAppPath(STRING apppath)
  {
    STRING ARRAY inapppath;
    STRING curpath;

    OBJECT s := NEW StringParser(apppath);
    WHILE (NOT s->eof)
    {
      curpath := curpath || s->ParseWhileNotInSet("/\\");
      if (s->TryParse("/"))
      {
        INSERT curpath INTO inapppath AT END;
        curpath := "";
        CONTINUE;
      }
      ELSE IF (s->TryParse("\\"))
        curpath := curpath || s->ParseN(1);
    }

    INSERT curpath INTO inapppath AT END;
    RETURN inapppath;
  }

  RECORD FUNCTION ParseAppName(STRING appname)
  {
    // Parses appname of form appname(parameter1,parameter2(bla\)bla),parameter3)/inapppath1,inapppath2
    STRING ARRAY parameters;
    STRING ARRAY inapppath;

    OBJECT s := NEW StringParser(appname);
    appname := s->ParseWhileNotInSet("/(");
    IF (s->TryParse("("))
    {
      STRING params;
      INTEGER depth := 1;
      WHILE (depth > 0 AND NOT s->eof)
      {
        params := params || s->ParseWhileNotInSet("()\\");
        IF (s->TryParse("("))
        {
          params := params || "(";
          depth := depth + 1;
        }
        ELSE IF (s->TryParse(")"))
        {
          depth := depth - 1;
          IF (depth != 0)
            params := params || ")";
        }
        ELSE IF (s->TryParse("\\"))
        {
          params := params || s->ParseN(1);
        }
      }
      parameters := Tokenize(params, ",");
    }
    IF (s->TryParse("/"))
    {
      inapppath := this->ParseInAppPath(s->remaining_data);
    }

    RETURN CELL[ appname, parameters, inapppath];
  }

  //Get user specific event groups.
  STRING ARRAY FUNCTION GetEventGroups()
  {
    STRING ARRAY groups := [ GetBroadcastAllTarget() ];
    IF(ObjectExists(this->currentuser)) //ADDME use impersonated or our user ?
    {
      STRING groupname:= this->currentuser->GetBroadcastTarget();
      IF (groupname != "")
        INSERT groupname INTO groups AT END;
    }
    RETURN groups;
  }

  PUBLIC RECORD ARRAY FUNCTION GetApplications()
  {
    IF(NOT ObjectExists(this->currentuser))
      RETURN DEFAULT RECORD ARRAY;

    BOOLEAN showdeveloperapps := this->currentuser->ShowDeveloperOptions();

    RECORD ARRAY grouped := GetAllInstalledApplications();
    FOREVERY(RECORD appgroup FROM grouped)
    {
      appgroup.apps := SELECT *
                         FROM appgroup.apps
                        WHERE (apprec.isdeveloperapp = FALSE OR showdeveloperapps)
                              AND IsApplicationAccessible(this->currentuser, apprec);

      grouped[#appgroup] := appgroup;
    }

    RETURN grouped;
  }

  PUBLIC RECORD ARRAY FUNCTION GetPersonalShortcuts(RECORD ARRAY appgroups)
  {
    IF(NOT ObjectExists(this->currentuser))
      RETURN DEFAULT RECORD ARRAY;

    RECORD ARRAY favorites := this->currentuser->GetRegistryKey("tollium.dashboard.shortcuts", RECORD[]);
    IF(Length(favorites) = 0)
      RETURN DEFAULT RECORD ARRAY;

    RECORD ARRAY allapps;
    FOREVERY(RECORD appgroup FROM appgroups)
      allapps := allapps CONCAT appgroup.apps;

    RECORD ARRAY menuitems;
    FOREVERY(RECORD favorite FROM favorites)
    {
      RECORD matchingapp := SELECT * FROM allapps WHERE allapps.name = favorite.app;

      IF(NOT RecordExists(matchingapp))
        CONTINUE;

      INSERT [ title := favorite.title
             , instr := GetAppLaunchInstruction(favorite.app,
                    CELL[ target  := favorite.target
                        , message := favorite.message
                        , reuse_instance := "never"
                        ])
             , icon := matchingapp.icon
             , link := matchingapp.link
             ] INTO menuitems AT END;
    }
    RETURN menuitems;
  }

  RECORD ARRAY FUNCTION GetAppMenu()
  {
    STRING openas := GetWebVariable("openas");
    STRING addvars := openas != "" ? "&openas=" || EncodeURL(GetWebVariable("openas")) : "";
    RECORD ARRAY allapps := SELECT *
                                 , apps := (SELECT *, link := "?app=" || EncodeURL(name) || addvars FROM apps)
                              FROM this->GetApplications();

    STRING manualbaseurl := `?language=${EncodeURL(GetTidLanguage())}`;
    RECORD ARRAY appmenu := SELECT title := GetTid(tid)
                                 , apps := (SELECT TEMPORARY translatedtitle := GetTid(title)
                                                 , title := translatedtitle
                                                 , instr := GetInstructionForAppRec(manualbaseurl, apps)
                                                 , link
                                                 , icon
                                                 , shortcut
                                              FROM appgroups.apps
                                             WHERE title != ""
                                                   AND (Length(this->config.applicationmasks) = 0 OR MatchCommonXMLWildcardMasks(name,this->config.applicationmasks))
                                          ORDER BY ToUppercase(translatedtitle))
                                 , groupname := name
                              FROM allapps AS appgroups
                             WHERE tid != "";
    DELETE FROM appmenu WHERE Length(apps) = 0;

    RECORD ARRAY personalmenu := this->GetPersonalShortcuts(allapps);
    IF(Length(personalmenu) > 0)
    {
      // For editing favorites, open the personal settings app if it's not opened yet and send a message to open the 'startmenu' tab
      INSERT [ title := GetTid("tollium:shell.personalmenu")
             , apps := personalmenu
             , editinstr := GetAppLaunchInstruction(this->config.personalsettingsapp, [ reuse_instance := "always"
                                                                                      , message := [ opentab := "startmenu" ]
                                                                                      ])
             , edittitle := GetTid("tollium:shell.editfavorites")
             , groupname := "system:favorites"
             ] INTO appmenu AT 0;
    }

    FOREVERY(STRING updater FROM this->config.dashboardmenuupdaters)
      appmenu := MakeFunctionPtr(updater)(appmenu, [ user := this->currentuser ]);

    RETURN SELECT *, DELETE groupname FROM appmenu;
  }

  RECORD FUNCTION GetDashboardBackground()
  {
    OBJECT dashboardbg := OpenWHFSObjectByPath("/webhare-private/system/backend/dashboardbackground");
    RETURN ObjectExists(dashboardbg) ? WrapCachedImage(dashboardbg->GetWrapped(), [ method := "none" ]) : DEFAULT RECORD;
  }
  STRING FUNCTION GetTabBarImage()
  {
    OBJECT tabbarimg := OpenWHFSObjectByPath("/webhare-private/system/backend/tabbarimage");
    IF(ObjectExists(tabbarimg))
      RETURN GetCachedFSImageURL("", tabbarimg->id, [ method := "none" ]);
    RETURN "";
  }

  STRING FUNCTION RenderNewsItem(RECORD input)
  {
    OBJECT renderer := this->webdesign->OpenRTD(input);
    RETURN renderer->RenderAllToString();
  }

  RECORD ARRAY FUNCTION GetNewsItems()
  {
    //Don't gather newsitems if not logged in or if there may be config issues (we don't want to harden newsapi against all possible failures)
    IF (NOT ObjectExists(this->currentuser) OR NOT ObjectExists(this->wrdauthplugin) OR this->isoverriding)
      RETURN RECORD[];

    INTEGER ARRAY units := INTEGER[ this->currentuser->GetParentUnitId() ];

    RECORD ARRAY newsitems := GetCurrentNewsItemsForUnits(this->wrdauthplugin->userapi, units);
    newsitems := FilterHiddenNewsItems(this->wrdauthplugin->userapi, this->currentuser->entityid, newsitems);

    newsitems := SELECT title
                      , html := this->RenderNewsItem(text)
                      , creationdate := this->currentuser->FormatDateTime(creationdate, "minutes", TRUE, FALSE)
                      , publicationdate := this->currentuser->FormatDateTime(publish_from, "minutes", TRUE, FALSE)
                      , id
                      , author
                   FROM newsitems
                  WHERE lang IN [ "nlen", this->currentuser->language ];
    RETURN newsitems;
  }

  RECORD FUNCTION GetCurrentShellSettings()
  {
    BOOLEAN issysop := ObjectExists(this->currentuser) AND this->currentuser->HasRight("system:sysop");
    BOOLEAN issupervisor := issysop OR (ObjectExists(this->currentuser) AND this->currentuser->HasRight("system:supervisor"));

    RECORD portalsettings :=
            [ lang := GetTidLanguage()
            , eventgroups := this->GetEventGroups()
            , now := FormatISO8601Datetime(GetCurrentDatetime(), "day", "milliseconds")
            , apps := this->GetAppMenu()
            , newsitems := this->GetNewsItems()

            , dashboard := this->config.dashboard AND GetWebVariable("appmode") != "singleapp" //TODO singleapp should hide entire menubar
            , dashboardbg := this->GetDashboardBackground()
            , displayimage := ""
            , displayname := ""
            , personalsettings := GetAppLaunchInstruction(this->config.personalsettingsapp, [reuse_instance := "always" ])
            , userdisplayname :=    ObjectExists(this->currentuser) ? this->currentuser->realname ?? this->currentuser->login : ""
            , browsertitleprefix := ObjectExists(this->currentuser) ? this->currentuser->GetRegistryKey("system.prefs.browsertitleprefix", "") : ""
            , openinfo := ""
            , checkinterval := issupervisor ? ToInteger(GetWebVariable("checkinterval"), 5 * 60) * 1000 : 0
            , issysop := issysop
            , notificationslocation := GetWebVariable("notifications") IN ["0","off"] ? "none"
                                       : GetWebVariable("notifications") = "browser" ? "browser"
                                       : ObjectExists(this->currentuser) ? this->currentuser->GetRegistryKey("system.prefs.shownotifications", "desktop") : "desktop"
            , feedbacktoken := ReadRegistryKey("system.backend.features.feedback") AND ObjectExists(this->currentuser) AND IsModuleInstalled("connect") ? GetFeedbackWebToken(this->currentuser) : ""
            , initialinstructions := this->CalculateInitialInstructions()
            ];

    IF(this->impersonatedentity != 0) //no images, no layout title
    {
      portalsettings.openinfo := GetTid("tollium:shell.openedas", this->impersonatedtitle);
    }
    ELSE
    {
      OBJECT tabbarimg := OpenWHFSObjectByPath("/webhare-private/system/backend/tabbarimage");
      IF(ObjectExists(tabbarimg))  //image takes precedence over layout title
        portalsettings.displayimage := GetCachedImageLink(tabbarimg->GetWrapped(),  [ method := "fit", setheight := 72 ]); //2x apptabs height
      ELSE  //show the (configurable) system title
        portalsettings.displayname := ReadRegistryKey("system.backend.layout.title");
    }
    RETURN portalsettings;
  }

  PUBLIC RECORD FUNCTION WCS_GetCurrentVersion()
  {
    RETURN CELL[ jsversion := this->jsversion ];
  }

  PUBLIC RECORD FUNCTION WDS_GetCurrentShellSettings(RECORD options)
  {
    this->SetupPortal();
    RETURN this->GetCurrentShellSettings();
  }

  PUBLIC RECORD FUNCTION WDS_StartPortal(RECORD options) {
    this->SetupPortal();

    //FIXME Get the actual loginmethod and restrictions from the specified appservice

    RECORD params;
    IF(CellExists(options, "params"))
      params := options.params;

    IF(ObjectExists(this->wrdauthplugin) AND ObjectExists(this->wrdauthplugin->userapi) AND NOT ObjectExists(this->currentuser)) {
      RECORD ARRAY loginmethods := SELECT *, title := GetTid(tid) FROM this->config.loginmethods;
      RETURN CELL[
        isloggedin := FALSE,
        settings := this->GetCurrentShellSettings(),
        loginconfig := CELL[
          loginmethods,
          this->config.passwordlogin,
          infotext  := ReadRegistryKey("system.backend.layout.infotext"),
          infotitle := ReadRegistryKey("system.backend.layout.infotitle")
        ]
      ];
    } ELSE {
      RETURN [ isloggedin := TRUE
            , settings := this->GetCurrentShellSettings()
            ];
    }
  }

  //marking params as unused as dropping them can give JS errors when updating
  PUBLIC RECORD FUNCTION WCS_StartApp(STRING appname, RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    options := ValidateOptions(appstartoptions, EnforceStructure([webvars := RECORD[]], options));

    IF(options.protocolversion != expecttoddprotocolversion)
      RETURN [ error := "unexpectedprotocolversion", errormessage := GetTid("tollium:shell.login.unexpectedprotocolversion") ];

    this->SetupPortal();
    STRING toddbase := ResolveToAbsoluteURL(GetRequestURL(), "/tollium_todd/");

    IF(ReadRegistryKey("system.webserver.global.locking.sysoponly") AND (NOT ObjectExists(this->currentuser) OR NOT this->currentuser->HasRight("system:supervisor")))
      RETURN CELL[ error := "launcherror", errormessage := GetTid("tollium:commondialogs.messageboxes.webhareclosedformaintenance") ];

    IF(ObjectExists(this->wrdauthplugin) AND NOT ObjectExists(this->currentuser))
      RETURN [ error := "notloggedin" ];

    IF(Length(this->config.applicationmasks) > 0 AND NOT MatchCommonXMLWildcardMasks(appname, this->config.applicationmasks))
      THROW NEW Exception(`Application '${appname}' is not permitted by this portal's applicationmasks`);

    RECORD parsedappname := this->ParseAppName(appname);
    RECORD app := GetApplicationToLaunch(parsedappname.appname);
    IF(NOT RecordExists(app))
      RETURN CELL[ error := "launcherror", errormessage := GetTid("tollium:commondialogs.messageboxes.applaunchfailure", parsedappname.appname) ];

    IF (options.frontendid = "")
      THROW NEW Exception("Missing frontendid");

    RECORD invoke := CELL[ ...options
                         , app :=          parsedappname.appname
                         , parameters :=   parsedappname.parameters
                         , inapppath :=    parsedappname.inapppath
                         , url :=          GetClientRequestURL()
                         , browsertriplet := options.browser
                         , webdebugsettings := __webdebugsettings
                         , debug :=        IsWHDebugOptionSet("tol")
                         , profile :=      IsWHDebugOptionSet("apr")
                         , memoryprofile := GetWebVariable("profile") = "memory"
                         , requestdata :=
                            [ remoteip :=     GetClientRemoteIP()
                            , remoteport :=   GetClientRemotePort()
                            , localip :=      GetClientLocalIP()
                            , localport :=    GetClientLocalPort()
                            , webserver :=    GetClientWebserver()
                            , proxyid :=      ToInteger(GetWebVariable("X-WebHare-Proxy"),0)
                            , browsertriplet := options.browser
                            ]
                         , launchuserinfo := this->launchuserinfo
                         , lang := GetTidLanguage()
                         ];

    RETURN BootApp(app, invoke);
  }

  PUBLIC RECORD FUNCTION WCS_GetCheckResult()
  {
    this->SetupPortal();
    BOOLEAN issupervisor := ObjectExists(this->currentuser) AND this->currentuser->HasRight("system:supervisor");
    IF(NOT issupervisor)
      RETURN [ privileged := FALSE ];

    RECORD ARRAY messages := GetCurrentCheckIssues();
    DELETE FROM messages WHERE snoozed;
    IF(Length(messages)>0)
      RETURN [ privileged := TRUE, numleft := Length(messages) - 1, firstmessage := messages[0].message_text ];
    ELSE
      RETURN [ privileged := TRUE, numleft := 0, firstmessage := "" ];
  }

  ///Execute an action through the portal
  PUBLIC RECORD FUNCTION WCS_ExecuteAction(RECORD action)
  {
    this->SetupPortal();
    IF(NOT ObjectExists(this->currentuser))
      THROW NEW Exception("Unknown user");

    RETURN ExecuteAction(this->wrdauthplugin->userapi, this->currentuser, action);
  }
>;

PUBLIC OBJECT FUNCTION TolliumPortalRouter(OBJECT webdesign) {
  RECORD urlinfo := GetDynamicPageParameters();
  IF(urlinfo.subpath != "")
    AbortWithHTTPError(404);

  IF (GetWebVariable("proxy_test") != "") {
    IF (GetWebVariable("proxy_test") = ReadRegistryKey("system.webserver.global.proxyservers.verificationcode"))
      SendWebFile(StringToBlob("ok"));

    AddHTTPHeader("Status", "500 Wrong proxy verification code", FALSE);
    SendWebFile(StringToBlob("Wrong proxy verification code"));
  }

  IF (GetWebVariable("oauth_responseid") != "") { //a peering response
    BroadcastEvent("tollium:oauth_response",
        [ responseid :=       GetWebVariable("oauth_responseid")
        , responsetype :=     GetWebVariable("responsetype")
        , version :=          ToInteger(GetWebVariable("version"), 0)
        , token :=            GetWebVariable("token")
        , scopes :=           GetWebVariable("scopes") = "" ? DEFAULT STRING ARRAY : Tokenize(GetWebVariable("scopes"), ",")
        , error :=            GetWebVariable("error")
        , expires :=          MakeDatetimeFromUnixTimestamp(ToInteger(GetWebVariable("expires"), 0))
        ]);

    ExecuteSubmitInstruction([ type := "close" ]);
  }


  STRING overridetoken := GetWebVariable('overridetoken');
  IF(overridetoken != "") {
    IF(NOT VerifyOverrideToken(overridetoken))
      AbortWithHTTPError(403, "The specified overridetoken is not, or no longer, valid", [ htmlbody := "<p>The specified overridetoken is not, or no longer, valid</p>" ]);
  }

  OBJECT authplugin := webdesign->GetWRDAuthPlugin();

  IF(GetWebHeader("Content-Type") LIKE "application/json*")
    RETURN NEW TolliumInterfacePage(ObjectExists(authplugin) ? authplugin->GetCurrentWebhareUser() : DEFAULT OBJECT); //incoming RPC. TODO get rid of the WCS_/WDS_ calls above and this bypass can go away too

  RECORD config := GetApplicationPortalConfig(webdesign->targetobject->id);
  OBJECT currentuser;

  IF(overridetoken = "" AND ObjectExists(authplugin)) { //we really need to enable auth things
    IF(config.usewrdauthloginpage) {
      /* TODO: should we still require this siteprofile option or just check if you have a loginpage= ?
               it's a bit double but also I'm not sure if we want to keep loginpage= and if it shouldn't
               be wrdauth or somewhere configuration (much easier to disable in case of problems) */
      currentuser := authplugin->RequireLoggedinUser(); //is expected to redirect if user is not logged in
    } ELSE { //use our 'embeded' auth page
      //FIXME convert to callback? as we ONLY need to retrieve images if rendering the login page
      RECORD images := RPC_RetrieveImages([ [ data := [ imgnames := (SELECT AS STRING ARRAY icon FROM config.loginmethods), color := "c", height := 24, width := 24 ] ] ], FALSE);
      OBJECT page := GetWRDAuthPages(webdesign, CELL[
        //TODO transition away from siteprl loginmethods, but we may need this for compatibility with existing installations
        //     this should probably be an undocumented method and WRDAuth should look at the schema's configuration
        loginmethods :=
            (SELECT TEMPORARY image := #loginmethods < Length(images.images.images) ? images.images.images[#loginmethods] : DEFAULT RECORD
                  , tag := type || ":" || tag
                  , title := GetTid(tid)
                    // Images from RPC_RetrieveImages are already base64-encoded
                  , icon := RecordExists(image) ? `data:${image.type};base64,${image.data}` : wrdicon
              FROM config.loginmethods
              WHERE visibility = "always")
      ]);

      IF(ObjectExists(page)) {
        OBJECT loginbg := OpenWHFSObjectByPath("/webhare-private/system/backend/loginbackground");
        IF (ObjectExists(loginbg))
          webdesign->pageconfig.mainbackgroundcss := WrapCachedImage(loginbg->GetWrapped(), [ method := "none" ]).css;

        RETURN page;
      }

      currentuser := authplugin->GetCurrentWebhareUser();
    }
  }

  RETURN NEW TolliumInterfacePage(currentuser);
}
