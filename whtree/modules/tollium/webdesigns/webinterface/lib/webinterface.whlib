<?wh

LOADLIB "wh::os.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::tollium/lib/componentbase.whlib";

LOADLIB "mod::publisher/lib/webdesign.whlib";

LOADLIB "mod::system/lib/cache.whlib";
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";


PUBLIC STATIC OBJECTTYPE WebInterfaceDesign EXTEND WebDesignBase
<
  MACRO NEW()
  {
    //We sometimes run in weird contexts and wrdauth plugins can confuse us... kill any if present
    IF(IsRequest() AND UnpackURL(GetRequestURL()).urlpath LIKE ".publisher/debug/*")
    {
      OBJECT wrdauth := this->GetWRDAuthPlugin();
      IF(ObjectExists(wrdauth))
        wrdauth->__disableintegration := TRUE;
    }

    this->robotstag.noindex := TRUE;
    this->robotstag.nofollow := TRUE;
  }
  UPDATE PUBLIC RECORD FUNCTION GetPageConfig()
  {
    IF (NOT this->targetobject->isfolder
        AND this->targetobject->typens = "http://www.webhare.net/xmlns/publisher/richdocumentfile"
        AND this->targetfolder->typens = "http://www.webhare.net/xmlns/tollium/manual")
      INSERT "wh-tollium--manual" INTO this->htmlclasses AT END;

    STRING banner; //Allow to set a banner using ?banner= for testing purposes
    STRING dtapstage := GetDtapStage();
    IF(dtapstage = "development" AND IsRequest())
      banner := GetWebvariable("banner");

    IF(banner = "")
    {
      STRING restoreinfo := GetEnvironmentVariable("WEBHARE_ISRESTORED");
      IF(restoreinfo != "")
        banner := GetTid("tollium:shell.isrestoredwebhare") || (restoreinfo != "1" ? ": " || restoreinfo : "");
    }

    // For the common auth pages, retrieve the login background. The application portal router also sets this property when
    // the login page is shown.
    STRING mainbackgroundcss;
    IF(IsRequest() AND UnpackURL(GetRequestURL()).urlpath LIKE ".wh/common/authpages/*")
    {
      OBJECT loginbg := OpenWHFSObjectByPath("/webhare-private/system/backend/loginbackground");
      IF (ObjectExists(loginbg))
        mainbackgroundcss := WrapCachedImage(loginbg->GetWrapped(), [ method := "none" ]).css;
    }

    RETURN CELL
        [ favicon :=
          [ touchicon := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/apple-touch-icon.png")
          , icon32 := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/favicon-32x32.png")
          , icon16 := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/favicon-16x16.png")
          , icon96 := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/favicon-96x96.png")
          , iconsvg := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/favicon.svg")
          , webmanifest := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon/" || dtapstage || "/site.webmanifest")
          ]
        , banner
        , mainbackgroundcss
        ];
  }

  UPDATE PUBLIC MACRO PrepareErrorPage(INTEGER errorcode, RECORD harescriptinfo, STRING url) {
    STRING requrl := Tokenize(url,'?')[0];
    IF(errorcode IN [404,500] AND UnpackURL(requrl).urlpath = UnpackURL(this->targetsite->webroot).urlpath)
      IF(NOT (IsDebugTagEnabled("etr") OR IsWHDebugOptionSet("etr"))) {
        //A failure to load the interface root may just be an initialization issue
        this->pagewitty->RunComponent("rootfailure");
        TerminateScript();
      }

    WebDesignBase::PrepareErrorPage(errorcode, harescriptinfo, url);
  }
>;
