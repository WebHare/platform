<?wh
LOADLIB "wh::internet/urls.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";


PUBLIC STATIC OBJECTTYPE WebInterfaceDesign EXTEND WebDesignBase
<
  MACRO NEW()
  {
    //We sometimes run in weird contexts and wrdauth plugins can confuse us... kill any if present
    IF(IsRequest() AND UnpackURL(GetRequestURL()).urlpath LIKE ".publisher/debug/*")
    {
      OBJECT wrdauth := this->GetWRDAuthPlugin();
      IF(ObjectExists(wrdauth))
        wrdauth->__disableintegration := TRUE;
    }
  }
  UPDATE PUBLIC RECORD FUNCTION GetPageConfig()
  {
    IF (NOT this->targetobject->isfolder
        AND this->targetobject->typens = "http://www.webhare.net/xmlns/publisher/richdocumentfile"
        AND this->targetfolder->typens = "http://www.webhare.net/xmlns/tollium/manual")
      INSERT "wh-tollium--manual" INTO this->htmlclasses AT END;

    RETURN [ favicon := GetModuleResourceURL("mod::tollium/webdesigns/webinterface/web/img/favicon.ico")
           ];
  }

  UPDATE PUBLIC MACRO PrepareErrorPage(INTEGER errorcode, RECORD harescriptinfo, STRING url)
  {
    STRING requrl := Tokenize(url,'?')[0];

    IF(errorcode IN [404,500] AND UnpackURL(requrl).urlpath = UnpackURL(this->targetsite->webroot).urlpath)
    {
      //A failure to load the interface root may just be an initialization issue
      this->pagewitty->RunComponent("rootfailure");
      TerminateScript();
    }
    WebDesignBase::PrepareErrorPage(errorcode, harescriptinfo, url);
  }
>;

PUBLIC STATIC OBJECTTYPE TolliumWRDAuthSupport EXTEND WRDAuthSupportBase
<
  UPDATE PUBLIC RECORD FUNCTION IsLoginDenied(INTEGER userid, RECORD options)
  {
    RECORD userinfo := this->plugin->accounttype->GetEntityFields(userid, ["WHUSER_DISABLED"]);
    IF(userinfo.whuser_disabled)
      RETURN [ code := "DISABLED" ];

    RETURN DEFAULT RECORD;
  }
>;
