<?wh
/** @topic tollium/dialogs */

LOADLIB "wh::ooxml/spreadsheet.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";

/** Show a column file (eg CSV, XLSX) export dialog.
    @long If the list of rows empty, the dialog will report this and prevent downloading an empty file
    @param parent Parent window
    @param options @includecelldef %GenerateXLSXFile.options
    @return True if the download was completed or there was nothing to download
*/
PUBLIC BOOLEAN FUNCTION RunColumnFileExportDialog(OBJECT parent, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := __ValidateXLSXFileOptions(CELL[ timezone := parent->contexts->user->timezone
                                           , ...options
                                           ]);
  IF(Length(options.rows) = 0)
  {
    parent->contexts->screen->RunSimpleScreen("info", GetTid("tollium:commondialogs.columnfileexport.exportisempty"));
    RETURN TRUE;
  }

  OBJECT scr := parent->contexts->screen->LoadScreen("mod::tollium/tolliumapps/dialogs/columnfileexport.xml");
  scr->columns := options.columns;
  scr->rows := options.rows;
  IF(options.exporttitle != "")
    scr->title := GetTid("tollium:commondialogs.columnfileexport.exporttitle", options.exporttitle);

  scr->filename := options.filename;
  RETURN scr->RunModal() = "ok";
}

/** Show a dialog reporting an exception
    @param parent Parent window
    @param exceptionobject The exception
*/
PUBLIC MACRO RunExceptionReportDialog(OBJECT parent, OBJECT exceptionobject, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  (parent->contexts->screen ?? parent->contexts->controller)->LoadScreen("tollium:commondialogs.exceptionscreen", CELL[ type := "exception", exception := exceptionobject, options ])->RunModal();
}

/** Show a dialog with a stack trace
    @param parent Parent window
    @param stacktrace The trace
*/
PUBLIC MACRO RunStackTraceDialog(OBJECT parent, RECORD ARRAY stacktrace, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
      [ value :=      0
      ],
      options,
      [ notypecheck :=    [ "VALUE" ]
      ]);

  IF (LENGTH(stacktrace) = 0)
    RETURN;

  IF (CellExists(stacktrace[0], "ROWKEY"))
  {
    stacktrace := SELECT *, rowkey := #stacktrace + 1 FROM stacktrace;
    options := [ ...options, value := 0 ];
  }

  parent->RunScreen("mod::system/screens/monitor/processlist.xml#codeview",
      [ rows :=   stacktrace
      , value :=  options.value
      ]);
}

/** Show a frame in a screen
    @long Shows a dialog with an iframe and the URL for the iframe.
    @param parent Parent window
    @param url URL to show
    @cell(integer) options.frameminwidth Minimum hidth of the frame
    @cell(integer) options.frameminheight Minimum height of the frame
    @cell(boolean) options.allowresize Allow the dialog (and iframe) to be resized. Defaults to true
    @cell(boolean) options.showurl Whether to show the URL. Defaults to false
    #cell(string) options.title Dialog title. If empty, tries to take the parent's dialog title */
PUBLIC MACRO RunIframeDialog(OBJECT parent, STRING url, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  //TODO future versions should allow setting initial frame width apart from minimal frame width, but tollium can't combine that yet with 1pr sizes
  options := ValidateOptions(CELL[ frameminwidth := 853
                                 , frameminheight := 480
                                 , allowresize := TRUE
                                 , title := ""
                                 , showurl := FALSE
                                 ], options);
  parent->RunScreen("mod::tollium/tolliumapps/dialogs/iframe.xml#iframedialog", CELL[...options, url]);
}

