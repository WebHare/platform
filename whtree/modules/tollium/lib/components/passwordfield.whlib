<?wh
LOADLIB "wh::crypto.whlib";
LOADLIB "mod::tollium/lib/commondialogs.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/auth/passwordpolicy.whlib";


PUBLIC OBJECTTYPE PasswordField EXTEND TolliumFragmentBase
<
  BOOLEAN password_set;
  STRING pvt_value;
  STRING pvt_validationchecks;

  PUBLIC BOOLEAN entercurrentpassword;
  PUBLIC BOOLEAN ishashed;
  PUBLIC STRING defaulthash;
  PUBLIC PROPERTY value(GetValue, SetValue);
  PUBLIC PROPERTY validationchecks(pvt_validationchecks, SetValidationChecks);

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    this->entercurrentpassword := TRUE;
    this->ishashed := TRUE;
  }

  UPDATE PUBLIC MACRO StaticInit(RECORD description)
  {
    TolliumFragmentBase::StaticInit(description);

    //this->passwordfield->title := description.title;
    this->ishashed := description.ishashed;
    this->defaulthash := description.defaulthash;
    this->entercurrentpassword := description.entercurrentpassword;

    IF(description.value!="")
      this->value := description.ishashed ? CreateWebHarePasswordHash(description.value) : description.value;

    ^passwordfield->lengthmeasure := "characters";
  }

  UPDATE MACRO PreInitComponent()
  {
    this->passwordfield->title := this->title;
  }

  STRING FUNCTION GetValue()
  {
    IF (this->ishashed)
      RETURN this->pvt_value ?? this->defaulthash;

    RETURN this->pvt_value;
  }

  MACRO SetValue(STRING newvalue)
  {
    IF (newvalue != "")
      this->password_set := TRUE; //setting through code also counts as a password-set

    this->pvt_value := newvalue;
    this->passwordfield->value := newvalue != "" ? "password" : "";
  }

  UPDATE PUBLIC BOOLEAN FUNCTION IsUpdateValue()
  {
    RETURN this->password_set OR (this->pvt_value = "" AND this->ishashed AND this->defaulthash != "");
  }

  MACRO SetValidationChecks(STRING newvalidationchecks)
  {
    ParsePasswordChecks(newvalidationchecks);
    this->pvt_validationchecks := newvalidationchecks;
  }

  MACRO DoChangePassword()
  {
    OBJECT dialog := CreatePasswordChangeDialog(this->owner);
    dialog->value := this->entercurrentpassword ? this->value : "";
    dialog->validationchecks := this->pvt_validationchecks;
    dialog->ishashed := this->ishashed;
    dialog->requirepassword := this->required;
    dialog->displayemptywarning := NOT this->required AND this->value != "";

    IF (dialog->RunModal()="ok")
    {
      STRING newpwd := dialog->GetNewPassword();
      IF(newpwd="")
        this->value := "";
      ELSE
        this->value := newpwd LIKE "WEBHARE-PASSWORD:*" ? DecodeUFS(Substring(newpwd,17))
                       : this->ishashed ? CreateWebHarePasswordHash(newpwd)
                       : newpwd;

      this->password_set := TRUE;
    }
  }

  UPDATE PUBLIC MACRO ValidateValue(OBJECT work)
  {
    IF(NOT this->enabled) //nothing to check on inactive fields
      RETURN;

    STRING fieldtitle := this->errorlabel!="" ? this->errorlabel : this->title;
    IF(this->required AND this->value = "")
      work->AddErrorFor(this, GetTid("tollium:common.errors.field_required", fieldtitle));
  }

>;

PUBLIC STATIC OBJECTTYPE EditPassword EXTEND TolliumScreenBase
<
  BOOLEAN pvt_requirepassword;
  STRING pvt_validationchecks;

  /// Current password value (if set, must be filled in in 'oldpassword' field)
  PUBLIC STRING value;

  /// Whether the password is hahsed
  PUBLIC BOOLEAN ishashed;

  /// Validation checks (eg 'hibp minlength:10')
  PUBLIC PROPERTY validationchecks(pvt_validationchecks, SetPasswordChecks);

  /// Show a warning dialog when setting an empty password (because the user is resetting an existing pasword)
  PUBLIC BOOLEAN displayemptywarning;

  /// Whether filling in a non-empty password is required
  PUBLIC PROPERTY requirepassword(pvt_requirepassword, SetRequireNewPassword);

  MACRO NEW()
  {
    this->ishashed := TRUE;
    this->pvt_requirepassword := TRUE;
  }

  PUBLIC MACRO OnShow()
  {
    IF (this->value = "") // Hide "Old password" field
    {
      ^oldpassword->visible := FALSE;
      ^oldpassword->required := FALSE; // FIXME: By hiding the field, Tollium should do this (Arnold: ben ik het niet mee eens.... bovendien, wat doe je in het geval dat een gebruiker _nu_ nog geen wachtwoord heeft (En dus huidig wachtwoord geen zin heeft?))
    }
  }

  PUBLIC MACRO SetRequireNewPassword(BOOLEAN newval)
  {
    ^newpassword->required := newval;
    ^confirmpassword->required := newval AND ^confirmpassword->enabled;
  }

  MACRO SetPasswordChecks(STRING newchecks)
  {
    RECORD ARRAY parsed := ParsePasswordChecks(newchecks);
    this->pvt_validationchecks := newchecks;

    INTEGER minlength :=
        (SELECT AS INTEGER value + 1
           FROM parsed
          WHERE check = "minlength") - 1;

    ^newpassword->minlength := minlength > 0 ? minlength : -1;
    ^newpassword->showcounter := minlength > 0;
    ^validationchecksfield->value := DescribePasswordChecks(newchecks);
    ^validationchecksfield->visible := ^validationchecksfield->value != "";
  }

  MACRO OnShowPasswordsChange()
  {
    ^oldpassword->password := NOT ^showpasswords->value;
    ^confirmpassword->password := NOT ^showpasswords->value;
    ^newpassword->password := NOT ^showpasswords->value;
    ^confirmpassword->enabled := NOT ^showpasswords->value;
    ^confirmpassword->required := ^newpassword->required AND NOT ^showpasswords->value;
    IF (^showpasswords->value)
      ^confirmpassword->value := "";
  }

  /** @short Get the new password, unhashed, if any was entered
      @return New password
  */
  PUBLIC STRING FUNCTION GetNewPassword()
  {
    RETURN ^newpassword->value;
  }

  PUBLIC BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginFeedback();

    IF (this->value!="")
    {
      // Verify the value
      BOOLEAN match := this->ishashed ? VerifyWebHarePasswordHash(^oldpassword->value, this->value)
                                      : ^oldpassword->value = this->value;
      IF (NOT match)
        work->AddErrorFor(^oldpassword, GetTid("tollium:commondialogs.editpassword.current_password_not_equal"));
    }

    IF (this->pvt_validationchecks != "")
    {
      RECORD rec := CheckPassword(this->pvt_validationchecks, ^newpassword->value);
      IF (NOT rec.success)
        work->AddErrorFor(^newpassword, rec.message);
    }

    IF (NOT ^showpasswords->value AND ^newpassword->value != ""
        AND ^newpassword->value != ^confirmpassword->value)
      work->AddErrorFor(^confirmpassword, GetTid("tollium:commondialogs.editpassword.new_passwords_not_equal"));

    IF (NOT work->Finish())
      RETURN FALSE;

    IF (^newpassword->value = "" AND this->displayemptywarning)
    {
      IF (this->RunMessageBox(".sureclearpassword") != "yes")
      {
        this->tolliumresult := "cancel";
        RETURN FALSE;
      }
    }

    RETURN TRUE;
  }
>;
