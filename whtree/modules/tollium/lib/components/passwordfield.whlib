<?wh
LOADLIB "wh::crypto.whlib";
LOADLIB "mod::tollium/lib/commondialogs.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC OBJECTTYPE PasswordField EXTEND TolliumFragmentBase
<
  BOOLEAN password_set;
  STRING pvt_value;

  PUBLIC FUNCTION PTR onpolicycheck;
  PUBLIC STRING policy;
  PUBLIC BOOLEAN entercurrentpassword;
  PUBLIC BOOLEAN ishashed;
  PUBLIC STRING defaulthash;
  PUBLIC PROPERTY value(GetValue, SetValue);

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    this->entercurrentpassword := TRUE;
    this->ishashed := TRUE;
  }

  UPDATE PUBLIC MACRO StaticInit(RECORD description)
  {
    TolliumFragmentBase::StaticInit(description);

    this->onpolicycheck := description.onpolicycheck;
    this->policy := description.policy;
    //this->passwordfield->title := description.title;
    this->ishashed := description.ishashed;
    this->defaulthash := description.defaulthash;
    this->entercurrentpassword := description.entercurrentpassword;

    IF(description.value!="")
      this->value := description.ishashed ? CreateWebHarePasswordHash(description.value) : description.value;
  }

  UPDATE MACRO PreInitComponent()
  {
    this->passwordfield->title := this->title;
  }

  STRING FUNCTION GetValue()
  {
    IF (this->ishashed)
      RETURN this->pvt_value ?? this->defaulthash;

    RETURN this->pvt_value;
  }

  MACRO SetValue(STRING newvalue)
  {
    IF (newvalue != "")
      this->password_set := TRUE; //setting through code also counts as a password-set

    this->pvt_value := newvalue;
    this->passwordfield->value := newvalue != "" ? "password" : "";
  }

  UPDATE PUBLIC BOOLEAN FUNCTION IsUpdateValue()
  {
    RETURN this->password_set OR (this->pvt_value = "" AND this->ishashed AND this->defaulthash != "");
  }

  MACRO DoChangePassword()
  {
    OBJECT dialog := CreatePasswordChangeDialog(this->owner);
    dialog->onpolicycheck := this->onpolicycheck;
    dialog->policy := this->policy;
    dialog->value := this->entercurrentpassword ? this->value : "";
    dialog->ishashed := this->ishashed;
    dialog->requirepassword := this->required;
    dialog->displayemptywarning := NOT this->required AND this->value != "";

    IF (dialog->RunModal()="ok")
    {
      STRING newpwd := dialog->GetNewPassword();
      IF(newpwd="")
        this->value := "";
      ELSE
        this->value := newpwd LIKE "WEBHARE-PASSWORD:*" ? DecodeUFS(Substring(newpwd,17))
                       : this->ishashed ? CreateWebHarePasswordHash(newpwd)
                       : newpwd;

      this->password_set := TRUE;
    }
  }

  UPDATE PUBLIC MACRO ValidateValue(OBJECT work)
  {
    IF(NOT this->enabled) //nothing to check on inactive fields
      RETURN;

    STRING fieldtitle := this->errorlabel!="" ? this->errorlabel : this->title;
    IF(this->required AND this->value = "")
      work->AddErrorFor(this, GetTid("tollium:common.errors.field_required", fieldtitle));
  }

>;

PUBLIC OBJECTTYPE EditPassword EXTEND TolliumScreenBase
<
  BOOLEAN pvt_requirepassword;

  PUBLIC STRING value;
  PUBLIC BOOLEAN ishashed;

  PUBLIC FUNCTION PTR onpolicycheck;
  PUBLIC STRING policy;

  /// Show a warning dialog when setting an empty password (because the user is resetting an existing pasword)
  PUBLIC BOOLEAN displayemptywarning;

  /// Whether filling in a non-empty password is required
  PUBLIC PROPERTY requirepassword(pvt_requirepassword, SetRequireNewPassword);

  MACRO NEW()
  {
    this->ishashed := TRUE;
    this->pvt_requirepassword := TRUE;
  }

  PUBLIC MACRO OnShow()
  {
    IF (this->value = "") // Hide "Old password" field
    {
      this->oldpassword->visible := FALSE;
      this->oldpassword->required := FALSE; // FIXME: By hiding the field, Tollium should do this (Arnold: ben ik het niet mee eens.... bovendien, wat doe je in het geval dat een gebruiker _nu_ nog geen wachtwoord heeft (En dus huidig wachtwoord geen zin heeft?))
    }

    IF(this->policy = "")
    {
      this->policyfield->visible := FALSE;
    }
    ELSE
    {
      this->policyfield->visible := TRUE;
      this->policyfield->value := this->policy;
    }
  }

  PUBLIC MACRO SetRequireNewPassword(BOOLEAN newval)
  {
    this->newpassword->required := newval;
    this->confirmpassword->required := newval AND this->confirmpassword->enabled;
  }

  MACRO OnShowPasswordsChange()
  {
    this->oldpassword->password := NOT this->showpasswords->value;
    this->confirmpassword->password := NOT this->showpasswords->value;
    this->newpassword->password := NOT this->showpasswords->value;
    this->confirmpassword->enabled := NOT this->showpasswords->value;
    this->confirmpassword->required := this->newpassword->required AND NOT this->showpasswords->value;
    IF (this->showpasswords->value)
      this->confirmpassword->value := "";
  }

  /** @short Get the new password, unhashed, if any was entered */
  PUBLIC STRING FUNCTION GetNewPassword()
  {
    RETURN this->newpassword->value;
  }

  PUBLIC BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginFeedback();

    IF (this->value!="")
    {
      // Verify the value
      BOOLEAN match := this->ishashed ? VerifyWebHarePasswordHash(this->oldpassword->value, this->value)
                                      : this->oldpassword->value = this->value;
      IF (NOT match)
        work->AddErrorFor(this->oldpassword, GetTid("tollium:commondialogs.editpassword.current_password_not_equal"));
    }

    IF (this->onpolicycheck != DEFAULT FUNCTION PTR)
    {
      RECORD retval := this->onpolicycheck(this->newpassword->value);
      retval := ValidateOptions(
          [ success :=    FALSE
          , message :=    ""
          ], retval,
          [ required := [ "success" ]
          , title := "onpolicycheck return value"
          ]);
      IF (NOT retval.success)
        work->AddErrorFor(this->newpassword, retval.message);
    }

    IF (NOT this->showpasswords->value AND this->newpassword->value != ""
        AND this->newpassword->value != this->confirmpassword->value)
      work->AddErrorFor(this->confirmpassword, GetTid("tollium:commondialogs.editpassword.new_passwords_not_equal"));

    IF (NOT work->Finish())
      RETURN FALSE;

    IF (this->newpassword->value = "" AND this->displayemptywarning)
    {
      IF (this->RunMessageBox(".sureclearpassword") != "yes")
      {
        this->tolliumresult := "cancel";
        RETURN FALSE;
      }
    }

    RETURN TRUE;
  }
>;
