<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

/////////////////////////////////////////////////////////////////////
// Time field
PUBLIC OBJECTTYPE TolliumTime EXTEND TolliumFragmentBase
<
  STRING _valuetype;

  PUBLIC PROPERTY value(GetValue, SetValue);
  PUBLIC PROPERTY invalidtime(^dt->invalidtime, -);
  PUBLIC PROPERTY valuetype(_valuetype, SetValueType);
  PUBLIC PROPERTY precision(^dt->precision, ^dt->precision);     ///< Allowed values: 'minutes'/'seconds'/'milliseconds'
  UPDATE PUBLIC PROPERTY readonly(^dt->readonly, ^dt->readonly);
  UPDATE PUBLIC PROPERTY required(^dt->required, ^dt->required);
  UPDATE PUBLIC PROPERTY title(^dt->title, ^dt->title);
  UPDATE PUBLIC PROPERTY errorlabel(^dt->errorlabel, ^dt->errorlabel);

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    EXTEND this BY TolliumIsDirtyable;

    this->invisibletitle := TRUE;
    this->_valuetype := "integer";
  }

  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->precision := def.precision;
    this->title := this->pvt_title;
    this->required := def.required;
    this->readonly := def.readonly;
    this->_valuetype := def.valuetype;
  }

  UPDATE MACRO PreInitComponent()
  {
    this->dt->dirtylistener := this->dirtylistener;
  }

  VARIANT FUNCTION GetValue()
  {
    INTEGER retval := ^dt->value = DEFAULT DATETIME ? - 1 : GetMsecondCount(this->dt->value);
    IF(this->_valuetype = "integer64")
      RETURN INTEGER64(retval);
    RETURN retval;
  }
  MACRO SetValue(INTEGER64 newtime)
  {
    IF(newtime<0)
      this->dt->value := DEFAULT DATETIME;
    ELSE
      this->dt->value := MakeDateFromParts(1, INTEGER(newtime));
  }
  UPDATE PUBLIC BOOLEAN FUNCTION IsValidValue(VARIANT value)
  {
    RETURN TYPEID(value) IN [ TYPEID(INTEGER), TYPEID(INTEGER64) ] AND value >= -1;
  }

  UPDATE PUBLIC MACRO ValidateValue(OBJECT work)
  {
    this->dt->ValidateValue(work);
  }
  MACRO SetValueType(STRING newvaluetype)
  {
    IF(newvaluetype NOT IN ["integer","integer64"])
      THROW NEW TolliumException(this, `Invalid valuetype '${newvaluetype}'`);

    this->_valuetype := newvaluetype;
  }
>;
