<?wh
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/internal/components.whlib";
LOADLIB "mod::tollium/lib/components/action.whlib";

/////////////////////////////////////////////////////////////////////
// The menu item
PUBLIC OBJECTTYPE TolliumMenuItem EXTEND TolliumSpecialComponent
<
  STRING pvt_icon;
  RECORD ARRAY pvt_items;
  BOOLEAN pvt_selected;
  BOOLEAN pvt_checked;
  OBJECT pvt_action;
  PUBLIC STRING disablemode;
  INTEGER pvt_indent;
  BOOLEAN pvt_ellipsis;

  UPDATE PUBLIC PROPERTY renderedtitle(GetTitleWithEllipsis, -);

  PUBLIC PROPERTY icon(pvt_icon, SetIcon);
  PUBLIC PROPERTY items(pvt_items, SetItems);
  PUBLIC PROPERTY selected(pvt_selected, SetSelected);
  PUBLIC PROPERTY checked(pvt_checked, SetChecked);
  PUBLIC PROPERTY indent(pvt_indent, SetIndent);
  PUBLIC PROPERTY action(pvt_action, SetAction);
  PUBLIC PROPERTY ellipsis(pvt_ellipsis, SetEllipsis);

  MACRO NEW()
  {
    this->componenttype := "menuitem";
  }
  //FIXME Move action dependencies to Todd and simplify the whole lot
  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumSpecialComponent::StaticInit(def);
    this->items := SELECT isdivider := ObjectExists(component) = FALSE
                        , menuitem := component
                     FROM def.items;

    IF(CellExists(def,'action'))//it's a <item>, not a <menu>
    {
      this->pvt_icon := def.icon;
      this->disablemode := def.disablemode;
      this->pvt_indent := def.indent;
      this->pvt_ellipsis := def.ellipsis;
      this->action := def.action;
    }
  }

  STRING FUNCTION GetTitleWithEllipsis()
  {
    RETURN this->title || (this->ellipsis?"â€¦":"");
  }
  UPDATE MACRO SetEllipsis(BOOLEAN ellipsis)
  {
    this->pvt_ellipsis := ellipsis;
    this->ExtUpdatedTitle();
  }


  MACRO SetAction(OBJECT newaction)
  {
    IF(ObjectExists(this->pvt_action))
      this->pvt_action->UnregisterDependency(this);
    this->pvt_action := newaction;

    IF(ObjectExists(newaction) AND NOT (newaction EXTENDSFROM TolliumActionForwardBase))
      THROW NEW TolliumException(this, "Setting object '" || newaction->name || "' as action, but it does not appear to be an action component");

    IF (this->pvt_icon = "" AND ObjectExists(this->pvt_action))
    {
      this->pvt_action->SetDependency(this, [ icon := TRUE ]);
      this->pvt_icon := this->pvt_action->icon;
    }
    IF(this->pvt_title="" AND ObjectExists(this->pvt_action))
    {
      this->pvt_action->SetDependency(this, [ title := TRUE ]);
      this->pvt_title := this->pvt_action->title;
    }
    IF (this->pvt_ellipsis = FALSE AND ObjectExists(this->action))
    {
      this->action->SetDependency(this, [ ellipsis := TRUE ]);
      this->pvt_ellipsis := this->action->ellipsis;
    }


    IF (this->pvt_hint = "" AND ObjectExists(this->pvt_action))
    {
      this->pvt_action->SetDependency(this, [ hint := TRUE ]);
      this->pvt_hint := this->pvt_action->hint;
    }

    this->ExtUpdatedComponent();
  }

  UPDATE PUBLIC MACRO DeleteComponent()
  {
    IF (ObjectExists(this->pvt_action))
      this->pvt_action->UnregisterDependency(this);
    TolliumComponentBase::DeleteComponent();
  }

  PUBLIC BOOLEAN FUNCTION TolliumClick()
  {
    IF(this->pvt_action != DEFAULT OBJECT)
      RETURN this->pvt_action->TolliumClick();
    RETURN FALSE;
  }

  UPDATE PUBLIC MACRO ExecutePathAction(STRING path)
  {
    IF(path!="")
    {
      this->owner->RunSimpleScreen("error", "<action>s do not support any subactions");
      RETURN;
    }
    IF(NOT this->TolliumClick())
    {
      this->owner->RunSimpleScreen("error", `'${this->name}' did not find an actionhandler to execute`);
    }
  }

  UPDATE PUBLIC MACRO UpdatedDependentField(STRING fieldname)
  {
    SWITCH (fieldname)
    {
    CASE "title"
      {
        TolliumComponentBase::SetTitle(this->pvt_action->title);
      }
    CASE "icon"
      {
        IF (this->pvt_icon != this->pvt_action->icon)
        {
          this->pvt_icon := this->pvt_action->icon;
          this->ExtUpdatedComponent();
        }
      }
    CASE "hint"
      {
        TolliumComponentBase::SetHint(this->pvt_action->hint);
      }
    }
  }

  UPDATE MACRO SetTitle(STRING newtitle)
  {
    TolliumComponentBase::SetTitle(newtitle);
    IF (ObjectExists(this->pvt_action))
      this->pvt_action->SetDependency(this, [ title := FALSE ]);
  }

  MACRO SetIcon(STRING newicon)
  {
    this->pvt_icon := newicon;
    this->ExtUpdatedComponent();
    IF (ObjectExists(this->pvt_action))
      this->pvt_action->SetDependency(this, [ icon := FALSE ]);
  }

  UPDATE MACRO SetHint(STRING newhint)
  {
    TolliumComponentBase::SetHint(newhint);
    IF (ObjectExists(this->pvt_action))
      this->pvt_action->SetDependency(this, [ hint := FALSE ]);
  }

  MACRO SetSelected(BOOLEAN selected)
  {
    this->pvt_selected := selected;
    IF (this->pvt_selected)
      this->pvt_checked := FALSE;
    this->ExtUpdatedComponent();
  }

  MACRO SetChecked(BOOLEAN checked)
  {
    this->pvt_checked := checked;
    IF (this->pvt_checked)
      this->pvt_selected := FALSE;
    this->ExtUpdatedComponent();
  }

  MACRO SetIndent(INTEGER indent)
  {
    IF (this->pvt_indent != indent)
    {
      this->pvt_indent := indent;
      this->ExtUpdatedComponent();
    }
  }

  INTEGER FUNCTION GetMenuItemPosition(OBJECT component)
  {
    FOREVERY(RECORD item FROM this->items)
      IF(item.menuitem = component)
        RETURN #item;

    RETURN -1;
  }

  UPDATE PUBLIC OBJECT ARRAY FUNCTION GetChildComponents()
  {
    RETURN SELECT AS OBJECT ARRAY menuitem FROM this->items WHERE menuitem != DEFAULT OBJECT;
  }

  /** @short Insert a new menu item before an existing menu item
    @param menuitem Menu item to insert
    @param before Menu item which this component should precede (use DEFAULT OBJECT to add the component to the beginning of the menu)
    @param divider_inbetween Insert a divider after the newly created component */
  PUBLIC MACRO InsertMenuItemBefore(OBJECT menuitem, OBJECT before, BOOLEAN divider_inbetween)
  {
    this->DoInsertMenuItem(menuitem, before, divider_inbetween, FALSE);
  }

  /** @short Insert a new menu item after an existing menu item
    @param menuitem Menu item to insert
    @param after Menu item which this component should follow (use DEFAULT OBJECT to add the component to the end of the menu)
    @param divider_inbetween Insert a divider before the newly created component */
  PUBLIC MACRO InsertMenuItemAfter(OBJECT menuitem, OBJECT after, BOOLEAN divider_inbetween)
  {
    this->DoInsertMenuItem(menuitem, after, divider_inbetween, TRUE);
  }

  MACRO DoInsertMenuItem(OBJECT component, OBJECT insertpos, BOOLEAN break_inbetween, BOOLEAN doafter)
  {
    IF(component = DEFAULT OBJECT)
      THROW NEW TolliumException(this, "You must specify a menu item to insert");
    //IF(component->pvt_parent!=DEFAULT OBJECT)
    //  THROW NEW TolliumException(this, "The menu item you are trying to insert has already been inserted elsewhere");
    IF(component->owner=DEFAULT OBJECT)
      THROW NEW TolliumException(this, "The menu item you are trying to insert has already been deleted");
    IF(component->owner!=this->owner)
      THROW NEW TolliumException(this, "The menu item you are trying to insert is not owned by the same screen as the menu receiving it");

    INTEGER objpos;
    IF(ObjectExists(insertpos))
    {
      objpos := this->GetMenuItemPosition(insertpos);
      IF(objpos < 0)
        THROW NEW TolliumException(this, "Specified menu item is not part of this menu");

      IF(doafter)
        objpos := objpos + 1;
    }
    ELSE IF(doafter)
    {
      objpos := Length(this->items);
      IF(objpos<0)
        objpos:=0;
    }

    IF(break_inbetween)
    {
      //ADDME: Check for double dividers
      // Insert a new menudivider
      INSERT [ isdivider := TRUE, menuitem := DEFAULT OBJECT ] INTO this->items AT objpos;

      IF(doafter)
        objpos := objpos + 1;
    }
    INSERT [ isdivider := FALSE, menuitem := component ] INTO this->items AT objpos;

    //component->pvt_parent := this;
    component->RecursiveUpdateIsNowVisible();

    IF(this->owner->pvt_tolliumframepreinits)
      component->EnsurePreInit();
    IF (this->owner->pvt_tolliumframeinitrun) // Only execute postinit when frame init has been run
      component->EnsurePostInit();

    this->ExtUpdatedComponent();
  }

  UPDATE PUBLIC MACRO __RemoveChildComponent(OBJECT oldcomponent)
  {
    INTEGER pos := this->GetMenuItemPosition(oldcomponent);
    IF(pos < 0)
      RETURN; //TolliumWizard sometimes removes nonadded components

    DELETE FROM this->items AT pos;
    this->ExtUpdatedComponent();
  }

  MACRO SetItems(RECORD ARRAY items)
  {
    // Make sure all subitems inserted after our pre-init are also preinited
    IF (this->pvt_havepreinited)
    {
      FOREVERY (RECORD item FROM items)
        IF (ObjectExists(item.menuitem))
          item.menuitem->EnsurePreInit();
    }
    FOREVERY (RECORD item FROM items)
      IF (ObjectExists(item.menuitem))
      {
        //item.menuitem->pvt_parent := this;
        item.menuitem->RecursiveUpdateIsNowVisible();
      }

    this->pvt_items := items;
    this->ExtUpdatedComponent();

    // Make sure all subitems inserted after our post-init are also postinited
    IF (this->pvt_havepostinited)
    {
      FOREVERY (RECORD item FROM items)
        IF (ObjectExists(item.menuitem))
          item.menuitem->EnsurePostInit();
    }
  }

  UPDATE BOOLEAN FUNCTION CalculateIsNowVisible()
  {
    RETURN ObjectExists(this->pvt_owner);
  }

  UPDATE PUBLIC MACRO TolliumWebRender()
  {
    RECORD compinfo := [ hint := this->hint
                       , icon := this->icon
                       , disablemode := this->disablemode
                       , items := DEFAULT STRING ARRAY
                       , action := GetComponentName(this->action)
                       , selected := this->selected
                       , checked := this->checked
                       , indent := this->indent
                       , title := this->renderedtitle
                       , visible := this->visible
                       , shortcut := ObjectExists(this->action) ? this->action->shortcut : ''
                       ];

    FOREVERY(RECORD item FROM this->items)
    {
      IF(item.isdivider)
        INSERT "tollium$divider" INTO compinfo.items AT END;
      ELSE //IF(item.menuitem->IsVisible())
        INSERT GetComponentName(item.menuitem) INTO compinfo.items AT END;
    }

    this->owner->tolliumcontroller->SendComponent(this, compinfo);
    TolliumComponentBase::TolliumWebRender();
  }

  UPDATE PUBLIC MACRO SetVisible(BOOLEAN newstate)
  {
    IF(this->pvt_visible = newstate)
      RETURN;

    //Specials manage visibility themselves
    this->pvt_visible := newstate;
    this->ExtUpdatedComponent();
  }

>;
