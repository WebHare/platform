<?wh

LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/mime.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

RECORD ARRAY FUNCTION GatherAttachmentsFromMailTree(RECORD currentpart, STRING parenttype)
{
  STRING mimetype := ToLowercase(GetMIMEHeaderParameter(currentpart.mimetype,""));

  //under multipart/mixed, gather everything, otherwise gather non-rendered types
  IF(mimetype LIKE "multipart/*")
  {
    RECORD ARRAY attachments;
    FOREVERY(RECORD part FROM currentpart.subparts)
      attachments := attachments CONCAT GatherAttachmentsFromMailTree(part, mimetype);
    RETURN attachments;
  }

  IF(parenttype = "multipart/mixed" OR (parenttype = "multipart/alternative" AND mimetype NOT IN ["text/plain","text/html"]))
    RETURN [ currentpart ];

  RETURN RECORD[];
}

RECORD ARRAY FUNCTION GetAttachments(RECORD bodystruct)
{
  RECORD ARRAY squashed := GatherAttachmentsFromMailTree(bodystruct, "");
  RECORD ARRAY attachments :=
         (SELECT rowkey := "attachment-" || #squashed
               , name := GetMIMEHeaderParameter(squashed.mimetype, "name")
               , type := GetMIMEHeaderParameter(mimetype, "")
               , size := Length(data)
               , data
            FROM squashed);

  RETURN attachments;
}

PUBLIC STATIC OBJECTTYPE EmailContents EXTEND TolliumFragmentBase
<
  RECORD decodedmsg;

  BLOB original;
  BLOB __primarysource;
  BOOLEAN issensitive;
  BOOLEAN showsensitive;

  //HTML/Text source of the primary shown part
  PUBLIC PROPERTY primarysource(__primarysource, -);
  PUBLIC PROPERTY origin(this->^origin->value, SetOrgin);

  MACRO NEW()
  {
  }

  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->LoadEML(DEFAULT BLOB);
  }

  /** Load a raw message in MIME format
      @param data The raw message
      @cell(boolean) options.sensitive If set, this message is for sysop's eyes only */
  PUBLIC MACRO LoadEML(BLOB data, RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    options := ValidateOptions([ sensitive := FALSE ], options);
    this->issensitive := options.sensitive;
    this->original := data;
    this->decodedmsg := Length(data) > 0 ? DecodeMIMEMessage(data) : DEFAULT RECORD;

    this->Refresh();
  }

  MACRO SetOrgin(STRING neworigin)
  {
    ^origin->value := neworigin;
    ^origin->visible := neworigin != "";
  }

  MACRO Refresh()
  {
    BOOLEAN showcontents := this->showsensitive OR NOT this->issensitive;
    ^mailfrom->value         := RecordExists(this->decodedmsg) ? this->decodedmsg.sender : "";
    ^mailto->value           := RecordExists(this->decodedmsg) ? this->decodedmsg.tos : "";
    ^mailcc->value           := RecordExists(this->decodedmsg) ? this->decodedmsg.ccs : "";
    ^mailbcc->value          := RecordExists(this->decodedmsg) ? this->decodedmsg.bccs : "";
    ^replyto->value          := RecordExists(this->decodedmsg) ? this->decodedmsg.replyto : "";
    ^mailsubject->value      := (RecordExists(this->decodedmsg) ? this->decodedmsg.subject : "");
    ^attachmentrows->rows    := RecordExists(this->decodedmsg) AND showcontents ? GetAttachments(this->decodedmsg.data) : RECORD[];
    ^sensitivecontent->visible := this->issensitive;
    ^showsensitivecontentbutton->visible := this->issensitive AND this->contexts->user->HasRight("system:sysop");
    ^showsensitivecontentbutton->enabled := ^showsensitivecontentbutton->visible AND NOT showcontents;
    ^viewsource->enabled := showcontents;

    IF(RecordExists(this->decodedmsg))
    {
      RECORD returnpath := SELECT * FROM this->decodedmsg.headers WHERE ToUppercase(field) = ToUppercase("RETURN-PATH");
      IF(RecordExists(returnpath))
        ^errorsto->value := returnpath.value;
     }

    ^errorsto->value := "";
    IF(RecordExists(this->decodedmsg))
    {
      RECORD returnpath := SELECT * FROM this->decodedmsg.headers WHERE ToUppercase(field) = ToUppercase("RETURN-PATH");
      IF(RecordExists(returnpath))
        ^errorsto->value := returnpath.value;
     }

    ^mailcc->visible         := ^mailcc->value != "";
    ^mailbcc->visible        := ^mailbcc->value != "";
    ^mailsubject->visible    := ^mailsubject->value != "";
    ^replyto->visible        := ^replyto->value != "";
    ^errorsto->visible       := ^errorsto->value != "";
    ^attachmentpart->visible := LENGTH(^attachmentrows->rows) > 0;

    this->__primarysource := DEFAULT BLOB;
    IF(showcontents AND RecordExists(this->decodedmsg))
    {
      RECORD setresult := ^preview->SetEmail(this->decodedmsg.data);
      IF(RecordExists(setresult))
        this->__primarysource := setresult.source;
    }
    ELSE
    {
      ^preview->ResetContent();
    }
  }

  MACRO DoDownloadAttachment(OBJECT obj)
  {
    RECORD attrow := ^attachmentrows->selection;
    obj->SendFile(attrow.data, attrow.type, attrow.name);
  }

  MACRO DoViewSource()
  {
    this->owner->RunScreen("mod::tollium/screens/components/emailcontents.xml#viewsource", [ message := this->original ]);
  }

  MACRO DoShowSensitiveContent()
  {
    this->showsensitive := TRUE;
    this->Refresh();
  }
>;

PUBLIC STATIC OBJECTTYPE ViewSource EXTEND TolliumScreenBase
< PUBLIC MACRO Init(RECORD data)
  {
    STRING datastr := BlobToString(data.message); //FIXME limit size?

    datastr := Substitute(datastr, "\r\n", "\n");
    datastr := Substitute(datastr, "\r", "\n");

    ^text->value := datastr;
  }
>;

