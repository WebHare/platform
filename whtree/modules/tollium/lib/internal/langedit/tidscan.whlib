<?wh

PUBLIC BOOLEAN FUNCTION IsLegalTidLocalPart(STRING tid)
{
  IF (LEFT(tid,1) = "."
      OR RIGHT(tid,1) = "."
      OR SearchSubstring (tid, "..") != -1
      OR SearchSubstring (tid, " ") != -1)
    RETURN FALSE;
  RETURN TRUE;
}

PUBLIC RECORD FUNCTION SplitModulesFromTid(RECORD ARRAY intids)
{
  RECORD ARRAY rejected;
  RECORD ARRAY tids;

  FOREVERY(RECORD tidrec FROM intids)
  {
    INTEGER tidpos := SearchLastSubstring(tidrec.tid, ':');
    IF(tidpos = 0)
      CONTINUE; //simply remove a nontranslateable tid

    IF(tidpos=-1)
    {
      IF(Left(tidrec.tid,1)='~')
        CONTINUE;
      INSERT tidrec INTO rejected AT END;
      CONTINUE;
    }

    STRING module := Left(tidrec.tid, tidpos);
    STRING truetid := Substring(tidrec.tid, Length(module)+1);
    IF(NOT IsLegalTidLocalPart(truetid))
    {
      INSERT tidrec INTO rejected AT END;
      CONTINUE;
    }

    INSERT CELL module := module INTO tidrec;
    tidrec.tid := truetid;
    INSERT tidrec INTO tids AT END;
  }
  RETURN [ tids := tids
         , rejected := rejected
         ];
}
