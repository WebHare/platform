<?wh
LOADLIB "wh::crypto.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/networking/antivirus.whlib";

BOOLEAN FUNCTION DoAntiVirusCheck(OBJECT owner, RECORD item)
{
  RECORD scanresult := RunAntivirusScan(item.data);
  IF(scanresult.errorcode = "noscannersavailable")
    RETURN TRUE; //there are no scanners configured, so no need to scan

  RECORD auditdetails := CELL[ client_filename := item.filename
                             , client_mimetype := item.mimetype
                             , filehash := ToLowercase(EncodeBase16(GetHashForBlob(item.data, "SHA-256")))
                             , scanresult.errorcode
                             , scanresult.detail
                             , scanresult.message
                             ];

  LogAuditEvent(scanresult.success ? "system:antivirusscan.ok" : "system:antivirusscan.fail", auditdetails);
  IF(scanresult.success)
    RETURN TRUE; //this file is ok

  STRING issue;
  IF(scanresult.errorcode = "suspicious")
    issue := GetTid("tollium:common.errors.upload_suspicious", scanresult.detail);
  ELSE
    issue := GetTid("tollium:common.errors.upload_cannotcheck");

  IF(owner->contexts->user->HasRight("system:supervisor"))
  {
    IF(owner->RunSimpleScreen("verify", issue || "\n\n" || GetTid("tollium:common.errors.upload_allowsuspiciousfile")) = "yes")
    {
      LogAuditEvent("system:antivirusscan.override", auditdetails);
      RETURN TRUE;
    }
  }
  ELSE
  {
    owner->RunSimpleScreen("error", issue || "\n\n" || GetTid("tollium:common.errors.upload_blockedsuspiciousfile"));
  }
  RETURN FALSE;
}

//We are only included into tollium components so it should be safe to access ->owner
PUBLIC OBJECTTYPE TolliumFileDropAccepter
<
  UPDATE RECORD FUNCTION ProcessUpload(RECORD msgdata)
  {
    RECORD ARRAY resultitems;
    FOREVERY (RECORD item FROM RECORD ARRAY(msgdata.items))
    {
      IF (item.type != "file")
      {
        INSERT item INTO resultitems AT END;
        CONTINUE;
      }

      RECORD uploadedfile := GetUploadedFile(item.token);
      RECORD uploaded := CELL[
        ...WrapBlob(uploadedfile.data, CellExists(item,'name') ? item.name : uploadedfile.filename),
        type := "file" ,
        extradata := CellExists(item, "extradata") ? item.extradata : DEFAULT RECORD,
        fullpath := CellExists(item,"fullpath") ? item.fullpath : ""
      ];

      IF(NOT DoAntiVirusCheck(this->owner, uploaded))
        RETURN CELL[...msgdata, items := RECORD[] ]; //make it look like an empty upload to properly cancel it

      INSERT uploaded INTO resultitems AT END;
    }

    msgdata.items := resultitems;
    RETURN msgdata;
  }

  RECORD FUNCTION OnRequestCanUpload(RECORD manifest)
  {
    RECORD uploadsession := CallJS("@mod-tollium/js/internal/tolliumcalls.ts#createUploadSession", manifest);
    INSERT uploadsession.session_id INTO this->contexts->controller->__uploadsessions AT END;
    RETURN uploadsession;
  }
>;
