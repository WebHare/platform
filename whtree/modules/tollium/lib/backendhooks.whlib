<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::internal/interface.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/yaml.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/resources.whlib";

LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";


RECORD FUNCTION LoadScreenDefs(STRING mod)
{
  STRING resname := `mod::${mod}/modulescreens.yml`;
  RECORD data := DecodeYAML(BlobToString(GetWebHareResource(resname)), [tosnakecase := TRUE]);
  RETURN [ value := data, ttl := 30 * 60 * 1000, eventmasks := GetResourceEventMasks([resname]) ];
}

RECORD FUNCTION GetScreenDef(STRING screenname)
{
  STRING mod := Tokenize(screenname,':')[0];
  RECORD screendefs := GetAdhocCached(CELL[mod], PTR LoadScreenDefs(mod));

  STRING name := Tokenize(screenname,':')[1];
  IF(NOT CellExists(screendefs, name))
    THROW NEW Exception(`Screen '${name}' not found in builtinscreens.yml`);

  RECORD screendef := Getcell(screendefs, name);
  IF(RecordExists(screendef))
  {
    screendef := EnforceStructure(
      [ extend_tabs := [ component := ""
                       , insert_points := STRING[]
                       , extend_components := STRING[]
                       , insert_before := ""
                       ]
      , action_categories := DEFAULT RECORD
      ], screendef);

    IF(RecordExists(screendef.action_categories))
      screendef.action_categories := RepackRecord(SELECT COLUMN name, value := EnforceStructure(CELL[ insert_into := STRING[] ], value) FROM UnpackRecord(screendef.action_categories));
  }
  RETURN screendef;
}

RECORD FUNCTION GetAllHooks() {
  RECORD ARRAY allhooks := EnforceStructure(
    [[ tabsextension := ""
     , addactions := RECORD[]
     , when := DEFAULT RECORD
     ]], __GetExtractedConfig("hooks").backendhooks);

  RETURN [ ttl := 24 * 60 * 60 * 1000
         , value := allhooks
         , eventmasks := ["platform:appliedconfig"]
         ];
}

BOOLEAN FUNCTION MatchWhen(RECORD when, OBJECT contexts)
{
  IF(CellExists(when,'wrdschema'))
  {
    IF(NOT ObjectExists(contexts->wrdschema) OR TYPEID(when.wrdschema) != TYPEID(STRING))
      RETURN FALSE;

    RETURN contexts->wrdschema->tag = when.wrdschema;
  }

  RETURN TRUE;
}

RECORD ARRAY FUNCTION GetHooksFor(OBJECT contexts, STRING screenname)
{
  RECORD ARRAY allhooks := GetAdhocCached(CELL["allhooks"], PTR GetAllHooks);
  RETURN SELECT *
           FROM allhooks
          WHERE allhooks.screen = VAR screenname
                AND MatchWhen(allhooks.when, contexts);
}

MACRO InvokeWithContext(OBJECT contexts, MACRO PTR toinvoke)
{
  toinvoke(contexts);
}

PUBLIC MACRO SetupBackendScreenHooks(OBJECT screen, RECORD options)
{
  options := ValidateOptions(
    [ screenname := ""
    ], options, [ required := ["screenname" ]]);

  //Is anyone hooking this screen definition? Otherwise we don't have to set up anything
  //TODO but we should still do some static validation of hookdefs vs screedefs as not all hooks may be actually tested by the testsuite.
  RECORD ARRAY hooks := GetHooksFor(screen->contexts, options.screenname);
  IF(Length(hooks) = 0)
    RETURN;

  STRING ARRAY errors;

  IF(NOT __INTERNAL_GetIsObjectTypeStatic(screen))
    THROW NEW Exception(`SetupBackendScreenHooks requires your screen to be a STATIC OBJECTTYPE`);

  RECORD screendef := GetScreenDef(options.screenname); //made an option, we might at some point just derive the screenname straight from builtinscreens
  IF(RecordExists(screendef.extend_tabs) AND RecordExists(SELECT FROM hooks WHERE tabsextension != ""))
  {
    OBJECT tabcomp := GetMember(screen, "^" || screendef.extend_tabs.component);
    FOREVERY(STRING insertpoint FROM screendef.extend_tabs.insert_points)
    {
      IF(NOT MemberExists(screen, "^" || insertpoint))
        THROW NEW Exception(`Insert point '${insertpoint}' not found in screen '${options.screenname}' - fix the screen or its modulescreens.yml definition`);
      INSERT [ name := insertpoint, component := GetMember(screen, "^" || insertpoint) ] INTO tabcomp->insertpoints AT END;
    }
    FOREVERY(STRING extendcomponent FROM screendef.extend_tabs.extend_components)
    {
      IF(NOT MemberExists(screen, "^" || extendcomponent))
        THROW NEW Exception(`Extend component '${extendcomponent}' not found in screen '${options.screenname}' - fix the screen or its modulescreens.yml definition`);
      INSERT [ name := extendcomponent, component := GetMember(screen, "^" || extendcomponent) ] INTO tabcomp->extendcomponents AT END;
    }

    IF(screendef.extend_tabs.insert_before != "") {
      IF(NOT MemberExists(screen, "^" || screendef.extend_tabs.insert_before))
        THROW NEW Exception(`Tab '${screendef.extend_tabs.insert_before}' not found in screen '${options.screenname}' - fix the screen or its modulescreens.yml definition`);

      INTEGER pos := SearchElement(tabcomp->pages, GetMember(screen, "^" || screendef.extend_tabs.insert_before));
      IF(pos = -1)
        THROW NEW Exception(`insertbefore tab '${screendef.extend_tabs.insert_before}' is not a page of the tab`);

      tabcomp->extendposition := pos;
    }

    FOREVERY(RECORD hook FROM hooks)
      IF(hook.tabsextension != "")
        tabcomp->LoadTabsExtension(hook.tabsextension);
  }

  IF(RecordExists(screendef.action_categories) AND RecordExists(SELECT FROM hooks WHERE Length(addactions) > 0))
  {
    FOREVERY(RECORD category FROM UnpackRecord(screendef.action_categories))
    {
      OBJECT ARRAY insertmenus;
      FOREVERY(STRING insertmenu FROM category.value.insert_into)
        INSERT GetMember(screen, "^" || insertmenu) INTO insertmenus AT END;

      FOREVERY(RECORD hook FROM hooks)
        FOREVERY(RECORD action FROM hook.addactions)
        {
          IF(action.category = ToLowercase(category.name))
          {
            OBJECT actioncomp := screen->CreateTolliumComponent("action");
            FUNCTION PTR onexecute;
            TRY
            {
              onexecute := MakeFunctionPtr(action.onexecute);
            }
            CATCH(OBJECT e)
            {
              //TODO it would be nice to signal sysops on production servers without bothering all users
              LogHarescriptException(e);
              INSERT `Unable to register hook ${hook.module}:${ToLowercase(hook.tag)}: ${e->what}` INTO errors AT END;
              CONTINUE;
            }
            actioncomp->onexecute := PTR InvokeWithContext(screen->contexts, onexecute);

            FOREVERY(OBJECT insertmenu FROM insertmenus)
            {
              //Find the menu owning the inserted items TODO tollium should just track it.  and why does it allow item multi-insertion anyway ?
              OBJECT itemcomp := screen->CreateTolliumComponent("item");
              itemcomp->title := GetTid(action.title);
              itemcomp->action := actioncomp;

              OBJECT intomenu;
              FOREVERY(OBJECT item FROM screen->tolliumscreenmanager->GetSpecialComponents())
                IF(item->componenttype = "menuitem" AND RecordExists(SELECT FROM item->items WHERE menuitem=insertmenu))
                {
                  intomenu := item;
                  BREAK;
                }

              IF(NOT ObjectExists(intomenu))
                THROW NEW TolliumException(insertmenu, `Cannot find menu owning this item`);

              intomenu->InsertMenuItemBefore(itemcomp, insertmenu, FALSE);
            }
          }
        }
    }
  }

  IF(Length(errors) > 0 AND NOT IsDtapLive())
    screen->RunSimpleScreen("error", Detokenize(errors,'\n'));
}
