<?wh

LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/webdesign.whlib";

LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::consilio/lib/api.whlib";


PUBLIC STATIC OBJECTTYPE ConsilioSearchPage EXTEND WebPageBase
<
  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody()
  {
    INSERT "wh-searchpage" INTO this->webdesign->htmlclasses AT END;

    STRING querytext := GetWebVariable("q"); //?q= is more common and assumed by google, so why not..
    RECORD wittydata := CELL[ totalcount := 0
                            , results := RECORD[]
                            , searchurl := this->absolutebaseurl
                            , querytext
                            ];
    IF(querytext != "")
    {
      STRING catalog := GetConsilioPublisherCatalog(this->webdesign->targetfolder->id);
      IF(catalog = "")
        THROW NEW Exception(`Unable to determine which consilio catalog has indexed folder '${this->webdesign->targetfolder->whfspath}'`);

      RECORD query := CQParseUserQuery(querytext);
      RECORD result := RunConsilioSearch(catalog, query, [ summary_length := 150 ]);

      wittydata.results := SELECT *, link := objectid, DELETE objectid FROM result.results;
      wittydata.totalcount := result.totalcount;
    }

    RETURN PTR EmbedWittyComponent(Resolve("searchpage.witty:searchpage"), wittydata);
  }
>;
