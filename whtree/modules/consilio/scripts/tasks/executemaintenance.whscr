<?wh
LOADLIB "wh::datetime.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/indexmanager.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/whfs/base.whlib";

//ADDME: Maintenance-taak om out-of-date links, servers en pagina's uit de database halen
//ADDME: Shouldn't this script also remove checked_pages which are no longer in the index?

OBJECT trans := OpenPrimary( [ clientname := "consilio execute maintenance" ]);

// Clean up contentsources pointing to deleted folders
RECORD ARRAY brokensources := SELECT toremove := GroupedValues(contentsources.id), contentsources.catalogid
                                FROM consilio.contentsources, system.fs_objects
                               WHERE contentsources.contentprovider = "publisher:webhare"
                                     AND contentsources.fsobject = fs_objects.id
                                     AND IsRecycleOrHistoryWHFSPath(whfspath)
                             GROUP BY catalogid;

FOREVERY(RECORD source FROM brokensources)
{
  trans->BeginWork();
  OBJECT cat := OpenConsilioCatalogById(source.catalogid);
  IF(cat->ismanaged)
  {
    FOREVERY(INTEGER toremove FROM source.toremove)
      cat->OpenContentSourceById(toremove)->DeleteSelf();
  }
  ELSE //shouldn't happen to an unmanaged catalog..
  {
    DELETE FROM consilio.contentsources WHERE id IN source.toremove;
  }
  trans->CommitWork();
}

// Delete empty catalogs which no longer have their corresponding module installed
RECORD ARRAY deletioncandidates := SELECT * FROM ListConsilioCatalogs() WHERE tag NOT LIKE "?*:?*" OR Tokenize(tag,':')[0] NOT IN GetInstalledModuleNames();
FOREVERY(RECORD candidate FROM deletioncandidates)
  IF(GetIndexManagerSize(candidate.id) = 0)
  {
    Print(`Deleting empty catalog '${candidate.tag}' - its corresponding module isn't installed either\n`);
    trans->BeginWork();
    OpenConsilioCatalogById(candidate.id)->DeleteSelf();
    trans->CommitWork();
  }

trans->BeginWork();

// Clean up unreferenced links
INTEGER daystokeep := ReadRegistryKey("consilio.linkchecker.keeplinks");
DATETIME limitdate := AddDaysToDate(0-daystokeep,GetCurrentDateTime());
// First, collect all links that are referred to, but would be deleted based on their last_referred dates
INTEGER ARRAY links_to_keep := (SELECT AS INTEGER ARRAY DISTINCT checked_links.id
                                  FROM consilio.checked_links
                                     , consilio.checked_objectlinks
                                 WHERE checked_links.id = checked_objectlinks.link
                                       AND checked_links.last_referred < limitdate);
// Then delete all outdated links which are not referred to anymore
DELETE FROM consilio.checked_links WHERE last_referred < limitdate
                                         AND id NOT IN links_to_keep;

// Clean up old searched words
daystokeep := ReadRegistryKey("consilio.options.keepsearchterms");
limitdate := AddDaysToDate(0-daystokeep,GetCurrentDateTime());
DELETE FROM consilio.query_results WHERE when < limitdate;

// Clean up publisher:webhare contentsources without an fsobject. shouldn't ever happen
DELETE from consilio.contentsources WHERE fsobject = 0 AND contentprovider = "publisher:webhare";

trans->CommitWork();

