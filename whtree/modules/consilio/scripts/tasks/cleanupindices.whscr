<?wh

LOADLIB "wh::ipc.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/catalogs.whlib";
LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/contentproviders/contentprovider.whlib";
LOADLIB "mod::consilio/lib/internal/indexmanager.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";


OBJECT trans := OpenPrimary();
INTEGER ARRAY updated_contentsources;
BOOLEAN changes;
BOOLEAN debug := "--debug" IN GetConsoleArguments();

// Delete indices from the index managers that are no longer referred to or are marked for deletion
INTEGER ARRAY indexedcatalogs, faileddeletes;
FOREVERY (RECORD indexmanager FROM SELECT id, name FROM consilio.indexmanagers)
{
  RECORD result := GetIndexmanagerIndexList(indexmanager.id);
  IF (debug)
    Print(`Checking index manager ${indexmanager.id}: ${result.status}\n`);
  IF (result.status != SearchOk)
    CONTINUE;

  FOREVERY (RECORD indexrec FROM result.indices)
  {
    // Get the list of all catalogs + catalog indices for the indexid+indexname pair
    RECORD ARRAY cats :=
        SELECT catalogs.id
             , catalogs.name
             , catalogdeleted :=        name LIKE "$consilio$deleted$*"
             , catalogindexdeleted :=   deleted
             , catalogindexid :=        catalog_indexmanagers.id
          FROM consilio.catalogs
             , consilio.catalog_indexmanagers
         WHERE catalogs.id = indexrec.indexid
           AND catalog_indexmanagers.catalogid = catalogs.id
           AND catalog_indexmanagers.indexmanager = VAR indexmanager.id
           AND (indexrec.indexname = "" ? TRUE : indexname = indexrec.indexname);

    BOOLEAN none_found := IsDefaultValue(cats);
    BOOLEAN any_active := RecordExists(SELECT FROM cats WHERE NOT catalogdeleted AND NOT catalogindexdeleted);
    INTEGER ARRAY deleted_cis := SELECT AS INTEGER ARRAY id FROM cats WHERE catalogdeleted OR catalogindexdeleted;

    IF (none_found OR IsValueSet(deleted_cis))
    {
      STRING status := SearchOk;
      IF (NOT any_active)
      {
        result := DeleteIndexManagerIndex(indexmanager.id, indexrec.indexid, indexrec.indexname);
        IF (debug)
          Print(`  Deleted index ${indexrec.indexid} (name: ${EncodeJSON(indexrec.indexname)}) from index manager ${indexmanager.name}: ${result.status}\n`);
        status := result.status;
      }
      IF (result.status IN [ SearchOk, SearchIndexNotFound ])
      {
        trans->BeginWork();
        DELETE FROM consilio.catalog_indexmanagers WHERE id IN deleted_cis;
        trans->CommitWork();
      }
      ELSE
      {
        PrintTo(2, `  Failed deleting index ${indexrec.indexid} (name: ${EncodeJSON(indexrec.indexname)}) from index manager ${indexmanager.name}: ${result.status}\n`);
        SetConsoleExitCode(1);
        INSERT indexrec.indexid INTO faileddeletes AT END;
      }
    }
    IF (any_active)
      INSERT indexrec.indexid INTO indexedcatalogs AT END;
  }
}

RECORD ARRAY catalogs := ListConsilioCatalogs([ __withdeleted := TRUE ]);
FOREVERY (RECORD cat FROM catalogs)
{
  IF (debug)
    Print(`Checking catalog '${cat.tag}' (${cat.id})\n`);
  IF (cat.isdeleted AND cat.id NOT IN indexedcatalogs AND cat.id NOT IN faileddeletes)
  {
    // This catalog didn't exist on the index manager, delete it from the database
    IF (debug)
      Print(`Delete catalog '${cat.tag}' (${cat.id}) from database\n`);
    trans->BeginWork();
    DELETE FROM consilio.catalogs WHERE id = cat.id;
    trans->CommitWork();
    DELETE FROM catalogs WHERE id = cat.id;
    CONTINUE;
  }

  // Open the catalog
  OBJECT catalog := OpenConsilioCatalogById(cat.id);
  IF(NOT ObjectExists(catalog) OR NOT catalog->ismanaged)
    CONTINUE; //already gone..

  // Check the catalog's content sources
  RECORD ARRAY sources := catalog->ListContentSources([ __withdeleted := TRUE ]);
  FOREVERY (RECORD src FROM sources)
  {
    IF (debug)
      Print(`  Checking content source '${src.tag}' (${src.id})${src.isdeleted ? " deleted" : src.isorphan ? " orphaned" : ""}\n`);
    IF (src.isdeleted)
    {
      // This content source is marked for deletion, delete it from the index manager, then from the database
      RECORD result := DeleteIndexManagerContentSource(cat.indexmanager, cat.id, src.id);
      IF (debug)
        Print(`  Deleted content source: ${result.status}\n`);
      IF (result.status = SearchOk)
      {
        trans->BeginWork();
        DELETE FROM consilio.contentsources WHERE id = src.id;
        trans->CommitWork();
      }
      CONTINUE;
    }
    IF (src.isorphan)
      CONTINUE;

    // Update content source fields and titles
    trans->BeginWork();
    OBJECT source := catalog->OpenContentSourceById(src.id);
    TRY
    {
      OBJECT provider := source->OpenProvider();
      IF (provider->UpdateContentSourceFields().anychanges)
        INSERT src.id INTO updated_contentsources AT END;

      STRING title := provider->GetContentSourceTitle();
      IF (title != src.title)
        UPDATE consilio.contentsources SET title := VAR title WHERE id = src.id;
    }
    CATCH(OBJECT e)
    {
      LogHarescriptException(e);
      //TODO what to do? log, scream, go into error mode?
    }
    trans->CommitWork();
  }
}

IF (Length(updated_contentsources) > 0) //ADDME also fire something on eg priority change ?
{
  FOREVERY (INTEGER srcid FROM updated_contentsources)
  {
    BroadcastEvent("consilio:contentsource." || srcid, DEFAULT RECORD);
    OpenContentSourceById(srcid)->ReindexContentSource([ rebuild := TRUE ]);
  }
  changes := TRUE;
}

IF (changes)
{
  BroadcastEvent("consilio:contentsourceschanged", DEFAULT RECORD);
}
