<?wh
//bootstrap the first index manager

LOADLIB "wh::os.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/indexmanager.whlib";
LOADLIB "mod::consilio/lib/internal/support.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";


OBJECT trans := OpenPrimary();

INTEGER builtin_indexmanager := GetDefaultIndexManager();
IF(builtin_indexmanager != 0)
  RETURN; //we already have one

BOOLEAN debugstartup := GetEnvironmentVariable("WEBHARE_DEBUGSTARTUP") != "";
IF(debugstartup)
  Print("There is no inndexmanager yet, need to create one\n");

trans->BeginWork();
BOOLEAN setup_elasticsearch := GetEnvironmentVariable("TESTFW_SETUP_ELASTICSEARCH") != "";
builtin_indexmanager := MakeAutonumber(consilio.indexmanagers,"id");
IF(setup_elasticsearch)
{
  IF(debugstartup)
    Print("Creating ElasticSearch\n");
  INSERT INTO consilio.indexmanagers(id, name, type, address)
       VALUES (builtin_indexmanager, "Builtin Elastic search", 2, "builtin-elasticsearch");
}
ELSE
{
  IF(debugstartup)
    Print("Creating legacy indexmanager\n");
  INSERT INTO consilio.indexmanagers(id, name, type, address, enablestemming, loglevel)
       VALUES (builtin_indexmanager, "Builtin Consilio", 1, "builtin", ReadRegistryKey("consilio.indexmanager.enablestemming"), ReadRegistryKey("consilio.indexmanager.loglevel"));
}

GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
trans->CommitWork();

IF(debugstartup)
  Print("Waiting for indexmanager to come online\n");

STRING status := WaitForPromise(WaitForIndexManager(builtin_indexmanager));
IF (status != SearchOk)
{
  Print(`Got status '${status}' while trying to connect to newly created index manager ${builtin_indexmanager}\n`);
  RETURN;
}

IF(debugstartup)
  Print("Autocreated indexmanager is online\n");

FixConsilioIndices();

IF(debugstartup)
  Print("Autocreated indexmanager is configured\n");

