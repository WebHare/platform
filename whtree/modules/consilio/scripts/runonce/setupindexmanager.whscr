<?wh
//bootstrap the first index manager

LOADLIB "wh::os.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/indexmanager.whlib";
LOADLIB "mod::consilio/lib/internal/support.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";


BOOLEAN debugstartup := GetEnvironmentVariable("WEBHARE_DEBUGSTARTUP") != "";

MACRO LogStartup(STRING text)
{
  LogDebug("consilio:setupindexmanager", text);
  IF(debugstartup)
    Print("Setupindexmanager: " || text || "\n");
}

OBJECT trans := OpenPrimary();

INTEGER builtin_indexmanager := GetDefaultIndexManager();
IF(builtin_indexmanager != 0)
  RETURN; //we already have one

LogStartup("There is no indexmanager yet, need to create one");

trans->BeginWork();
BOOLEAN setup_elasticsearch := GetEnvironmentVariable("TESTFW_SETUP_ELASTICSEARCH") != "";
builtin_indexmanager := MakeAutonumber(consilio.indexmanagers,"id");
IF(setup_elasticsearch)
{
  LogStartup("Creating ElasticSearch");
  INSERT INTO consilio.indexmanagers(id, name, type, address)
       VALUES (builtin_indexmanager, "Builtin Elastic search", 2, "builtin-elasticsearch");
}
ELSE
{
  LogStartup("Creating legacy indexmanager");
  INSERT INTO consilio.indexmanagers(id, name, type, address, enablestemming, loglevel)
       VALUES (builtin_indexmanager, "Builtin Consilio", 1, "builtin", ReadRegistryKey("consilio.indexmanager.enablestemming"), ReadRegistryKey("consilio.indexmanager.loglevel"));
}

GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
trans->CommitWork();

LogStartup("Waiting for indexmanager to come online");

STRING status := WaitForPromise(WaitForIndexManager(builtin_indexmanager));
IF (status != SearchOk)
{
  LogStartup(`Got status '${status}' while trying to connect to newly created index manager ${builtin_indexmanager}`);
  Print(`Got status '${status}' while trying to connect to newly created index manager ${builtin_indexmanager}\n`);
  RETURN;
}

LogStartup("Autocreated indexmanager is online");

FixConsilioIndices();

LogStartup("Autocreated indexmanager is configured");

