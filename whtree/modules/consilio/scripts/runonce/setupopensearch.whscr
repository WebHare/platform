<?wh
//bootstrap the first index manager, wait for it to come online and apply the first index settings

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/elasticsearch.whlib";
LOADLIB "mod::consilio/lib/internal/support.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/database.whlib";


BOOLEAN debugstartup := GetEnvironmentVariable("WEBHARE_DEBUGSTARTUP") != "";

MACRO LogStartup(STRING text)
{
  LogDebug("consilio:setupopensearch", text);
  IF(debugstartup)
    Print("system:setupopensearch: " || text || "\n");
}

OBJECT trans := OpenPrimary();

INTEGER builtin_indexmanager := SELECT AS INTEGER id FROM consilio.indexmanagers WHERE address = "builtin-elasticsearch";
IF(builtin_indexmanager != 0) //old name, we need to update it. can go away past 5.02
{
  trans->BeginWork();
  UPDATE consilio.indexmanagers SET address := "builtin-opensearch" WHERE address = "builtin-elasticsearch";
  GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
  trans->CommitWork();
}

builtin_indexmanager := SELECT AS INTEGER id FROM consilio.indexmanagers WHERE address = "builtin-opensearch";
IF(builtin_indexmanager = 0)
{
  LogStartup("There is no local OpenSearch yet, need to create one");

  trans->BeginWork();
  builtin_indexmanager := MakeAutonumber(consilio.indexmanagers,"id");
  LogStartup("Creating OpenSearch");
  INSERT INTO consilio.indexmanagers(id, name, address)
       VALUES (builtin_indexmanager, "Builtin OpenSearch", "builtin-opensearch");

  GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
  trans->CommitWork();
}

LogStartup("Waiting for OpenSearch to come online");
WHILE(TRUE)
{
  TRY
  {
    SendRawJSONToElasticsearch(builtin_indexmanager, "GET", "_cluster/health?local=true", DEFAULT RECORD);
    //Any succesful health response we'll see as "meh, usable". It's now up to callers to retry requests where needed
    BREAK;
  }
  CATCH(OBJECT<SearchUnavailableException> e)
  {
    LogStartup(`Consilio not yet available: ${e->what}`);
    Sleep(500);
  }
}

LogStartup("Autocreated OpenSearch is online");

FixConsilioIndices();

LogStartup("Autocreated OpenSearch is configured");

