<?wh
//bootstrap the first index manager

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/elasticsearch.whlib";
LOADLIB "mod::consilio/lib/internal/support.whlib";
LOADLIB "mod::consilio/lib/internal/updateindices.whlib";

LOADLIB "mod::system/lib/database.whlib";


BOOLEAN debugstartup := GetEnvironmentVariable("WEBHARE_DEBUGSTARTUP") != "";

MACRO LogStartup(STRING text)
{
  LogDebug("consilio:setupopensearch", text);
  IF(debugstartup)
    Print("system:setupopensearch: " || text || "\n");
}

OBJECT trans := OpenPrimary();

INTEGER builtin_indexmanager := SELECT AS INTEGER id FROM consilio.indexmanagers WHERE address = "builtin-elasticsearch";
IF(builtin_indexmanager != 0) //old name, we need to update it. can go away past 5.02
{
  trans->BeginWork();
  UPDATE consilio.indexmanagers SET address := "builtin-opensearch" WHERE address = "builtin-elasticsearch";
  GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
  trans->CommitWork();
}

builtin_indexmanager := SELECT AS INTEGER id FROM consilio.indexmanagers WHERE address = "builtin-opensearch";
IF(builtin_indexmanager != 0)
  RETURN; //we already have one

LogStartup("There is no local OpenSearch yet, need to create one");

trans->BeginWork();
builtin_indexmanager := MakeAutonumber(consilio.indexmanagers,"id");
LogStartup("Creating OpenSearch");
INSERT INTO consilio.indexmanagers(id, name, address)
     VALUES (builtin_indexmanager, "Builtin OpenSearch", "builtin-opensearch");

GetPrimary()->BroadcastOnCommit("system:apprunner.internal.rescan", DEFAULT RECORD);
trans->CommitWork();

LogStartup("Waiting for OpenSearch to come online");

STRING status := WaitForPromise(WaitForElasticsearch(builtin_indexmanager, MAX_DATETIME));
IF (status != SearchOk)
{
  LogStartup(`Got status '${status}' while trying to connect to newly created index manager ${builtin_indexmanager}`);
  Print(`Got status '${status}' while trying to connect to newly created index manager ${builtin_indexmanager}\n`);
  RETURN;
}

LogStartup("Autocreated OpenSearch is online");

FixConsilioIndices();

LogStartup("Autocreated OpenSearch is configured");

