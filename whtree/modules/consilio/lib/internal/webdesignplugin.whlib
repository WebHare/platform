<?wh
LOADLIB "wh::xml/xsd.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";

PUBLIC STATIC OBJECTTYPE RobotsPlugin EXTEND WebDesignPluginBase
<
  OBJECT webdesign;

  PUBLIC PROPERTY noindex(GetNoIndex, SetNoIndex);
  PUBLIC PROPERTY nofollow(GetNoFollow, SetNoFollow);
  PUBLIC PROPERTY noarchive(GetNoArchive, SetNoArchive);

  UPDATE PUBLIC RECORD FUNCTION ParseConfigurationNode(OBJECT siteprofile, OBJECT node)
  {
    RETURN [ noindex  := ParseXSBoolean(node->GetAttribute("noindex"))
           , nofollow := ParseXSBoolean(node->GetAttribute("nofollow"))
           , noarchive := ParseXSBoolean(node->GetAttribute("noarchive"))
           ];
  }

  UPDATE PUBLIC MACRO ConfigurePlugin(OBJECT webcontext, RECORD mergedconfiguration) //override this to receive configuration
  {
    this->webdesign := webcontext;

    //Only set these if they're actually disabled, gives us some protection against weird init ordering (at least prevents us from incorrectly resetting)
    IF(mergedconfiguration.noindex)
      this->noindex := mergedconfiguration.noindex;
    IF(mergedconfiguration.nofollow)
      this->nofollow := mergedconfiguration.nofollow;
    IF(mergedconfiguration.noarchive)
      this->noarchive := mergedconfiguration.noarchive;
  }

  BOOLEAN FUNCTION GetNoIndex()
  {
    RETURN this->webdesign->robotstag.noindex;
  }

  BOOLEAN FUNCTION GetNoArchive()
  {
    RETURN this->webdesign->robotstag.noarchive;
  }

  BOOLEAN FUNCTION GetNoFollow()
  {
    RETURN this->webdesign->robotstag.nofollow;
  }

  MACRO SetNoIndex(BOOLEAN noindex)
  {
    this->webdesign->robotstag.noindex := noindex;
  }

  MACRO SetNoArchive(BOOLEAN noarchive)
  {
    this->webdesign->robotstag.noarchive := noarchive;
  }

  MACRO SetNoFollow(BOOLEAN nofollow)
  {
    this->webdesign->robotstag.nofollow := nofollow;
  }
>;
