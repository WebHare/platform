<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::regex.whlib";

LOADLIB "mod::consilio/lib/api.whlib";
LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/internal/indexing.whlib";
LOADLIB "mod::consilio/lib/internal/indexmanager.whlib";

LOADLIB "mod::system/lib/logging.whlib";


INTEGER indexmanager;// := whconstant_consilio_indexmanager_opensearch; // Set to specific index manager to test (0 for default builtin Consilio)

PUBLIC OBJECTTYPE ConsilioTestExtensions
<
  PUBLIC INTEGER testcatalog_1;
  PUBLIC INTEGER testcatalog_2;


  MACRO NEW()
  {
    this->RegisterTest("consilio.prepare", PTR this->ConsilioPrepare);
  }

  MACRO ConsilioPrepare()
  {
    this->BeginWork();
    DELETE FROM consilio.indices WHERE name LIKE "webhare_testsuite:consiliotest_*";
    this->CommitWork(); //Must be two separate commits to prevent cleanup interfering with new indices....

    this->BeginWork();
    RECORD options := [ title := "test catalog", priority := 9, loglevel := 3 ];
    IF (indexmanager != 0)
      INSERT CELL indexmanager := indexmanager INTO options;
    CreateConsilioCatalog(this->GetCatalogName_1(), CELL[ ...options, fieldgroups := [ "webhare_testsuite:pagelistfields" ] ]);
    CreateConsilioCatalog(this->GetCatalogName_2(), options);
    this->testcatalog_1 := SELECT AS INTEGER id FROM consilio.indices WHERE name = this->GetCatalogName_1();
    this->testcatalog_2 := SELECT AS INTEGER id FROM consilio.indices WHERE name = this->GetCatalogName_2();
    this->CommitWork();
  }

  PUBLIC MACRO WaitQueueEmpty()
  {
    this->WaitIndexDone(this->testcatalog_1);
    this->WaitIndexDone(this->testcatalog_2);
  }

  PUBLIC MACRO WaitConsilioQueueEmpty()
  {
    this->WaitQueueEmpty();
  }

  PUBLIC MACRO RefreshIndexManager(INTEGER indexid)
  {
    INTEGER indexmgrid := SELECT AS INTEGER COLUMN indexmanager FROM consilio.indices WHERE id = indexid;
    IF (indexmgrid != 0)
      RefreshIndexManager(indexmgrid, indexid);
  }

  PUBLIC MACRO WaitContentSourceDone(INTEGER contentsourceid)
  {
    DoWaitForContentSourceDone(contentsourceid, [ verbose := TRUE ]);
    this->RefreshIndexManager(SELECT AS INTEGER COLUMN indexid FROM consilio.contentsources WHERE id = contentsourceid AND contentsources.tag NOT LIKE "$consilio$deleted$*");
  }

  PUBLIC MACRO WaitIndexDone(INTEGER indexid)
  {
    FOREVERY(RECORD contentsource FROM OpenConsilioCatalogById(indexid)->ListContentSources())
      this->WaitContentSourceDone(contentsource.id);
  }

  PUBLIC STRING FUNCTION GetCatalogName_1()
  {
    RETURN "webhare_testsuite:consiliotest_1";
  }

  PUBLIC STRING FUNCTION GetCatalogName_2()
  {
    RETURN "webhare_testsuite:consiliotest_2";
  }

  PUBLIC STRING FUNCTION GetCatalogName_3()
  {
    RETURN "webhare_testsuite:consiliotest_3";
  }

  PUBLIC STRING FUNCTION GetCustomContentName()
  {
    RETURN "webhare_teststuite:beta_customsource";
  }

  PUBLIC INTEGER FUNCTION GetCatalogIdByName(STRING catalogname)
  {
    RETURN
        SELECT AS INTEGER id
          FROM consilio.indices
         WHERE indices.name = catalogname;
  }

  PUBLIC INTEGER FUNCTION GetPublisherContentSource(STRING catalogname, INTEGER folderid)
  {
    RETURN
        SELECT AS INTEGER contentsources.id
          FROM consilio.indices
             , consilio.contentsources
         WHERE indices.name = catalogname
           AND indexid = indices.id
           AND contentprovider = "publisher:webhare"
           AND fsobject = folderid
           AND contentsources.tag NOT LIKE "$consilio$deleted$*";
  }

  PUBLIC RECORD FUNCTION GetContentSource(INTEGER sourceid)
  {
    RETURN
        SELECT *
          FROM consilio.contentsources
         WHERE id = sourceid
               AND contentsources.tag NOT LIKE "$consilio$deleted$*";
  }
>;

PUBLIC STRING ARRAY FUNCTION TestInvoke_GetPxlLogFiles(STRING id, STRING rangestart)
{
  RETURN FilterRecentHits(id, MakeDateFromText(rangestart));
}

// Borrowed from mod::system/lib/internal/cache/imgcache.whlib
STRING ARRAY FUNCTION FilterRecentHits(STRING event, DATETIME rangestart)
{
  // Regex for the requested event
  OBJECT scanregex := NEW RegEx(`[^?]*?.*\\bpe=${EncodeUrl(event)}\\b`);

  // Wait for access log to catch up
  Sleep(5000);

  OBJECT accesslog := OpenWebHareLogStream("pxl", rangestart, GetCurrentDateTime());
  STRING ARRAY lines;
  WHILE (TRUE)
  {
    RECORD row := accesslog->ReadRecord();
    IF (NOT RecordExists(row))
      BREAK;
    IF (row.method != "HEAD")
      CONTINUE;

    // Simple heuristic to avoid expensive regex calls
    IF (row.url NOT LIKE "*pe=*")
      CONTINUE;

    RECORD ARRAY scanres := scanregex->Exec(row.url);

    IF (NOT RecordExists(scanres))
      CONTINUE;
    INSERT row.url INTO lines AT END;
  }
  RETURN lines;
}
