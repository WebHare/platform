<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::consilio/lib/database.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";


/** @short Consilio debug levels
*/
PUBLIC CONSTANT INTEGER
    ConsilioDebugError := 0,   ///< Log (fatal) fetching/parsing errors
    ConsilioDebugSkipped := 1, ///< Log skipped documents/pages
    ConsilioDebugInfo := 2,    ///< Log extended debugging information
    ConsilioDebugAll := 3;     ///< Log as much as possible

/** @short Public error codes (returned by the public search functions)
*/
PUBLIC CONSTANT STRING
    SearchOk                := "ok",                  ///< Everything went fine, we have results
    SearchError             := "unknownerror",        ///< An (unknown) error has occurred
    SearchIndexNotFound     := "indexnotfound",       ///< The requested index was not found
    SearchCommandUnsupported := "commandunsupported", ///< The requested command is not supported by the index
    SearchNoAccess          := "noaccess",            ///< No access to the requested index manager or index
    SearchConnectError      := "connecterror",        ///< Could not connect to the index manager
    SearchSendError         := "senderror",           ///< Could not send search request to the index manager
    SearchHTTPError         := "httperror",           ///< HTTP error (no 200 response received)
    SearchNoTotalError      := "nototalerror",        ///< No total number of search results received
    SearchUnavailable       := "unavailable",         ///< No indices are currently available
    SearchTimeOutError      := "timeouterror",        ///< Connection to index manager timed out
    SearchInvalidArgument   := "invalidargument",     ///< Invalid argument
    SearchSleep             := "sleep",               ///< Slept trying to send to index manager
    SearchRebuildCatalog    := "rebuild";             ///< Catalog must be rebuilt after content source type update

/** IP address reported by the consilio fetcher
    @topic consilio/api
    @public
    @loadlib mod::consilio/lib/api.whlib
*/
PUBLIC CONSTANT STRING fetcher_trusted_ip := whconstant_consilio_fetcher_trusted_ip;

/// The default search window to use, before switching to retrieving all results which uses scroll contexts
PUBLIC CONSTANT INTEGER fetcher_search_window := 1000;

/// The default number of results to track
PUBLIC CONSTANT INTEGER default_track_total_hits := 10000;

/// The default number of results to return when using RunConsilioSearch
PUBLIC CONSTANT INTEGER default_consilio_search_count := 100;

PUBLIC CONSTANT INTEGER default_contentcheckinterval:= 1440;

PUBLIC CONSTANT RECORD default_opensearch_configuration :=
    [ initialmemorypool := 128
    , maximummemorypool := 350
    , warnheapusage := 90
    ];

PUBLIC CONSTANT INTEGER contentsourcestatus_disabled := -1;
PUBLIC CONSTANT INTEGER contentsourcestatus_idle := 0;
PUBLIC CONSTANT INTEGER contentsourcestatus_rebuilding := 2;
PUBLIC CONSTANT INTEGER contentsourcestatus_error := 4;

PUBLIC INTEGER FUNCTION GetConsilioURLHash(STRING url)
{
  RETURN ToInteger(SubString(EncodeBase16(GetMD5Hash(url)), 0, 7), 0, 16);
}


/** @short Clean up whitespace
    @long Replace all occurrences of multiple space, tab, newline and non-breaking space characters with a single space and
          trim whitespace at beginning and end of text.
*/
PUBLIC STRING FUNCTION CleanWhitespace(STRING text)
{
  IF (text = "")
    RETURN "";

  // Change carriage returns, newlines, tabs and non-breaking spaces to spaces
  text := Substitute(text, "\r", " ");
  text := Substitute(text, "\n", " ");
  text := Substitute(text, "\t", " ");
  text := Substitute(text, "\xC2\xA0", " ");  // C2 A0 is the UTF-8 sequence for the &#160; character (non-breaking space)

  // Remove all extra spaces
  WHILE (SearchSubString(text, "  ") >= 0)
    text := Substitute(text, "  ", " ");

  // Trim leading and trailing whitespace and return
  RETURN TrimWhitespace(text);
}

PUBLIC STRING FUNCTION DateTimeToString(DATETIME value)
{
  IF (value = DEFAULT DATETIME)
    RETURN "";

  RETURN "@" || Right("0000000" || ToString(GetDayCount(value), 16), 8) || Right("0000000" || ToString(GetMsecondCount(value), 16), 8);
}

PUBLIC DATETIME FUNCTION StringToDateTime(STRING value)
{
  IF (value NOT LIKE "@????????????????") // invalid datetime string
    RETURN DEFAULT DATETIME;

  RETURN MakeDateFromParts(ToInteger(Substring(value,1,8),0,16), ToInteger(Substring(value,9,8),0,16));
}

PUBLIC STRING FUNCTION NormalizeUrl(STRING url, STRING fromurl)
{
  // UrlEncode all '^' characters to make sure we don't have mixed '^'s and '%5E's
  STRING result := Substitute(url, "^", "%5E");
  result := ResolveToAbsoluteURL(fromurl, result);

  IF (NOT IsValidPlainHTTPURL(result))
  {
    // Not indexing non-HTTP URLs
    RETURN "";
  }
  RETURN result;
}

PUBLIC RECORD FUNCTION ParseIndexmanagerConfiguration(STRING configuration)
{
  RECORD config;
  IF(configuration != "")
    config := DecodeHSON(configuration);
  config := EnforceStructure(default_opensearch_configuration, config);

  RETURN config;
}

PUBLIC RECORD ARRAY FUNCTION GetConsilioAppRunnerConfig()
{
  OpenPrimary();
  RECORD ARRAY apps;

  RECORD builtin_opensearch := SELECT id
                                    , config := ParseIndexmanagerConfiguration(configuration)
                                 FROM consilio.indexmanagers WHERE address="builtin-opensearch";
  IF(RecordExists(builtin_opensearch))
  {
    INSERT
        [ name := "consilio:opensearch"
        , args := STRING[ GetWebhareConfiguration().installationroot || "bin/launch_opensearch.sh"
                        , ToString(builtin_opensearch.config.initialmemorypool)
                        , ToString(builtin_opensearch.config.maximummemorypool)
                        ]
        , isstandalone := TRUE
        ] INTO apps AT END;
  }
  RETURN apps;
}

PUBLIC RECORD ARRAY FUNCTION GetContentSourcesByTag(STRING contentsourcetag)
{
  IF (contentsourcetag = "")
    RETURN DEFAULT RECORD ARRAY;
  IF (contentsourcetag LIKE "#*")
    RETURN SELECT id
                , catalogid
             FROM consilio.contentsources
            WHERE id = ToInteger(Substring(VAR contentsourcetag,1),0)
                  AND contentsources.tag NOT LIKE "$consilio$deleted$*";

  RETURN SELECT id
              , catalogid
           FROM consilio.contentsources
          WHERE ToUppercase(tag) = ToUppercase(contentsourcetag);
}

PUBLIC STRING FUNCTION GenerateConsilioIndexName()
{
  RETURN "c_" || ToLowercase(EncodeBase16(DecodeUFS(GenerateUFS128BitId())));
}

PUBLIC RECORD ARRAY FUNCTION GetContentSourceMatches(INTEGER ARRAY candidateids)
{
  RETURN SELECT catalogid, fsobject, contentsourceid := id
           FROM consilio.contentsources
          WHERE fsobject IN candidateids
                AND contentprovider = VAR whconstant_consilio_contentprovider_site
                AND orphansince = DEFAULT DATETIME
                AND tag NOT LIKE "$consilio$deleted$*"
       ORDER BY SearchElement(candidateids, fsobject) DESC; //prefer closest match

}
