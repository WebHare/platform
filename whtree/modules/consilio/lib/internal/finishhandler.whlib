<?wh
LOADLIB "wh::dbase/transaction.whlib";
LOADLIB "mod::system/lib/database.whlib";

STATIC OBJECTTYPE ConsilioFinishHandler EXTEND TransactionFinishHandlerBase
<
  PUBLIC BOOLEAN mustcleanupindices;
  PUBLIC MACRO PTR ARRAY execoncommit;

  UPDATE PUBLIC MACRO OnPreCommit(OBJECT transaction)
  {
    IF(this->mustcleanupindices)
      transaction->ScheduleTask("consilio:cleanupindices", DEFAULT DATETIME); //remove stale indices ASAP
  }

  UPDATE PUBLIC MACRO OnCommit()
  {
    FOREVERY(MACRO PTR todo FROM this->execoncommit)
      todo();
  }
>;

PUBLIC OBJECT FUNCTION GetConsilioFinishHandler()
{
  OBJECT handler := GetPrimary()->GetFinishHandler("consilio:finish");
  IF (NOT ObjectExists(handler))
  {
    handler := NEW ConsilioFinishHandler;
    GetPrimary()->SetFinishHandler("consilio:finish", handler);
  }
  RETURN handler;
}

