<?wh
/** @private direct queuemgt has been deprecated, a limited set of APIs should appear in consilio/lib/api.whlib */
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::promise.whlib";

LOADLIB "mod::consilio/lib/database.whlib";
LOADLIB "mod::consilio/lib/internal/fetcher_commands.whlib";
LOADLIB "mod::consilio/lib/internal/fetcher_queue.whlib";
LOADLIB "mod::consilio/lib/internal/support.whlib" EXPORT ConsilioDebugError, ConsilioDebugSkipped, ConsilioDebugInfo, ConsilioDebugAll;

BOOLEAN FUNCTION IsValidIndexId(INTEGER indexid)
{
  RETURN indexid IN [ 0 ] CONCAT (SELECT AS INTEGER ARRAY id FROM consilio.indices WHERE name NOT LIKE "$consilio$deleted$*");
}

INTEGER FUNCTION GetIndexForContentSource(INTEGER contentsourceid)
{
  RETURN SELECT AS INTEGER indexid FROM consilio.contentsources WHERE id = contentsourceid AND tag NOT LIKE "$consilio$deleted$*";
}

/** @short Rebuild one index or all indexes
    @param indexid The index to check, or 0 for all indexes
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioIndex(INTEGER indexid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexCatalog([ rebuild := TRUE ])")
{
  IF (NOT IsValidIndexId(indexid))
    RETURN FALSE;

  LogDebug("consilio", "ContentSourceStatus", "CheckConsilioIndex", indexid);
  STRING command := "CHECKINDEX " || indexid;
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Update one index or all indexes
    @param indexid The index to check, or 0 for all indexes
*/
PUBLIC BOOLEAN FUNCTION UpdateConsilioIndex(INTEGER indexid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexCatalog()")
{
  IF (NOT IsValidIndexId(indexid))
    RETURN FALSE;

  LogDebug("consilio", "ContentSourceStatus", "UpdateConsilioIndex", indexid);
  STRING command := "UPDATEINDEX " || indexid;
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Check a content source
    @param contentsourceid The content source to check
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioContentSource(INTEGER contentsourceid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexContentSource([ rebuild := TRUE ])")
{
  INTEGER indexid := GetIndexForContentSource(contentsourceid);
  IF (indexid = 0)
    RETURN FALSE;

  LogDebug("consilio", "ContentSourceStatus", "CheckConsilioContentSource", indexid, contentsourceid);
  STRING command := "CHECKINDEX " || indexid || " " || contentsourceid;
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Check a content source
    @param contentsourcetag Tag of the content source to check
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioContentSourceByTag(STRING contentsourcetag) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexContentSource([ rebuild := TRUE ])")
{
  RECORD ARRAY sources := GetContentSourcesByTag(contentsourcetag);
  IF(Length(sources) = 0)
    THROW NEW Exception(`There are no content sources matching the tag '${contentsourcetag}'`);

  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    LogDebug("consilio", "ContentSourceStatus", "CheckConsilioContentSourceByTag", contentsourcetag, contentsource.indexid, contentsource.id);
    STRING command := "CHECKINDEX " || contentsource.indexid || " " || contentsource.id;
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

/** @short Check a content source
    @param contentsourceid The content source to update
*/
PUBLIC BOOLEAN FUNCTION UpdateConsilioContentSource(INTEGER contentsourceid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexContentSource()")
{
  INTEGER indexid := GetIndexForContentSource(contentsourceid);
  IF (indexid = 0)
    RETURN FALSE;

  LogDebug("consilio", "ContentSourceStatus", "UpdateConsilioContentSource", indexid, contentsourceid);
  STRING command := "UPDATEINDEX " || indexid || " " || contentsourceid;
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Update a content source
    @param contentsourcetag Tag of the content source to update
*/
PUBLIC BOOLEAN FUNCTION UpdateConsilioContentSourceByTag(STRING contentsourcetag) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexContentSource()")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    LogDebug("consilio", "ContentSourceStatus", "UpdateConsilioContentSourceByTag", contentsourcetag, contentsource.indexid, contentsource.id);
    STRING command := "UPDATEINDEX " || contentsource.indexid || " " || contentsource.id;
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

/** @short Add or update a group in the given content source, replacing all content
    @param contentsourceid Id of the content source which is responsible for the group
    @param groupid Id of the group to add or update
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioGroup(INTEGER contentsourceid, STRING groupid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexGroup(groupid, [ rebuild := TRUE ])")
{
  INTEGER indexid := GetIndexForContentSource(contentsourceid);
  IF (indexid = 0 AND ToInteger(groupid, -1) <= 0)
    RETURN FALSE;

  STRING command := "FASTCHECKGROUP " || indexid || " " || contentsourceid || " \"" || EncodeJava(groupid) || "\"";
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Add or update a group in the given content source
    @param contentsourcetag Tag of the content source which is responsible for the group
    @param groupid Id of the group to add or update
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioGroupByTag(STRING contentsourcetag, STRING groupid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexGroup(groupid, [ rebuild := TRUE ])")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    STRING command := "FASTCHECKGROUP " || contentsource.indexid || " " || contentsource.id || " \"" || EncodeJava(groupid) || "\"";
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

PUBLIC OBJECT FUNCTION CheckConsilioGroupSynchronously(STRING contentsourcetag, STRING groupid, RECORD options DEFAULTSTO DEFAULT RECORD) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexGroup(groupid, [ rebuild := TRUE, foreground := TRUE ])")
{
  options := ValidateOptions([ force := FALSE ], options);

  RECORD data :=
      CELL[ action := "CHECKGROUP"
          , commanddate := GetCurrentDateTime()
          , contentsourcetag
          , groupid
          ];
  RETURN CreatePromise(PTR HandleCommandSynchronously(data, #1, #2, options));
}

/** @short Add or update a group in the given content source, only replacing the modified content
    @param contentsourcetag Tag of the content source which is responsible for the group
    @param groupid Id of the group to add or update
*/
PUBLIC BOOLEAN FUNCTION UpdateConsilioGroupByTag(STRING contentsourcetag, STRING groupid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexGroup(groupid)")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    STRING command := "UPDATEGROUP " || contentsource.indexid || " " || contentsource.id || " \"" || EncodeJava(groupid) || "\"";
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

/** @short Delete the group from the given index or all indexes
    @param indexid Id of the index to delete the group from, or 0 for all indexes
    @param groupid Id of the group to delete
*/
PUBLIC BOOLEAN FUNCTION DeleteConsilioGroup(INTEGER indexid, STRING groupid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->DeleteGroup(groupid)")
{
  IF (NOT IsValidIndexId(indexid) OR groupid = "")
    RETURN FALSE;

  STRING command := "DELETEGROUP " || indexid || " 0 \"" || EncodeJava(groupid) || "\"";
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Delete the group from the index of the given content source
    @param contentsourcetag Tag of the content source which is responsible for the group
    @param groupid Id of the group to delete
*/
PUBLIC BOOLEAN FUNCTION DeleteConsilioGroupByTag(STRING contentsourcetag, STRING groupid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->DeleteGroup(groupid)")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    STRING command := "DELETEGROUP " || contentsource.indexid || " " || contentsource.id || " \"" || EncodeJava(groupid) || "\"";
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

/** @short Add or update a single object in the index
    @param contentsourceid Id of the content source which is responsible for the object
    @param groupid Id of the group which contains the object
    @param objectid Id of the object
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioObject(INTEGER contentsourceid, STRING groupid, STRING objectid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexObject(groupid, objectid)")
{
  INTEGER indexid := GetIndexForContentSource(contentsourceid);
  IF (indexid = 0 OR objectid = "")
    RETURN FALSE;

  STRING command := "CHECKOBJECT " || indexid || " " || contentsourceid || " \"" || EncodeJava(groupid) || "\" \"" || EncodeJava(objectid) || "\"";
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Add or update a single object in the index
    @param contentsourcetag Tag of the content source which is responsible for the object
    @param groupid Id of the group which contains the object
    @param objectid Id of the object
*/
PUBLIC BOOLEAN FUNCTION CheckConsilioObjectByTag(STRING contentsourcetag, STRING groupid, STRING objectid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->ReindexObject(groupid, objectid)")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    STRING command := "CHECKOBJECT " || contentsource.indexid || " " || contentsource.id || " \"" || EncodeJava(groupid) || "\" \"" || EncodeJava(objectid) || "\"";
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

/** @short Delete the object from the given index or all indexes
    @param indexid Id of the index to delete the object from, or 0 for all indexes
    @param groupid Id of the object to delete
*/
PUBLIC BOOLEAN FUNCTION DeleteConsilioObject(INTEGER indexid, STRING objectid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->DeleteObject(objectid)")
{
  IF (NOT IsValidIndexId(indexid) OR objectid = "")
    RETURN FALSE;

  STRING command := "DELETEOBJECT " || indexid || " \"" || EncodeJava(objectid) || "\"";
  RETURN RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command)));
}

/** @short Delete the object from the index of the given content source
    @param contentsourcetag Tag of the content source which is responsible for the object
    @param groupid Id of the object to delete
*/
PUBLIC BOOLEAN FUNCTION DeleteConsilioObjectByTag(STRING contentsourcetag, STRING objectid) __ATTRIBUTES__(DEPRECATED "Direct queuemgmt has been deprecated, switch to the consilio API and use ->DeleteObject(objectid)")
{
  FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
  {
    STRING command := "DELETEOBJECT " || contentsource.indexid || " \"" || EncodeJava(objectid) || "\"";
    IF (NOT RecordExists(SendQueueManagerData(GetQueueDataFromCommand(command))))
      RETURN FALSE;
  }
  RETURN TRUE;
}

MACRO HandleCommandSynchronously(RECORD basedata, FUNCTION PTR resolve, FUNCTION PTR reject, RECORD options)
{
  TRY
  {
    STRING contentsourcetag := basedata.contentsourcetag;
    DELETE CELL contentsourcetag FROM basedata;
    FOREVERY (RECORD contentsource FROM GetContentSourcesByTag(contentsourcetag))
    {
      RECORD data :=
          [ ...basedata
          , indexid := contentsource.indexid
          , contentsourceid := contentsource.id
          ];

      SWITCH (data.action)
      {
        CASE "CHECKGROUP", "UPDATEGROUP"
        {
            RECORD result := CheckGroup(data, [ rebuild := data.action != "UPDATEGROUP"
                                              , refresh := TRUE
                                              ]);
            IF (Length(result.errors) > 0)
              THROW NEW Exception(result.errors[0].message);
        }
        CASE "DELETEGROUP"
        {
          RECORD result := DeleteGroup(data, [ refresh := TRUE ]);
          IF (RecordExists(result.error))
            THROW NEW Exception(result.error.message);
        }
      }
    }
    resolve(DEFAULT RECORD);
  }
  CATCH (OBJECT e)
  {
    reject(e);
  }
}
