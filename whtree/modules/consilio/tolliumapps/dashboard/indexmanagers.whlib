<?wh

LOADLIB "wh::ipc.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/opensearch.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";

PUBLIC RECORD ARRAY FUNCTION GetIndexManagerStatus(INTEGER indexid)
{
  IF(indexid = 0)
    RETURN RECORD[];

  OpenPrimary();
  RETURN SELECT TEMPORARY toks := Tokenize(indexname, "-")
              , rowkey := uuid
              , docs
              , size
              , shards
              , indexname := toks[0]
              , name := indexname
              , health
           FROM ListOpenSearchIndices(indexid);
}


PUBLIC STATIC OBJECTTYPE IndexMgrs EXTEND DashboardPanelBase
<
  INTEGER freshcallback;
  OBJECT statuspromise;

  MACRO Init(RECORD data)
  {
    this->frame->flags.issysop := this->contexts->user->HasRight("system:sysop");
    this->RefreshIndexManagers();
    ^indexmanager->onchange := PTR this->PollIndexmanagerStatus; //not running initally, we'll wait for RefreshDashboardPanel to come in
  }
  MACRO RefreshIndexManagers()
  {
    ^indexmanager->options := SELECT rowkey := id, title := name, address
                                FROM consilio.indexmanagers
                            ORDER BY ToUppercase(name);
  }
  UPDATE PUBLIC MACRO RefreshDashboardPanel() //called whenever a panel should refresh itself (either manually invoked by user, or automatically)
  {
    this->RefreshIndexManagers();
    this->PollIndexmanagerStatus();
  }
  MACRO DoManageMgrs()
  {
    this->RunScreen(Resolve("managemanagers.xml"));
    this->RefreshIndexManagers();
  }
  MACRO OnIndexManagerChange()
  {
    this->PollIndexmanagerStatus();
  }
  ASYNC MACRO PollIndexmanagerStatus()
  {
    IF(ObjectExists(this->statuspromise))
    {
      this->statuspromise->Cancel();
      this->statuspromise := DEFAULT OBJECT;
    }

    ^indices->empty := this->GetTid(".waitingforstatus");
    this->statuspromise := AsyncCallFunctionFromJob(Resolve("#GetIndexManagerStatus"), ^indexmanager->value);

    TRY
    {
      RECORD ARRAY result := AWAIT this->statuspromise;
      RECORD ARRAY catalogs :=
          SELECT catalog_indexmanagers.indexname
               , catalog := catalogs.name
            FROM consilio.catalogs
               , consilio.catalog_indexmanagers
           WHERE catalog_indexmanagers.catalogid = catalogs.id;
      result := JoinArrays(result, "indexname", catalogs, [ catalog := "" ], [ rightouterjoin := TRUE ]);

      ^indices->rows :=
          SELECT *
               , catalog_sort := (catalog = "" ? "1" : "0") || catalog // sort connected indices first
               , rawhealth := health
               , rawdocs := docs
               , rawsize := size
               , health := this->GetHealthIcon(health, ^indices)
               , docs := this->contexts->user->FormatMoney(docs, 0, TRUE)
               , size := this->contexts->user->FormatFileSize(size, 0, TRUE)
               , size_sort := Right("000000000000000000" || size, 19)
               , listrowclasses := catalog = "" ? [ "grayedout" ] : STRING[]
            FROM result;
      ^indices->empty := this->GetTid(".noindices");
      this->UpdateFooterRows();
    }
    CATCH(OBJECT<OPERATIONCANCELLEDEXCEPTION> e)
    {
      RETURN;
    }
    CATCH(OBJECT e)
    {
      ^indices->rows := RECORD[];
      ^indices->empty := e->what;
    }
  }

  MACRO DoGetMapping()
  {
    RECORD res := SendRawJSONToOpenSearch(^indexmanager->value, "GET", `/${EncodeURL(^indices->selection[0].name)}/_mapping`, DEFAULT RECORD);
    Reflect(GetCell(res.result, ^indices->selection[0].name).mappings);
  }
  MACRO DoDelete()
  {
    IF(this->RunSimpleScreen("confirm", this->GetTid(".confirmdeleteindices")) != "yes")
      RETURN;

    //ADDME combine into one command? possible with commas..
    FOREVERY(RECORD tokill FROM ^indices->selection)
      SendRawJSONToOpenSearch(^indexmanager->value, "DELETE", `/${EncodeURL(tokill.name)}/`, DEFAULT RECORD);

    this->PollIndexmanagerStatus();
  }
  INTEGER FUNCTION GetHealthIcon(STRING health, OBJECT list)
  {
    SWITCH (health)
    {
      CASE "green"  { RETURN list->GetIcon("tollium:status/positive"); }
      CASE "yellow" { RETURN list->GetIcon("tollium:status/neutral"); }
      CASE "red"    { RETURN list->GetIcon("tollium:status/error"); }
    }
    RETURN 0;
  }
  MACRO UpdateFooterRows()
  {
    RECORD ARRAY relevantrows := ^indices->selection ?? ^indices->rows;
    // The cluster status is controlled by the worst index status
    STRING clusterhealth := SELECT AS STRING rawhealth FROM relevantrows ORDER BY SearchElement([ "red", "yellow", "green" ], rawhealth);
    ^indices->footerrows :=
        SELECT TEMPORARY totalsize := Sum(rawsize)
             , docs := this->contexts->user->FormatMoney(Sum(rawdocs), 0, TRUE)
             , health := this->GetHealthIcon(clusterhealth, ^indices)
             , name := this->GetTid(".indicescount", ToString(Count(*)))
             , catalog := this->GetTid(".unconnectedcount", ToString(Length(SELECT FROM VAR relevantrows WHERE catalog = "")))
             , shards := Sum(shards)
             , size := this->contexts->user->FormatFileSize(totalsize, 0, TRUE)
          FROM relevantrows
         LIMIT 1;
  }
>;
