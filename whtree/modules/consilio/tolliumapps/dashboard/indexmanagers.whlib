<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::consilio/lib/internal/dbschema.whlib";
LOADLIB "mod::consilio/lib/internal/elasticsearch.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";

PUBLIC RECORD ARRAY FUNCTION GetIndexManagerStatus(INTEGER indexid)
{
  IF(indexid = 0)
    RETURN RECORD[];

  OpenPrimary();
  RETURN SELECT rowkey := uuid
              , docs
              , size
              , shards
              , name := indexname
              , health
           FROM ListOpenSearchIndices(indexid);
}


PUBLIC STATIC OBJECTTYPE IndexMgrs EXTEND DashboardPanelBase
<
  INTEGER freshcallback;
  OBJECT statuspromise;

  MACRO Init(RECORD data)
  {
    this->frame->flags.issysop := this->contexts->user->HasRight("system:sysop");
    ^indexmanager->options := SELECT rowkey := id, title := name, address
                                FROM consilio.indexmanagers
                            ORDER BY ToUppercase(name);
    ^indexmanager->onchange := PTR this->PollIndexmanagerStatus; //not running initally, we'll wait for RefreshDashboardPanel to come in
  }
  UPDATE PUBLIC MACRO RefreshDashboardPanel() //called whenever a panel should refresh itself (either manually invoked by user, or automatically)
  {
    this->PollIndexmanagerStatus();
  }
  MACRO DoManageMgrs()
  {
    this->RunScreen(Resolve("managemanagers.xml"));
  }
  MACRO OnIndexManagerChange()
  {
    this->PollIndexmanagerStatus();
  }
  ASYNC MACRO PollIndexmanagerStatus()
  {
    IF(ObjectExists(this->statuspromise))
    {
      this->statuspromise->Cancel();
      this->statuspromise := DEFAULT OBJECT;
    }

    ^indices->empty := this->GetTid(".waitingforstatus");
    this->statuspromise := AsyncCallFunctionFromJob(Resolve("#GetIndexManagerStatus"), ^indexmanager->value);

    TRY
    {
      RECORD ARRAY result := AWAIT this->statuspromise;
      ^indices->rows := SELECT *
                             , health := this->GetHealthIcon(health, ^indices)
                             , size := this->contexts->user->FormatFileSize(size, 0, TRUE)
                             , size_sort := Right("000000000000000000" || size, 19)
                          FROM result;
      ^indices->empty := this->GetTid(".noindices");
    }
    CATCH(OBJECT<OPERATIONCANCELLEDEXCEPTION> e)
    {
      RETURN;
    }
    CATCH(OBJECT e)
    {
      ^indices->rows := RECORD[];
      ^indices->empty := e->what;
    }
  }

  MACRO DoGetMapping()
  {
    RECORD res := SendRawJSONToElasticsearch(^indexmanager->value, "GET", `/${EncodeURL(^indices->selection[0].name)}/_mapping`, DEFAULT RECORD);
    Reflect(GetCell(res.result, ^indices->selection[0].name).mappings);
  }
  MACRO DoDelete()
  {
    IF(this->RunSimpleScreen("confirm", this->GetTid(".confirmdeleteindices")) != "yes")
      RETURN;

    //ADDME combine into one command? possible with commas..
    FOREVERY(RECORD tokill FROM ^indices->selection)
      SendRawJSONToElasticsearch(^indexmanager->value, "DELETE", `/${EncodeURL(tokill.name)}/`, DEFAULT RECORD);

    this->PollIndexmanagerStatus();
  }
  INTEGER FUNCTION GetHealthIcon(STRING health, OBJECT list)
  {
    SWITCH (health)
    {
      CASE "green"  { RETURN list->GetIcon("tollium:status/positive"); }
      CASE "yellow" { RETURN list->GetIcon("tollium:status/neutral"); }
      CASE "red"    { RETURN list->GetIcon("tollium:status/error"); }
    }
    RETURN 0;
  }
>;
