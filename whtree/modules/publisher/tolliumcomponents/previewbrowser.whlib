<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/database.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/internal/tollium-helpers.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/structuredef.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/preview.whlib";

LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";


BOOLEAN FUNCTION IsPreviewableWidget(INTEGER widgetid)
{
  //Only preview widgets that have a RTDTYPE
  RECORD settings := GetRTDSettingsForFile(widgetid);
  RETURN settings.namespace != "";
}


PUBLIC STATIC OBJECTTYPE PreviewBrowser EXTEND TolliumFragmentBase <
  // ---------------------------------------------------------------------------
  //
  // Variables
  //

  STRING lastsethtmlvalue;
  ///'official' target we preview, eg where we take apply rules from (targetobject)
  INTEGER previewfileid;
  ///Actual URL we want to show
  STRING currenturl;
  BOOLEAN currentsandbox;
  ///Suppress page rendering?
  STRING suppressrendering;

  INTEGER devicewidth;
  INTEGER deviceheight;

  RECORD ARRAY previewmessages;

  BOOLEAN pvt_urlbarvisible;

  // ---------------------------------------------------------------------------
  //
  // Public variables and properties
  //

  PUBLIC MACRO PTR onpreviewedobjectchange;

  PUBLIC PROPERTY urlbarvisible(pvt_urlbarvisible, Seturlbarvisible);

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO NEW() {
    this->pvt_urlbarvisible := TRUE;
  }

  UPDATE PUBLIC MACRO StaticInit(RECORD data)
  {
    TolliumFragmentBase::StaticInit(data);
    this->onpreviewedobjectchange := data.onpreviewedobjectchange;
    this->urlbarvisible := data.urlbarvisible;
  }

  UPDATE MACRO PostInitComponent()
  {
    TolliumFragmentBase::PostInitComponent();

    RECORD ARRAY devices;
    devices := [ [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.phone")
                 // iPhone 4
                 , width := 320, height := 480, pixelratio := 2
                 ]
               , [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.phone")
                 // iPhone 6(s)+
                 , width := 414, height := 736, pixelratio := 3
                 ]
               , [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.tablet")
                 // iPad landscape
                 , width := 1024, height := 768, pixelratio := 2
                 ]
               , [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.tablet")
                 // iPad portrait
                 , width := 768, height := 1024, pixelratio := 2
                 ]
               , [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.desktop")
                 // Desktop 4:3
                 , width := 1280, height := 960, pixelratio := 1
                 ]
               , [ title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.desktop")
                 // Desktop HD
                 , width := 1920, height := 1080, pixelratio := 1
                 ]
               ];


    devices := SELECT *
                    , rowkey := "d-" || width || "x" || height
                    , title := title || " (" || width || "x" || height || ")"
                 FROM devices;

    INSERT [ rowkey := "f"
           , title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.fill")
           ] INTO devices AT 0;
    INSERT [ isdivider := TRUE ] INTO devices AT 1;
    INSERT [ rowkey := "c"
           , title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.customsize") || "â€¦"
           ] INTO devices AT END;

    STRING selected := this->tolliumuser->GetRegistryKey("publisher.previewbrowser.device", "f");
    IF (selected LIKE "c-*")
      INSERT [ rowkey := selected
             , title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.customsize") || " (" || Tokenize(selected, "-")[1] || ")"
             ] INTO devices AT END;

    ^deviceselect->options := devices;
    ^deviceselect->value := selected;

    ^previewmessage->linkactions :=
        [ [ url := "preview:showsite"
          , action := ^viewsite
          ]
        , [ url := "preview:showexternallink"
          , action := ^showexternallink
          ]
        ];
  }

  // ---------------------------------------------------------------------------
  //
  // Getters & setters
  //

  MACRO Seturlbarvisible(BOOLEAN newurlbarvisible) {
    this->pvt_urlbarvisible := newurlbarvisible;
    ^urlpanel->visible := newurlbarvisible;
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  RECORD FUNCTION GetPreviewData(RECORD selectedfile) {
    //Currently the best way to test/verify this function may be to run mod::webhare_testsuite/tests/publisher/tollium/filemanager.whscr
    //and head over to https://my.webhare.dev/?app=publisher(/webhare-tests/webhare_testsuite.testsite/TestPages/

    //Determine the type we're dealing with
    BOOLEAN isfilelink := selectedfile.type IN [ whconstant_whfstype_internallink, whconstant_whfstype_contentlink ];
    RECORD targetfile := isfilelink ? RECORD(SELECT id, name, type, published, filelink, parentsite FROM system.fs_objects WHERE id = selectedfile.filelink AND isactive) ?? selectedfile : selectedfile;
    RECORD typeinfo := DescribeContentTypeById(targetfile.type, [ isfolder := FALSE, mockifmissing := TRUE ]);
    BOOLEAN ishtml := typeinfo.needstemplate OR (targetfile.type = whconstant_whfstype_imagefile AND ToUppercase(GetExtensionFromPath(selectedfile.name)) IN [".HTM", ".HTML", ".SHTML"]);

    //Figure out how to show the file
    STRING url;
    BOOLEAN sandbox;
    STRING suppresswhy;
    RECORD ARRAY messages;

    IF(selectedfile.link != "") { //We have potentially something to show
      IF(ishtml) { // and it's a webpage
        url := selectedfile.link;  //we show it!
      }
    }

    IF(url = "" AND suppresswhy = "") { //no renderable URL found yet
      //TODO whconstant_whfstype_dynamicfoldercontents is an assumption though. we *assume* it's safe and it generally will be, but it's a SHTML so it can do anything. Either way we can't use preview link as it needs direct execution
      IF(typeinfo.id = whconstant_whfstype_dynamicfoldercontents)
        url := SELECT AS STRING link FROM system.fs_objects WHERE id = selectedfile.id;
      ELSE IF(typeinfo.browserpreview != "" OR typeinfo.iswidgettype OR typeinfo.needstemplate OR typeinfo.id = whconstant_whfstype_dynamicfoldercontents) {
        url := GetPreviewLink(selectedfile.id);
      }
    }

    RECORD checkerrorsfor := selectedfile; //for which file to report any errors? internallinks change this

    //Calculate any warnings/messages to give
    IF(isfilelink) {
      RECORD target := SELECT id, name, type, published, parentsite, indexurl, fullpath, isfolder FROM system.fs_objects WHERe id = selectedfile.filelink AND isactive;
      IF(RecordExists(target)) {
        checkerrorsfor := target;
        RECORD site := SELECT name FROM system.sites WHERE id = target.parentsite;
        STRING message := selectedfile.type = whconstant_whfstype_internallink
                             ? (target.indexurl = "" ? GetHTMLTid("publisher:tolliumcomponents.previewbrowser.internallink-unpublished", (RecordExists(site) ? site.name || ":" : "") || target.fullpath)
                                                     : GetHTMLTid("publisher:tolliumcomponents.previewbrowser.internallink", (RecordExists(site) ? site.name || ":" : "") || target.fullpath))
                             : GetHTMLTid("publisher:tolliumcomponents.previewbrowser.contentlink", (RecordExists(site) ? site.name || ":" : "") || target.fullpath);
        INSERT CELL[ message ] INTO messages AT END;
      } ELSE {
        INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.internallink-broken")
               ] INTO messages AT END;
        suppresswhy := "broken-internallink";
      }
    } ELSE IF(selectedfile.type = whconstant_whfstype_externallink) {
      // Show external link target
      INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.externallink", EncodeHTML(selectedfile.externallink)) || ' <a href="preview:showexternallink">' || GetHTMLTid("publisher:tolliumcomponents.previewbrowser.clickexternallink") || '</a>'
             ] INTO messages AT END;
      suppresswhy := "is-externallink"; // Probably safest to not show externallinks within WebHare
    }

    // Informing about publication status
    IF(typeinfo.iswidgettype) { //Inform users this is a widget preview. Not surse about the added value?
      INSERT CELL[ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.widgetpreview")
                 ] INTO messages AT END;
    } ELSE IF (GetErrorFromPublished(selectedfile.published) = 0 AND NOT GetOncePublishedFromPublished(selectedfile.published)) {
      IF(typeinfo.ispublishable) {
        INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.notpublished")
               ] INTO messages AT END;
        url := GetPreviewLink(selectedfile.id);
      }
    }

    //Check for errors or warnings
    IF (GetErrorFromPublished(checkerrorsfor.published) NOT IN [ 0, 112 ] AND NOT IsQueuedForPublication(checkerrorsfor.published)) { // Ignore 'locked site' errors
      // Publication errors
      INSERT CELL[ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.errors") || ' <a href="preview:showerrors">' || GetHTMLTid("publisher:tolliumcomponents.previewbrowser.clickerrors") || '</a>'
                 ] INTO messages AT END;

      IF (GetOncePublishedFromPublished(checkerrorsfor.published)) { // We have a cached version
        INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.internallink-cachedversion") //Note the actual text doesn't refer to internallinks
               ] INTO messages AT END;
      }
    } ELSE IF(TestFlagFromPublished(checkerrorsfor.published, PublishedFlag_Warning)) {
      // Publication warnings
      INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.warnings") || ' <a href="preview:showwarnings">' || GetHTMLTid("publisher:tolliumcomponents.previewbrowser.clickwarnings") || '</a>'
             ] INTO messages AT END;
    }

    IF (GetScheduledFromPublished(selectedfile.published)) {
      // There are tasks defined for the selected file, display the first one
      RECORD task := SELECT * FROM publisher.schedule WHERE file = selectedfile.id ORDER BY when;
      IF (RecordExists(task)) {
        SWITCH (task.event) {
          CASE whconstant_publisherschedule_publish {
            INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.scheduledpublish", this->owner->tolliumuser->FormatDateTime(task.when, "minutes", TRUE, TRUE))
                   ] INTO messages AT END;
          }
          CASE whconstant_publisherschedule_unpublish {
            INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.scheduledunpublish", this->owner->tolliumuser->FormatDateTime(task.when, "minutes", TRUE, TRUE))
                   ] INTO messages AT END;
          }
          CASE whconstant_publisherschedule_move {
            INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.scheduledmove", this->owner->tolliumuser->FormatDateTime(task.when, "minutes", TRUE, TRUE))
                   ] INTO messages AT END;
          }
          CASE whconstant_publisherschedule_delete{
            INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.scheduleddelete", this->owner->tolliumuser->FormatDateTime(task.when, "minutes", TRUE, TRUE))
                   ] INTO messages AT END;
          }
        }
      } ELSE {
        INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.scheduled")
               ] INTO messages AT END;
      }
    }

    IF(url = "" AND suppresswhy = "") {
      INSERT [ message := GetTid("publisher:tolliumcomponents.previewbrowser.hasnopreview")
              ] INTO messages AT END;
      suppresswhy := "has-no-preview"; // Nothing to show
    }

    //PDF sandboxing is broken in chrome, https://bugs.chromium.org/p/chromium/issues/detail?id=413851 - so drop sandbox for PDFs
    BOOLEAN disable_sandbox := targetfile.type = whconstant_whfstype_pdffile AND ToUppercase(GetExtensionFromPath(selectedfile.name)) = ".PDF";

    RETURN CELL[ url, suppresswhy, messages, sandbox := NOT disable_sandbox ];
  }

  MACRO ShowErrors()
  {
    IF(this->previewfileid != 0)
    {
      INTEGER fileid := SELECT AS INTEGER filelink FROM system.fs_objects WHERE id = this->previewfileid AND type = 19;
      IF (fileid = 0)
        fileid := this->previewfileid;
      OBJECT faultinfo := this->owner->LoadScreen("publisher:filemgrdialogs.failedpublish", [ id := fileid ]);
      faultinfo->RunModal();
    }
  }

  // Refresh browser frame contents
  MACRO RefreshBrowserFrame() {
    this->lastsethtmlvalue := Detokenize((SELECT AS STRING ARRAY message FROM this->previewmessages), '<br />');
    ^previewmessage->htmlvalue := this->lastsethtmlvalue;
    ^info->visible := Length(this->previewmessages) > 0;

    STRING src := this->suppressrendering != "" ? ResolveToAbsoluteURL(this->contexts->controller->baseurl, "/.wh/common/preview/nopreview.html")
                : UpdateURLVariables(this->currenturl,
                      [ "__whpub_clock_" := FormatISO8601DateTime(GetCurrentDateTime(), "", "milliseconds", "", FALSE)
                      ]);

    ^browserframe->src := src;
    ^browserframe->sandbox := this->currentsandbox ? "allow-forms allow-popups allow-popups-to-escape-sandbox allow-same-origin allow-scripts" : "none";
  }

  MACRO ReloadCurrent(BOOLEAN reloadmessages, BOOLEAN reloadsource)
  {
    IF(this->previewfileid = 0 AND this->currenturl != "" )
    {
      this->RefreshBrowserFrame();
    }
    ELSE
    {
      this->SetFileId(this->previewfileid, reloadmessages OR reloadsource);
    }
  }

  // Set the currently previewed file
  MACRO SetFileId(INTEGER fileid, BOOLEAN force) {
    IF (fileid = this->previewfileid AND NOT force)
      RETURN; //did not detect a relevant change

    RECORD baseinfo; //previewfile is the file we'll be viewing (baseinfo unless baseinfo is an index document)
    RECORD previewfile; //baseinfo is the selected file
    IF(fileid > 0)
      baseinfo := SELECT id, parent, name, link, type, published, filelink, externallink, parentsite, isfolder, objecturl, indexdoc
                    FROM system.fs_objects
                   WHERE id = fileid AND parentsite != 0; //if not in a site we have no idea what <webdesign> to use..

    //previewurl is the url shown to the user in the previewbrowsers's navigation bar and is available for sharing/copying
    IF(RecordExists(baseinfo)) {
      ^previewurl->value := baseinfo.objecturl;

      IF(baseinfo.isfolder) {
        IF(baseinfo.indexdoc != 0) {
          previewfile := (SELECT id, parent, name, link, type, published, filelink, externallink, parentsite, isfolder, objecturl FROM system.fs_objects WHERE id = baseinfo.indexdoc AND isactive);
        }
      } ELSE { //file
        previewfile := baseinfo;
      }
    }

    ^previewurl->value := RecordExists(baseinfo) ? baseinfo.objecturl : "";

    RECORD previewdata;
    IF(RecordExists(previewfile)) {
      previewdata := this->GetPreviewData(previewfile); //also sets suppressrendering
      this->previewfileid := previewfile.id;
      ^previewlistener->masks := [ "system:whfs.folder." || previewfile.parent, "publisher:publish.folder." || previewfile.parent ];
    } ELSE IF (RecordExists(baseinfo)) { //no indexdocument but a live folder
      STRING message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.folderwithoutindex", baseinfo.name);
      previewdata := [ sandbox := TRUE, url := "", suppresswhy := "folder-without-index", messages := [[ message := message ]]
                     ];
      ^previewlistener->masks := STRING[];
    } ELSE { //we had nothing to show you
      previewdata := [ sandbox := TRUE, url := "", suppresswhy := "nothing-selected", messages := RECORD[]
                     ];
    }

    this->UpdateBrowser(previewdata);
  }

  MACRO UpdateBrowser(RECORD previewdata) {

    IF(previewdata.url != "" AND previewdata.url NOT LIKE "https://*" AND this->owner->tolliumcontroller->secure) {
      INSERT [ message := GetHTMLTid("publisher:tolliumcomponents.previewbrowser.nonsecuresite") || ' <a href="preview:showsite">' || GetHTMLTid("publisher:tolliumcomponents.previewbrowser.clickviewsite") || '</a>'
             ] INTO previewdata.messages AT 0;
      previewdata.suppresswhy := "is-http"; // Cannot show unsecure content within secure WebHare, show default page
    }

    this->previewmessages := previewdata.messages;
    this->currenturl := previewdata.url;
    this->currentsandbox := previewdata.sandbox;
    this->suppressrendering := previewdata.suppresswhy;

    this->RefreshBrowserFrame();
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnbrowserMessage(RECORD data, STRING origin)
  {
    data := EnforceStructure([ type := "", location := ""], data);
    IF(LookupPublisherURL(origin).webserver = 0) //not hosted here
    {
      //TODO consider telling the server to block this origin from communicating any further messages? a hostile site could trigger a lot of traffic - but that might be better handled at iframe.whlib level
      Print(`Ignoring postmessage from unknown origin '${origin}'`);
      RETURN; //ignore
    }

    IF(data.type = "webhare-navigation")
    {
      Print(`Got webhare-location with url ${data.location}\n`);
      //^browserframe->src := data.location; //TODO why update location? this would only trigger navigational resets!
      IF(this->onpreviewedobjectchange != DEFAULT MACRO PTR)
      {
        STRING url := Tokenize(Tokenize(data.location, "#")[0], "?")[0];

        //TODO not sure why we need to update currenturl ... why should we worry about that state which we can't 100% guaranteed follow anyway?
        this->currenturl := url;

        RECORD findit := LookupPublisherURL(url);
        this->onpreviewedobjectchange(findit.file ?? findit.folder, url);
      }
    }
  }
  MACRO OnBrowserFrameCallback(RECORD data)
  {
    SWITCH (data.type)
    {
      CASE "navigationprevented"
      {
        IF (data.uri = "")
          RETURN;

        this->owner->RunSimpleScreen("info", GetTid("publisher:tolliumcomponents.previewbrowser.cannotshowinsecurecontent", data.uri));
      }
    }
  }

  MACRO OnDeviceChange()
  {
    STRING selected := ^deviceselect->value;
    IF (selected LIKE "d-*")
    {
      RECORD selection := ^deviceselect->selection;
      DELETE FROM ^deviceselect->options WHERE rowkey LIKE "c-*";

      this->devicewidth := selection.width;
      this->deviceheight := selection.height;
      ^browserframe->SetViewport(this->devicewidth, this->deviceheight);
    }
    ELSE IF (selected LIKE "c*")
    {
      IF (selected LIKE "c-*")
      {
        RECORD selection := ^deviceselect->selection;
        this->devicewidth := selection.width;
        this->deviceheight := selection.height;
      }
      ELSE
      {
        DELETE FROM ^deviceselect->options WHERE rowkey LIKE "c-*";

        OBJECT dlg := this->owner->LoadScreen(Resolve("previewbrowser.xml#previewbrowsercustom"));
        dlg->width->value := this->devicewidth;
        dlg->height->value := this->deviceheight;
        IF (dlg->RunModal() = "ok" AND dlg->width->value > 0 AND dlg->height->value > 0)
        {
          this->devicewidth := dlg->width->value;
          this->deviceheight := dlg->height->value;
          selected := "c-" || this->devicewidth || "x" || this->deviceheight;
          INSERT [ rowkey := selected
                 , title := GetTid("publisher:tolliumcomponents.previewbrowser.devices.customsize") || " (" || this->devicewidth || "x" || this->deviceheight || ")"
                 , width := this->devicewidth
                 , height := this->deviceheight
                 ] INTO ^deviceselect->options AT END;
          ^deviceselect->value := selected;
        }
        ELSE
        {
          ^deviceselect->value := this->tolliumuser->GetRegistryKey("publisher.previewbrowser.device", "f");
          RETURN;
        }
      }

      ^browserframe->SetViewport(this->devicewidth, this->deviceheight);
    }
    ELSE IF (selected = "f")
    {
      DELETE FROM ^deviceselect->options WHERE rowkey LIKE "c-*";
      this->devicewidth := 0;
      this->deviceheight := 0;
      ^browserframe->ResetViewport();
    }
    ELSE
    {
      selected := "";
    }

    IF (selected != "")
    {
      IF (IsDatabaseWritable())
        this->tolliumuser->DelayedSetRegistryKey("publisher.previewbrowser.device", selected);
      this->ReloadCurrent(TRUE, FALSE); //force reresh of the error messages
    }
  }

  MACRO OnPreviewMessageClick(STRING href)
  {
    IF (href LIKE "preview:*")
    {
      href := Substring(href, 8);
      SWITCH (href)
      {
        CASE "showwarnings", "showerrors"
        {
          this->ShowErrors();
        }
      }
    }
  }

  MACRO OnPreviewUpdateEvents(RECORD ARRAY events) { //Any update or publish - refresh the preview and any messages
    FOREVERY (RECORD event FROM events)
      FOREVERY (RECORD file FROM event.data.files)
        IF (file.file = this->previewfileid AND ("update" IN file.events OR "pub" IN file.events)) {
            this->ReloadCurrent(TRUE, TRUE); //force reresh of both frame and the error messages
            RETURN;
          }
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoShowExternalLink(OBJECT handler)
  {
    RECORD fsobj := SELECT externallink FROM system.fs_objects WHERE id = this->previewfileid;

    IF (RecordExists(fsobj) AND fsobj.externallink != "")
      handler->SendURL(fsobj.externallink);
  }

  MACRO DoViewSite(OBJECT handler)
  {
    IF(this->currenturl != "")
      handler->SendURL(this->currenturl);
  }

  MACRO DoPreviewRefresh()
  {
    this->SetFileId(this->previewfileid, TRUE); //force reresh of both frame and the error messages
  }

  MACRO ClearForNewURL(STRING url)
  {
  }


  // ---------------------------------------------------------------------------
  //
  // Public API
  //

  PUBLIC MACRO SetPreview(INTEGER objid)
  {
    Print(`Owner asked previewbrowser to go to #${objid} ${SELECT AS STRING "(" || whfspath || ")" FROM system.fs_objects WHERE id=objid}\n`);
    this->SetFileId(objid, FALSE);
  }

  PUBLIC MACRO SetPreviewURL(STRING newurl) {
    this->previewfileid := 0;
    ^previewlistener->masks := STRING[];
    ^previewurl->value := newurl;

    RECORD previewdata := [ sandbox := TRUE, url := newurl, suppresswhy := newurl != "" ? "" : "no-url", messages := RECORD[]
                          ];
    this->UpdateBrowser(previewdata);
  }

  PUBLIC RECORD FUNCTION GetPreviewState() //introspection needed for testing
  {
    RETURN [ displayurl := ^previewurl->value
           , browserurl := ^browserframe->src
           , showingmessages := ^info->visible
           , messages := ^info->visible ? this->lastsethtmlvalue : ""
           , messagecomp := ^previewmessage
           , previewrefresh := ^previewrefresh
           ];
  }
>;
