<?wh

LOADLIB "mod::publisher/lib/components/fsobjectoverviewbase.whlib";

LOADLIB "mod::tollium/lib/componentbase.whlib";

LOADLIB "mod::system/lib/whfs.whlib";

PUBLIC OBJECTTYPE DCSlot EXTEND TolliumFragmentBase
<
  PUBLIC PROPERTY value(GetValue, SetValue);
  PUBLIC PROPERTY options(GetOptions, -);
  PUBLIC PROPERTY selection(GetSelection, -);
  UPDATE PUBLIC PROPERTY title(^slot->title,^slot->title);
  UPDATE PUBLIC PROPERTY required(^slot->required,^slot->required);

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    this->pvt_invisibletitle := TRUE;
  }
  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->title := def.title;
    this->required := def.required;

    INTEGER ARRAY limittypes;
    FOREVERY(STRING type FROM def.slottypes)
    {
      OBJECT whfstype := OpenWHFSType(type);
      IF(NOT ObjectExists(whfstype))
        THROW NEW TolliumException(this, `No such type '${type}'`);
      INSERT whfstype->id INTO limittypes AT END;
    }
    ^fslibrary->limittypes := limittypes;
  }
  RECORD ARRAY FUNCTION GetOptions()
  {
    //not showing more fields than needed, so we can easily change implementation
    RETURN SELECT rowkey, title FROM ^slot->options;
  }
  RECORD FUNCTION GetSelection()
  {
    RECORD sel := ^slot->selection;
    //not showing more fields than needed, so we can easily change implementation
    RETURN sel.rowkey != 0 ? CELL[sel.rowkey, sel.title] : DEFAULT RECORD;
  }

  INTEGER FUNCTION GetValue()
  {
    RETURN ^slot->value;
  }
  MACRO SetValue(INTEGER slot)
  {
    ^slot->value := slot;
  }

>;
