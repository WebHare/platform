<?wh

LOADLIB "mod::publisher/lib/components/fsobjectoverviewbase.whlib";

LOADLIB "mod::tollium/lib/componentbase.whlib";

PUBLIC OBJECTTYPE Snippet EXTEND TolliumFragmentBase
<
  PUBLIC PROPERTY value(GetValue, SetValue);
  PUBLIC PROPERTY options(GetOptions, -);
  PUBLIC PROPERTY selection(GetSelection, -);
  UPDATE PUBLIC PROPERTY title(^snippet->title,^snippet->title);
  UPDATE PUBLIC PROPERTY required(^snippet->required,^snippet->required);

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    this->pvt_invisibletitle := TRUE;
  }
  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->title := def.title;
    this->required := def.required;
  }
  RECORD ARRAY FUNCTION GetOptions()
  {
    //not showing more fields than needed, so we can easily change implementation
    RETURN SELECT rowkey, title FROM ^snippet->options;
  }
  RECORD FUNCTION GetSelection()
  {
    RECORD sel := ^snippet->selection;
    //not showing more fields than needed, so we can easily change implementation
    RETURN sel.rowkey != 0 ? CELL[sel.rowkey, sel.title] : DEFAULT RECORD;
  }

  INTEGER FUNCTION GetValue()
  {
    RETURN ^snippet->value;
  }
  MACRO SetValue(INTEGER snippet)
  {
    ^snippet->value := snippet;
  }

>;
