<?wh
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/internal/inputvalidation.whlib";

MACRO ExpandHyperlinkComponents(OBJECT selectbox)
{
  FOREVERY(RECORD opt FROM selectbox->options)
  {
    OBJECT insertpos := selectbox->GetButtoncomponent(opt.rowkey);

    FOREVERY(OBJECT comp FROM opt.enablecomponents)
    {
      selectbox->parentpanel->InsertComponentAfter(comp, insertpos, FALSE);
      insertpos := comp;
    }
  }
}


PUBLIC OBJECTTYPE IntExtLink EXTEND TolliumFragmentBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD ARRAY saveoptions;

  INTEGER current_linktype;

  BOOLEAN initialvalueset;

  OBJECT ARRAY __linkhandlers; //internal link <linkhandlers> object

  RECORD ARRAY originaloptions;

  BOOLEAN __internallinks;

  // ---------------------------------------------------------------------------
  //
  // Public variables and properties
  //

  /// Value
  PUBLIC PROPERTY value(GetValue, SetValue);

  /// Title
  UPDATE PUBLIC PROPERTY title(^linktype->title, ^linktype->title);

  PUBLIC PROPERTY linkhandlers(__linkhandlers, SetLinkHandlers);

  PUBLIC PROPERTY internallinks(__internallinks, SetInternalLinks);

  // ---------------------------------------------------------------------------
  //
  // Constructor
  //

  PUBLIC MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    EXTEND this BY TolliumIsDirtyable;

    this->__internallinks := TRUE;
  }

  // ---------------------------------------------------------------------------
  //
  // Tollium admin stuff
  //

  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->saveoptions := ^linktype->options;
    this->title := def.title;
    this->__internallinks := def.internallinks;
    IF(ObjectExists(def.linkhandlers))
      this->linkhandlers := def.linkhandlers->linkhandlers;

    this->ConfigureOptions();
  }

  UPDATE PUBLIC MACRO PreInitComponent()
  {
    IF(Length(this->originaloptions) = 0)
      this->ConfigureOptions();
  }

  MACRO ConfigureOptions()
  {
    IF(Length(this->originaloptions) = 0)
      this->originaloptions := ^linktype->options;

    RECORD ARRAY opts := this->originaloptions;
    IF(this->pvt_required)
      DELETE FROM opts WHERE rowkey = 0;
    IF(NOT this->__internallinks)
      DELETE FROM opts WHERE rowkey = 2;

    FOREVERY(OBJECT handler FROM this->linkhandlers)
    {
      OBJECT ARRAY components := handler->CreateComponents(this->owner);

      INSERT [ rowkey := #handler + 100
             , title := handler->title
             , handler := handler
             , enablecomponents := components
             , tolliumselected := FALSE
             ] INTO opts AT END;
    }
    UPDATE opts SET tolliumselected := TRUE WHERE rowkey = ^linktype->value;
    ^linktype->options := opts;

    ExpandHyperlinkComponents(^linktype);
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  RECORD FUNCTION GetCurrentInternalLinkValue()
  {
    INTEGER objid := ^internal->value;
    IF(objid!=0)
    {
      BOOLEAN valid_append := Left(^append->value, 1) IN [ "#", "?", "!" ];
      RETURN MakeIntExtInternalLink(objid, valid_append ? ^append->value : "");
    }
    RETURN DEFAULT RECORD;
  }

  RECORD FUNCTION GetValue()
  {
    RECORD selected := ^linktype->selection;
    IF(NOT RecordExists(selected) OR selected.rowkey = 0) //'no link'
      RETURN DEFAULT RECORD;

    IF(^linktype->value = 2) //intnternal link
      RETURN this->GetCurrentInternalLinkValue();
    ELSE IF(^linktype->value = 1) //intnternal link
      RETURN MakeIntExtExternalLink(^external->value);

    RETURN MakeIntExtExternalLink(selected.handler->GetLink());
  }

  // ---------------------------------------------------------------------------
  //
  // Getters & setters
  //

  MACRO SetLinkHandlers(OBJECT ARRAY newlinkhandlers)
  {
    this->__linkhandlers := newlinkhandlers;
    IF(Length(this->originaloptions) > 0) //need to reconfig?
      this->ConfigureOptions();
  }

  MACRO SetInternalLinks(BOOLEAN newinternallinks)
  {
    IF(this->__internallinks = newinternallinks)
      RETURN;

    this->__internallinks := newinternallinks;
    IF(Length(this->originaloptions) > 0) //need to reconfig?
      this->ConfigureOptions();
  }

  UPDATE MACRO SetRequired(BOOLEAN new_required)
  {
    IF(new_required = this->pvt_required)
      RETURN;

    TolliumFragmentBase::SetRequired(new_required);
    this->ConfigureOptions();
  }

  UPDATE MACRO SetEnabled(BOOLEAN nowenable)
  {
    IF(this->pvt_enabled = nowenable)
      RETURN;

    this->pvt_enabled := nowenable;
    ^linktype->enabled := nowenable;
  }

  MACRO SetValue(RECORD value)
  {
    this->EnsurePreInit();

    IF(NOT RecordExists(value) OR (value.internallink = 0 AND value.externallink = ""))
    {
      ^linktype->value := 0;
      ^internal->value := 0;
      ^append->value := "";
      ^external->value := "";
    }
    ELSE IF(value.internallink != 0 AND this->internallinks)
    {
      ^linktype->value := 2;
      ^internal->value := value.internallink;
      ^append->value := value.append;
    }
    ELSE
    {
      BOOLEAN didset;
      FOREVERY(RECORD opt FROM ^linktype->options)
      {
        IF(opt.rowkey < 100) //not handling builtins here
         CONTINUE;

        IF(opt.handler->SetLinkIfSupported(value.externallink))
        {
          didset := TRUE;
          ^linktype->value := opt.rowkey;
          BREAK;
        }
      }
      IF(NOT didset)
      {
        ^linktype->value := 1; //external link
        ^external->value := value.externallink;
      }
    }

    IF (this->initialvalueset)
      this->SetDirty();
    ELSE
      this->initialvalueset := TRUE;
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnLinkTypeChange()
  {
    IF (^linktype->value = 2 AND this->current_linktype = 1)
    { // Try to convert internal links to external links
      IF (this->external->value != "")
      {
        //fill from external
        RECORD match := LookupPublisherURL(this->external->value);
        INTEGER matchid := match.file ?? match.folder;

        IF(matchid != 0)
        {
          this->internal->value := matchid;
          STRING baseurl := SELECT AS STRING indexurl FROM system.fs_objects WHERE id = matchid;
          IF (baseurl = "" OR Left(this->external->value, Length(baseurl)) != baseurl OR Substring(this->external->value, Length(baseurl), 1) NOT IN ['#','?','!'])
            baseurl := SELECT AS STRING objecturl FROM system.fs_objects WHERE id = matchid;

          IF (baseurl != "" AND Left(this->external->value, Length(baseurl)) = baseurl AND Substring(this->external->value, Length(baseurl), 1) IN ['#','?','!'])
            this->append->value := Substring(this->external->value, Length(baseurl));

          this->external->value := "";
        }
      }
    }
    ELSE IF (^linktype->value = 1 AND this->current_linktype = 2)
    {
      RECORD intextlink := this->GetCurrentInternalLinkValue();
      IF(RecordExists(intextlink))
      {
        // Not using GetIntextlinkTarget, because that returns '' when for a folder without index document
        // (because it uses indexurl instead of url)
        STRING exturl := SELECT AS STRING objecturl FROM system.fs_objects WHERE id = intextlink.internallink AND isactive;
        IF (exturl != "")
          exturl := exturl || intextlink.append;

        this->external->value := exturl;
      }

      this->internal->value := 0;
      this->append->value := "";
    }

    IF (this->current_linktype != ^linktype->value)
    {
      this->current_linktype := ^linktype->value;
      this->SetDirty();
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Public API
  //

  PUBLIC UPDATE MACRO ValidateValue(OBJECT work)
  {
    STRING fieldtitle := this->errorlabel ?? this->title;
    IF(NOT this->enabled) //nothing to check on inactive fields
      RETURN;

    IF(this->required AND ^linktype->value = 0)
    {
      work->AddErrorFor(this, GetTid("tollium:common.errors.field_required", fieldtitle));
    }
    ELSE IF(^linktype->value = 2)
    {
      this->internal->ValidateValue(work);
      IF (Left(this->append->value, 1) NOT IN [ "", "#", "?", "!" ])
      {
        work->AddErrorFor(this, GetTid("publisher:components.errors.invalidappend", fieldtitle));
      }
      ELSE
      {
        STRING url := GetIntextlinkTarget(this->GetValue());
        IF(url != "")
        {
          STRING validate_error := ValidateInput(url, fieldtitle, [ "url" ]);
          IF (validate_error != "")
            work->AddErrorFor(this, validate_error);
        }
      }
    }
    ELSE IF(^linktype->value = 1)
    {
      ^external->ValidateValue(work);
    }
  }

  /** Preselect internal or external link */
  PUBLIC MACRO SetLinkType(STRING type)
  {
    IF(type = "internal")
      ^linktype->value := 2;
    ELSE IF(type = "external")
      ^linktype->value := 1;
    ELSE
      THROW NEW TolliumException(this, `Unrecognized SetLinkType value '${type}'`);
  }

  /** Focus the external link entry. Used by RTE to autofocus the most likely spot (at least for keyboard entry) on new links */
  PUBLIC MACRO FocusExternalLinkEntry()
  {
    IF(^linktype->value = 1)
      this->owner->frame->focused := ^external;
  }
>;
