<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/webserver/auth.whlib";

RECORD FUNCTION LookupShortURL(INTEGER urlset, STRING findname)
{
  OpenPrimary();

  RECORD match := SELECT id, link FROM system.fs_objects WHERE parent = urlset AND ToUppercase(name) = ToUppercase(findname);
  STRING targeturl, addvariables;
  STRING ARRAY eventmasks := ["publisher:shorturlconfig"];

  IF(RecordExists(match))
  {
    OBJECT shorturldomaintype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/shorturl/domainfolder");
    RECORD urlsetsettings := shorturldomaintype->GetInstanceData(urlset);

    addvariables := urlsetsettings.addvariables;
    eventmasks := eventmasks CONCAT OpenWHFSObject(RecordExists(match) ? match.id : urlset)->GetEventMasks();
    targeturl := match.link;
  }

  RETURN CELL[ value := targeturl != "" ? CELL[ targeturl, addvariables ] : DEFAULT RECORD
             , ttl := 60000
             , eventmasks := eventmasks
             ];
}

RECORD rd := SELECT *
               FROM GetWebHareAccessRules()
              WHERE CellExists(data.ruledata,'type') AND data.ruledata.type = "eph-shorturls";

IF(NOT RecordExists(rd)) //we got a match on shorturl.shtml but no shorturl config...
  AbortWithHTTPError(500);

INTEGER shorturlset := rd.data.ruledata.id;
STRING findshortname := Substring(UnpackURL(Tokenize(GetRequestURL(),'?')[0]).urlpath, Length(rd.data.path)-1);


//Adhoccache/ratelimit to reduce DB hits?
RECORD match := GetAdhocCached(CELL[ shorturlset, shortname := findshortname ], PTR LookupShortURL(shorturlset,findshortname));
IF(NOT RecordExists(match))
  AbortWithHTTPError(404);

STRING targeturl := match.targeturl;

RECORD ARRAY requestvars := GetAllVariablesFromURL(GetRequestURL());
STRING ARRAY requestvarnames  := SELECT AS STRING ARRAY ToUppercase(name) FROM requestvars;
STRING ARRAY destvarnames := SELECT AS STRING ARRAY ToUppercase(name) FROM GetAllVariablesFromURL(match.targeturl);

IF(match.addvariables != "") //add any variables from the shorturl domain if not set on either request url or target url
  FOREVERY(RECORD varrec FROM GetAllVariablesFromURL("?"||match.addvariables))
    IF(ToUppercase(varrec.name) NOT IN (requestvarnames CONCAT destvarnames))
      targeturl := AddVariableToURL(targeturl, varrec.name, varrec.value);

FOREVERY(RECORD varrec FROM requestvars)
{
  IF(ToUppercase(varrec.name) IN destvarnames)
    targeturl := DeleteVariableFromURL(targeturl, varrec.name);
  targeturl := AddVariableToURL(targeturl, varrec.name, varrec.value);
}

Redirect(targeturl,302);
