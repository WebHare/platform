<?wh

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


OpenPrimary();
OBJECT webdesign := GetWebdesignForUrl(GetRequestUrl());

IF (IsRequestPost())
{
  IF (GetWebHeader("Content-Type") != "application/json")
    AbortWithHTTPError(400);

  OBJECT wrdschema := OpenWRDSchema("publisher:feedback");

  AddHTTPHeader("Content-Type", "application/json", FALSE);
  RECORD userdata := DecodeJSONBlob(GetRequestBody());

  RECORD response := CELL
      [ token := GetFeedbackWebToken(userdata)
      , topics := (SELECT *, DELETE wrd_ordering FROM wrdschema->^topic->RunQuery([ outputcolumns := CELL[ tag := "wrd_tag", title := "wrd_title", "wrd_ordering" ]]) ORDER BY wrd_ordering)
        // If we have a webdesign, redirect to the site's root
      , redirect := ObjectExists(webdesign) ? webdesign->targetsite->webroot : ""
      ];

  SendBlobTo(0, EncodeJSONBlob(response));
  RETURN;
}

MACRO PrintForm()
{
  Print(`
    <form id="publisher-feedbacklogin" class="wh-form wh-styledinput wh-form--allowsubmit" action="javascript:console.error('Form not active')">
      <div class="wh-form__page">
        <div class="wh-form__fieldgroup wh-form__fieldgroup--textedit">
          <label class="wh-form__label" for="name">${EncodeHTML(GetTid("publisher:site.feedback.name"))}</label>
          <div class="wh-form__fields">
            <div class="wh-form__fieldline">
              <input name="name" type="text" class="wh-form__textinput" required />
            </div>
          </div>
        </div>
        <div class="wh-form__fieldgroup">
          <label class="wh-form__label" for="email">${EncodeHTML(GetTid("publisher:site.feedback.email"))}</label>
          <div class="wh-form__fields">
            <div class="wh-form__fieldline">
              <input name="email" type="email" class="wh-form__textinput" required />
            </div>
          </div>
        </div>
        <div class="wh-form__buttongroup wh-form__navbuttons">
          <button type="submit" class="wh-form__button wh-form__button--submit">${EncodeValue(GetTid("publisher:site.feedback.login"))}</button>
        </div>
      </div>
    </form>
    <script src="feedbacklogin.js"></script>
  `);
}

IF (ObjectExists(webdesign))
  webdesign->RunPageWithContents(PTR PrintForm);
ELSE
  PrintForm();
