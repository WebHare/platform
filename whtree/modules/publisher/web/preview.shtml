<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::publisher/lib/internal/pagerendering.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/webcontext.whlib";
LOADLIB "mod::publisher/lib/preview.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/printer.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::system/lib/internal/webserver/errorhandler.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

OpenPrimary();

PUBLIC OBJECTTYPE PreviewPage EXTEND DynamicPageBase
<
  PUBLIC RECORD data;

  MACRO NEW()
  {
    this->data := [ incorrectpassword := FALSE
                  , password := GetWebVariable("password")
                  , feedbackurl := GetRequestURL()
                  , component := ""
                  ];
  }

  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody(OBJECT webdesign)
  {
    webdesign->isstandardcontentpage := TRUE;
    RETURN PTR EmbedWittyComponent("mod::publisher/lib/internal/builtinpages/preview.witty:" || this->data.component, this->data);
  }
>;

IF(GetWebVariable("__preview_pdf")="1" AND GetDtapStage()="development") //experimental option, so masking it behind 'dep' just to be save
{
  STRING realurl := UpdateURLVariables(GetRequestURL(), [ __preview_pdf := "" ]);
  SendWrappedWebFile(GeneratePDFFromURL(realurl, [ media := "print"
                                                 ]) );
}

MACRO SendFileDirect(OBJECT openobj, INTEGER source, BOOLEAN usepublish)
{
  OBJECT sourceobj := OpenWHFSObject(source);

  // Determine the mimetype by extension of the original source file (openobj is probably a draft, with its id as name)
  STRING extension := SubString(ToLowercase(GetExtensionFromPath(sourceobj->fullpath)), 1);
  STRING mimetype :=
      SELECT AS STRING COLUMN mimetype
        FROM system.mimetypes
       WHERE COLUMN extension = VAR extension;

  //When explicitly previewing, disable mimetypes which we know trigger downloads (eg chrome)
  IF(ToUppercase(Tokenize(mimetype,';')[0]) IN ["TEXT/CALENDAR"])
    mimetype := "text/plain";

  IF(usepublish)
  {
    RECORD fp := RunFilePublish(sourceobj->id, FALSE);
    IF(fp.success)
    {
      IF (mimetype != "")
        AddHTTPHeader("Content-type", mimetype, FALSE);
      SendWebFile(fp.files[0].data);
    }
    ELSE IF(Length(fp.hserrors) > 0)
      GenerateHTTPErrorPage(500, [ allerrors := fp.hserrors, groupid := fp.hsgroupid ]);
    ELSE
      ABORT(fp); //what to do? we'll sort it out when we see the cases
  }
  ELSE
  {
    IF (mimetype != "")
      AddHTTPHeader("Content-type", mimetype, FALSE);
    SendWebFile(openobj->data);
  }
}

MACRO RunPublisherPreview(OBJECT openobj, INTEGER source, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  /* previews should never be crawled/stored. We'll use headers to prevent that so you can still view-source in preview mode
     and inspect the result of noindex/nofollow settings on the document */
  AddHTTPHeader("X-Robots-Tag", "noindex, nofollow", FALSE);

  OBJECT applytester := GetApplyTesterForObject(source);
  IF(NOT ObjectExists(applytester))
    THROW NEW Exception("Unable to locate the webdesign for #" || source);

  __ValidateWebdesign(applytester);

  RECORD webdesigninfo := applytester->GetWebDesignObjinfo();
  IF(NOT RecordExists(webdesigninfo))
  {
    Print(EncodeHTML("The live preview is only available for sites using <webdesign>"));
    RETURN;
  }

  OBJECT contentobject := openobj;
  IF(openobj->type = whconstant_whfstype_contentlink) //content link
  {
    contentobject := OpenWHFSObject(openobj->filelink);
    IF(NOT ObjectExists(contentobject))
    {
      Print(EncodeHTML("The contentlink target does not exist"));
      RETURN;
    }
  }

  OBJECT contentfiletype := OpenWHFSTypeById(contentobject->type);
  BOOLEAN renderwidgetpreview := contentfiletype->iswidgettype;

  // Check if the file type needs a template (if not, send it directly)
  IF (NOT renderwidgetpreview AND NOT contentfiletype->needstemplate)
  {
    RECORD usetempl := applytester->GetUsePublishTemplate();
    SendFileDirect(openobj, source, NOT usetempl.isdefault );
    RETURN;
  }

  STRING baseurl := Tokenize(GetRequestURL(),'?')[0];
  baseurl := Detokenize(ArraySlice(Tokenize(baseurl, '/'),3,3),'/') || "/";
  __pageinfo := __SplitBaseAndSubUrl(GetRequestURL(), baseurl);

  RECORD sourceinfo := GetSystemRedirectInfo(source);
  IF(sourceinfo.success)
  {
    RunThePage(source, sourceinfo, contentobject->id);
    RETURN;
  }

  INSERT CELL pagefolder := GetDirectoryFromPath(Tokenize(webdesigninfo.renderinfo.objectname,'#')[0]) INTO __pageinfo;
  IF(webdesigninfo.renderinfo.renderer != "")
  {
    RECORD capturedpage := CallJS("mod::publisher/js/internal/capturejsdesign.ts#captureJSPage", source, openobj->id);
    SendWebFile(capturedpage.body);
  }
  ELSE IF(webdesigninfo.renderinfo.objectname = "")
    THROW NEW Exception("Don't know how to render a type without a rendering object");

  OBJECT pageobject := MakeObject(webdesigninfo.renderinfo.objectname, contentobject);
  OBJECT webdesign := InstantiateWebDesign(applytester, webdesigninfo, 200, contentobject, pageobject, renderwidgetpreview);

  //Update robot tags and remove analytics/gtm from previews, they generally confuse users
  webdesign->__renderpreview := NOT IsWHDebugOptionSet("npm"); //TODO move this into instantiate so NEW() can rely on it too

  IF (renderwidgetpreview) {
    OBJECT type := OpenWHFSTypeById(openobj->type);
    RECORD instancedata := type->GetInstanceData(openobj->id);
    INSERT CELL whfstype := type->namespace INTO instancedata;
    INSERT CELL whfsfileid := openobj->id INTO instancedata;

    // Lookup RTD type
    RECORD rtdtypesettings := applytester->GetRTDTypeSettings();
    STRING rtdtype := RecordExists(rtdtypesettings) ? rtdtypesettings.rtdtype : "";

    IF (rtdtype = "")
      THROW NEW Exception("An RTD type is required to preview a widget, please set an <rtddoc rtdtype=...>");

    webdesign->RunAsWidgetPreview(PTR webdesign->RenderWidgetInstance(instancedata));
    RETURN;
  }

  webdesign->RunPage();
}

RECORD topreview := ParsePreviewURL(GetRequestURL());
OBJECT draftobj := OpenWHFSDraft(topreview.id);
OBJECT openobj := ObjectExists(draftobj) ? draftobj->fsobject : OpenWHFSObject(topreview.id);

INTEGER designid;
IF(Objectexists(draftobj))
  designid := SELECT AS INTEGER parentsite FROM system.fs_objects WHERE id=draftobj->source;
ELSE IF(Objectexists(openobj))
  designid := SELECT AS INTEGER parentsite FROM system.fs_objects WHERE id = VAR topreview.id;
ELSE
  designid := 16; //builtin clean design - should we use this or just revert to a standard 404 if we're not sure about the object you're accessing ?

INTEGER source := ObjectExists(draftobj) ? draftobj->source : topreview.id;

IF(NOT ObjectExists(openobj) OR topreview.validuntil < GetCurrentDatetime() OR topreview.creationdate != openobj->creationdate)
{
  OBJECT page := NEW PreviewPage();
  page->data.component := "previewexpired";
  //FIXME should probably signal using something like SetupErrorPage that we'll be previewing/drafting..
  page->RunPageForWHFSId(designid);
  RETURN;
}

STRING pwd := TrimWhitespace(GetWebVariable("password"));
IF(topreview.password != "" AND topreview.password != pwd)
{
  OBJECT page := NEW PreviewPage();
  page->data.component := "previewpassword";
  page->data.incorrectpassword := pwd != "";
  //FIXME should probably signal using something like SetupErrorPage that we'll be previewing/drafting..
  page->RunPageForWHFSId(designid);
  RETURN;
}

IF (NOT ObjectExists(draftobj) AND NOT ObjectExists(openobj))
  THROW NEW Exception("Document to preview doesn't exist. It might have been deleted or there's a new draft available.");

RunPublisherPreview(openobj, source);
