<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/history.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::publisher/lib/internal/pagerendering.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/preview.whlib";
LOADLIB "mod::publisher/lib/internal/webdesign/webcontext.whlib";
LOADLIB "mod::publisher/lib/preview.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/printer.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::system/lib/internal/webserver/errorhandler.whlib";

OpenPrimary();

PUBLIC OBJECTTYPE PreviewPage EXTEND DynamicPageBase
<
  PUBLIC RECORD data;

  MACRO NEW()
  {
    this->data := [ incorrectpassword := FALSE
                  , password := GetWebVariable("password")
                  , feedbackurl := GetRequestURL()
                  , component := ""
                  ];
  }

  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody(OBJECT webdesign)
  {
    webdesign->isstandardcontentpage := TRUE;
    RETURN PTR EmbedWittyComponent("mod::publisher/lib/internal/builtinpages/preview.witty:" || this->data.component, this->data);
  }
>;

IF(GetWebVariable("__preview_pdf")="1" AND GetDtapStage()="development") //experimental option, so masking it behind 'dep' just to be save
{
  STRING realurl := UpdateURLVariables(GetRequestURL(), [ __preview_pdf := "" ]);
  SendWrappedWebFile(GeneratePDFFromURL(realurl, [ media := "print"
                                                 ]) );
}

MACRO SendFileDirect(OBJECT openobj, INTEGER source, BOOLEAN usepublish)
{
  OBJECT sourceobj := OpenWHFSObject(source);

  // Determine the mimetype by extension of the original source file (openobj is probably a draft, with its id as name)
  STRING extension := SubString(ToLowercase(GetExtensionFromPath(sourceobj->fullpath)), 1);
  STRING mimetype :=
      SELECT AS STRING COLUMN mimetype
        FROM system.mimetypes
       WHERE COLUMN extension = VAR extension;

  //When explicitly previewing, disable mimetypes which we know trigger downloads (eg chrome)
  IF(ToUppercase(Tokenize(mimetype,';')[0]) IN ["TEXT/CALENDAR"])
    mimetype := "text/plain";

  IF(usepublish)
  {
    RECORD fp := RunFilePublish(sourceobj->id, FALSE);
    IF(fp.success)
    {
      IF (mimetype != "")
        AddHTTPHeader("Content-type", mimetype, FALSE);
      SendWebFile(fp.files[0].data);
    }
    ELSE IF(Length(fp.hserrors) > 0)
      GenerateHTTPErrorPage(500, [ allerrors := fp.hserrors, groupid := fp.hsgroupid ]);
    ELSE
      ABORT(fp); //what to do? we'll sort it out when we see the cases
  }
  ELSE
  {
    IF (mimetype != "")
      AddHTTPHeader("Content-type", mimetype, FALSE);
    SendWebFile(openobj->data);
  }
}

MACRO RunPublisherPreview(RECORD targetobject, RECORD contentobject, BOOLEAN ispublicobject) {
  /* previews should never be crawled/stored. We'll use headers to prevent that so you can still view-source in preview mode
     and inspect the result of noindex/nofollow settings on the document */
  AddHTTPHeader("X-Robots-Tag", "noindex, nofollow", FALSE);

  OBJECT applytester := GetApplyTesterForObject(targetobject.id);
  IF(NOT ObjectExists(applytester))
    THROW NEW Exception("Unable to locate the webdesign for #" || targetobject.id);

  __ValidateWebdesign(applytester);

  RECORD webdesigninfo := applytester->GetWebDesignObjinfo();
  IF(NOT RecordExists(webdesigninfo))
  {
    Print(EncodeHTML("The live preview is only available for sites using <webdesign>"));
    RETURN;
  }

  RECORD contenttypeinfo := DescribeContentTypeById(contentobject.type, [ isfolder := FALSE, mockifmissing := TRUE ]);
  IF(contenttypeinfo.browserpreview != "") {
    //TODO The problem is, this is just an URL to a script. So we can't easily capture its output and password protect it. We need a HTML-generating callback instead
    //     pdf.shtml can't just be embeddewd
    //     we could probably deal with image directly if we need it.
    IF(ispublicobject)
      Redirect(ResolveToAbsoluteURL(GetPrimaryWebhareInterfaceURL(), contenttypeinfo.browserpreview) || "?preview=" || GetPreviewToken(targetobject, contentobject));

    Print(EncodeHTML("No live preview available for this object"));
    RETURN;
  }

  BOOLEAN renderwidgetpreview := contenttypeinfo.iswidgettype;

  IF(NOT renderwidgetpreview AND NOT contenttypeinfo.needstemplate) { //Some might be viewable such as HTML but others will require a download wrapper
    Print(EncodeHTML("This object type is not directly viewable"));
    RETURN;
  }

  STRING baseurl := Tokenize(GetRequestURL(),'?')[0];
  baseurl := Detokenize(ArraySlice(Tokenize(baseurl, '/'),3,3),'/') || "/";
  __pageinfo := __SplitBaseAndSubUrl(GetRequestURL(), baseurl);

  RECORD sourceinfo := GetSystemRedirectInfo(targetobject.id); //TODO this one redoes applytester?
  IF(sourceinfo.success)
  {
    RunThePage(targetobject.id, sourceinfo, contentobject.id);
    RETURN;
  }

  INSERT CELL pagefolder := GetDirectoryFromPath(Tokenize(webdesigninfo.renderinfo.objectname,'#')[0]) INTO __pageinfo;
  IF(webdesigninfo.renderinfo.renderer != "")
  {
    RECORD capturedpage := CallJS("mod::publisher/js/internal/capturejsdesign.ts#captureJSPage", contentobject.id, targetobject.id);
    SendWebFile(capturedpage.body);
  }
  ELSE IF(webdesigninfo.renderinfo.objectname = "")
    THROW NEW Exception("Don't know how to render a type without a rendering object");

  OBJECT contentfsobj := OpenWHFSObject(contentobject.id);
  OBJECT pageobject := MakeObject(webdesigninfo.renderinfo.objectname, contentfsobj);
  OBJECT webdesign := InstantiateWebDesign(applytester, webdesigninfo, 200, contentfsobj, pageobject, renderwidgetpreview);

  //Update robot tags and remove analytics/gtm from previews, they generally confuse users
  webdesign->__renderpreview := NOT IsWHDebugOptionSet("npm"); //TODO move this into instantiate so NEW() can rely on it too

  IF (renderwidgetpreview) {
    OBJECT contentfiletype := OpenWHFSTypeById(contentobject.type);
    RECORD instancedata := contentfiletype->GetInstanceData(contentobject.id);
    INSERT CELL whfstype := contentfiletype->namespace INTO instancedata;
    INSERT CELL whfsfileid := targetobject.id INTO instancedata;

    // Lookup RTD type
    RECORD rtdtypesettings := applytester->GetRTDTypeSettings();
    STRING rtdtype := RecordExists(rtdtypesettings) ? rtdtypesettings.rtdtype : "";

    IF (rtdtype = "")
      THROW NEW Exception("An RTD type is required to preview a widget, please set an <rtddoc rtdtype=...>");

    webdesign->RunAsWidgetPreview(PTR webdesign->RenderWidgetInstance(instancedata));
    RETURN;
  }

  webdesign->RunPage();
}

RECORD topreview := ParsePreviewURL(GetRequestURL());
RECORD targetobject;

IF(topreview.validuntil > GetCurrentDatetime()) { //still valid
  STRING pwd := TrimWhitespace(GetWebVariable("password"));
  IF(topreview.password != "" AND topreview.password != pwd) {
    OBJECT page := NEW PreviewPage();
    page->data.component := "previewpassword";
    page->data.incorrectpassword := pwd != "";
    //FIXME should probably signal using something like SetupErrorPage that we'll be previewing/drafting..
    page->RunPageForWHFSId(whconstant_whfsid_webharebackend);
    RETURN;
  }

  targetobject := SELECT id, whfspath, filelink, parent, creationdate, type, publish FROM system.fs_objects WHERE id = topreview.id AND creationdate = topreview.creationdate;
}

IF(NOT RecordExists(targetobject)) {
  OBJECT page := NEW PreviewPage();
  page->data.component := "previewexpired";
  //FIXME should probably signal using something like SetupErrorPage that we'll be previewing/drafting..
  page->RunPageForWHFSId(whconstant_whfsid_webharebackend);
  RETURN;
}

RECORD contentobject := targetobject;
BOOLEAN ispublicobject;
BOOLEAN getmostrecentversion := topreview.share = "mostrecentversion";

// abort(targetobject);
IF(ToUppercase(targetobject.whfspath) LIKE "/WEBHARE-PRIVATE/SYSTEM/WHFS/SNAPSHOTS/*"
   OR ToUppercase(targetobject.whfspath) LIKE "/WEBHARE-PRIVATE/SYSTEM/WHFS-DRAFTS/*"
   OR ToUppercase(targetobject.whfspath) LIKE "/WEBHARE-PRIVATE/SYSTEM/WHFS-AUTOSAVES/*") {
  //Resolve the base object for the snapshot/draft
  targetobject := SELECT id, parentsite, type FROM system.fs_objects WHERE id = contentobject.filelink;
  IF(NOT RecordExists(targetobject))
    THROW NEW Exception(`The target object for this preview (${contentobject.filelink}) does not exist`);

} ELSE IF(ToUppercase(targetobject.whfspath) LIKE "/WEBHARE-PRIVATE/SYSTEM/WHFS-VERSIONS/*/*") {
  //This is the recycle bin!
  INTEGER deletionid := ToInteger(Tokenize(targetobject.whfspath,'/')[4],0);
  INTEGER originalsite := SELECT AS INTEGER filelink FROM system.fs_objects WHERE id = deletionid;
  targetobject := SELECT id, parentsite := id, type FROM system.fs_objects WHERE id = originalsite;
} ELSE IF(ToUppercase(targetobject.whfspath) LIKE "/WEBHARE-PRIVATE/*") {
  //Recyclebin or other custom stuff we didn't consider?
  THROW NEW Exception(`Unsupported path: ${targetobject.whfspath}`);
} ELSE IF(NOT targetobject.publish) {
  /* The target isn't published, and we'd normally see a "is not published" warning in the preview.
    However, previewing it would show the initial content (often an empty page) but the draft that is about to be published is much more useful */
  getmostrecentversion := TRUE;
} ELSE {
  //It seems not to be in offline space
  ispublicobject := topreview.password = "";

  IF(targetobject.type = whconstant_whfstype_contentlink) {
    //Resolve the contentobject for the contentlink
    contentobject := SELECT id, parentsite, type FROM system.fs_objects WHERE id = targetobject.filelink;
    IF(NOT RecordExists(contentobject))
      THROW NEW Exception(`The content object for this preview (${targetobject.filelink}) does not exist`);
  }
}

IF(getmostrecentversion) {
 //TODO more efficient direct selection needed
  RECORD lastdraft := SELECT * FROM ListObjectHistory(targetobject.id) WHERE type="saved" ORDER BY when DESC;
  IF(RecordExists(lastdraft))
    contentobject := SELECT id, parentsite, type FROM system.fs_objects WHERE id = lastdraft.snapshot;
}

//TODO or copy the actual targets CSP ?
AddHTTPHeader("Content-Security-Policy", "", FALSE);
AddHTTPHeader("X-Frame-Options", "", FALSE);
RunPublisherPreview(targetobject, contentobject, ispublicobject);
