<?wh

LOADLIB "mod::publisher/lib/preview.whlib";
LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

RECORD req := DescribePreviewRequest();


INTEGER FUNCTION GetRootindexDoc()
{
  INTEGER rootindexdoc := (SELECT AS INTEGER indexdoc FROM system.fs_objects WHERE id = 16) ?? 16;
  RETURN rootindexdoc;
}

//TODO Don't build up a whole tollium webdesign, just replace this all with a simple witty
OBJECTTYPE ShowMessagePage EXTEND DynamicPageBase
<
  STRING url;
  STRING msg;
  STRING type;
  STRING location;
  STRING lang;

  MACRO NEW(STRING lang, STRING url, STRING msg, STRING type, STRING location)
  {
    this->url := url;
    this->msg := msg;
    this->type := type;
    this->location := location;
    this->lang:=lang;
  }

  UPDATE PUBLIC MACRO PrepareForRendering(OBJECT webdesign)
  {
    //ADDME share directly with applicationportal.whlib
    INSERT "previewframe" INTO webdesign->htmlclasses AT END;
  }

  UPDATE PUBLIC MACRO RunBody(OBJECT webdesign)
  {
    IF(this->lang!="")
      SetTIDLanguage(this->lang);

    // Shown when a file is selected with a MIME type which is not directly viewable within the browser (i.e. not an image or text)
    STRING title := GetTid("publisher:components.previewbrowser.showmessage.notviewable-title");
    STRING message := GetTid("publisher:components.previewbrowser.showmessage.notviewable-message", this->type);
    STRING urlmessage := GetTid("publisher:components.previewbrowser.showmessage.notviewable-urlmessage");

    Print(`<div id="bg">`);
    Print(`<div id="msg">
            <h1>${EncodeHTML(title)}</h1>
            <p>${EncodeHTML(message)}</p>
            <p><a href="${EncodeValue(this->url)}" target="_blank">${EncodeHTML(urlmessage)}</a></p>`);

    IF(GetDtapStage() = "development")
    {
      //check if we're dealing with a dynamic page
      RECORD fileinfo := SELECT type, isfolder FROM system.fs_objects WHERE id = req.contentobject;
      RECORD typeinfo := DescribeContentTypeById(fileinfo.type, CELL[ fileinfo.isfolder, mockifmissing := TRUE ]);
      IF(RecordExists(typeinfo.dynamicexecution) AND NOT typeinfo.needstemplate)
      {
        //developers often forgot needstemplate=true in the past because it wasn't really necessary for a SHTML file, its behaviour didn't really change
        Print(`<br><br><p>${EncodeHTML(`If you expected a preview for this <dynamicexecution> file you should specify kind="virtualfile" or needstemplate="true" for filetype ${EncodeHTML(typeinfo.namespace)}`)}</p>`);
      }
    }

    Print(`</div></div>`);
  }
>;


OpenPrimary();
RECORD contentinfo := OpenWHFSObject(req.contentobject)->GetWrapped();
NEW ShowMessagePage(req.languagecode, GetRequestURL(), "notviewable", contentinfo.mimetype,"")->RunPageForWHFSId(GetRootindexDoc());
