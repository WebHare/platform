<?wh

LOADLIB "wh::witty.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::publisher/lib/widgets.whlib";


PUBLIC STATIC OBJECTTYPE AnchorWidget EXTEND TolliumTabsExtensionBase
<
  BOOLEAN FUNCTION CheckAnchorName(STRING name)
  {
    /* ID and NAME tokens must begin with a letter ([A-Za-z]) and may be followed by any number of letters, digits ([0-9]),
       hyphens ("-"), underscores ("_"), colons (":"), and periods ("."). */
    STRING letters := "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz";
    STRING characters := letters || "0123456789-_:.";

    FOR (INTEGER i := 0; i < Length(name); i := i + 1)
      IF (SearchSubstring(i = 0 ? letters : characters, Substring(name, i, 1)) = -1)
        RETURN FALSE;

    RETURN TRUE;
  }

  UPDATE PUBLIC MACRO ValidateValue(OBJECT feedback)
  {
    TolliumTabsExtensionBase::ValidateValue(feedback);
    IF (NOT this->CheckAnchorName(^anchorname->value))
      feedback->AddErrorFor(^anchorname, GetTid("publisher:widgets.anchorwidget.invalidname", ^anchorname->value));
  }
>;

PUBLIC STATIC OBJECTTYPE AnchorWidgetBase EXTEND WidgetBase <
  UPDATE PUBLIC MACRO RenderEmail(OBJECT mailcomposer) {
    this->RenderLive(); //we should render in emails!
  }

  UPDATE PUBLIC MACRO RenderSearchPreview() {
    // Don't render the anchor in search results
  }
>;

PUBLIC STATIC OBJECTTYPE WittyWidget EXTEND WidgetBase
<
  UPDATE PUBLIC MACRO Render()
  {
    Print(EncodeHTML(this->data.wittycode));
  }

  UPDATE PUBLIC MACRO RenderEmail(OBJECT mailcomposer)
  {
    OBJECT templ := NEW WittyTemplate("HTML-NI");
    templ->LoadCodeDirect(this->data.wittycode);
    templ->Run();
  }

  UPDATE PUBLIC MACRO RenderLive()
  {
    OBJECT templ := NEW WittyTemplate("HTML-NI");
    templ->LoadCodeDirect(this->data.wittycode);
    templ->Run();
  }
>;
