<?wh
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/configure.whlib";


PUBLIC STATIC OBJECTTYPE Prefs EXTEND TolliumScreenBase
<
  MACRO Init()
  {
    STRING ARRAY opts;
    IF(this->tolliumuser->GetRegistryKey("publisher.prefs.propsafterupload",FALSE))
      INSERT "propsafterupload" INTO opts AT END;
    IF(this->tolliumuser->GetRegistryKey("publisher.prefs.nouploadrename",FALSE))
      INSERT "nouploadrename" INTO opts AT END;
    IF(this->tolliumuser->GetRegistryKey("publisher.prefs.groupfolders", TRUE))
      INSERT "groupfolders" INTO opts AT END;

    IF(this->contexts->user->HasRight("system:supervisor"))
    {
      IF(this->tolliumuser->GetRegistryKey("publisher.prefs.showforeignfolders", FALSE))
        INSERT "showforeignfolders" INTO opts AT END;
    }
    ELSE
    {
      DELETE FROM ^opts->options WHERE rowkey = "showforeignfolders";
    }

    IF(this->tolliumuser->GetRegistryKey("publisher.prefs.showmenubar", FALSE))
      INSERT "showmenubar" INTO opts AT END;

    ^opts->value := opts;

    ^autotitles->value := this->tolliumuser->GetRegistryKey("publisher.prefs.autotitles", ReadRegistryKey("publisher.filemgr.defaultautotitles"));

    ^tweaks->visible := this->tolliumuser->HasRight("system:sysop");
    ^showwhfsprivate->value := this->tolliumuser->GetRegistryKey("publisher.filemgr.showwhfsprivate", FALSE);
    ^showwebharebackend->value := this->tolliumuser->GetRegistryKey("publisher.filemgr.showwebharebackend", FALSE);
  }

  BOOLEAN FUNCTION Submit()
  {
    STRING ARRAY opts := ^opts->value;

    IF( (^showwhfsprivate->value AND NOT this->tolliumuser->GetRegistryKey("publisher.filemgr.showwhfsprivate", FALSE))
        OR (^showwebharebackend->value AND NOT this->tolliumuser->GetRegistryKey("publisher.filemgr.showwebharebackend",FALSE))
      )
    {
      IF(this->RunSimpleScreen("confirm", this->GetTid(".confirmenabletweak")) != "yes")
        RETURN FALSE;
    }

    OBJECT work:=this->BeginWork();
    this->tolliumuser->SetRegistryKey("publisher.prefs.propsafterupload", "propsafterupload" IN opts);
    this->tolliumuser->SetRegistryKey("publisher.prefs.nouploadrename", "nouploadrename" IN opts);
    this->tolliumuser->SetRegistryKey("publisher.prefs.groupfolders", "groupfolders" IN opts);
    this->tolliumuser->SetRegistryKey("publisher.prefs.showforeignfolders", "showforeignfolders" IN opts);
    this->tolliumuser->SetRegistryKey("publisher.prefs.showmenubar", "showmenubar" IN opts);
    this->tolliumuser->SetRegistryKey("publisher.prefs.autotitles", ^autotitles->value);

    IF(^tweaks->visible)
    {
      this->tolliumuser->SetRegistryKey("publisher.filemgr.showwhfsprivate", ^showwhfsprivate->value);
      this->tolliumuser->SetRegistryKey("publisher.filemgr.showwebharebackend", ^showwebharebackend->value);
    }
    RETURN work->Finish();
  }
>;
