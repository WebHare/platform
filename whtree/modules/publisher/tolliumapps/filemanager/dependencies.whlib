<?wh

LOADLIB "wh::ipc.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/remoting/client.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::publisher/lib/internal/webdesign/support.whlib";
LOADLIB "mod::publisher/lib/internal/blit/api.whlib";

PUBLIC OBJECTTYPE DepTab EXTEND TolliumScreenBase
<
  INTEGER fsobjectid;

  MACRO Init(RECORD data)
  {
    this->fsobjectid := data.fsobjectid;

    OBJECT depslocaltype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/localdesignsettings");
    IF(this->fsobjectid != 0)
    {
      RECORD depslocal := depslocaltype->GetInstanceData(this->fsobjectid);
      this->outputmode_js->value := depslocal.outputmode_js;
      this->outputmode_css->value := depslocal.outputmode_css;
    }
    ELSE
    {
      this->outputmode_js->enabled := FALSE;
      this->outputmode_css->enabled := FALSE;
    }
    IF (data.fsobjectid != 0)
      this->RefreshDeps();
  }

  MACRO OnOutputmodeChange()
  {
    //FIXME Really shouldn't take effect immediately, but the objectprops interface is too poor to allow us to synchronize with Ok/Cancel buttons
    IF(this->fsobjectid=0)
      RETURN;

    OBJECT depslocaltype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/localdesignsettings");
    RECORD depslocal := depslocaltype->GetInstanceData(this->fsobjectid);
    IF(depslocal.outputmode_js = this->outputmode_js->value AND depslocal.outputmode_css = this->outputmode_css->value)
      RETURN;

    OBJECT work := this->BeginWork();
    depslocaltype->SetInstanceData(this->fsobjectid,
        [ outputmode_js := this->outputmode_js->value
        , outputmode_css := this->outputmode_css->value
        ]);
    work->Finish();
    BroadcastEvent("blexdev_designfiles:resetdependencies", DEFAULT RECORD);
  }

  MACRO DoResetDependencies()
  {
    OBJECT work := this->BeginUnvalidatedFeedback();
    FOREVERY (RECORD rec FROM this->repositories->rows)
      IF (rec.localversionid = 0)
      {
        TRY
        {
          OBJECT repo := OpenDesignfilesRepository(rec.server, rec.repository);
          RECORD status := repo->GetStatus();

          UpdateRepositoryVersion(OpenWHFSObject(this->fsobjectid), rec.uuid, status.version, repo);

          CloseRemotingSession(repo);
        }
        CATCH(OBJECT e)
        {
          work->AddError(this->GetTid(".cannotopenrepository", e->what));
        }
      }
    work->Finish();
    this->RefreshDeps();
  }

  MACRO RefreshDeps()
  {
    RECORD settings := GetDependencySettings(this->fsobjectid);

    this->repositories->rows := SELECT *
                                     , rowkey := #repositories
                                     , localversionid := localversion
                                     , localversion := localversion != 0
                                          ? this->GetTid(".local", (SELECT AS STRING whfspath FROM system.fs_objects WHERE id = repositories.localversion))
                                          : this->GetTid(".remote", ToString(version))
                                  FROM settings.repositories;
  }

  MACRO DoAddRepository()
  {
    OBJECT screen := this->LoadScreen(".repository", [ fsobjectid := this->fsobjectid, repository := -1 ]);
    screen->RunModal();
    this->RefreshDeps();
  }

  MACRO DoEditRepository()
  {
    OBJECT screen := this->LoadScreen(".repository", [ fsobjectid := this->fsobjectid, repository := this->repositories->value ]);
    screen->RunModal();
    this->RefreshDeps();
  }

  MACRO DoEditDep()
  {
    OBJECT depeditor := this->LoadScreen(".editdep", [ fsobjectid := this->fsobjectid, dep := this->dependencies->value ]);
    depeditor->RunModal();
    this->RefreshDeps();
  }

  MACRO DoOverrideSettings()
  {
    OBJECT screen := this->LoadScreen(".overrides", [ fsobjectid := this->fsobjectid ]);
    screen->RunModal();
    this->RefreshDeps();
  }
>;

PUBLIC OBJECTTYPE EditDep EXTEND TolliumScreenBase
<
  INTEGER fsobjectid;
  STRING depname;

  MACRO Init(RECORD data)
  {
    this->fsobjectid := data.fsobjectid;
    this->depname := data.dep;

    OBJECT depstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/designfolder");
    RECORD currentdeps := depstype->GetInstanceData(this->fsobjectid);
    this->version->value := SELECT AS INTEGER version FROM currentdeps.dependencies WHERE dependencies.name = this->depname;
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();

    OBJECT depstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/designfolder");
    RECORD currentdeps := depstype->GetInstanceData(this->fsobjectid);
    UPDATE currentdeps.dependencies SET version := this->version->value WHERE dependencies.name = this->depname;
    depstype->SetInstanceData (this->fsobjectid, [ dependencies := currentdeps.dependencies ]);

    IF(NOT work->Finish())
      RETURN FALSE;

    BroadcastEvent("blexdev_designfiles:resetdependencies", DEFAULT RECORD);
    RETURN TRUE;
  }
>;

PUBLIC OBJECTTYPE Repository EXTEND TolliumScreenBase
<
  INTEGER fsobjectid;
  INTEGER reposnum;

  MACRO Init(RECORD data)
  {
    this->fsobjectid := data.fsobjectid;
    this->reposnum := data.repository;

    IF(this->reposnum!=-1)
    {
      OBJECT depstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/designfolder");
      OBJECT depslocaltype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/localdesignsettings");
      RECORD repository := depstype->GetInstanceData(this->fsobjectid).repositories[this->reposnum];
      RECORD depslocal := depslocaltype->GetInstanceData(this->fsobjectid);

      this->server->value := repository.server;
      this->repository->value := repository.repository;
      this->version->value := repository.version;
      this->localversion->value := SELECT AS INTEGER localversion FROM depslocal.repositories WHERE repositories.uuid = repository.uuid;
      this->bindtype->value := this->localversion->value != 0 ? "local" : "remote";
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    //Quick validation first
    OBJECT work := this->BeginFeedback();

    IF (this->bindtype->value = "local" AND this->localversion->value = 0)
      work->AddError(this->GetTid(".nolocalrepositoryset"));

    IF(work->HasFailed())
      RETURN work->Finish();

    //Now let's ensure the repository is actually up
    RECORD status;
    TRY
    {
      OBJECT repo := OpenDesignfilesRepository(this->server->value, this->repository->value);
      status := repo->GetStatus();

      IF (this->bindtype->value = "remote")
      {
        IF (this->version->value > status.version)
          work->AddError(this->GetTid(".toohighversion", ToString(status.version)));
        ELSE
        {
          IF (this->version->value = 0)
            this->version->value := status.version;

          // Make sure the selected version has been retrieved
          UpdateRepositoryVersion(OpenWHFSObject(this->fsobjectid), status.repository_uuid, this->version->value, repo);
        }
      }

      CloseRemotingSession(repo);
    }
    CATCH(OBJECT e)
    {
      work->AddError(this->GetTid(".cannotopenrepository", e->what));
    }

    IF (NOT work->Finish())
      RETURN FALSE;

    work := this->BeginWork();

    OBJECT depstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/designfolder");
    RECORD ARRAY repositories := depstype->GetInstanceData(this->fsobjectid).repositories;

    IF(RecordExists(SELECT FROM repositories WHERE repositories.uuid = status.repository_uuid AND #repositories != this->reposnum))
      work->AddError(this->GetTid(".duplicaterepository"));

    RECORD updaterepos :=
        [ server :=         this->server->value
        , repository :=     this->repository->value
        , uuid :=           status.repository_uuid
        , version :=        0
        ];

    INTEGER localversion;

    IF (this->bindtype->value = "local")
      localversion := this->localversion->value;
    ELSE
      updaterepos.version := this->version->value;

    IF(this->reposnum = -1)
      INSERT updaterepos INTO repositories AT END;
    ELSE
      repositories[this->reposnum] := updaterepos;

    depstype->SetInstanceData (this->fsobjectid, [ repositories := repositories ]);

    OBJECT depslocaltype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/localdesignsettings");
    RECORD depslocal := depslocaltype->GetInstanceData(this->fsobjectid);
    DELETE FROM depslocal.repositories WHERE repositories.uuid = status.repository_uuid;

    IF (localversion != 0)
    {
      INSERT
          [ uuid :=           status.repository_uuid
          , localversion :=   localversion
          ] INTO depslocal.repositories AT END;
    }
    depslocaltype->SetInstanceData(this->fsobjectid, depslocal);

    IF(NOT work->Finish())
      RETURN FALSE;

    BroadcastEvent("blexdev_designfiles:resetdependencies", DEFAULT RECORD);
    RETURN TRUE;
  }

  MACRO DoResetToCurrent()
  {
    OBJECT work := this->BeginUnvalidatedFeedback();
    TRY
    {
      OBJECT repo := OpenDesignfilesRepository(this->server->value, this->repository->value);
      RECORD status := repo->GetStatus();

      this->version->value := status.version;

      CloseRemotingSession(repo);
      work->Finish();
    }
    CATCH(OBJECT e)
    {
      work->AddError(this->GetTid(".cannotopenrepository", e->what));
    }
  }
>;
