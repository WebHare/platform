<?wh
LOADLIB "wh::files.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC STATIC OBJECTTYPE MDEditor EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    this->contexts->editdocumentapi->onsetenabled := PTR this->SetEnabled;
    this->contexts->editdocumentapi->onloaddata := PTR this->LoadData;
    this->contexts->editdocumentapi->onstoredata := PTR this->StoreData;
  }

  MACRO OnDirty()
  {
    IF (^dirtylistener->dirty)
      this->contexts->editdocumentapi->MarkDocumentAsDirty();
  }

  MACRO LoadData()
  {
    RECORD filedata := this->contexts->editdocumentapi->GetInstanceData("http://www.webhare.net/xmlns/publisher/markdownfile");
    ^markdowntext->value := RecordExists(filedata.data) ? BlobToString(filedata.data.text) : "";
    ^dirtylistener->ClearDirty();
  }

  MACRO StoreData()
  {
    RECORD data;
    STRING text := TrimWhitespace(^markdowntext->value);
    IF(text != "")
    {
      data := [ type := "publisher:markdown"
              , text := StringToBlob(text)
              ];
    }

    ^dirtylistener->ClearDirty();
    this->contexts->editdocumentapi->SetInstanceData("http://www.webhare.net/xmlns/publisher/markdownfile", CELL[data]);
  }

  MACRO SetEnabled(BOOLEAN enabled)
  {
    ^markdowntext->readonly := NOT enabled;
  }
>;
