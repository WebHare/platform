<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::javascript.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::system/lib/validation.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";


PUBLIC STATIC OBJECTTYPE AssetPackControlPanel EXTEND DashboardPanelBase
<
  OBJECT assetpackcontrol;
  OBJECT assetpackcontrolpromise;
  RECORD laststatus;

  UPDATE PUBLIC INTEGER FUNCTION GetSuggestedRefreshFrequency()
  {
    RETURN 0; //disable autorefresh
  }

  UPDATE PUBLIC MACRO RefreshDashboardPanel() //called whenever a panel should refresh itself (either manually invoked by user, or automatically)
  {
    IF(NOT ObjectExists(this->assetpackcontrolpromise))
      this->ResetConnection();

    RECORD ARRAY bundles;
    IF(RecordExists(this->laststatus))
      bundles := EnforceStructure([[ lastcompile := DEFAULT DATETIME ]], this->laststatus.bundles);

    ^bundles->rows := SELECT TEMPORARY status := hasstatus
                                                  ? haserrors
                                                        ? (iscompiling ? "COMPILING (was ERROR)" : requirecompile ? "DIRTY (was ERROR)" : "ERROR")
                                                        : iscompiling ? "COMPILING" : requirecompile ? "DIRTY" : "OK"
                                                  : ""
                           , rowkey := outputtag
                           , id
                           , status := status
                           , status_sort := status || " " || outputtag
                           , outputtag
                           , lastcompile
                           , watchcount
                           , dev := isdev ? 1 : 0
                           , dummy := ""
                           , compatibility := compatibility ?? whconstant_default_compatibility
                       FROM bundles;

    this->LoadConfig();
    this->OnBundleSelect();
  }

  MACRO LoadConfig()
  {
    ^suspendautocompile->value := ReadRegistryKey("publisher.bundledassets.suspendautocompile");
  }

  MACRO OnAutocompileChange()
  {
    IF(ReadRegistryKey("publisher.bundledassets.suspendautocompile") != ^suspendautocompile->value)
    {
      GetPrimary()->BeginWork();
      WriteRegistryKey("publisher.bundledassets.suspendautocompile", ^suspendautocompile->value);
      GetPrimary()->CommitWork();
      WaitForPromise(this->assetpackcontrol->Reload());
    }
  }

  /** @short This monitor has been activated, start refreshing */
  UPDATE PUBLIC MACRO EnableRefresh()
  {
    ^listener->enabled := TRUE;
    ^configlistener->enabled := TRUE;
  }
  /** @short This monitor has been deactivated, stop refreshing */
  UPDATE PUBLIC MACRO DisableRefresh()
  {
    ^listener->enabled := FALSE;
    ^configlistener->enabled := FALSE;
  }

  MACRO ResetConnection()
  {
    IF (ObjectExists(this->assetpackcontrol))
      this->assetpackcontrol->CloseService();

    this->assetpackcontrol := DEFAULT OBJECT;
    this->assetpackcontrolpromise := OpenWebHareService("platform:assetpacks", [ arguments := VARIANT["dashboard"]]);
    this->assetpackcontrolpromise->Then(PTR this->GotAssetPackControl)->OnError(PTR this->GotAssetPackError);
  }

  MACRO GotAssetPackError(OBJECT error)
  {
    LogHarescriptException(error);
    RegisterTimedCallback(AddTimeToDate(3000, GetCurrentDatetime()), PTR this->ResetConnection);
  }

  MACRO GotAssetPackControl(OBJECT controller)
  {
    IF(ObjectExists(this->assetpackcontrol))
      this->assetpackcontrol->CloseService();

    this->assetpackcontrol := controller;
    this->PollAssetPackStatus();
  }

  MACRO PollAssetPackStatus()
  {
    IF(ObjectExists(this->assetpackcontrol))
      this->assetpackcontrol->GetStatus()->Then(PTR this->GotAssetpackControlStatus)->OnError(PTR this->GotAssetPackError);
    ELSE
      this->RefreshDashboardPanel();
  }

  MACRO GotAssetpackControlStatus(RECORD status)
  {
    this->laststatus := status;
    this->RefreshDashboardPanel();
  }

  MACRO OnAssetEvent(RECORD ARRAY events)
  {
    FOREVERY (RECORD rec FROM events)
      IF (rec.event = "system:webhareservice.publisher:assetpackcontrol.start")
      {
        this->ResetConnection();
        RETURN;
      }

    this->PollAssetPackStatus();
  }
  MACRO OnConfigEvent(RECORD ARRAY events)
  {
    this->LoadConfig();
  }

  MACRO DoBundleProperties()
  {
    this->RunScreen(Resolve("assetpackcontrol.xml#bundleprops"),
                                   [ assetpackcontrol := this->assetpackcontrol
                                   , uuid := ^bundles->value[0]
                                   , id := ^bundles->selection[0].id
                                   ]);
  }

  MACRO OnBundleSelect()
  {
    STRING ARRAY bundleids := ^bundles->value;
    ^messages->rows := DEFAULT RECORD ARRAY;

    IF(Length(bundleids) = 1 AND ObjectExists(this->assetpackcontrol)) //TODO also request new messages if the new assetpackcontrol connection comes just *after* this onselect... TODO or just don't bother assetpackcontrol and read the stats fron disk/db directly
      this->assetpackcontrol->GetBundleStatus(bundleids[0])->Then(PTR this->ProcessBundleStatus(bundleids[0], #1));
  }

  MACRO ProcessBundleStatus(STRING bundleid, RECORD status)
  {
    STRING ARRAY bundleids := ^bundles->value;
    IF(Length(bundleids) != 1 OR bundleids[0] != bundleid)
      RETURN; //stale response

    RECORD ARRAY messages;
    IF(RecordExists(status))
      messages := RECORD ARRAY(status.messages);

    ^messages->rows := SELECT rowkey := 1 + #messages
                            , message := FormatValidationMessageWithType(status.messages)
                         FROM messages;
  }
  MACRO OnSelectMessage()
  {
    ^message->value := RecordExists(^messages->selection) ? ^messages->selection.message : "";
  }

  MACRO DoRecompileBundles()
  {
    IF(NOT ObjectExists(this->assetpackcontrol))
      WaitForPromise(this->assetpackcontrolpromise);
    IF(NOT ObjectExists(this->assetpackcontrol))
      RETURN;

    RECORD status;
    TRY
    {
      FOREVERY (RECORD rec FROM ^bundles->selection)
        WaitForPromise(this->assetpackcontrol->RecompileBundle(rec.outputtag));
    }
    CATCH (OBJECT e)
    {
      this->ResetConnection();
    }
  }
>;

PUBLIC STATIC OBJECTTYPE BundleProps EXTEND TolliumScreenBase
<
  OBJECT assetpackcontrol;
  STRING uuid;

  MACRO Init(RECORD data)
  {
    this->assetpackcontrol := data.assetpackcontrol;
    this->uuid := data.uuid;
    ^listener->masks := [ "publisher:assetpackcontrol.change." || data.uuid ];
    ^aboutmetafile->htmlvalue := this->GetHTMLTid(".aboutmetafile");

    RECORD settings := WaitForPromise(this->assetpackcontrol->GetBundleStatus(this->uuid));
    ^dev->value := settings.isdev;

    this->Reload();
  }

  MACRO DoDownloadmetafile(OBJECT filereceiver)
  {
    STRING metafile := CallJS("@mod-platform/js/assetpacks/support.ts#getAssetPackMetaDataFile", this->uuid);
    IF(Length(metafile) > 0)
      filereceiver->SendFile(StringToBlob(metafile), "application/json", "metafile-" || Substitute(this->uuid, ":", "-") || ".json");
    ELSE
      this->RunSimpleScreen("error", this->GetTid(".nometafile"));
  }

  MACRO GotEvents(RECORD ARRAY events)
  {
    this->Reload();
  }

  MACRO Reload()
  {
    IF(NOT ObjectExists(this->assetpackcontrol))
      RETURN;

    RECORD status := WaitForPromise(this->assetpackcontrol->GetBundleStatus(this->uuid));
    IF(NOT RecordExists(status))
    {
      this->tolliumresult := "cancel";
      RETURN;
    }

    STRING messages;
    FOREVERY(RECORD msg FROM status.messages)
      messages := messages || FormatValidationMessageWithType(msg);

    ^filedependencies->rows := ToRecordArray(status.filedependencies, "path");
    ^missingdependencies->rows := ToRecordArray(status.missingdependencies,"path");
    ^outputtag->value := this->uuid;
    ^errors->value := messages;
    ^entrypoint->value := status.entrypoint;
    IF (RecordExists(status.bundleconfig))
    {
      ^extrarequires->value := Detokenize(STRING ARRAY(status.bundleconfig.extrarequires),'\n');
      ^languages->value := Detokenize(STRING ARRAY(status.bundleconfig.languages),', ');
      ^environment->value := status.bundleconfig.environment;
    }
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginFeedback();
    IF(NOT work->HasFailed())
      WaitForPromise(this->assetpackcontrol->UpdateBundleSettings(this->uuid, CELL[ dev := ^dev->value ]));
    RETURN work->Finish();
  }
>;
