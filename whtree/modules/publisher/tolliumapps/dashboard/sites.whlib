<?wh

LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/langspecific.whlib";

LOADLIB "mod::publisher/lib/control.whlib"; // republish functions
LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";

LOADLIB "mod::tollium/lib/dialogs.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";


INTEGER max_publishers_at_once := 10;
INTEGER max_tabs_at_once := 20;


PUBLIC STATIC OBJECTTYPE Sites EXTEND DashboardPanelBase
<
  RECORD ARRAY sites;

  UPDATE PUBLIC INTEGER FUNCTION GetSuggestedRefreshFrequency()
  {
    RETURN 0;
  }

  UPDATE PUBLIC MACRO RefreshDashboardPanel() //called whenever a panel should refresh itself (either manually invoked by user, or automatically)
  {
    this->RefreshSitesList();
  }

  MACRO Init()
  {
    this->RefreshFilterOptions();
    this->OnLinkedToTypeChange();
    this->frame->flags.issysop := this->contexts->user->HasRight("system:sysop");
  }

  MACRO DoExport()
  {
    RECORD siteexport :=
        CELL[ rows := this->sites
            , columns :=  [[ name := "name",        type := "string", title := this->GetTid(".name") ]
                          ,[ name := "sitedesign",  type := "string", title := GetTid("publisher:commondialogs.siteprops.sitedesign") ]
                          ,[ name := "webroot",     type := "string", title := this->GetTid(".url") ]
                          ,[ name := "webfeatures", type := "string", title := GetTid("publisher:commondialogs.siteprops.webfeatures") ]
                          ,[ name := "cdnbaseurl",  type := "string", title := GetTid("publisher:commondialogs.siteprops.cdnbaseurl") ]
                          ,[ name := "comments",    type := "string", title := GetTid("~comments") ]
                          ]
            , exporttitle := "Sites"
            , contexts := this->contexts
            ];

    siteexport := RunModuleHookTarget("publisher:dashboard_sites_export", siteexport);
    RunColumnFileExportDialog(this, CELL[...siteexport, DELETE contexts ]);
  }

  MACRO OnLinkedToTypeChange()
  {
    this->UpdateVisibleFilters();
    this->RefreshSitesList();
  }

  MACRO UpdateVisibleFilters()
  {
    ^webdesign->visible := ^linkedtotype->value = "webdesign";
    ^webfeature->visible := ^linkedtotype->value = "webfeature";
  }

  MACRO RefreshFilterOptions()
  {
    RECORD ARRAY sites :=
            SELECT id
                 , name
                 , webroot
                 , outputweb
                 , cdnbaseurl
              FROM system.sites
          ORDER BY NormalizeText(name, this->tolliumuser->language);

    OBJECT sitesettingstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/sitesettings");
    sites := sitesettingstype->Enrich(sites, "id", ["sitedesign", "webfeatures", "comments"]);

    this->sites := sites;

    RECORD ARRAY webdesigns :=
          [ [ rowkey := "", title := "" ] ]
          CONCAT
          SELECT TEMPORARY _title := GetTid(title) || " (" || Length(SELECT FROM sites WHERE sitedesign = wdesign.rowkey OR (wdesign.rowkey = "publisher:nodesign" AND sitedesign = "")) || ")"
               , rowkey
               , title := _title
            FROM GetAvailableWebDesigns(FALSE) AS wdesign
        ORDER BY NormalizeText(_title, this->tolliumuser->language);

    ^webdesign->options := webdesigns;

    RECORD ARRAY webfeatures :=
          [ [ rowkey := "", title := "" ] ]
          CONCAT
          SELECT TEMPORARY _title := GetTid(title) || " (" || Length(SELECT FROM sites WHERE wfeature.rowkey IN sites.webfeatures) || ")"
               , rowkey
               , title := _title
            FROM GetAvailableWebFeatures() AS wfeature
        ORDER BY NormalizeText(_title, this->tolliumuser->language);

    ^webfeature->options := webfeatures;

    ^weboutput->options :=
          [ [ rowkey := 0, title := "" ] ]
          CONCAT
          SELECT TEMPORARY _title := baseurl || " (" || Length(SELECT FROM sites WHERE outputweb = webservers.id) || ")"
               , rowkey := id
               , title  := _title
            FROM system.webservers
           WHERE type != 6 // type 6 is a group;
        ORDER BY NormalizeText(_title, this->tolliumuser->language);
  }

  MACRO RefreshSitesList()
  {
    INTEGER icon_positive := ^siteslist->GetIcon("tollium:status/checked");
    INTEGER icon_negative := ^siteslist->GetIcon("tollium:status/negative");

    STRING querylike;
    IF (^query->value != "")
      querylike := "*" || ToUpperCase(^query->value) || "*";

    RECORD ARRAY rows :=
        SELECT rowkey := id
             , name
             , webdesign := sitedesign
             , hasactivefeatures := Length(webfeatures) > 0  ? icon_positive : icon_negative
             , activefeatures := DeTokenize(webfeatures, ", ")
             , url := webroot
             , canview := webroot != "" // not locked, has output, ...
             , comments
          FROM this->sites
         WHERE (^weboutput->value = 0  ? TRUE : outputweb  = ^weboutput->value)
           AND (^linkedtotype->value != "webdesign"  OR ^webdesign->value = ""   ? TRUE : (^webdesign->value  =  sitedesign OR (^webdesign->value = "publisher:nodesign" AND sitedesign = "")))
           AND (^linkedtotype->value != "webfeature" OR ^webfeature->value = ""  ? TRUE : ^webfeature->value  IN webfeatures)
           AND (^query->value = ""     ? TRUE : (ToUpperCase(name || " " || sitedesign || " " || webroot || " " || comments) LIKE querylike));

    ^siteslist->rows := rows;

    INTEGER results := Length(rows);
    IF (results = 0)
      ^queryresultsinfo->value := this->GetTid(".querymatched-none");
    ELSE IF (results = 1)
      ^queryresultsinfo->value := this->GetTid(".querymatched-single");
    ELSE
      ^queryresultsinfo->value := this->GetTid(".querymatched", ToString(Length(rows)));
  }

  MACRO OnViewBrowserList(OBJECT openhandler)
  {
    openhandler->SendURL(^sitelist->selection[0].url);
  }

  MACRO DoShowInNewPublishers()
  {
    RECORD ARRAY selection := ^siteslist->selection;
    INTEGER items := Length(selection);

    IF (items > max_publishers_at_once)
    {
      this->RunSimpleScreen("info", this->GetTid(".toomanytoopen_publisher", ToString(max_publishers_at_once)));
      RETURN;
    }

    IF (items > 3 AND this->RunSimpleScreen("confirm", this->GetTid(".suretoopenmultiple_publisher", ToString(items))) != "yes")
      RETURN;

    FOREVERY(RECORD site FROM selection)
      RunPublisherFileManager(this, site.rowkey);
  }

  MACRO DoOpenSiteProperties()
  {
    // RunFSObjectPropertiesDialog doesn't return if anything has changed, so assume something has changed and update filters and list
    RunFSObjectPropertiesDialog(this, ^siteslist->value[0], [ siteproperties := TRUE ]);
    this->RefreshFilterOptions();
    this->RefreshSitesList();
  }

  MACRO DoUpdateDesign()
  {
    // Update filters and list if anything changed (siteprops return -1 if nothing changed)
    IF (this->RunScreen("#siteprops", [ sites := ^siteslist->selection ]) > 0)
    {
      this->RefreshFilterOptions();
      this->RefreshSitesList();
    }
  }

  MACRO DoRepublish()
  {
    RECORD ARRAY selecteditems := ^siteslist->selection;

    OBJECT work := this->BeginUnvalidatedWork();

    FOREVERY(RECORD sel FROM selecteditems)
      ScheduleFolderRepublish(sel.rowkey, TRUE, FALSE);

    work->Finish();
  }
>;

PUBLIC STATIC OBJECTTYPE SiteProps EXTEND TolliumScreenBase
<
  RECORD ARRAY sites;

  MACRO Init(RECORD data)
  {
    this->sites := data.sites;

    STRING ARRAY cursitedesigns :=
        SELECT AS STRING ARRAY webdesign ?? "publisher:nodesign"
          FROM this->sites;

    RECORD ARRAY sitedesigns :=
        SELECT rowkey
             , title := GetTid(title)
             , tolliumselected := rowkey IN cursitedesigns
          FROM GetAvailableWebDesigns(FALSE)
         WHERE rowkey IN VAR cursitedesigns OR NOT hidden;

    // Add rows for designs that don't exist
    sitedesigns := sitedesigns CONCAT
        SELECT rowkey := design
             , title := design
             , tolliumselected := TRUE
          FROM ToRecordArray(ArrayDelete(cursitedesigns, SELECT AS STRING ARRAY rowkey FROM sitedesigns), "design");

    // If sites with different designs are selected, add a 'multiple designs' row
    IF (Length(SELECT FROM sitedesigns WHERE tolliumselected) > 1)
    {
      UPDATE sitedesigns SET tolliumselected := FALSE;
      INSERT [ rowkey := "", title := this->GetTid(".multipledesigns"), tolliumselected := TRUE ] INTO sitedesigns AT END;
    }

    ^sitedesign->options :=
        SELECT *
          FROM sitedesigns
         ORDER BY rowkey != "" // 'multiple designs' first
                , ToUppercase(title);
  }

  INTEGER FUNCTION Submit()
  {
    IF (^sitedesign->value = "")
      RETURN -1; // 'multiple designs' selected, don't update

    RECORD ARRAY toupdate := SELECT * FROM this->sites WHERE webdesign != ^sitedesign->value;
    IF (Length(toupdate) = 0)
      RETURN -1; // Nothing changed

    IF (this->RunSimpleScreen("verify", this->GetTid(".confirmupdatedesigns", ToString(Length(toupdate)), toupdate[0].name, ^sitedesign->selection.title)) != "yes")
      RETURN 0;

    OBJECT work := this->BeginWork();
    OBJECT sitesettingstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/sitesettings");
    FOREVERY (RECORD site FROM toupdate)
      sitesettingstype->SetInstanceData(site.rowkey, [ sitedesign := ^sitedesign->value ]);
    RETURN work->Finish() ? 1 : 0;
  }
>;
