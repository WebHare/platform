<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/compiler.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/support.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

BOOLEAN FUNCTION IsReferenced(INTEGER typid)
{
  RETURN RecordExists(SELECT FROM system.fs_objects AS objects WHERE type = typid LIMIT 1);
}

///list the contenttypes known in system.fs_types
RECORD ARRAY FUNCTION ListFSContentTypes(STRING mask)
{
  RECORD ARRAY contenttypes :=
    SELECT id
         , namespace
         , candelete := orphan //AND NOT this->IsReferenced(namespace)
         , isfoldertype
         , orphansince
      FROM system_internal.fs_types
     WHERE (VAR mask != "*" ? ToUppercase(namespace) LIKE ToUppercase(mask) : TRUE);

  RECORD ARRAY iconinfo := SELECT id
                                , tolliumicon := RecordExists(filetype) ? filetype.tolliumicon : foldertype.tolliumicon
                             FROM GetCachedSiteProfiles().contenttypes
                            WHERE RecordExists(filetype) OR RecordExists(foldertype);

  contenttypes := JoinArrays(contenttypes, "id", iconinfo, [ tolliumicon := "" ], [ rightouterjoin := TRUE ]);

  UPDATE contenttypes SET candelete := FALSE WHERE candelete = TRUE AND IsReferenced(id);
  RETURN contenttypes;
}

PUBLIC STATIC OBJECTTYPE Panel EXTEND DashboardPanelBase
<
  UPDATE PUBLIC INTEGER FUNCTION GetSuggestedRefreshFrequency()
  {
    RETURN 0;
  }

  MACRO DoRecompile()
  {
    RECORD result := RecompileSiteProfiles();
    IF (Length(result.errors) > 0 OR Length(result.warnings) > 0)
    {
      // Group errors by file, show fatal errors first, order by line number
      RECORD ARRAY messages := (SELECT *, iserror := TRUE FROM result.errors)
                               CONCAT
                               (SELECT *, iserror := FALSE FROM result.warnings);

      this->RunScreen("#ctypeerrors", CELL[messages]);
      RETURN;
    }
    ELSE
    {
      this->RunSimpleScreen("info", this->GetTid(".siteprofilesrecompiled"));
    }
    this->RefreshDashboardPanel();
  }

  UPDATE PUBLIC MACRO RefreshDashboardPanel()
  {
    //Gather our information..
    STRING mask := ^filter->value != "" ? Substitute("*" || Substitute(TrimWhitespace(NormalizeWhitespace(^filter->value)), " ", "*") || "*", "**", "*") : "*";
    RECORD ARRAY contenttypes := ListFSContentTypes(mask);
    ^types->rows := SELECT *
                         , rowkey := id
                         , listrowclasses:= candelete ? ["grayedout"] : STRING[]
                         , typeicon := tolliumicon != "" ? ^types->GetIcon(tolliumicon) : 0
                      FROM contenttypes;
  }

  MACRO GotFilterChange()
  {
    this->RefreshDashboardPanel();
  }

  MACRO DoViewType()
  {
    this->RunScreen("#ctypemembers", [ ctypeid := ^types->value, namespace := ^types->selection.namespace ]);
  }

  MACRO DoDelete()
  {
    IF(this->RunSimpleScreen("confirm", this->GetTid(".deletectypeconfirm", ^types->selection.namespace)) != "yes")
      RETURN;

    OBJECT work := this->BeginWork();

    DELETE FROM system.fs_types WHERE id = ^types->value;
    GetWHFSCommitHandler()->FSTypesChanged();

    IF (work->Finish())
      this->RefreshDashboardPanel();
  }
>;

PUBLIC STATIC OBJECTTYPE CTypeMembers EXTEND TolliumScreenBase
<
  INTEGER ctypeid;
  INTEGER parentid;

  MACRO Init(RECORD data)
  {
    RECORD type := DescribeContentTypeById(data.ctypeid);
    this->ctypeid := data.ctypeid;

    IF (RecordExists(type))
    {
      ^siteprofile->value := type.siteprofile;
      ^linenum->value := ToString(type.line);
    }
    ELSE
    {
      ^locationbox->visible := FALSE;
    }
  }

  RECORD ARRAY FUNCTION GetMembers(INTEGER ctypeid, INTEGER parentid)
  {
    RECORD ARRAY ctypemembers := SELECT rowkey := id
                                      , name
                                      , type
                                      , typename := GetFSMemberTypeName(type) ?? "#" || type
                                      , publish
                                      , candelete := orphan
                                      , expanded := FALSE
                                      , orphansince
                                   FROM system_internal.fs_members
                                  WHERE fs_type = ctypeid
                                        AND fs_members.parent = parentid
                               ORDER BY ToUpperCase(name);

    RETURN SELECT *
                , expandable := type = 12
                , listrowclasses:= candelete ? ["grayedout"] : STRING[]
                , publishicon := type = 5 ? (publish ? 2 : 1) : 0
             FROM ctypemembers;
  }

  RECORD ARRAY FUNCTION OnGetChildren(RECORD baserow)
  {
    IF (NOT RecordExists(baserow))
      RETURN this->GetMembers(this->ctypeid, 0);

    RETURN this->GetMembers(this->ctypeid, baserow.rowkey);
  }

  INTEGER ARRAY FUNCTION OnGetPath(INTEGER rowkey)
  {
    RETURN DEFAULT INTEGER ARRAY;
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".deletectypememberconfirm", ^members->selection.name)) != "yes")
      RETURN;

    OBJECT work := this->BeginWork();

    DELETE FROM system.fs_members WHERE id = ^members->value;
    GetWHFSCommitHandler()->FSTypesChanged();

    IF (work->Finish())
      ^members->ReloadList();
  }
>;

PUBLIC STATIC OBJECTTYPE CTypeErrors EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    ^messagelist->rows := SELECT TEMPORARY file := GetNameFromPath(resourcename)
                               , file := file
                               , fullpath := resourcename
                               , line
                               , message
                               , icon := iserror ? 2 : 1
                            FROM data.messages
                        ORDER BY ToUppercase(file), line, col;
  }
>;
