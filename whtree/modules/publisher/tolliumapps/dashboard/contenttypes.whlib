<?wh
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";

LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/compiler.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/support.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/screenbase.whlib";
LOADLIB "mod::system/lib/internal/whfs/events.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

PUBLIC STATIC OBJECTTYPE Panel EXTEND DashboardPanelBase
<
  UPDATE PUBLIC INTEGER FUNCTION GetSuggestedRefreshFrequency()
  {
    RETURN 0;
  }

  MACRO DoSearchFSObjects()
  {
    this->contexts->controller->SendApplicationMessage("publisher:app", CELL[ search := CELL[ whfstype := ^types->selection.namespace ] ], DEFAULT RECORD, TRUE);
  }
  MACRO DoSearchWidgets()
  {
    this->contexts->controller->SendApplicationMessage("publisher:app", CELL[ search := CELL[ widgettype := ^types->selection.namespace ] ], DEFAULT RECORD, TRUE);
  }

  MACRO DoRecompile()
  {
    RECORD result := RecompileSiteProfiles();
    // Group errors by file, show fatal errors first, order by line number
    RECORD ARRAY messages := SELECT *, iserror := type = "error" FROM result.messages WHERE type IN ["error","warning"] ORDER BY type="error" DESC;
    IF (Length(messages) > 0) {
      this->RunScreen("#ctypeerrors", CELL[messages]);
    } ELSE {
      this->RunSimpleScreen("info", this->GetTid(".siteprofilesrecompiled"));
    }
    this->RefreshDashboardPanel();
  }

  UPDATE PUBLIC MACRO RefreshDashboardPanel()
  {
    //Gather our information..
    RECORD ARRAY contenttypes := CallJS("@mod-platform/js/devsupport/siteprofiles.ts#listFSContentTypes", `*${^filter->value}*`);
    ^types->rows := SELECT *
                         , rowkey := id
                         , listrowclasses:= candelete ? ["grayedout"] : STRING[]
                         , typeicon := tolliumicon != "" ? ^types->GetIcon(tolliumicon) : 0
                         , iswidgettype := type = "widgettype"
                         , isfsobjecttype := type = "filetype" OR type = "foldertype" OR type = "widgettype"
                      FROM contenttypes;
  }

  MACRO GotFilterChange()
  {
    this->RefreshDashboardPanel();
  }

  MACRO DoViewType()
  {
    this->RunScreen("#ctypemembers", [ ctypeid := ^types->value, namespace := ^types->selection.namespace ]);
  }

  MACRO DoDelete()
  {
    IF(this->RunSimpleScreen("confirm", this->GetTid(".deletectypeconfirm", ^types->selection.namespace)) != "yes")
      RETURN;

    OBJECT work := this->BeginWork();

    DELETE FROM system.fs_types WHERE id = ^types->value;
    GetWHFSCommitHandler()->FSTypesChanged();

    IF (work->Finish())
      this->RefreshDashboardPanel();
  }
>;

PUBLIC STATIC OBJECTTYPE CTypeMembers EXTEND TolliumScreenBase
<
  INTEGER ctypeid;
  INTEGER parentid;

  MACRO Init(RECORD data)
  {
    RECORD type := DescribeContentTypeById(data.ctypeid);
    this->ctypeid := data.ctypeid;

    IF (RecordExists(type))
    {
      ^siteprofile->value := type.siteprofile;
      ^linenum->value := ToString(type.line);
    }
    ELSE
    {
      ^locationbox->visible := FALSE;
    }
  }

  RECORD ARRAY FUNCTION GetMembers(INTEGER ctypeid, INTEGER parentid)
  {
    RECORD ARRAY ctypemembers := SELECT rowkey := id
                                      , name
                                      , type
                                      , typename := GetFSMemberTypeName(type) ?? "#" || type
                                      , candelete := orphan
                                      , expanded := FALSE
                                      , orphansince
                                   FROM system_internal.fs_members
                                  WHERE fs_type = ctypeid
                                        AND fs_members.parent = parentid
                               ORDER BY ToUpperCase(name);

    RETURN SELECT *
                , expandable := type = 12
                , listrowclasses:= candelete ? ["grayedout"] : STRING[]
             FROM ctypemembers;
  }

  RECORD ARRAY FUNCTION OnGetChildren(RECORD baserow)
  {
    IF (NOT RecordExists(baserow))
      RETURN this->GetMembers(this->ctypeid, 0);

    RETURN this->GetMembers(this->ctypeid, baserow.rowkey);
  }

  INTEGER ARRAY FUNCTION OnGetPath(INTEGER rowkey)
  {
    RETURN DEFAULT INTEGER ARRAY;
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".deletectypememberconfirm", ^members->selection.name)) != "yes")
      RETURN;

    OBJECT work := this->BeginWork();

    DELETE FROM system.fs_members WHERE id = ^members->value;
    GetWHFSCommitHandler()->FSTypesChanged();

    IF (work->Finish())
      ^members->ReloadList();
  }
>;

PUBLIC STATIC OBJECTTYPE CTypeErrors EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    ^messagelist->rows := SELECT TEMPORARY file := GetNameFromPath(resourcename)
                               , file := file
                               , fullpath := resourcename
                               , line
                               , message
                               , icon := iserror ? 2 : 1
                            FROM data.messages
                        ORDER BY ToUppercase(file), line, col;
  }
>;
