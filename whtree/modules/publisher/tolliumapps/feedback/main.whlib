<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::float.whlib";
LOADLIB "wh::witty.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/resources.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


CONSTANT FLOAT maxscreenshotsize := 640;

PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
<
  MACRO NEW()
  {
    this->contexts->wrdschema := OpenWRDSchema("publisher:feedback");
  }

  MACRO Init(RECORD data)
  {
    INTEGER scopeid := this->contexts->wrdschema->^scope->Search("tag", "tollium:webharebackend");
    ^feedback->listfilters := [ [ field := "wrd_leftentity", value := scopeid ] ];
  }

  MACRO DoView()
  {
    this->RunScreen("#feedback", ^feedback->selection);
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("verify", this->GetTid(".messages.confirmdeletefeedback")) = "yes")
    {
      OBJECT work := this->BeginWork();
      this->contexts->wrdschema->^feedback->UpdateEntity(^feedback->selection.wrd_id, [ wrd_limitdate := GetCurrentDateTime() ]);
      work->Finish();
    }
  }

  MACRO DoExit()
  {
    this->tolliumresult := "close";
  }
>;

PUBLIC STATIC OBJECTTYPE Feedback EXTEND TolliumScreenBase
<
  RECORD data;

  MACRO Init(RECORD data)
  {
    this->data := data;
    ^data->value := data;

    IF (CellExists(this->data.extradata, "extrafields") AND TypeID(this->data.extradata.extrafields) = TypeID(RECORD ARRAY))
    {
      ^noextradata->DeleteComponent();
      FOREVERY (RECORD extra FROM this->data.extradata.extrafields)
      {
        extra := EnforceStructure(
            [ field := ""
            , type := ""
            , title := ""
            , tid := ""
            , optiongid := ""
            ], extra);
        IF (NOT CellExists(this->data.extradata, extra.field))
          CONTINUE;

        OBJECT comp;
        SWITCH (extra.type)
        {
          CASE "string" {
            comp := this->CreateTolliumComponent("text");
            comp->value := GetCell(this->data.extradata, extra.field);
          }
          CASE "integer" {
            comp := this->CreateTolliumComponent("text");
            comp->value := ToString(GetCell(this->data.extradata, extra.field));
          }
          CASE "tid" {
            comp := this->CreateTolliumComponent("text");
            STRING tid := GetCell(this->data.extradata, extra.field);
            IF (tid NOT LIKE "*:*" AND tid NOT LIKE "*.*")
            {
              IF (extra.optiongid != "")
                tid := extra.optiongid || "." || tid;
              ELSE
                tid := extra.tid || "-" || tid;
            }
            IF (tid LIKE "*:*")
              comp->value := GetTid(tid);
            ELSE
              comp->value := tid;
          }
        }

        IF (ObjectExists(comp))
        {
          comp->title := extra.tid != "" ? GetTid(extra.tid) : extra.title;
          IF (comp->componenttype = "text")
            comp->selectable := TRUE;
          this->frame->bodynode->InsertComponentAfter(comp, DEFAULT OBJECT, TRUE);
        }
      }
    }

    ^screenshot->value := LoadWittyLibrary(Resolve("screenshot.witty"), "HTML-NI")->RunComponentToString("container", CELL
        [ width := INTEGER(Ceil(data.size.width))
        , height := INTEGER(Ceil(data.size.height))
        , url := ^screenshot->AddEmbeddedObject(this->GenerateScreenshotHtml(), "text/html") || "&__test=sandbox"
        , scaled := ^scaled->value
        ]);
  }

  BLOB FUNCTION GenerateScreenshotHtml()
  {
    STRING ARRAY stylesheets := this->data.screenshot.stylesheets;
    INSERT BlobToString(GetWebHareResource("mod::publisher/js/feedback/styles.css")) INTO stylesheets AT END;

    RETURN LoadWittyLibrary(Resolve("screenshot.witty"), "HTML-NI")->RunComponentToBlob("screenshot", CELL
        [ this->data.screenshot.htmlattrs
        , stylesheets
        , this->data.screenshot.bodyattrs
        , this->data.screenshot.bodycontents
        , element := EnforceStructure([ top := 0, left := 0, width := 0, height := 0 ], this->data.element)
        ]);
  }

  MACRO OnScaleIframe()
  {
    ^screenshot->PostMessage([ type := "scaled", scaled := ^scaled->value ], "*");
  }

  MACRO DoDownload(OBJECT handler)
  {
    handler->SendFile(this->GenerateScreenshotHtml(), "text/html", `screenshot-${this->tolliumuser->FormatDateTime(this->data.wrd_creationdate, "minutes", TRUE, TRUE)}.html`);
  }
>;
