<?wh

LOADLIB "wh::float.whlib";
LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::system/lib/configure.whlib";

LOADLIB "mod::tollium/lib/dialogs.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/internal/support.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/screenbase.whlib";


CONSTANT FLOAT maxscreenshotsize := 640;
OBJECT issuewitty := LoadWittyLibrary(Resolve("issues.witty"), "HTML-NI");

PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Initialization
  //
  BOOLEAN doneinit;
  RECORD ARRAY forwardactions;

  MACRO NEW()
  {
    this->contexts->wrdschema := OpenWRDSchema("publisher:feedback");
  }

  MACRO Init(RECORD data)
  {
    ^status->SetValueIfValid(this->contexts->user->GetRegistryKey("publisher.feedback.laststatus", ""));
    this->SetRowlayout(this->contexts->user->GetRegistryKey("publisher.feedback.feedbackrowlayout", "multirow"));

    ^feedback->eventmasks := this->contexts->wrdschema->^feedback->GetEventMasks();
    ^loglistener->masks := this->contexts->wrdschema->^log->GetEventMasks();

    ^feedbackdetails->contents := this->LoadScreen(Resolve("main.xml#feedback"), [ embedded := TRUE ]);

    this->frame->flags.issysop := this->contexts->user->HasRight("system:sysop");
    this->ReloadInterface();
    this->doneinit := TRUE;

    IF(CellExists(data.target, 'feedback'))
      ^feedback->SetValueIfValid(data.target.feedback);
    ELSE IF(Length(data.params) > 0)
    {
      INTEGER fbid := this->contexts->wrdschema->^feedback->Search("feedbackid", data.params[0]);
      IF(fbid != 0)
        ^feedback->SetValueIfValid(fbid);
    }
  }

  MACRO ReloadInterface()
  {
    RECORD ARRAY roles :=
           SELECT *
             FROM this->contexts->wrdschema->^role->RunQuery([ outputcolumns := CELL[ rowkey := "wrd_id", title := "wrd_title", "wrd_ordering" ] ])
         ORDER BY wrd_ordering
                , ToUppercase(title)
                , rowkey;
    ^topic->options := ^topic->options CONCAT
        SELECT *
             , DELETE wrd_ordering
          FROM this->contexts->wrdschema->^topic->RunQuery([ outputcolumns := CELL[ rowkey := "wrd_guid", title := "wrd_title", "wrd_ordering" ] ])
         ORDER BY wrd_ordering
                , ToUppercase(title)
                , rowkey;
    ^role->options := RECORD[ ^role->options[0], ...roles ];

    FOREVERY(RECORD fwd FROM this->forwardactions)
    {
      fwd.action->DeleteComponent();
      fwd.menuitem->DeleteComponent();
    }
    this->forwardactions := RECORD[];

    FOREVERY(RECORD role FROM roles)
    {
      RECORD fwd := CELL[ action := this->CreateTolliumComponent("action")
                        , menuitem := this->CreateTolliumComponent("menuitem")
                        ];

      fwd.action->onexecute := PTR this->DoForwardTo(role.rowkey);
      fwd.menuitem->title := role.title;
      fwd.menuitem->action := fwd.action;
      INSERT fwd INTO this->forwardactions AT END;
    }

    ^forwardtomenu->items := SELECT menuitem, isdivider := FALSE FROM this->forwardactions;
    ^forwardtoitem->items := SELECT menuitem, isdivider := FALSE FROM this->forwardactions;
  }

  MACRO DoForwardTo(INTEGER targetrole)
  {
    OBJECT work := this->BeginUnvalidatedWork();
    UpdateFeedbackItem(this->contexts->wrdschema, ^feedback->value, [ role := targetrole ], this->contexts->user->GetUserDataForLogging());
    work->Finish();
  }

  //----------------------------------------------------------------------------
  //
  // Action handlers
  //

  MACRO DoEscalate()
  {
    STRING message := this->RunScreen("#escalate");
    IF (message = "")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    this->contexts->wrdschema->^feedback->UpdateEntity(^feedback->value, [ status := "escalated" ]);
    this->contexts->wrdschema->^log->CreateEntity(
        [ wrd_leftentity := ^feedback->value
        , statuschange := "escalated"
        , text := message
        , webhareuser := this->contexts->user->GetUserDataForLogging()
        ]);
    work->Finish();
  }

  MACRO DoClose()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".messages.confirmclosefeedback")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    this->contexts->wrdschema->^feedback->UpdateEntity(^feedback->value, [ status := "closed" ]);
    this->contexts->wrdschema->^log->CreateEntity(
        [ wrd_leftentity := ^feedback->value
        , statuschange := "closed"
        , webhareuser := this->contexts->user->GetUserDataForLogging()
        ]);
    work->Finish();
  }

  MACRO DoReopen()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".messages.confirmreopenfeedback")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    this->contexts->wrdschema->^feedback->UpdateEntity(^feedback->value, [ status := "new" ]);
    this->contexts->wrdschema->^log->CreateEntity(
        [ wrd_leftentity := ^feedback->value
        , statuschange := "new"
        , webhareuser := this->contexts->user->GetUserDataForLogging()
        ]);
    work->Finish();
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".messages.confirmdeletefeedback")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    // Don't archive it, assume deleted feedback may contain PII. If random deletion becomes a problem, solve it with rights
    this->contexts->wrdschema->^feedback->DeleteEntity(^feedback->value);
    work->Finish();
  }

  MACRO DoToggleFilter()
  {
    ^filterpanel->visible := NOT ^filterpanel->visible;
    ^listfilterbtn->pressed := ^filterpanel->visible;
  }

  MACRO DoSetFeedbackMulti()
  {
    this->SetRowLayout("multirow");
  }

  MACRO DoSetFeedbackSingle()
  {
    this->SetRowLayout("singlerow");
  }

  MACRO DoExport()
  {
    RECORD ARRAY rows := this->contexts->wrdschema->^feedback->RunQuery(
        [ outputcolumns := CELL[ "wrd_id", "feedbackid", "userdata", "type", "remarks", "wrd_creationdate" ]
        , filters := [ [ field := "wrd_leftentity", value := tollium_feedback_scope ]
                     ]
        ]);

    rows := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], userdata)
                 , *
                 , reportedby      := reportinfo.realname ?? reportinfo.login
                 , reportedbyemail := reportinfo.emailaddress
              FROM rows;

    rows := this->contexts->wrdschema->^feedbacktype->Enrich(rows, "TYPE", [ type := "wrd_title" ], [ rightouterjoin := TRUE ]);
    rows := SELECT *, type := GetTid(type) FROM rows ORDER BY wrd_creationdate;

    RECORD ARRAY log := this->contexts->wrdschema->^log->RunQuery(
        [ outputcolumns := CELL[ "wrd_creationdate", "wrd_leftentity", "text", "webhareuser" ]
        , filters := [ [ field := "wrd_leftentity", value := SELECT AS INTEGER ARRAY wrd_id FROM rows ]
                     ]
        ]);

    log := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], webhareuser)
                 , *
                 , reportedby      := reportinfo.realname ?? reportinfo.login
                 , reportedbyemail := reportinfo.emailaddress
              FROM log;

    FOREVERY(RECORD row FROM rows)
    {
      RECORD ARRAY logentries := SELECT * FROM log WHERE wrd_leftentity = row.wrd_id ORDER BY wrd_creationdate;
      STRING logtext;

      FOREVERY(RECORD entry FROM logentries)
      {
        //Spaces to show properly when excel puts all on one line
        logtext := `${logtext}${entry.reportedby} ${this->tolliumuser->FormatDatetime(entry.wrd_creationdate, "minutes", TRUE, TRUE)}   \n${entry.text}   \n\n`;
      }
      INSERT CELL issuelog := logtext INTO rows[#row];
    }

    RunColumnFileExportDialog(this,
      CELL[ rows
          , columns := [[ type := "text",     name := "feedbackid",           title := this->GetTid(".feedbackid") ]
                       ,[ type := "datetime", name := "wrd_creationdate",     title := this->GetTid(".wrd_creationdate"), storeutc := TRUE ]
                       ,[ type := "text",     name := "reportedby",           title := this->GetTid(".reportedby") ]
                       ,[ type := "text",     name := "reportedbyemail",      title := GetTid("~email") ]
                       ,[ type := "text",     name := "type",                 title := this->GetTid(".type") ]
                       ,[ type := "text",     name := "remarks",              title := this->GetTid(".remarks") ]
                       ,[ type := "text",     name := "issuelog",             title := this->GetTid(".issuelog") ]
                       ]
          , exporttitle := "Feedback"
          ]);
  }

  MACRO DoManageTopics()
  {
    this->RunScreen("#managetopics");
    this->ReloadInterface();
  }

  MACRO DoManageRoles()
  {
    this->RunScreen("#manageroles");
    this->ReloadInterface();
  }

  MACRO DoSettings()
  {
    this->RunScreen("#settings");
  }

  MACRO DoExit()
  {
    this->tolliumresult := "close";
  }


  //----------------------------------------------------------------------------
  //
  // Callbacks
  //

  RECORD ARRAY FUNCTION GetFeedbackRows(RECORD unused)
  {
    RECORD ARRAY filters :=
        [ [ field := "wrd_leftentity", value := tollium_feedback_scope ]
        ];
    IF (^status->value != "")
      INSERT CELL[ field := "status", ^status->value ] INTO filters AT END;
    ELSE
      INSERT [ field := "status", matchtype := "!=", value := "creating" ] INTO filters AT END;
    IF (^topic->value != "")
      INSERT CELL[ field := "topic", ^topic->value ] INTO filters AT END;
    IF (^role->value != 0)
      INSERT CELL[ field := "role", ^role->value ] INTO filters AT END;

    RECORD ARRAY rows := this->contexts->wrdschema->^feedback->RunQuery(CELL
        [ outputcolumns := CELL
            [ rowkey := "wrd_id", "version", "wrd_creationdate", "wrd_modificationdate", "browser", "device", "userdata"
            , "feedbackid", "remarks", "status", "topic", "url", "role"
            ]
        , filters
        ]);

    rows := this->contexts->wrdschema->^topic->Enrich(rows, "topic", [ topic := "wrd_title" ], [ rightouterjoin := TRUE ]);
    rows := this->contexts->wrdschema->^role->Enrich(rows, "role", [ role := "wrd_title" ], [ rightouterjoin := TRUE ]);

    rows :=
        SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], userdata)
             , *
             , reportedby     := reportinfo.realname ?? reportinfo.login ?? GetTid("publisher:tolliumapps.feedback.common.anonymous")
             , reportedbylink := reportinfo.emailaddress != "" ? "mailto:" || reportinfo.emailaddress : ""
               // For now, always allow escalating/closing
             , canescalate := status != "escalated"
             , canclose := status != "closed"
             , canreopen := status = "closed"
          FROM rows;

    RETURN rows;
  }

  MACRO OnFeedbackSelect()
  {
    INTEGER fbid := ^feedback->value;
    this->contexts->controller->applicationtarget := fbid != 0 ? [ feedback := fbid ] : DEFAULT RECORD;
    ^feedbackdetails->contents->LoadFeedbackItem(fbid);
  }

  MACRO OnScaleIframe()
  {
    ^screenshot->PostMessage([ type := "scaled", scaled := ^scaled->value ], "*");
  }

  MACRO OnSelectStatus()
  {
    ^feedback->Invalidate();

    IF (this->doneinit)
      this->contexts->user->SetRegistryKey("publisher.feedback.laststatus", ^status->value);
  }

  MACRO OnSelectTopic()
  {
    ^feedback->Invalidate();
  }

  MACRO OnSelectRole()
  {
    ^feedback->Invalidate();
  }

  MACRO OnLogEvent(RECORD ARRAY events)
  {
    ^feedbackdetails->contents->RefreshLog(); //TODO should be inside feedbackdetails screen ?
  }


  //----------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO SetRowLayout(STRING layout)
  {
    IF (layout NOT IN [ "singlerow", "multirow" ] OR ^feedback->rowlayout = layout)
      RETURN;
    ^feedback->rowlayout := layout;
    this->contexts->user->SetRegistryKey("publisher.feedback.feedbackrowlayout", layout);
    ^feedbackmulti->pressed := layout = "multirow";
    ^feedbacksingle->pressed := layout = "singlerow";
  }
>;

PUBLIC STATIC OBJECTTYPE Feedback EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD data;
  INTEGER currentissue;
  STRING screenshot;

  //----------------------------------------------------------------------------
  //
  // Initialization
  //
  MACRO NEW()
  {
    this->contexts->wrdschema := OpenWRDSchema("publisher:feedback");
  }

  MACRO Init(RECORD data)
  {
    this->data := ValidateOptions( CELL[ embedded := FALSE, rowkey := 0 ], data);
    ^comments->visible := this->data.embedded;
    this->LoadFeedbackItem(this->data.rowkey);
  }

  //----------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO RefreshLog()
  {
    IF(this->currentissue = 0)
    {
      ^issuelog->blobvalue := DEFAULT BLOB;
      ^shareurl->value := "";
      RETURN;
    }

    RECORD curissue := this->contexts->wrdschema->^feedback->GetEntityFields(this->currentissue, [ "wrd_creationdate", "remarks", "userdata", "feedbackid" ]);
    RECORD ARRAY logentries := this->contexts->wrdschema->^log->RunQuery(
        [ outputcolumns := CELL[ "wrd_id", "wrd_creationdate", "text", "webhareuser", "statuschange", "newrole" ]
        , filters := [ [ field := "wrd_leftentity", value := this->currentissue ]
                     ]
        ]);

    logentries := SELECT *
                       , userinfo := EnforceStructure([ realname := "", login := "" ], webhareuser)
                    FROM logentries;

    IF(NOT RecordExists(SELECT FROM logentries WHERE statuschange = "new")) //not pre-inserted as we started doing 28feb2023?
      INSERT CELL[ curissue.wrd_creationdate
                 , text := curissue.remarks
                 , userinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], curissue.userdata)
                 , statuschange := "new" // Assume the status of the feedback was 'new' at the time of the first comment
                 , newrole := 0
                 ] INTO logentries AT END;

    logentries := SELECT when := this->tolliumuser->FormatDatetime(wrd_creationdate, "minutes", TRUE, FALSE)
                       , text
                       , entryby := userinfo.realname ?? userinfo.login ?? GetTid("publisher:tolliumapps.feedback.common.anonymous")
                       , statuschange
                       , newrole
                    FROM logentries
                ORDER BY wrd_creationdate;

    STRING status;
    INTEGER currole;
    FOREVERY (RECORD entry FROM logentries)
    {
      STRING oldstatus, newstatus, oldrole, newrole;
      IF (entry.statuschange != "")
      {
        oldstatus := status;
        newstatus := GetTid("publisher:tolliumapps.feedback.types." || entry.statuschange);
        status := newstatus;
      }
      IF (entry.newrole != 0)
      {
        oldrole := this->contexts->wrdschema->^feedback->GetDomValTitle("ROLE", currole);
        newrole := this->contexts->wrdschema->^feedback->GetDomValTitle("ROLE", entry.newrole);
        currole := entry.newrole;
      }
      logentries[#entry] := CELL[ ...entry, oldstatus, newstatus, oldrole, newrole, DELETE statuschange ];
    }

    ^issuelog->blobvalue := issuewitty->RunComponentToBlob("entries",
        [ log := logentries
        , screenshot := EncodeBase64(this->screenshot)
        ]);
    ^shareurl->value := GetPrimaryWebHareInterfaceURL() || "?app=publisher:feedback(" || EncodeURL(curissue.feedbackid) || ")";
  }

  PUBLIC MACRO LoadFeedbackItem(INTEGER id)
  {
    this->currentissue := id;

    IF(id = 0)
    {
      ^screenshot->src := this->contexts->controller->baseurl || ".wh/common/preview/nopreview.html";
      ^urlpanel->visible := FALSE;
      ^comment->enabled := FALSE;
      RETURN;
    }

    RECORD feedback := this->contexts->wrdschema->^feedback->GetEntityFields(id, [ "size", "url", "version", "extradata", "feedbackid", ...^data->GetComposedCellnames() ]);
    feedback.size := EnforceStructure([ width := 0f, height := 0f], feedback.size);
    this->screenshot := GetScreenshotWitty()->RunComponentToString("container", CELL
        [ screenshot := [ width := INTEGER(Ceil(feedback.size.width))
                        , height := INTEGER(Ceil(feedback.size.height))
                        , scaled := TRUE// For now, we'll always scale, we can make it an option later: ^scaled->value
                        ]
        , url := GetFeedbackScreenshotURL(id)
        ]);

    // Load the stored screenshot into the container iframe
    ^screenshot->value := this->screenshot;
    ^url->value := feedback.url;
    ^urlpanel->visible := feedback.version = 2; // version 2 adds the url of the visited page

    ^comment->enabled := TRUE;

    this->RefreshLog();

    ^data->value := feedback;
    this->frame->title := this->frame->title || " - " || feedback.feedbackid;

    RECORD ARRAY extrafields := GetFeedbackExtraFields(feedback.extradata);
    IF(Length(extrafields) > 0)
    {
      ^extradata->visible := TRUE;
      FOREVERY (RECORD extra FROM extrafields)
      {
        OBJECT comp;
        SWITCH (extra.type)
        {
          CASE "text"
          {
            comp := this->CreateTolliumComponent("text");
            comp->value := extra.value;
          }
        }

        IF (ObjectExists(comp))
        {
          comp->title := extra.title;
          IF (comp->componenttype = "text")
            comp->selectable := TRUE;
          this->frame->bodynode->InsertComponentBefore(comp, ^extradatainsertpoint, TRUE);
        }
      }
    }
  }

  MACRO DoAddComment()
  {
    OBJECT work := this->BeginWork( [ validate := OBJECT[ ^comment ]]);
    UpdateFeedbackItem(this->contexts->wrdschema, this->currentissue, [ remarks := ^comment->value ], this->contexts->user->GetUserDataForLogging());
    IF(NOT work->Finish())
      RETURN;

    ^comment->value := "";
  }
>;

PUBLIC STATIC OBJECTTYPE Escalate EXTEND TolliumScreenBase
<
  STRING FUNCTION Submit()
  {
    RETURN ^message->value;
  }
>;

PUBLIC STATIC OBJECTTYPE Settings EXTEND TolliumScreenBase
<
  MACRO Init()
  {
    ^enabletolliumfeedback->value := ReadRegistryKey("system.backend.features.feedback");
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    BOOLEAN refreshtollium;
    IF (^enabletolliumfeedback->value != ReadRegistryKey("system.backend.features.feedback"))
    {
      WriteRegistryKey("system.backend.features.feedback", ^enabletolliumfeedback->value);
      refreshtollium := TRUE;
    }

    IF (NOT work->Finish())
      RETURN FALSE;

    IF (refreshtollium)
      BroadcastToAllSessions( [ type := "tollium:shell.refreshdashboard" ]);
    RETURN TRUE;
  }
>;

PUBLIC STATIC OBJECTTYPE EditTopic EXTEND WRDEntityEditScreenBase
<
  MACRO OnAllowedPriorities()
  {
    ^entity->^default_priority->options := [ [ rowkey := 0, title := "", invalidselection := TRUE ] ] CONCAT ^entity->^allowed_priorities->selection;
  }
>;
