<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::float.whlib";

LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/internal/support.whlib";

LOADLIB "mod::system/lib/configure.whlib";

LOADLIB "mod::wrd/lib/api.whlib";

CONSTANT FLOAT maxscreenshotsize := 640;

PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Initialization
  //
  BOOLEAN doneinit;

  MACRO NEW()
  {
    this->contexts->wrdschema := OpenWRDSchema("publisher:feedback");
  }

  MACRO DoSettings()
  {
    this->RunScreen("#settings", [ scope := ^scope->value ]);
  }

  MACRO Init(RECORD data)
  {
    ^scope->options :=
        SELECT rowkey := ToLowercase(wrd_tag)
             , title := ToLowercase(wrd_tag)
          FROM this->contexts->wrdschema->^scope->RunQuery([ outputcolumns := [ "wrd_id", "wrd_tag" ]])
         WHERE this->tolliumuser->HasRightOn("publisher:managefeedbackscope", wrd_id)
         ORDER BY wrd_tag;

    IF (Length(^scope->options) <= 1)
      ^scope->visible := FALSE;

    this->doneinit := TRUE;
    ^scope->SetValueIfValid(this->contexts->user->GetRegistryKey("publisher.feedback.lastscope", ""));
  }


  //----------------------------------------------------------------------------
  //
  // Action handlers
  //

  MACRO DoView()
  {
    IF (^feedback->selection.version != 1)
      this->RunSimpleScreen("warning", this->GetTid(".messages.unsupportedversion"));
    ELSE
      this->RunScreen("#feedback", ^feedback->selection);
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".messages.confirmdeletefeedback")) = "yes")
    {
      OBJECT work := this->BeginWork();
      this->contexts->wrdschema->^feedback->UpdateEntity(^feedback->selection.wrd_id, [ wrd_limitdate := GetCurrentDateTime() ]);
      work->Finish();
    }
  }

  MACRO DoExit()
  {
    this->tolliumresult := "close";
  }


  //----------------------------------------------------------------------------
  //
  // Callbacks
  //
  RECORD ARRAY FUNCTION MapFeedbackRows(RECORD ARRAY rows)
  {
    rows := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], userdata)
                 , *
                 , reportedby     := reportinfo.realname ?? reportinfo.login
                 , reportedbylink := reportinfo.emailaddress != "" ? "mailto:" || reportinfo.emailaddress : ""
              FROM rows;

    RETURN rows;
  }

  MACRO OnSelectScope()
  {
    INTEGER scopeid := this->contexts->wrdschema->^scope->Search("wrd_tag", ToUppercase(^scope->value));
    ^feedback->listfilters := [ [ field := "wrd_leftentity", value := scopeid ] ];

    IF(this->doneinit)
      this->contexts->user->SetRegistryKey("publisher.feedback.lastscope", ^scope->value);
  }
>;

PUBLIC STATIC OBJECTTYPE Feedback EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD data;


  //----------------------------------------------------------------------------
  //
  // Initialization
  //

  MACRO Init(RECORD data)
  {
    this->data := data;
    RECORD feedback := this->contexts->wrdschema->^feedback->GetEntityFields(this->data.rowkey, ["EXTRADATA","SIZE"] CONCAT ^data->GetComposedCellnames());
    ^data->value := feedback;

    RECORD ARRAY extrafields := GetFeedbackExtraFields(feedback.extradata);
    IF(Length(extrafields) > 0)
    {
      // The 'there is no extra data available' text doesn't have to be displayed
      ^noextradata->DeleteComponent();

      FOREVERY (RECORD extra FROM extrafields)
      {
        OBJECT comp;
        SWITCH (extra.type)
        {
          CASE "text"
          {
            comp := this->CreateTolliumComponent("text");
            comp->value := extra.value;
          }
        }

        IF (ObjectExists(comp))
        {
          comp->title := extra.title;
          IF (comp->componenttype = "text")
            comp->selectable := TRUE;
          this->frame->bodynode->InsertComponentAfter(comp, DEFAULT OBJECT, TRUE);
        }
      }
    }

    // Load the stored screenshot into the container iframe
    ^screenshot->value := GetScreenshotWitty()->RunComponentToString("container", CELL
        [ width := INTEGER(Ceil(feedback.size.width))
        , height := INTEGER(Ceil(feedback.size.height))
        , url := GetFeedbackScreenshotURL(this->data.rowkey)
        , scaled := ^scaled->value
        ]);
  }


  //----------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnScaleIframe()
  {
    ^screenshot->PostMessage([ type := "scaled", scaled := ^scaled->value ], "*");
  }
>;

PUBLIC STATIC OBJECTTYPE Settings EXTEND TolliumScreenBase
<
  STRING scope;

  MACRO Init(RECORD data)
  {
    this->scope := data.scope;

    ^enabletolliumfeedback->visible := data.scope = "tollium:webharebackend";
    IF(^enabletolliumfeedback->visible)
    {
      ^enabletolliumfeedback->value := ReadRegistryKey("system.backend.features.feedback");
    }
  }
  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    BOOLEAN refreshtollium;
    IF(^enabletolliumfeedback->visible AND ^enabletolliumfeedback->value != ReadRegistryKey("system.backend.features.feedback"))
    {
      WriteRegistryKey("system.backend.features.feedback", ^enabletolliumfeedback->value);
      refreshtollium := TRUE;
    }

    IF(NOT work->Finish())
      RETURN FALSE;

    IF(refreshtollium)
      BroadcastToAllSessions( [ type := "tollium:shell.refreshdashboard" ]);
    RETURN TRUE;
  }
>;
