<?wh

LOADLIB "wh::float.whlib";
LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::tollium/lib/dialogs.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/internal/support.whlib";

LOADLIB "mod::system/lib/configure.whlib";

LOADLIB "mod::wrd/lib/api.whlib";

CONSTANT FLOAT maxscreenshotsize := 640;

PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Initialization
  //
  BOOLEAN doneinit;
  OBJECT ARRAY logentries;
  OBJECT issuewitty;

  MACRO NEW()
  {
    this->contexts->wrdschema := OpenWRDSchema("publisher:feedback");
    this->issuewitty := LoadWittyLibrary(Resolve("issues.witty"),"HTML-NI");
  }

  MACRO DoSettings()
  {
    this->RunScreen("#settings", [ scope := ^scope->value ]);
  }

  MACRO Init(RECORD data)
  {
    ^scope->options :=
        SELECT rowkey := wrd_id
             , title := ToLowercase(wrd_tag)
          FROM this->contexts->wrdschema->^scope->RunQuery([ outputcolumns := [ "wrd_id", "wrd_tag" ]])
         WHERE this->tolliumuser->HasRightOn("publisher:managefeedbackscope", wrd_id)
         ORDER BY wrd_tag;

    IF (Length(^scope->options) <= 1)
      ^scope->visible := FALSE;

    this->doneinit := TRUE;
    ^scope->SetValueIfValid(this->contexts->user->GetRegistryKey("publisher.feedback.lastscopeid", 0));
    ^loglistener->masks := this->contexts->wrdschema->^log->GetEventMasks();
  }


  //----------------------------------------------------------------------------
  //
  // Action handlers
  //

  MACRO DoView()
  {
    IF (^feedback->selection.version != 1)
      this->RunSimpleScreen("warning", this->GetTid(".messages.unsupportedversion"));
    ELSE
      this->RunScreen("#feedback", ^feedback->selection);
  }

  MACRO DoDelete()
  {
    IF (this->RunSimpleScreen("confirm", this->GetTid(".messages.confirmdeletefeedback")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    // Don't archive it, assume deleted feedback may contain PII. If random deletion becomes a problem, solve it with rights
    this->contexts->wrdschema->^feedback->DeleteEntity(^feedback->value);
    work->Finish();
  }

  MACRO DoExit()
  {
    this->tolliumresult := "close";
  }

  MACRO DoAddComment()
  {
    OBJECT work := this->BeginWork( [ validate := OBJECT[ ^comment ]]);
    this->contexts->wrdschema->^log->CreateEntity(
      [ wrd_leftentity := ^feedback->value
      , text := ^comment->value
      , webhareuser := this->contexts->user->GetUserDataForLogging()
      ]);

    IF(NOT work->Finish())
      RETURN;

    ^comment->value := "";
  }


  //----------------------------------------------------------------------------
  //
  // Callbacks
  //

  RECORD ARRAY FUNCTION MapFeedbackRows(RECORD ARRAY rows)
  {
    rows := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], userdata)
                 , *
                 , reportedby     := reportinfo.realname ?? reportinfo.login
                 , reportedbylink := reportinfo.emailaddress != "" ? "mailto:" || reportinfo.emailaddress : ""
              FROM rows;

    rows := this->contexts->wrdschema->^feedbacktype->Enrich(rows, "TYPE", [ type := "wrd_title" ], [ rightouterjoin := TRUE ]);
    rows := SELECT *, type := GetTid(type) FROM rows;

    RETURN rows;
  }

  MACRO OnFeedbackSelect()
  {
    FOREVERY(OBJECT logentry FROM this->logentries)
      logentry->DeleteComponent();
    this->logentries := OBJECT[];

    ^comment->enabled := ^feedback->value != 0;

    this->RefreshLog();
  }

  MACRO RefreshLog()
  {
    IF(^feedback->value = 0)
    {
      ^issuelog->blobvalue := DEFAULT BLOB;
      RETURN;
    }

    RECORD ARRAY logentries := this->contexts->wrdschema->^log->RunQuery(
        [ outputcolumns := CELL[ "WRD_ID", "WRD_CREATIONDATE", "TEXT", "WEBHAREUSER", "STATUSCHANGE" ]
        , filters := [ [ field := "wrd_leftentity", value := ^feedback->value ]
                     ]
        ]);

    logentries := SELECT *
                       , userinfo := EnforceStructure([ realname := "", login := "" ], webhareuser)
                    FROM logentries;

    RECORD curissue := ^feedback->selection;
    INSERT CELL[ curissue.wrd_creationdate
               , text := curissue.remarks
               , userinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], curissue.userdata)
               ] INTO logentries AT END;

    logentries := SELECT when := this->tolliumuser->FormatDatetime(wrd_creationdate, "minutes", TRUE, FALSE)
                       , text
                       , entryby := userinfo.realname ?? userinfo.login
                    FROM logentries
                ORDER BY wrd_creationdate DESC;

    ^issuelog->blobvalue := this->issuewitty->RunComponentToBlob("entries", [ log := logentries ]);
  }

  MACRO OnSelectScope()
  {
    ^feedback->listfilters := [ [ field := "wrd_leftentity", value := ^scope->value ]
                              , [ field := "status", matchtype := "!=", value := "creating" ]
                              ];

    IF(this->doneinit)
      this->contexts->user->SetRegistryKey("publisher.feedback.lastscopeid", ^scope->value);
  }

  MACRO OnLogEvent(RECORD ARRAY events)
  {
    this->RefreshLog();
  }

  MACRO DoExport()
  {
    RECORD ARRAY rows := this->contexts->wrdschema->^feedback->RunQuery(
        [ outputcolumns := CELL[ "wrd_id", "feedbackid", "userdata", "type", "remarks", "wrd_creationdate" ]
        , filters := [ [ field := "wrd_leftentity", value := ^scope->value ]
                     ]
        ]);

    rows := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], userdata)
                 , *
                 , reportedby      := reportinfo.realname ?? reportinfo.login
                 , reportedbyemail := reportinfo.emailaddress
              FROM rows;

    rows := this->contexts->wrdschema->^feedbacktype->Enrich(rows, "TYPE", [ type := "wrd_title" ], [ rightouterjoin := TRUE ]);
    rows := SELECT *, type := GetTid(type) FROM rows ORDER BY wrd_creationdate;

    RECORD ARRAY log := this->contexts->wrdschema->^log->RunQuery(
        [ outputcolumns := CELL[ "WRD_CREATIONDATE", "WRD_LEFTENTITY", "TEXT", "WEBHAREUSER" ]
        , filters := [ [ field := "WRD_LEFTENTITY", value := SELECT AS INTEGER ARRAY wrd_id FROM rows ]
                     ]
        ]);

    log := SELECT TEMPORARY reportinfo := EnforceStructure([ realname := "", login := "", emailaddress := "" ], webhareuser)
                 , *
                 , reportedby      := reportinfo.realname ?? reportinfo.login
                 , reportedbyemail := reportinfo.emailaddress
              FROM log;

    FOREVERY(RECORD row FROM rows)
    {
      RECORD ARRAY logentries := SELECT * FROM log WHERE wrd_leftentity = row.wrd_id ORDER BY wrd_creationdate;
      STRING logtext;

      FOREVERY(RECORD entry FROM logentries)
      {
        //Spaces to show properly when excel puts all on one line
        logtext := `${logtext}${entry.reportedby} ${this->tolliumuser->FormatDatetime(entry.wrd_creationdate, "minutes", TRUE, TRUE)}   \n${entry.text}   \n\n`;
      }
      INSERT CELL issuelog := logtext INTO rows[#row];
    }

    RunColumnFileExportDialog(this,
      CELL[ rows
          , columns := [[ type := "text",     name := "feedbackid",       title := this->GetTid(".feedbackid") ]
                       ,[ type := "datetime", name := "wrd_creationdate", title := this->GetTid(".wrd_creationdate"), storeutc := TRUE ]
                       ,[ type := "text",     name := "reportedby",       title := this->GetTid(".reportedby") ]
                       ,[ type := "text",     name := "reportedbyemail",  title := GetTid("~email") ]
                       ,[ type := "text",     name := "type",             title := this->GetTid(".type") ]
                       ,[ type := "text",     name := "remarks",          title := this->GetTid(".remarks") ]
                       ,[ type := "text",     name := "issuelog",         title := this->GetTid(".issuelog") ]
                       ]
          , exporttitle := "Feedback"
          ]);
  }
>;

PUBLIC STATIC OBJECTTYPE Feedback EXTEND TolliumScreenBase
< //----------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD data;


  //----------------------------------------------------------------------------
  //
  // Initialization
  //

  MACRO Init(RECORD data)
  {
    this->data := data;
    RECORD feedback := this->contexts->wrdschema->^feedback->GetEntityFields(this->data.rowkey, ["EXTRADATA","SIZE","FEEDBACKID","APPVERSIONS"] CONCAT ^data->GetComposedCellnames());
    ^data->value := feedback;
    this->frame->title := this->frame->title || " - " || feedback.feedbackid;

    RECORD ARRAY extrafields := GetFeedbackExtraFields(feedback.extradata);
    IF(Length(extrafields) > 0)
    {
      // The 'there is no extra data available' text doesn't have to be displayed
      ^noextradata->DeleteComponent();

      FOREVERY (RECORD extra FROM extrafields)
      {
        OBJECT comp;
        SWITCH (extra.type)
        {
          CASE "text"
          {
            comp := this->CreateTolliumComponent("text");
            comp->value := extra.value;
          }
        }

        IF (ObjectExists(comp))
        {
          comp->title := extra.title;
          IF (comp->componenttype = "text")
            comp->selectable := TRUE;
          this->frame->bodynode->InsertComponentAfter(comp, DEFAULT OBJECT, TRUE);
        }
      }
    }

    ^appversions->rows := feedback.appversions;

    // Load the stored screenshot into the container iframe
    ^screenshot->value := GetScreenshotWitty()->RunComponentToString("container", CELL
        [ width := INTEGER(Ceil(feedback.size.width))
        , height := INTEGER(Ceil(feedback.size.height))
        , url := GetFeedbackScreenshotURL(this->data.rowkey)
        , scaled := ^scaled->value
        ]);
  }


  //----------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnScaleIframe()
  {
    ^screenshot->PostMessage([ type := "scaled", scaled := ^scaled->value ], "*");
  }
>;

PUBLIC STATIC OBJECTTYPE Settings EXTEND TolliumScreenBase
<
  INTEGER scope;

  MACRO Init(RECORD data)
  {
    this->scope := data.scope;
    BOOLEAN iswebharescope := ToLowercase(this->contexts->wrdschema->^scope->GetEntityField(this->scope,"WRD_TAG")) = "tollium:webharebackend";

    ^enabletolliumfeedback->visible := iswebharescope;
    IF(^enabletolliumfeedback->visible)
    {
      ^enabletolliumfeedback->value := ReadRegistryKey("system.backend.features.feedback");
    }
  }
  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    BOOLEAN refreshtollium;
    IF(^enabletolliumfeedback->visible AND ^enabletolliumfeedback->value != ReadRegistryKey("system.backend.features.feedback"))
    {
      WriteRegistryKey("system.backend.features.feedback", ^enabletolliumfeedback->value);
      refreshtollium := TRUE;
    }

    IF(NOT work->Finish())
      RETURN FALSE;

    IF(refreshtollium)
      BroadcastToAllSessions( [ type := "tollium:shell.refreshdashboard" ]);
    RETURN TRUE;
  }
>;
