<?wh

LOADLIB "wh::javascript.whlib";

LOADLIB "mod::system/lib/internal/whfs/base.whlib";

LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

// Base class for a property editor. Preparing to share common code with objectprops
PUBLIC STATIC OBJECTTYPE PublisherEditorBase EXTEND TolliumScreenBase
<
  /// Id of edited object, 0 for new or duplicate objects
  INTEGER itemid;

  // Metatabs config as received from describeMetaTabsForHS
  RECORD metatabsconfig;
  // Controllers for the metadata. For now we'll rely on compositions
  RECORD ARRAY metacontrollers;
  /// Whether to assume the user has write access (often used by editdocument into otherwise locked/hidden folders)
  BOOLEAN assumewriteaccess;
  /// If the user only has read-only access to this file (fs_browse)
  BOOLEAN readonly;

  MACRO RefreshReadonly(OBJECT fsobj)
  {
    BOOLEAN setreadonly := (ObjectExists(fsobj) AND IsReadonlyWHFSSpace(fsobj->whfspath))
                            OR (this->itemid != 0 AND NOT this->assumewriteaccess AND NOT this->tolliumuser->HasRightOn("system:fs_fullaccess", this->itemid));
    IF(this->readonly = setreadonly)
      RETURN;

    this->readonly := setreadonly;
    this->OnReadonlyChange();
  }

  MACRO OnReadonlyChange()
  {
    this->RefreshMetaTabsReadonly();
  }

  BOOLEAN FUNCTION SetupMetaTabs(INTEGER objectid, INTEGER parent, BOOLEAN isfolder, INTEGER type)
  {
    this->metatabsconfig := CallJS("@mod-publisher/lib/internal/siteprofiles/metatabs.ts#describeMetaTabsForHS", CELL[objectid, parent, isfolder, type] );
    IF(NOT RecordExists(this->metatabsconfig))
      RETURN FALSE; //nothing to do

    this->AddTypesTabs();
    RETURN TRUE;
  }

  MACRO RefreshMetaTabsReadonly()
  {
    FOREVERY(RECORD controller FROM this->metacontrollers)
      controller.fsinstance->enabled := NOT this->readonly;
  }

  MACRO LoadMetadata(OBJECT sourceobject)
  {
    FOREVERY(RECORD controller FROM this->metacontrollers)
    {
      controller.fsinstance->value := controller.type->GetInstanceData(sourceobject->id);
    }
  }

  MACRO SaveMetadata(INTEGER targetobject)
  {
    //FIXME ensure any specific validation rule *and* all valueConstraints are honored
    FOREVERY(RECORD controller FROM this->metacontrollers)
    {
      //broken fields (eg due to incompatible data/vaueConstraints) should be able to opt out, IsUpdateValue ?
      controller.type->SetInstanceData(targetobject, controller.fsinstance->GetUpdateValue());
    }
  }

  MACRO AddTypesTabs()
  {
    //TODO also take care of the SEO props on tab 1 to generalize the logic

    INTEGER insertpos := 1;
    FOREVERY(RECORD type FROM this->metatabsconfig.types)
    {
      OBJECT fsinstance := this->CreateTolliumComponent("record");

      FOREVERY(RECORD section FROM type.sections)
      {
        //TODO restore tab merging - or have metatabsconfig calculate this for us?
        OBJECT newtab := ^maintabs->InsertTabAt(insertpos, GetTid(section.title));
        insertpos := insertpos + 1;
        FOREVERY(RECORD field FROM section.fields)
        {
          OBJECT newcomp := this->CreateCustomComponent(field.component.ns, field.component.component);
          IF(MemberExists(newcomp, 'composition'))
            newcomp->composition := fsinstance;
          IF(MemberExists(newcomp, 'cellname'))
            newcomp->cellname := field.name;
          IF(MemberExists(newcomp, 'dirtylistener'))
            newcomp->dirtylistener := ^dirtylistener;

          newcomp->SetYamlProps(field.component.yamlprops);
          newcomp->title := GetTid(field.title);
          newtab->InsertComponentAfter(newcomp, DEFAULT OBJECT, TRUE);
        }

        INSERT CELL [ fsinstance, type := OpenWHFSType(type.namespace) ] INTO this->metacontrollers AT END;
        IF(ObjectExists(this->contexts->editdocumentapi)) //TODO it looks like *we* should manage the list of editcontentttypes
          INSERT type.namespace INTO this->contexts->editdocumentapi->editcontenttypes AT END;
      }
    }

    this->RefreshMetaTabsReadonly();
  }

>;
