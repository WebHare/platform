<?wh

LOADLIB "wh::javascript.whlib";

LOADLIB "mod::publisher/lib/history.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";

LOADLIB "mod::system/lib/internal/whfs/base.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/yamlcomponents.whlib";

RECORD FUNCTION GetRecycledObjectInfo(OBJECT whuser, INTEGER objectid)
{
  RECORD res;

  RECORD historyinfo := SELECT * FROM system.fs_history WHERE fs_history.type = whconstant_historytype_recycled AND fs_history.fs_object = objectid ORDER BY when DESC;
  IF(NOT RecordExists(historyinfo))
    RETURN DEFAULT RECORD;

   RETURN [ deletionuser := historyinfo.user
          , deletiondate := historyinfo.when
          , origlocation := historyinfo.currentparent
          , originalpath := (SELECT AS STRING whfspath FROM system.fs_objects WHERE id=historyinfo.currentparent)
          , origname :=     historyinfo.currentname
          ];
}

PUBLIC STATIC OBJECTTYPE FieldScope EXTEND YamlScope
<
  OBJECT editor;
  RECORD controller;

  MACRO NEW(OBJECT editor, RECORD controller)
  {
    this->editor := editor;
    this->controller := controller;
  }
  UPDATE PUBLIC OBJECT FUNCTION ResolveComponentRef(OBJECT forcomp, STRING name)
  {
    IF(name LIKE "$type.*")
    {
      STRING ARRAY toks := Tokenize(name, '.');
      OBJECT match := this->controller.fsinstance->GetComponent(ToSnakeCase(toks[1]));
      IF(NOT ObjectExists(match))
      {
        STRING ARRAY availablerefs := SELECT AS STRING ARRAY ToCamelCase(compname) FROM ToRecordArray(this->controller.fsinstance->GetComponents(),'compname');
        THROW NEW TolliumException(forcomp, `No such field '${toks[1]}' available in type '${this->controller.type->namespace}'. Available: ${Detokenize(availablerefs, ', ')}`);
      }
      IF(Length(toks) > 2)
        THROW NEW TolliumException(forcomp, `No subfield support yet`);
      RETURN match;
    }
    RETURN YamlsCope::ResolveComponentRef(forcomp, name);
  }
>;

// Base class for ObjectProperties and the Document Editor. Preparing to share common code with objectprops
PUBLIC STATIC OBJECTTYPE PublisherEditorBase EXTEND TolliumScreenBase <
  /// Id of edited object, 0 for new or duplicate objects
  INTEGER itemid;

  // Metatabs renderer
  OBJECT renderer;
  /// Whether to assume the user has write access (often used by editdocument into otherwise locked/hidden folders)
  BOOLEAN assumewriteaccess;
  /// Whether the edited object is in the recycle bin, and if so, what the original name etc was
  RECORD recyclebininfo;
  /// If the user only has read-only access to this file (fs_browse)
  BOOLEAN readonly;
  /// Whether the edited (or new) object is a folder
  BOOLEAN isfolder;
  /// Whether the object is a new object. It may already live in the user's autosave folder! (TODO EditProps may need to be careful when *actually* requesting props in the /webhare-private/ folder? or just not activate meta apply rules there)
  BOOLEAN isnew;
  /// Parent folder of the new/edited object
  OBJECT parentfolder;

  RECORD basesettings;

  OBJECT workflowmgr;

  MACRO RefreshReadonly()
  {
    BOOLEAN setreadonly := this->workflowmgr->IsReadOnly();
    this->readonly := setreadonly;
    this->OnReadonlyChange();
  }

  BOOLEAN FUNCTION CanWriteCurrentItem()
  {
    RETURN this->isnew OR (NOT RecordExists(this->recyclebininfo) AND this->tolliumuser->HasRightOn("system:fs_fullaccess", this->itemid));
  }

  MACRO OnReadonlyChange()
  {
    this->RefreshMetaTabsReadonly();
  }

  MACRO SetupBaseSettings()
  {
    this->basesettings := GetBaseProperties(this->contexts->applytester);
    ^description->visible := this->basesettings.description;
    ^keywords->visible := this->basesettings.keywords;
    ^isunlisted->visible := this->basesettings.isunlisted;
    ^title->required := this->basesettings.requiretitle;

    BOOLEAN needstemplate := this->isfolder ? FALSE : this->contexts->applytester->IsTypeNeedsTemplate();
    //As these are published, only files that go through a webdesign can show something useful
    ^seotitle->visible := needstemplate AND this->basesettings.seotitle;

    //TODO generalize further so metadata.ts also managed SEO settings
    this->renderer->RegisterMetaController(^webmetadata->type, ^webmetadata);
  }

  RECORD FUNCTION UpdatePinned()
  {
    //Do not honor 'pinned' for new objects... then it should apply only AFTER we finalize the dialog. Also ensures valid-whfsname validation still happens
    BOOLEAN pinned := (^pinobject->value OR RecordExists(this->recyclebininfo)) AND NOT this->isnew;
    BOOLEAN parentfullaccess := this->tolliumuser->HasRightOn("system:fs_fullaccess", this->parentfolder->id);

    ^name->enabled := NOT pinned AND parentfullaccess AND NOT this->readonly;

    RETURN CELL [pinned, parentfullaccess];
  }

  MACRO SetupEditBaseForExistingObject(INTEGER objectid, RECORD options) {
    this->__SetupEditBase(objectid, 0, FALSE, -1, options);
  }

  MACRO SetupEditBaseForNewObject(INTEGER parent, BOOLEAN isfolder, INTEGER type) {
    this->__SetupEditBase(0, parent, isfolder, type, DEFAULT RECORD);
  }

  MACRO __SetupEditBase(INTEGER objectid, INTEGER parent, BOOLEAN isfolder, INTEGER type, RECORD options) {
    options := ValidateOptions(CELL[ readonly := FALSE
                                   , useworkflow := FALSE
                                   , assumewriteaccess := FALSE
                                   , workflowtypes := STRING[]
                                  ], options);

    /* The ordering here is pretty sensitive:
       - we need the meta tabs description first just to get the editcontenttypes
       - we need to load the subscreen for the document editor just in case that one registers more types too at runtime
         (TODO it would help if the user declared these types on <objecteditor>
       - and then we can setup a workflow manager
       */

    STRING ARRAY workflowtypes := STRING[ "platform:web.config", "platform:web.metadata", ...options.workflowtypes ];

    this->renderer := CreateMetatabsRenderer(CELL[ objectid, parent, isfolder, type, requireworkflow :=options.useworkflow ]);
    this->contexts->applytester := this->renderer->applytester;

    IF(objectid != 0) {
      IF(options.useworkflow AND RecordExists(this->renderer->metatabsconfig)) {
         //add the metacontroller requested types to the type edit list
        FOREVERY(RECORD t FROM this->renderer->metatabsconfig.types)
          INSERT t.namespace INTO workflowtypes AT END;
      }
    }

    IF(objectid != 0)
      this->workflowmgr := NEW WorkflowManager(objectid,
        CELL[ user := this->contexts->user
            , ...options
            , workflowtypes := options.useworkflow ? workflowtypes : STRING[]
            ]);

    IF(NOT ObjectExists(this->contexts->applytester))
      THROW NEW TolliumException(this, "No applytester found for this object");

    this->parentfolder := this->contexts->applytester->objparent = 0 ? OpenWHFSRootObject() : OpenWHFSObject(this->contexts->applytester->objparent);
    this->itemid := objectid;
    this->SetupBaseSettings();
    this->isnew := objectid = 0;
    this->assumewriteaccess := options.assumewriteaccess;
  }

  MACRO DoInspectMetaTabs() {
    Reflect(this->renderer->metatabsconfig);
  }

  MACRO RefreshMetaTabsReadonly()
  {
    this->renderer->SetControllersEnabled(NOT this->readonly);

    BOOLEAN needstemplate := this->isfolder ? FALSE : this->contexts->applytester->IsTypeNeedsTemplate();
    //As these are published, only files that go through a webdesign can show something useful
    ^seotitle->visible := needstemplate AND this->basesettings.seotitle;
    ^title->enabled := NOT this->readonly AND ^title->visible;
    ^description->enabled := NOT this->readonly AND ^description->visible;
    ^keywords->enabled := NOT this->readonly AND ^keywords->visible;
    this->UpdatePinned();
  }

  MACRO LoadBaseData(OBJECT sourceobject) {
    this->recyclebininfo := IsRecycleBinWHFSPath(sourceobject->whfspath) ? GetRecycledObjectInfo(this->contexts->user, sourceobject->id) : DEFAULT RECORD;
    ^pinobject->value := sourceobject->ispinned AND NOT this->isnew;

    IF(this->isnew)
      ^name->value := "";
    ELSE IF(RecordExists(this->recyclebininfo))
      ^name->value := this->recyclebininfo.origname;
    ELSE
      ^name->value := sourceobject->name;

    FOREVERY (STRING field FROM ["title", "description", "keywords" ])
    {
      OBJECT fieldobj := GetMember(this, "^" || field);
      fieldobj->value := GetMember(sourceobject, field);
    }
  }

  MACRO LoadMetadata(OBJECT sourceobject) {
    FOREVERY(RECORD controller FROM this->renderer->metacontrollers)
      controller.fsinstance->value := sourceobject->GetInstanceData(controller.type->namespace);
  }

  MACRO SaveMetadata(INTEGER targetobject) { //editdocument will always be invoking us with a draft as parameter
    OBJECT target := ObjectExists(this->workflowmgr) AND this->workflowmgr->useworkflow ? this->workflowmgr : OpenWHFSObject(targetobject);

    //FIXME ensure any specific validation rule *and* all valueConstraints are honored
    FOREVERY(RECORD controller FROM this->renderer->metacontrollers)
    {
      //broken fields (eg due to incompatible data/vaueConstraints) should be able to opt out, IsUpdateValue ?
      target->SetInstanceData(controller.type->namespace, controller.fsinstance->GetUpdateValue());
    }

    target->UpdateMetadata(CELL [ title := ^title->value
                                , description := ^description->value
                                , keywords := ^keywords->value
                                //FIXME unsafe when renaming from RTD, needs the same checks as objectprops?
                                , ...(ObjectExists(this->workflowmgr) AND this->workflowmgr->useworkflow ? [ name := ^name->value ] : DEFAULT RECORD)
                                ]);
  }

  BOOLEAN FUNCTION AddTypesTabs() {
    INTEGER insertpos := 1;

    IF(RecordExists(this->renderer->metatabsconfig)) {
      FOREVERY(RECORD type FROM this->renderer->metatabsconfig.types) {
        RECORD insertresult := this->renderer->RenderTypeIntoTabs(type, ^maintabs, insertpos, [ dirtylistener := ^dirtylistener ]);
        insertpos := insertpos + insertresult.sections;
      }
    }

    this->RefreshMetaTabsReadonly();
    RETURN RecordExists(this->renderer->metatabsconfig) AND Length(this->renderer->metatabsconfig.types) > 0;
  }
>;

PUBLIC STATIC OBJECTTYPE MetatabsRenderer <
  // Metatabs config as received from describeMetaTabsForHS
  PUBLIC RECORD metatabsconfig;

  // Applytester for the object
  PUBLIC OBJECT applytester;

  // Controllers for the metadata. For now we'll rely on compositions
  PUBLIC RECORD ARRAY metacontrollers;

  MACRO NEW(RECORD config, OBJECT applytester) {
    this->metatabsconfig := config;
    this->applytester := applytester;
  }

  PUBLIC MACRO RegisterMetaController(OBJECT type, OBJECT composition) {
    INSERT CELL[ fsinstance := composition, type ] INTO this->metacontrollers AT END;
  }

  PUBLIC RECORD FUNCTION RenderTypeIntoTabs(RECORD type, OBJECT maintabs, INTEGER insertpos, RECORD options DEFAULTSTO DEFAULT RECORD) {
    options := ValidateOptions([
      dirtylistener := DEFAULT OBJECT
    ], options);
    OBJECT fsinstance := maintabs->owner->CreateTolliumComponent("record");
    RECORD controller := CELL [ fsinstance, type := OpenWHFSType(type.namespace) ];
    fsinstance->yamlscope := NEW FieldScope(fsinstance, controller);
    INSERT controller INTO this->metacontrollers AT END;

    FOREVERY(RECORD section FROM type.sections) {
      //TODO restore tab merging - or have metatabsconfig calculate this for us?
      OBJECT newtab := maintabs->InsertTabAt(insertpos, GetTid(section.title));
      insertpos := insertpos + 1;
      FOREVERY(RECORD field FROM section.fields) {
        STRING title := GetTid(field.title);
        OBJECT newcomp := CreateYamlComponent(maintabs->owner, field.component, [
          composition := fsinstance,
          cellname := field.name,
          dirtylistener := options.dirtylistener,
          yamlscope := fsinstance->yamlscope
        ]);

        STRING layout := CellExists(field, 'layout') ? field.layout : newcomp->blockelement ? "block" : "inline";
        IF(title != "" AND layout = "block") { //put it in the H1 above.
          OBJECT heading := maintabs->owner->CreateTolliumComponent("heading");
          heading->title := title;
          newtab->InsertComponentAfter(heading, DEFAULT OBJECT, TRUE);
        }

        newcomp->title := (layout = "inline" ? title : "");
        newcomp->errorlabel := title;
        newtab->layout := layout = "inline" ? "form" : "left";  //update tab's layout as that's what new lines will use
        newtab->InsertComponentAfter(newcomp, DEFAULT OBJECT, TRUE);
      }
    }
    RETURN [
      sections := Length(type.sections)
    ];
  }

  PUBLIC MACRO SetControllersEnabled(BOOLEAN enabled) {
    FOREVERY(RECORD controller FROM this->metacontrollers)
      controller.fsinstance->enabled := enabled;
  }
>;

/** Create a metatabs renderer
    @param objdata Configuration record for the metatabs renderer
    @cell(integer) objdata.objectid
    @cell(integer) objdata.parent
    @cell(integer) objdata.isfolder
    @cell(integer) objdata.type
    @cell(boolean) objdata.requireworkflow
*/
PUBLIC OBJECT FUNCTION CreateMetatabsRenderer(RECORD objdata) {
  objdata := ValidateOptions([
    objectid := 0,
    parent := 0,
    isfolder := FALSE,
    type := -1,
    requireworkflow := FALSE
  ], objdata, [
    required := STRING[ "objectid", "parent", "isfolder", "type", "requireworkflow" ]
  ]);

  RECORD metatabsconfig := CallJS("@mod-publisher/lib/internal/siteprofiles/metatabs.ts#describeMetaTabsForHS", objdata);
  OBJECT applytester := RecordExists(metatabsconfig) ? GetApplyTesterForHSInfo(metatabsconfig.__hsinfo) : GetApplyTesterForObject(objdata.objectid); //fallback. test-whfs-history-v4 triggers it

  RETURN NEW MetatabsRenderer(metatabsconfig, applytester);
}
