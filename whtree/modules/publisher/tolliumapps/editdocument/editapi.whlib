<?wh

LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";

LOADLIB "mod::system/lib/database.whlib";

// TODO factor out parts to share with objectprops
PUBLIC STATIC OBJECTTYPE EditDocumentAPI
<
  OBJECT editscreen;

  RECORD ARRAY instancedatacache;

  RECORD ARRAY extensiondata;

  MACRO PTR pvt_oncanview;
  MACRO PTR pvt_oncanpreview;


  /// Deprecated and ignored since WH5.9.
  PUBLIC STRING ARRAY editcontenttypes;

  /// Can this file be published? Some library/reusable content may disable this. This is not foolproof but prevents obvious mistakes
  PUBLIC BOOLEAN canpublish;

  /// Callback to mark us as readonly
  PUBLIC MACRO PTR onsetenabled;

  /// Callback invoked when the editor should (re)load the edited object's instance data using GetInstanceData
  PUBLIC MACRO PTR onloaddata;

  /// Callback invoked when the editor should write the edited object's instance data using SetInstanceData
  PUBLIC MACRO PTR onstoredata;

  /// Callback BOOLEAN FUNCTION(BOOLEAN ispublish) invoked to verify whether the save should happen. ispublish is FALSE if we're saving as draft. This can be used to add additional confirmation dialogs, but does not replace any existing dialog
  PUBLIC MACRO PTR onprechecksave;

  /// Callback BOOLEAN FUNCTION() invoked to verify whether to publish. Overwrite this to replace the confirmsaveandpublish dialog
  PUBLIC MACRO PTR onconfirmsaveandpublish;

  /// Callback invoked with a feedback object for validation before explicit save
  PUBLIC MACRO PTR onvalidate;

  /// Callback MACRO(OBJECT work, BOOLEAN ispublish) invoked when we've saved the document. ispublish is FALSE if we're saving as draft
  PUBLIC MACRO PTR onsave;

  /// Callback BOOLEAN FUNCTION(OBJECT fsobject) invoked after changes to check if the published file can be viewed
  PUBLIC PROPERTY oncanview(pvt_oncanview, SetOnCanView);

  /// Window open action callback MACRO(OBJECT openhandler, OBJECT fsobject) invoked to view the published file
  PUBLIC MACRO PTR onview;

  /// Callback BOOLEAN FUNCTION(OBJECT fsobject) invoked after changes to check if the file draft can be previewed
  PUBLIC PROPERTY oncanpreview(pvt_oncanpreview, SetOnCanPreview);

  /// Window open action callback MACRO(OBJECT openhandler, OBJECT fsobject, BOOLEAN autosave) invoked to view the draft or autosave
  PUBLIC MACRO PTR onpreview;

  /// Callback STRING FUNCTION(OBJECT fsobject, DATETIME validuntil, STRING password) invoked to retrieve a preview link suitable for sharing/emailing
  PUBLIC MACRO PTR onpreviewlink;

  /// Root folders to add for internal link dialogs
  PUBLIC INTEGER ARRAY internallinkroots;

  PUBLIC PROPERTY baseobject(GetBaseObject, -);
  PUBLIC PROPERTY editobject(GetEditObject, -);

  PUBLIC PROPERTY readonly(this->editscreen->readonly, -);

  MACRO NEW(OBJECT editscreen)
  {
    this->editscreen := editscreen;
    this->canpublish := TRUE;
    this->onconfirmsaveandpublish := PTR this->editscreen->DefaultConfirmAndSavePublishHandler;
  }

  /// Should be invoked by editors to mark the document as dirty, is implicitly called when using SetInstanceData
  PUBLIC MACRO MarkDocumentAsDirty()
  {
    //DumpValue(GetStackTrace(), [ name := "BE DIRTY!"]);
    this->editscreen->^dirtylistener->SetDirty();
  }

  PUBLIC RECORD FUNCTION GetMetadata()
  {
    RETURN SELECT data FROM system.fs_objects WHERE id = this->editobject;
  }

  PUBLIC MACRO UpdateMetadata(RECORD fields)
  {
    fields := ValidateOptions(CELL[ data := DEFAULT BLOB], fields, [ optional := ["data"]]);
    this->editscreen->GetInstanceObject()->UpdateMetadata(fields);
  }

  MACRO AssertWorkflowSupport(STRING type) {
    RECORD typeinfo := LookupContentTypeByName(type);
    IF(NOT RecordExists(typeinfo))
      THROW NEW Exception(`Type '${type}' is not a known content type`);
    IF(NOT typeinfo.workflow)
      THROW NEW Exception(`Type '${type}' has not been enabled for workflow. To enable it, set workflow="true" in the content type definition`);
  }

  /// Get instance data of a given type from the object being edited (containing last changes)
  PUBLIC RECORD FUNCTION GetInstanceData(STRING type) {
    this->AssertWorkflowSupport(type);

    RECORD originaldata := this->editscreen->GetInstanceObject()->GetInstanceData(type);
    RECORD cached := SELECT * FROM this->instancedatacache WHERE namespace = type;
    IF (RecordExists(cached))
      RETURN CELL[...originaldata, ...cached.data];

    RETURN originaldata;
  }

  /** Write instance data of a given type to the object being edited. We store any cell modified, but shouldn't 'save and continously replace' a cell that was never touched by the editor
  */
  PUBLIC MACRO SetInstanceData(STRING type, RECORD data)   {
    this->AssertWorkflowSupport(type);

    IF (NOT RecordExists(SELECT FROM this->instancedatacache WHERE namespace = type)) {
      INSERT [ namespace := type, data := data ]
        INTO this->instancedatacache AT END;
    } ELSE {
      UPDATE this->instancedatacache
         SET data := CELL[...COLUMN data, ...VAR data]
       WHERE namespace = type;
    }
    this->MarkDocumentAsDirty();
  }

  PUBLIC MACRO CancelEditor()
  {
    this->editscreen->tolliumresult := "cancel";
  }

  /* Extend the editor meta tabs.
     This is an EXPERIMENTAL API for PTHU and it will go away in the future, hopefully to be replaced with clean siteprl hooks not requiring OnSave etc hooking (eg we might start passing 'contentdata' to the tabs) */
  PUBLIC MACRO __LoadMetaTabs(STRING tabsptr)
  {
    /* API TODO ?
       - We should ask the registrant to tell us their contenttype and add it to the hooks ourselves
       - We should be able to invoke Load and Store on the tabs? or offer a more generic way for users
         to hook these callbacks (like ADdEventListener) so we don't have to supply all those custom extension bases
       - We should allow access to the dirtylisteners
       */

    this->editscreen->^maintabs->LoadTabsExtension(tabsptr);
    IF(NOT this->editscreen->^metapart->visible AND this->editscreen->^maintabs->GetNumVisiblePages() > 0)
    {
      this->editscreen->^metapart->visible := TRUE;
    }
  }

  PUBLIC BOOLEAN FUNCTION __HaveMetaTabs()
  {
    RETURN this->editscreen->^metapart->visible;
  }

  PUBLIC MACRO __FlushInstanceData(OBJECT targetobject)
  {
    this->onstoredata();
    FOREVERY (RECORD cached FROM this->instancedatacache)
    {
      targetobject->SetInstanceData(cached.namespace, cached.data);
    }

    this->instancedatacache := DEFAULT RECORD ARRAY;
  }

  INTEGER FUNCTION GetBaseObject()
  {
    RETURN this->editscreen->GetBaseFSObject()->id;
  }

  INTEGER FUNCTION GetEditObject()
  {
    RETURN this->editscreen->GetInstanceObject()->id;
  }

  MACRO SetOnCanView(MACRO PTR callback)
  {
    this->pvt_oncanview := callback;
    this->editscreen->RefreshState();
  }

  MACRO SetOnCanPreview(MACRO PTR callback)
  {
    this->pvt_oncanpreview := callback;
    this->editscreen->RefreshState();
  }
>;
