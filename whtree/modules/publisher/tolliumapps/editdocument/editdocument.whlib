<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/langspecific.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/history.whlib";
LOADLIB "mod::publisher/lib/preview.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/internal/support.whlib";
LOADLIB "mod::publisher/lib/internal/tollium-helpers.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editapi.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editbase.whlib";

LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

/* Saving logic:
   Always:
    autosave -> save to private draft

   File not in versioned site:
      File not published
         save -> save to live object
         publish -> save to live object, publish the live file
      File published
         save -> save to draft object
         publish -> save draft to live object

   File in versioned site:
      Contenttype not in versioned data
         save -> save to live object
         publish -> save to live object, create draft (+publish), ask 'submit for publicaction'

      Contenttype in versioned data
         locked? Exit now
         save -> save to public draft
         publish -> save to public draft, add (+publish), ask 'submit for publicaction'
*/

CONSTANT STRING ARRAY requiredapiproperties :=  [ "onsetenabled", "onloaddata", "onstoredata" ];

PUBLIC STATIC OBJECTTYPE EditDocumentApp EXTEND PublisherEditorBase <
  // ---------------------------------------------------------------------------
  //
  // Variables
  //

  /// Last modificationdate of the published version (inited when opened)
  DATETIME lastmod;

  /// Rich document file instancedata
  RECORD filedata;

  /// When autosave may start working (set to TRUE when is RTE initialized, set to FALSE after 'Save on close: NO')
  BOOLEAN canautosave;

  /// Whether or not to show the draft bar during this session
  BOOLEAN showdraftbar;

  /// Type of the file being edited
  INTEGER filetype;

  RECORD filetypeinfo;

  // ---------------------------------------------------------------------------
  //
  // Init
  //
  MACRO NEW() : PublisherEditorBase(FALSE) {

  }

  MACRO Init(RECORD data) {
    data := EnforceStructure( [ assumewriteaccess := FALSE, objeditor := DEFAULT RECORD ], data);

    IF(NOT RecordExists(data.objeditor))
      THROW NEW Exception("EditDocumentApp requires objeditor parameter");

    this->frame->flags.isdeveloper := this->contexts->user->ShowDeveloperOptions();
    this->frame->flags.isdirty := FALSE;
    ^developersmenu->visible := this->frame->flags.isdeveloper;
    this->contexts->editdocumentapi := NEW EditDocumentAPI(PRIVATE this);

    this->frame->flags.canedit := FALSE;
    this->frame->flags.canpublish := FALSE;

    this->contexts->editdocumentapi->internallinkroots := data.internallinkroots;
    this->SetupEditBaseForExistingObject(data.id, CELL[ data.readonly, data.assumewriteaccess, useworkflow := TRUE, workflowtypes := data.objeditor.types ]);

    OBJECT fsobject := OpenWHFSObject(this->itemid);

    IF(this->workflowmgr->IsReadOnly() AND NOT this->tolliumuser->HasRightOn("system:fs_browse", this->itemid)) //not even browse access?
    {
      this->tolliumresult := "cancel";
      RETURN;
    }
    IF (NOT this->GetExclusiveAccess([ id := this->itemid ], [ onbeforesteal := PTR this->GotScreenStolen ]))
      RETURN;

    this->showdraftbar := TRUE;

    //ADDME deal with cases where users already make other modifications while we're stil opening ?

    IF(NOT ObjectExists(fsobject))
    {
      this->tolliumresult := "cancel";
      RETURN;
    }


    this->filedata := [ name := fsobject->name, modificationdate := fsobject->modificationdate ];
    this->filetype := fsobject->type;
    this->filetypeinfo := DescribeContentTypeById(fsobject->type, [ isfolder := FALSE, mockifmissing := TRUE ]);

    //Loads the extensions for the file type and invokes additional editor hooks
    this->SetupForFileEdit(this->itemid, data.objeditor);
    this->RefreshReadonly();

    ^publicationlistener->masks := [ "system:whfs.folder." || fsobject->parent, "publisher:publish.folder." || fsobject->parent ];

    RECORD competingdraft := this->workflowmgr->GetCurrentDraft();
    IF(RecordExists(competingdraft) AND NOT competingdraft.mine) {
      STRING user := this->contexts->userapi->GetUserDisplayName(competingdraft.user_auth_object);
      STRING when := this->tolliumuser->FormatDatetime(competingdraft.draft_saved, "minutes", TRUE, FALSE);

      IF (this->RunSimpleScreen("confirm"
                               , GetTid("publisher:tolliumapps.editdocument.messageboxes.otherhasdraft", user, when)
                               , [ buttons := [ "ok","cancel"]
                                 , dontshowkey := "publisher.editdocument.otherhasdraft"
                                 , dontshowbuttons := ["ok"]
                                 ]) != "ok") {
        this->tolliumresult := "cancel";
        RETURN;
      }
    }

    RECORD continue_autosave := this->workflowmgr->GetResumableAutosave();
    BOOLEAN mustloadrte := TRUE;
    IF(RecordExists(continue_autosave)) {
      this->ReloadRTEContents(); //make sure you can see the autosave in question

      STRING response := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.resumeautosave"), [ buttons := [ "yes", "no", "cancel" ]]);
      IF(response != "yes" AND response != "no") {
        this->tolliumresult := "cancel";
        RETURN;
      }

      IF(response = "yes") {
        mustloadrte := FALSE;
        ^dirtylistener->SetDirty();
      } ELSE {
        OBJECT work := this->BeginUnvalidatedWork();
        this->workflowmgr->DiscardAutosavedChanges();
        work->Finish();
      }
    }

    this->lastmod := this->filedata.modificationdate;
    //this->historybtn->pressed := this->historypart->visible ;

    IF(mustloadrte)
      this->ReloadRTEContents();

    FOREVERY(RECORD extendprop FROM this->extendprops)
      extendprop.tabs->RunExtensionsPostInit();

    // Can start autosaving when RTE is initialized (not before, would save empty document)
    this->canautosave := NOT this->readonly;


    /*
    When we are called from another application which tells us the user MUST be able to have write access,
    we can assume that application is meant to be used to manage this document.
    This means we don't want to confuse users by pointing them to the original source file/folder in the Publisher.
    Also they may not have access to the original location of the document.
    */
    IF (data.assumewriteaccess)
      ^openinpublisher->enabled := FALSE;

    this->RefreshState();

    IF(NOT this->readonly AND this->workflowmgr->HaveDraftOrAutosavedChanged())
      this->CatchupWithChanges();

    RegisterTimedCallback(AddTimeToDate(1, GetCurrentDatetime()), PTR this->CalculateInitialDirty);
  }

  MACRO CatchupWithChanges() {
    //FIXME compare previous and current published versions for title/description - update or warn if these differ from the draft
    // dumpvalue(ListObjectHistory(this->itemid), 'boxed');
  }

  MACRO AddTypesSections() { //Insert metadata sheets as new sections
    INTEGER insertpos := 1;

    FOREVERY(RECORD type FROM this->renderer->metatabsconfig.types) {
      FOREVERY(RECORD section FROM this->renderer->PrepareTypeSections(this->contexts, type, [ dirtylistener := ^dirtylistener ])) {
        OBJECT newsection := this->CreateTolliumComponent("section");
        newsection->title := section.title;
        newsection->open := TRUE;

        FOREVERY(RECORD line FROM section.lines) {
          newsection->panel->layout := line.layout;
          newsection->panel->InsertComponentAfter(line.line, DEFAULT OBJECT, TRUE);
        }
        ^metadatatab->InsertComponentAfter(newsection, DEFAULT OBJECT, TRUE);
      }
    }

    this->RefreshMetaTabsReadonly();
  }

  MACRO AddLegacyExtensions() {
    //Legacy extensions expect to be loaded into a tab. Build tabs to make them happy
    FOREVERY(RECORD ext FROM this->renderer->metatabsconfig.extend_props) {
      //Build a tabs container and an instance to exchange whfstype data
      OBJECT tabs := this->CreateTolliumComponent("tabs");
      OBJECT whfsinstance := this->CreateCustomComponent("http://www.webhare.net/xmlns/publisher/components", "whfsinstance");
      whfsinstance->type := OpenWHFSType(ext.whfs_type);
      //TODO setup whfsinstance->basefsobject like objectprops does? but objectprops seems a bit dangerous for New Item and Duplicate Item, where sourceitem does not point to the target

      tabs->extendcomponents := [[ name := "contentdata", component := whfsinstance ]];
      tabs->width := "1pr";

      //'Dummy' main tab for compoonents which expect an insertpoint to exist there
      OBJECT maintab := tabs->InsertTabAt(0, GetTid("publisher:tolliumapps.objectprops.objectprops.maintab"));
      OBJECT insertpoint := this->CreateTolliumComponent("spacer");
      insertpoint->visible := FALSE;
      maintab->InsertComponentAfter(insertpoint, DEFAULT OBJECT, TRUE);
      FOREVERY(STRING name FROM [ "name", "title", "description", "keywords", "publicationsettings", "settings", "properties", "seosettings", "advancesettings" ])
        INSERT CELL[ name, component := insertpoint ] INTO tabs->insertpoints AT END;

      RECORD loadresult := tabs->LoadTabsExtension(ext.extension);
      IF(Length(maintab->GetChildComponents()) = 1) //no inserts
        maintab->visible := FALSE;

      OBJECT ARRAY visiblepages := SELECT AS OBJECT ARRAY page FROM ToRecordArray(tabs->pages,"page") WHERE page->visible;
      IF(Length(visiblepages) = 1) {
        tabs->selectedtab := visiblepages[0];
        visiblepages[0]->spacers := DEFAULT RECORD;
        tabs->type := "server";
      }

      //Add the tabs as a section
      OBJECT newsection := this->CreateTolliumComponent("section");
      //Copy only tab's title unless its just the General tab
      newsection->title := tabs->type = "server" AND maintab->visible = FALSE? visiblepages[0]->title : GetTid(ext.title);
      newsection->open := TRUE;
      newsection->panel->InsertComponentAfter(tabs, DEFAULT OBJECT, TRUE);
      ^metadatatab->InsertComponentAfter(newsection, DEFAULT OBJECT, TRUE);

      FOREVERY(OBJECT comp FROM loadresult.components) {
        IF(MemberExists(comp,'dirtylistener')) //TODO recursive dirty event capturing would be nicer
          comp->dirtylistener := ^dirtylistener;
      }

      INSERT CELL[ ext.whfs_type
                 , loadresult.components
                 , loadresult.fragment
                 , whfsinstance
                 , tabs
                 ] INTO this->extendprops AT END;
    }
  }

  MACRO SetupForFileEdit(INTEGER objectid, RECORD objecteditor)
  {
    this->AddTypesSections();
    this->AddLegacyExtensions();
    ^metapart->visible := TRUE;

    OBJECT filehandler := this->LoadScreen(objecteditor.documenteditor);

    FOREVERY(STRING requiredprop FROM requiredapiproperties)
      IF(IsDefaultValue(GetMember(this->contexts->editdocumentapi, requiredprop)))
        THROW NEW Exception(`Document editor ${objecteditor.documenteditor} must implement '${requiredprop}'`);

    ^doceditor->contents := filehandler;

    // Merge document editor toolbar
    IF(Length(^doceditor->contents->frame->toolbars) > 0)
      ^toolbar->items := ^toolbar->items CONCAT ^doceditor->contents->frame->toolbars[0]->items;

    // Merge document editor main menu
    IF (ObjectExists(^doceditor->contents->frame->menubar))
      this->MergeIntoMainMenu(this->frame->menubar, ^doceditor->contents->frame->menubar->items);

    // Apply editor hooks. We do this after setting up the main screen (which will already have hooked into the EditDocumentAPI at this point)
    this->contexts->applytester->RunSiteprofileHookTarget("publisher:documenteditor",
        [ contexts := this->contexts
        , objectid := objectid
        ]);
  }

  MACRO MergeIntoMainMenu(OBJECT mymenu, RECORD ARRAY newitems)
  {
    // Is there an 'editdocumentmenu' insertion point?
    INTEGER pos :=
        (SELECT AS INTEGER #items + 1
           FROM mymenu->items
          WHERE NOT isdivider
                AND menuitem->title = "#####") - 1;
    IF (pos < 0)
      pos := Length(mymenu->items);

    FOREVERY (RECORD newitem FROM newitems)
    {
      // Check if a submenu with the new item's name already exists (to merge submenus)
      OBJECT existing := NOT newitem.isdivider AND newitem.menuitem->name NOT LIKE "tollium_*$*" AND RecordExists(newitem.menuitem->items)
          ? SELECT AS OBJECT menuitem
              FROM mymenu->items
             WHERE NOT isdivider
                   AND menuitem->name NOT LIKE "tollium_*$*"
                   AND ToUppercase(menuitem->name) = ToUppercase(newitem.menuitem->name)
                   AND RecordExists(menuitem->items)
          : DEFAULT OBJECT;
      IF (ObjectExists(existing))
      {
        this->MergeIntoMainMenu(existing, newitem.menuitem->items);
      }
      ELSE
      {
        INSERT newitem INTO mymenu->items AT pos;
        pos := pos + 1;
      }
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO CalculateInitialDirty() {
    // Need the client state at this moment, so require a sync.
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->RefreshState();
  }

  MACRO ReloadRTEContents() {
    this->contexts->editdocumentapi->onloaddata();
    IF(this->tolliumresult != "") //onloaddata may have cancelled us
      RETURN;

    this->LoadBaseData(this->workflowmgr); //TODO should be instance object once versioning works
    this->LoadMetadata(this->workflowmgr, FALSE);
    this->RefreshState();
  }

  MACRO RefreshState()  {
    // Do we have a published version?
    OBJECT fsobj := OpenWHFSObject(this->itemid);
    BOOLEAN ispublishabletoweb := ObjectExists(fsobj) AND fsobj->parentsite != 0 AND OpenSite(fsobj->parentsite)->outputweb != 0 AND this->filetypeinfo.ispublishable AND this->contexts->editdocumentapi->canpublish;
    this->RefreshReadonly();

    IF (ObjectExists(fsobj)) {
      IF (this->contexts->editdocumentapi->oncanpreview != DEFAULT MACRO PTR)
        this->frame->flags.canpreview := this->contexts->editdocumentapi->oncanpreview(fsobj);
      ELSE
        this->frame->flags.canpreview := ispublishabletoweb;
      IF (this->contexts->editdocumentapi->oncanview != DEFAULT MACRO PTR)
        this->frame->flags.canview := this->contexts->editdocumentapi->oncanview(fsobj);
      ELSE
        this->frame->flags.canview := GetOncePublishedFromPublished(fsobj->published) AND ispublishabletoweb;
    } ELSE {
      this->frame->flags.canpreview := FALSE;
      this->frame->flags.canview := FALSE;
    }
    this->frame->flags.canrevertdraft := this->workflowmgr->HaveDraftOrAutosavedChanged() AND this->frame->flags.canedit AND NOT this->readonly;

    // TODO: perhaps we still want to do something like ^title->onchange := PTR this->SyncNameWithTitle if the doc was never saved?

    //ADDME: Show draft if no published version available
    RECORD currentdraft := this->workflowmgr->GetCurrentDraft();
    IF(RecordExists(currentdraft) AND this->showdraftbar AND NOT this->readonly) {
      STRING when := this->tolliumuser->FormatDatetime(currentdraft.draft_saved, "minutes", TRUE, FALSE);
      INTEGER modifiedby := currentdraft.user_auth_object;
      STRING basetext;
      IF(modifiedby = this->tolliumuser->authobjectid) {
        basetext := this->GetTid(".draftmode-by-you", when);
      } ELSE {
        STRING user := this->contexts->userapi->GetUserDisplayName(modifiedby);
        basetext := this->GetTid(".draftmode-by-other", user, when);
      }

      ^draftmode->htmlvalue := EncodeHTML(basetext);
      ^draftbar_canceldraft->visible := this->frame->flags.canrevertdraft;
      ^draftbar->visible := TRUE;
    } ELSE
      ^draftbar->visible := FALSE;

    BOOLEAN allowpublish;

    this->contexts->editdocumentapi->onsetenabled(NOT this->readonly);
    this->frame->flags.canedit := NOT this->readonly;

    RECORD state;

    IF (this->frame->flags.isdirty)
    {
      state :=
          [ icon :=   "tollium:status/unknown"
          , text :=   this->GetTid(".documenthasunsavedchanges")
          ];
    }
    ELSE IF (RecordExists(currentdraft))
    {
      state :=
          [ icon :=   "tollium:actions/edit"
          , text :=   this->GetTid(".documenthasunpublishedchanges")
          ];
    }
    ELSE
    {
      // Get published file FS object
      IF (ObjectExists(fsobj))
      {
        INTEGER published := fsobj->published;
        IF (fsobj->lastpublishdate > AddTimeToDate(-1000, GetCurrentDateTime()))
        {
          published := ConvertToWillPublish(published, FALSE, FALSE, PubPrio_DirectEdit);
          RegisterTimedCallback(AddTimeToDate(500, GetCurrentDateTime()), PTR this->RefreshState());
        }

        STRING publishedat := this->tolliumuser->FormatDateTime(fsobj->modificationdate, "minutes", TRUE, TRUE);
        state := GetFileStatusFromPublished(published, ispublishabletoweb, publishedat, DEFAULT RECORD);
      }
      ELSE
      {
        state := GetFileStatusFromPublished(0, FALSE, "", DEFAULT RECORD);
      }
    }

    BOOLEAN havesomethingtopublish := RecordExists(currentdraft) OR (ObjectExists(fsobj) AND NOT fsobj->publish);
    //We do not care about ispublishabletoweb for the publish button as we let the Publish button act as 'publish when site goes online' button
    this->frame->flags.canpublish := this->frame->flags.canedit AND (this->frame->flags.isdirty OR havesomethingtopublish) AND this->contexts->editdocumentapi->canpublish;
    this->frame->title := SELECT AS STRING name FROM system.fs_objects WHERE id = this->itemid;

    ^filestatusicon->SetImageSrc(state.icon);
    ^filestatustext->value := state.text;

    ^publishmenu->visible := NOT this->readonly;
    ^savemenu->visible := NOT this->readonly;
    ^canceldraftmenu->visible := NOT this->readonly;
    ^publishbutton->visible := NOT this->readonly;
    ^saveasdraftbutton->visible := NOT this->readonly;

  }

  MACRO OnDraftmodeclick(STRING link)
  {
    IF(link LIKE "*#publish")
      this->DoPublish();
    ELSE IF(link LIKE "*#cancel")
      this->DoCanceldraft();
  }

  BOOLEAN FUNCTION ExecuteSave(BOOLEAN withpublish) {
    IF(NOT this->UpdateAutosave()) { //NOTE Flushes to an autosave if needed but may also clear unneeded autosave objects and set this->currentautosave to DEFAULT OBJECT
      //There is no autosave anymore - we are in sync with the latest draft/final version
      IF(NOT withpublish) { //we're not asked to update a final object
        Print("save ignored - no autosave to update draft/final with\n");
        RETURN TRUE; //nothing to do. UpdateAutosave will have updated dirty flag & state
      }
    }

    IF(NOT this->assumewriteaccess AND NOT this->tolliumuser->HasRightOn("system:fs_fullaccess", this->itemid)) {
      Print("save aborted - not privileged\n");
      RETURN FALSE; //FIXME report
    }

    IF (this->contexts->editdocumentapi->onvalidate != DEFAULT MACRO PTR) {
      OBJECT feedback := this->BeginFeedback();
      this->contexts->editdocumentapi->onvalidate(feedback);
      IF(NOT feedback->Finish())
        RETURN FALSE;
    }

    IF(this->contexts->editdocumentapi->onprechecksave != DEFAULT FUNCTION PTR AND NOT this->contexts->editdocumentapi->onprechecksave(withpublish))
      RETURN FALSE; //aborted because some warning was scary enough

    //When just saving there's no need to validate - it would even be extremely annoying if there's a lot of required metadata
    OBJECT work := this->BeginWork([ validate := withpublish ? [this->frame] : OBJECT[] ]);
    OBJECT finalitem := OpenWHFSObject(this->itemid); //represents OpenHWHFSObject(this->itemid) in the original code, but we can't update this->itemid until we've committed
    this->CheckNameConflict(work);

    iF(withpublish AND finalitem->publish = FALSE AND finalitem->ispinned)
      work->AddError(this->GetTid(".cannotpublishpinnedfile"));


    IF (NOT work->HasFailed()) {
      //As we can't apply external changes yet (CatchupWithChanges) just don't apply changes to fields that aren't controlled xurrently
      STRING ARRAY updatemetadatafields;
      IF(^title->visible AND ^metapart->visible)
        INSERT "title" INTO updatemetadatafields AT END;
      IF(^description->visible AND ^metapart->visible)
        INSERT "description" INTO updatemetadatafields AT END;
      IF(^keywords->visible AND ^metapart->visible)
        INSERT "keywords" INTO updatemetadatafields AT END;

      this->workflowmgr->Save(CELL[ finalize := withpublish, publish := withpublish, updatemetadatafields ]);

      //Warn if publishing to an unpublished site (but not if publishing outside a site)
      IF (withpublish AND finalitem->parentsite != 0 AND OpenSite(finalitem->parentsite)->outputweb = 0)
        work->AddWarning(this->GetTid(".sitenotpublished"));
    }

    IF (NOT work->HasFailed() AND this->contexts->editdocumentapi->onsave != DEFAULT FUNCTION PTR)
      this->contexts->editdocumentapi->onsave(work, withpublish);

    IF (NOT work->Finish())
      RETURN FALSE;

    ^dirtylistener->ClearDirty();
    //DumpValue(GetStackTrace(), [ name := "BE CLEAN!"]);
    this->LoadBaseData(this->workflowmgr); //this also updates name/title requirements
    this->RefreshState();

    RETURN TRUE;
  }

  BOOLEAN FUNCTION UpdateAutosave()
  {
    IF (NOT this->frame->flags.isdirty AND NOT this->workflowmgr->HaveAutosavedChanges())
      RETURN FALSE; //nothing to do, don't bother the database with work

    OBJECT work := this->BeginUnvalidatedWork();
    IF (this->frame->flags.isdirty) {
      this->contexts->editdocumentapi->__FlushInstanceData(this->workflowmgr);
      this->SaveMetadata(-1); //doesn't matter which ID will pass, editbase will talk to workflowmgr anyway
    } ELSE IF (this->workflowmgr->HaveAutosavedChanges()) {
      this->workflowmgr->DiscardAutosavedChanges();
      this->frame->flags.isdirty := FALSE;
    }

    work->Finish();
    this->RefreshState();
    RETURN this->workflowmgr->HaveAutosavedChanges();
  }

  OBJECT FUNCTION GetBaseFSObject()
  {
    RETURN OpenWHFSObject(this->itemid);
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO GotUnload()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Just save what we have
    this->UpdateAutosave();
  }

  MACRO OnInterval()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Request client state. Wait max 30 secs for client reply, else don't save.
    // Don't need to lock the client to wait for our updates
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->UpdateAutosave();
  }

  MACRO GotFolderChangeEvents(RECORD ARRAY events)
  {
    BOOLEAN have_change;
    FOREVERY (RECORD event FROM events)
    {
      IF (RecordLowerBound(event.data.files, [ file := this->itemid ], [ "FILE" ]).found)
        have_change := TRUE;
    }

    IF (have_change)
      this->RefreshState();
  }

  MACRO GotDirty()
  {
    this->frame->flags.isdirty := ^dirtylistener->dirty;
    this->RefreshState();
  }

  MACRO GotScreenStolen()
  {
    // Screen was stolen, don't save changes on onload
    this->canautosave := FALSE;
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoSave() {
    this->ExecuteSave(FALSE);
  }

  MACRO DoPublish() { //Save and publish the file
    RECORD metadata;
    OBJECT targetfile := OpenWHFSObject(this->itemid);

    IF(targetfile->publish = FALSE AND targetfile->ispinned) { // The file's publish status is locked by the pin
      this->RunSimpleScreen("error", this->GetTid(".cannotpublishpinnedfile"));
      RETURN;
    }

    this->UpdateAutosave();
    IF(NOT this->contexts->editdocumentapi->onconfirmsaveandpublish())
      RETURN;

    this->ExecuteSave(TRUE);
  }

  BOOLEAN FUNCTION DefaultConfirmAndSavePublishHandler()
  {
    RETURN this->RunSimpleScreen("confirm"
                              , GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmsaveandpublish")
                              , [ dontshowkey := "publisher.editdocument.confirmsaveandpublish"
                                , dontshowbuttons := ["yes"]
                                ]) = "yes";
  }

  MACRO DoSendPreviewLink() {
    IF (this->frame->flags.isdirty) { //you must save before sending a link, as autosaves are too transient
      IF(this->RunSimpleScreen("question",
        GetTid("publisher:tolliumapps.editdocument.messageboxes.savebeforepreview", OpenWHFSObject(this->itemid)->name),
        [ buttons := ["yes","no"], dontshowkey := "publisher.editdocument.savebeforepreview", dontshowbuttons := ["yes"]]) != "yes")
         RETURN; //cancelled

      this->ExecuteSave(FALSE);
    }

    this->RunScreen("#sendpreviewlink");
  }

  PUBLIC OBJECT FUNCTION GetPreviewObject(BOOLEAN autosave) {
    this->UpdateAutosave();
    RETURN autosave ? this->workflowmgr->__GetAutosavedObject() : this->workflowmgr->__GetDraftObject();
  }

  PUBLIC STRING FUNCTION __CreatePreviewLink(DATETIME validuntil, STRING password, BOOLEAN autosave, STRING share) {
    this->UpdateAutosave();
    RETURN GetPreviewLink(this->GetPreviewObject(autosave)->id, CELL[validuntil, password, share ]);
  }

  MACRO DoPreviewCurrentContent(OBJECT openhandler)
  {
    this->UpdateAutosave();
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(TRUE), TRUE);
    ELSE {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->workflowmgr->GetPreviewLink("autosave");
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoPreviewdraft(OBJECT openhandler)
  {
    this->UpdateAutosave();
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(FALSE), FALSE);
    ELSE {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->workflowmgr->GetPreviewLink("draft");
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoViewPublished(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onview(openhandler, OpenWHFSObject(this->itemid));
    ELSE
      openhandler->SendURL(OpenWHFSObject(this->itemid)->url);
  }

  BOOLEAN FUNCTION Cancel()
  {
    this->DoClose();
    RETURN this->tolliumresult != "";
  }

  MACRO DoClose()
  {
    IF (this->frame->flags.isdirty)
    {
      STRING res := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.savebeforeclose", OpenWHFSObject(this->itemid)->name), [ buttons := ["yes","no","cancel"]]);
      SWITCH (res)
      {
        CASE "yes"
        {
          IF (this->ExecuteSave(FALSE))
            this->tolliumresult := "ok";
        }
        CASE "no"
        {
          OBJECT work := this->BeginUnvalidatedWork();
          this->workflowmgr->DiscardAutosavedChanges();
          work->Finish();

          this->tolliumresult := "ok";
        }
      }
    }
    ELSE
      this->tolliumresult := "ok";
  }

  MACRO DoCanceldraft()
  {
    IF (this->RunSimpleScreen("confirm", GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmcanceldraft")) != "yes")
      RETURN;

    OBJECT work := this->BeginUnvalidatedWork();
    this->workflowmgr->DiscardDraftAndAutosavedChanges();
    IF (work->Finish()) {
      this->ReloadRTEContents();
      this->RefreshState();

      this->RunSimpleScreen("info", GetTid("publisher:tolliumapps.editdocument.messageboxes.draftcancelled"));
    }
  }

  MACRO DoHidedraftbar()
  {
    this->showdraftbar := FALSE;
    ^draftbar->visible := FALSE;
  }

  MACRO DoOpenInPublisher()
  {
    RunPublisherFileManager(this, this->itemid);
  }

  MACRO DoInspectAutosave() {
    this->DoInspect("autosave");
  }
  MACRO DoInspectDraft() {
    this->DoInspect("draft");
  }
  MACRO DoInspectFinal() {
    this->DoInspect("final");
  }
  MACRO DoInspect(STRING which) {
    INTEGER inspectid := this->workflowmgr->GetObjectIdForInspect(which);
    IF(inspectid != 0)
      RunFSObjectInspectDialog(this, inspectid);
    ELSE
      this->RunSimpleScreen("info", `No ${which} present`);
  }
>;

PUBLIC STATIC OBJECTTYPE SendPreviewLink EXTEND TolliumScreenBase <
  MACRO Init(RECORD data) {
    //round up to next hour, 7 days away
    ^validuntil->value := GetRoundedDatetime(AddTimeToDate(60*60000, AddDaysToDate(7, GetCurrentDateTime())), 60*60000);
  }
  BOOLEAN FUNCTION OnShareNext() {
    OBJECT work := this->BeginUnvalidatedFeedback();
    work->Validate(^sharepage);
    IF(^validuntil->value < GetCurrentDatetime())
      work->AddErrorFor(^validuntil, this->GetTid(".validinthepast"));
    ELSE IF(^validuntil->value > AddDaysToDate(31,GetCurrentDatetime()))
      work->AddErrorFor(^validuntil, this->GetTid(".validtoolong")); //the error is about 30 days, but we give you 1 day of tolerance

    RETURN work->Finish();
  }
  MACRO InitFinishPage(RECORD data) {
    IF (this->contexts->editdocumentapi->onpreviewlink != DEFAULT MACRO PTR) //FIXME we can't pass the 'share' value to the custom previewlink. reconsider API as its very specific to RTD too, can't user work through central preview API
      ^link->value := this->contexts->editdocumentapi->onpreviewlink(this->tolliumparent->GetPreviewObject(FALSE), ^validuntil->value, ^passwordprotect->value ? ^password->value : "");
    ELSE
      ^link->value := this->tolliumparent->__CreatePreviewLink(^validuntil->value, ^passwordprotect->value ? ^password->value : "", FALSE, ^share->value);

    STRING mailbody := this->GetTid(".mailbodypreview") || "\n\n" || ^link->value || "\n\n" || this->GetTid(".mailbodypreview2");
    STRING mailsubject := this->GetTid(".mailbodysubject");
    ^maillink->htmlvalue := '<a href="mailto:?to=&subject=' || EncodeURL(mailsubject) || '&body=' || EncodeURL(mailbody) || '">' || EncodeHTML(this->GetTid(".maillink")) || '</a>';
  }
>;
