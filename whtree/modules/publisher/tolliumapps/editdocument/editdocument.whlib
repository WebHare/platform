<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/langspecific.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/internal/support.whlib";
LOADLIB "mod::publisher/lib/internal/tollium-helpers.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editapi.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editbase.whlib";

LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";

/* Saving logic:
   Always:
    autosave -> save to private draft

   File not in versioned site:
      File not published
         save -> save to live object
         publish -> save to live object, publish the live file
      File published
         save -> save to draft object
         publish -> save draft to live object

   File in versioned site:
      Contenttype not in versioned data
         save -> save to live object
         publish -> save to live object, create draft (+publish), ask 'submit for publicaction'

      Contenttype in versioned data
         locked? Exit now
         save -> save to public draft
         publish -> save to public draft, add (+publish), ask 'submit for publicaction'
*/

CONSTANT STRING ARRAY requiredapiproperties :=  [ "onsetenabled", "onloaddata", "onstoredata" ];

PUBLIC OBJECTTYPE EditDocumentApp EXTEND PublisherEditorBase <
  // ---------------------------------------------------------------------------
  //
  // Variables
  //

  /// Last modificationdate of the published version (inited when opened)
  DATETIME lastmod;

  /// Rich document file instancedata
  RECORD filedata;

  /// Current autosave object
  OBJECT currentautosave;

  /// Current draft object
  OBJECT currentdraft;

  /// When autosave may start working (set to TRUE when is RTE initialized, set to FALSE after 'Save on close: NO')
  BOOLEAN canautosave;

  /// Whether or not to show the draft bar during this session
  BOOLEAN showdraftbar;

  /// Type of the file being edited
  INTEGER filetype;

  RECORD filetypeinfo;

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init(RECORD data)  {
    this->frame->flags.isdeveloper := this->contexts->user->ShowDeveloperOptions();
    this->frame->flags.isdirty := FALSE;
    this->developersmenu->visible := this->frame->flags.isdeveloper;
    this->contexts->editdocumentapi := NEW EditDocumentAPI(PRIVATE this);
    this->requireworkflow := TRUE;

    this->frame->flags.canedit := FALSE;
    this->frame->flags.canpublish := FALSE;

    IF(CellExists(data,'assumewriteaccess'))
      this->assumewriteaccess := data.assumewriteaccess;

    this->contexts->editdocumentapi->internallinkroots := data.internallinkroots;
    this->SetupEditBaseForExistingObject(data.id, data.readonly);

    OBJECT fsobject := OpenWHFSObject(this->itemid);

    IF(this->IsReadOnly(fsobject) AND NOT this->tolliumuser->HasRightOn("system:fs_browse", this->itemid)) //not even browse access?
    {
      this->tolliumresult := "cancel";
      RETURN;
    }
    IF (NOT this->GetExclusiveAccess([ id := this->itemid ], [ onbeforesteal := PTR this->GotScreenStolen ]))
      RETURN;

    this->showdraftbar := TRUE;

    //ADDME deal with cases where users already make other modifications while we're stil opening ?

    IF(NOT ObjectExists(fsobject))
    {
      this->tolliumresult := "cancel";
      RETURN;
    }


    this->filedata := [ name := fsobject->name, modificationdate := fsobject->modificationdate ];
    this->filetype := fsobject->type;
    this->filetypeinfo := DescribeContentTypeById(fsobject->type, [ isfolder := FALSE, mockifmissing := TRUE ]);

    //Loads the extensions for the file type and invokes additional editor hooks
    this->SetupForFileEdit(this->itemid, data.objeditor);
    this->RefreshReadonly(fsobject);

    this->publicationlistener->masks := [ "system:whfs.folder." || fsobject->parent, "publisher:publish.folder." || fsobject->parent ];

    RECORD ARRAY drafts := this->RefreshCurrentDraft();

    RECORD otherdraft := SELECT * FROM drafts WHERE ispublic AND NOT mine ORDER BY creationdate DESC LIMIT 1;
    IF(NOT this->readonly AND RecordExists(otherdraft)) {
      STRING user := this->contexts->userapi->GetUserDisplayName(otherdraft.user);
      STRING when := this->tolliumuser->FormatDatetime(otherdraft.creationdate, "minutes", TRUE, FALSE);

      IF (this->RunSimpleScreen("confirm"
                               , GetTid("publisher:tolliumapps.editdocument.messageboxes.otherhasdraft", user, when)
                               , [ buttons := [ "ok","cancel"]
                                 , dontshowkey := "publisher.editdocument.otherhasdraft"
                                 , dontshowbuttons := ["ok"]
                                 ]) != "ok") {
        this->tolliumresult := "cancel";
        RETURN;
      }
    }

    BOOLEAN mustloadrte := TRUE;
    IF (NOT this->readonly) {
      RECORD myautosave := SELECT * FROM drafts WHERE mine AND NOT ispublic ORDER BY creationdate DESC LIMIT 1;
      IF(RecordExists(myautosave)) {
        this->currentautosave := OpenDraft(fsobject->id, myautosave.id);
        this->ReloadRTEContents();

        STRING response := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.resumeautosave"), [ buttons := [ "yes", "no", "cancel" ]]);
        IF(response != "yes" AND response != "no") {
          this->tolliumresult := "cancel";
          RETURN;
        }

        IF(response = "yes") {
          mustloadrte := FALSE;
          ^dirtylistener->SetDirty();
        } ELSE {
          this->currentautosave := DEFAULT OBJECT;
          OBJECT work := this->BeginUnvalidatedWork();
          CleanupMyPrivateDrafts(fsobject->id);
          work->Finish();
        }
      }
    }

    this->lastmod := this->filedata.modificationdate;
    //this->historybtn->pressed := this->historypart->visible ;

    IF(mustloadrte)
      this->ReloadRTEContents();

    // Can start autosaving when RTE is initialized (not before, would save empty document)
    this->canautosave := NOT this->readonly;


    /*
    When we are called from another application which tells us the user MUST be able to have write access,
    we can assume that application is meant to be used to manage this document.
    This means we don't want to confuse users by pointing them to the original source file/folder in the Publisher.
    Also they may not have access to the original location of the document.
    */
    IF (this->assumewriteaccess)
      ^openinpublisher->enabled := FALSE;

    this->RefreshState();

    RegisterTimedCallback(AddTimeToDate(1, GetCurrentDatetime()), PTR this->CalculateInitialDirty);
  }

  RECORD ARRAY FUNCTION RefreshCurrentDraft() {
    RECORD ARRAY drafts := GetDrafts(this->itemid);
    IF(Length(SELECT FROM drafts WHERE ispublic)>1)
      THROW NEW Exception("Multiple public drafts");

    /* Public draft present?
          Submitted for approval?
              Say so, exit
          Someone else created it?
              Say so, exit if user wants to exit

        Private draft present for current user?
            Open it, ask if user wants to resume
                No->revert to default version
                Yes->keep private draft as current version
    */

    this->currentdraft := DEFAULT OBJECT; //reset the current draft in case we can't find any active one,

    //ADDME: Add a way for read-only users to switch between draft en live?
    IF (NOT this->readonly) {
      RECORD currentdraft := SELECT * FROM drafts WHERE ispublic;
      IF(RecordExists(currentdraft)) {
        this->currentdraft := OpenDraft(this->itemid, currentdraft.id);
        IF(NOT ObjectExists(this->currentdraft))
          THROW NEW Exception("Failed to open single draft #" || currentdraft.id);
      }
    }

    RETURN drafts;
  }

  MACRO SetupForFileEdit(INTEGER objectid, RECORD objecteditor)
  {
    IF(objecteditor.documenteditor = "")
      objecteditor.documenteditor := "mod::tolliumapps/rtdedit/rtdedit.xml#richdoc";

    IF(this->AddTypesTabs() )
    {
      ^metapart->visible := TRUE;
    }

    OBJECT filehandler := this->LoadScreen(objecteditor.documenteditor);

    FOREVERY(STRING requiredprop FROM requiredapiproperties)
      IF(IsDefaultValue(GetMember(this->contexts->editdocumentapi, requiredprop)))
        THROW NEW Exception(`Document editor ${objecteditor.documenteditor} must implement '${requiredprop}'`);

    this->doceditor->contents := filehandler;

    // Merge document editor toolbar
    IF(Length(this->doceditor->contents->frame->toolbars) > 0)
      this->toolbar->items := this->toolbar->items CONCAT this->doceditor->contents->frame->toolbars[0]->items;

    // Merge document editor main menu
    IF (ObjectExists(this->doceditor->contents->frame->menubar))
      this->MergeIntoMainMenu(this->frame->menubar, this->doceditor->contents->frame->menubar->items);

    // Apply editor hooks. We do this after setting up the main screen (which will already have hooked into the EditDocumentAPI at this point)
    this->contexts->applytester->RunSiteprofileHookTarget("publisher:documenteditor",
        [ contexts := this->contexts
        , objectid := objectid
        ]);

    ^maintabs->RunExtensionsPostInit();
  }

  MACRO MergeIntoMainMenu(OBJECT mymenu, RECORD ARRAY newitems)
  {
    // Is there an 'editdocumentmenu' insertion point?
    INTEGER pos :=
        (SELECT AS INTEGER #items + 1
           FROM mymenu->items
          WHERE NOT isdivider
                AND menuitem->title = "#####") - 1;
    IF (pos < 0)
      pos := Length(mymenu->items);

    FOREVERY (RECORD newitem FROM newitems)
    {
      // Check if a submenu with the new item's name already exists (to merge submenus)
      OBJECT existing := NOT newitem.isdivider AND newitem.menuitem->name NOT LIKE "tollium_*$*" AND RecordExists(newitem.menuitem->items)
          ? SELECT AS OBJECT menuitem
              FROM mymenu->items
             WHERE NOT isdivider
                   AND menuitem->name NOT LIKE "tollium_*$*"
                   AND ToUppercase(menuitem->name) = ToUppercase(newitem.menuitem->name)
                   AND RecordExists(menuitem->items)
          : DEFAULT OBJECT;
      IF (ObjectExists(existing))
      {
        this->MergeIntoMainMenu(existing, newitem.menuitem->items);
      }
      ELSE
      {
        INSERT newitem INTO mymenu->items AT pos;
        pos := pos + 1;
      }
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO CalculateInitialDirty() {
    // Need the client state at this moment, so require a sync.
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->RefreshState();
  }

  MACRO SyncNameWithTitle()
  {
    ^name->placeholder := ^title->value != "" ? GetSafeName(^title->value) : "";
  }

  MACRO ReloadRTEContents() {
    this->contexts->editdocumentapi->onloaddata();
    IF(this->tolliumresult != "") //onloaddata may have cancelled us
      RETURN;

    this->LoadBaseData(this->GetBaseFSObject()); //TODO should be instance object once versioning works
    this->LoadMetadata(this->GetInstanceObject());
    this->RefreshState();
  }

  MACRO RefreshState()  {
    // Do we have a published version?
    OBJECT fsobj := OpenWHFSObject(this->itemid);
    BOOLEAN ispublishabletoweb := ObjectExists(fsobj) AND fsobj->parentsite != 0 AND OpenSite(fsobj->parentsite)->outputweb != 0 AND this->filetypeinfo.ispublishable AND this->contexts->editdocumentapi->canpublish;
    this->RefreshReadonly(fsobj);

    IF (ObjectExists(fsobj)) {
      IF (this->contexts->editdocumentapi->oncanpreview != DEFAULT MACRO PTR)
        this->frame->flags.canpreview := this->contexts->editdocumentapi->oncanpreview(fsobj);
      ELSE
        this->frame->flags.canpreview := ispublishabletoweb;
      IF (this->contexts->editdocumentapi->oncanview != DEFAULT MACRO PTR)
        this->frame->flags.canview := this->contexts->editdocumentapi->oncanview(fsobj);
      ELSE
        this->frame->flags.canview := GetOncePublishedFromPublished(fsobj->published) AND ispublishabletoweb;
    } ELSE {
      this->frame->flags.canpreview := FALSE;
      this->frame->flags.canview := FALSE;
    }
    this->frame->flags.canrevertdraft := ObjectExists(this->currentautosave ?? this->currentdraft) AND this->frame->flags.canedit AND NOT this->readonly;

    ^title->onchange := this->isnew ? PTR this->SyncNameWithTitle : DEFAULT MACRO PTR;
    IF(NOT this->isnew)
      ^name->placeholder := "";
    ^name->required := NOT this->isnew;

    //ADDME: Show draft if no published version available
    IF(ObjectExists(this->currentdraft) AND this->showdraftbar AND NOT this->readonly) {
      STRING when := this->tolliumuser->FormatDatetime(this->currentdraft->when, "minutes", TRUE, FALSE);
      INTEGER modifiedby := this->currentdraft->user;
      STRING basetext;
      IF(modifiedby = this->tolliumuser->authobjectid) {
        basetext := this->GetTid(".draftmode-by-you", when);
      } ELSE {
        STRING user := this->contexts->userapi->GetUserDisplayName(modifiedby);
        basetext := this->GetTid(".draftmode-by-other", user, when);
      }

      this->draftmode->htmlvalue := EncodeHTML(basetext);
      ^draftbar_canceldraft->visible := this->frame->flags.canrevertdraft;
      ^draftbar->visible := TRUE;
    } ELSE
      ^draftbar->visible := FALSE;

    BOOLEAN allowpublish;

    this->contexts->editdocumentapi->onsetenabled(NOT this->readonly);
    this->frame->flags.canedit := NOT this->readonly;

    RECORD state;

    IF (this->frame->flags.isdirty)
    {
      state :=
          [ icon :=   "tollium:status/unknown"
          , text :=   this->GetTid(".documenthasunsavedchanges")
          ];
    }
    ELSE IF (ObjectExists(this->currentdraft))
    {
      state :=
          [ icon :=   "tollium:actions/edit"
          , text :=   this->GetTid(".documenthasunpublishedchanges")
          ];
    }
    ELSE
    {
      // Get published file FS object
      IF (ObjectExists(fsobj))
      {
        INTEGER published := fsobj->published;
        IF (fsobj->lastpublishdate > AddTimeToDate(-1000, GetCurrentDateTime()))
        {
          published := ConvertToWillPublish(published, FALSE, FALSE, PubPrio_DirectEdit);
          RegisterTimedCallback(AddTimeToDate(500, GetCurrentDateTime()), PTR this->RefreshState());
        }

        STRING publishedat := this->tolliumuser->FormatDateTime(fsobj->modificationdate, "minutes", TRUE, TRUE);
        state := GetFileStatusFromPublished(published, ispublishabletoweb, publishedat, DEFAULT RECORD);
      }
      ELSE
      {
        state := GetFileStatusFromPublished(0, FALSE, "", DEFAULT RECORD);
      }
    }

    BOOLEAN havesomethingtopublish := ObjectExists(this->currentdraft) OR (ObjectExists(fsobj) AND NOT fsobj->publish);
    //We do not care about ispublishabletoweb for the publish button as we let the Publish button act as 'publish when site goes online' button
    this->frame->flags.canpublish := this->frame->flags.canedit AND (this->frame->flags.isdirty OR havesomethingtopublish) AND this->contexts->editdocumentapi->canpublish;
    this->frame->title := this->isnew ? GetTid("publisher:tolliumapps.editdocument.newdocument") : this->filedata.name;

    this->filestatusicon->SetImageSrc(state.icon);
    this->filestatustext->value := state.text;

    this->publishmenu->visible := NOT this->readonly;
    this->savemenu->visible := NOT this->readonly;
    this->canceldraftmenu->visible := NOT this->readonly;
    this->publishbutton->visible := NOT this->readonly;
    this->saveasdraftbutton->visible := NOT this->readonly;
  }

  OBJECT FUNCTION GetEditingSourceObject()
  {
    RETURN ObjectExists(this->currentautosave) ? this->currentautosave->fsobject : this->GetBaseSourceObject();
  }

  OBJECT FUNCTION GetBaseSourceObject()   {
    RETURN ObjectExists(this->currentdraft) ? this->currentdraft->fsobject : OpenWHFSObject(this->itemid);
  }

  MACRO OnDraftmodeclick(STRING link)
  {
    IF(link LIKE "*#publish")
      this->DoPublish();
    ELSE IF(link LIKE "*#cancel")
      this->DoCanceldraft();
  }

  BOOLEAN FUNCTION ExecuteSave(BOOLEAN withpublish) {
    IF(NOT this->UpdateAutosave()) { //NOTE Flushes to an autosave if needed but may also clear unneeded autosave objects and set this->currentautosave to DEFAULT OBJECT
      //There is no autosave anymore - we are in sync with the latest draft/final version
      IF(NOT withpublish) { //we're not asked to update a final object
        Print("save ignored - no autosave to update draft/final with\n");
        RETURN TRUE; //nothing to do. UpdateAutosave will have updated dirty flag & state
      }
    }

    IF(NOT this->isnew AND NOT this->assumewriteaccess AND NOT this->tolliumuser->HasRightOn("system:fs_fullaccess", this->itemid)) {
      Print("save aborted - not privileged\n");
      RETURN FALSE; //FIXME report
    }

    IF (this->contexts->editdocumentapi->onvalidate != DEFAULT MACRO PTR) {
      OBJECT feedback := this->BeginFeedback();
      this->contexts->editdocumentapi->onvalidate(feedback);
      IF(NOT feedback->Finish())
        RETURN FALSE;
    }

    IF(this->contexts->editdocumentapi->onprechecksave != DEFAULT FUNCTION PTR AND NOT this->contexts->editdocumentapi->onprechecksave(withpublish))
      RETURN FALSE; //aborted because some warning was scary enough

    OBJECT old_currentautosave := this->currentautosave;
    OBJECT old_currentdraft := this->currentdraft;

    OBJECT work := this->BeginWork();
    OBJECT finalitem := OpenWHFSObject(this->itemid); //represents OpenHWHFSObject(this->itemid) in the original code, but we can't update this->itemid until we've committed

    IF (NOT work->HasFailed()) {
      IF(this->isnew) { //TODO what we do here should be similar to objectprops name generation, can we share ? including the unique name check?
        //New items, even *unpublished*, when created through objectprops, always live in their final folder. We basically simulate that: move the autosave to the final location
        RECORD draftmeta := OpenWHFSType("http://www.webhare.net/xmlns/publisher/draftmetadata")->GetInstanceData(this->itemid);
        IF(draftmeta.parent = 0)
          THROW NEW Exception(`Missing parent for new document #${this->itemid}`);

        OBJECT parentfolder := OpenWHFSObject(draftmeta.parent);
        finalitem->MoveTo(parentfolder, ^name->value ?? ^name->placeholder);
        this->currentdraft := DEFAULT OBJECT;
      } ELSE { //We're updating an existing document
        IF (withpublish = FALSE) { ///This is "SAVE". 'currentautosave' was either linked to the current public draft or the current published object
          this->currentautosave->SaveAsNewPublicDraft(this->contexts->user);
        } ELSE { // commit or publish
          OBJECT source := this->currentautosave ?? this->currentdraft;
          IF (ObjectExists(source)) {
            // Deletes the autosave and the public draft too (but keep it when it's versioned, will probably contain other data)
            source->SaveAsFinal(this->contexts->user);
          }
        }
        this->RefreshCurrentDraft();
      }

      this->currentautosave := DEFAULT OBJECT;

      IF (withpublish) {
        //Warn if publishing to an unpublished site (but not if publishing outside a site)
        IF(finalitem->parentsite !=0 AND OpenSite(finalitem->parentsite)->outputweb = 0)
          work->AddWarning(this->GetTid(".sitenotpublished"));
        IF(finalitem->publish = FALSE AND finalitem->ispinned)
          work->AddError(this->GetTid(".cannotpublishpinnedfile"));
        finalitem->UpdateMetadata([ publish := TRUE ]);
      }
    }

    IF (NOT work->HasFailed() AND this->contexts->editdocumentapi->onsave != DEFAULT FUNCTION PTR)
      this->contexts->editdocumentapi->onsave(work, withpublish);

    IF (NOT work->Finish())
    {
      this->currentautosave := old_currentautosave;
      this->currentdraft := old_currentdraft;
      RETURN FALSE;
    }

    IF(this->isnew) { //it no longer is!
      this->isnew := FALSE; //TODO merge into refreshstate, just look at applytester or path
      ^name->value := finalitem->name;
      //Updates display info (eg frame title)
      this->filedata := [ name := finalitem->name, modificationdate := finalitem->modificationdate ];
    }

    ^dirtylistener->ClearDirty();
    //DumpValue(GetStackTrace(), [ name := "BE CLEAN!"]);
    this->RefreshState();

    RETURN TRUE;
  }

  BOOLEAN FUNCTION UpdateAutosave()
  {
    IF (NOT this->frame->flags.isdirty AND NOT ObjectExists(this->currentautosave))
      RETURN FALSE; //nothing to do, don't bother the database with work

    OBJECT work := this->BeginUnvalidatedWork();
    IF (this->frame->flags.isdirty)
    {
      OBJECT toupdate;
      IF(this->isnew) //no intermediate autosave needed for isnew .. as isnew *is* the autosave
      {
        toupdate := OpenWHFSObject(this->itemid);
      }
      ELSE
      {
        IF(NOT ObjectExists(this->currentautosave))  //The autosave must be linked to the file we're editing, but we should grab instance data from the draft if it exists, otherwise we miss uncached data (as __FLushInstanceData won't see it)
          this->currentautosave := StartWorkflowUpdate(ObjectExists(this->currentdraft) ? this->currentdraft->id : this->itemid, [ user := GetEffectiveUser() ]);

        toupdate := this->currentautosave->fsobject;
      }

      this->contexts->editdocumentapi->__FlushInstanceData(toupdate);
      this->SaveMetadata(toupdate->id);
    }
    ELSE IF (ObjectExists(this->currentautosave))
    {
      // Object isn't dirty anymore, remove last autosave
      this->currentautosave->DeleteSelf();
      this->currentautosave := DEFAULT OBJECT;
      this->frame->flags.isdirty := FALSE;
    }

    work->Finish();
    this->RefreshState();
    RETURN ObjectExists(this->currentautosave);
  }

  OBJECT FUNCTION GetInstanceObject()
  {
    RETURN ObjectExists(this->currentautosave)
        ? this->currentautosave->fsobject
        : this->GetEditingSourceObject();
  }

  OBJECT FUNCTION GetBaseFSObject()
  {
    RETURN OpenWHFSObject(this->itemid);
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO GotUnload()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Just save what we have
    this->UpdateAutosave();
  }

  MACRO OnInterval()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Request client state. Wait max 30 secs for client reply, else don't save.
    // Don't need to lock the client to wait for our updates
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->UpdateAutosave();
  }

  MACRO GotFolderChangeEvents(RECORD ARRAY events)
  {
    BOOLEAN have_change;
    FOREVERY (RECORD event FROM events)
    {
      IF (RecordLowerBound(event.data.files, [ file := this->itemid ], [ "FILE" ]).found)
        have_change := TRUE;
    }

    IF (have_change)
      this->RefreshState();
  }

  MACRO GotDirty()
  {
    this->frame->flags.isdirty := ^dirtylistener->dirty;
    this->RefreshState();
  }

  MACRO GotScreenStolen()
  {
    // Screen was stolen, don't save changes on onload
    this->canautosave := FALSE;
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoSave()
  {
    IF(this->name->value = "" AND this->title->value = "")
    {
      this->RunSimpleScreen("error", this->GetTid(".missingtitleorname"));
      RETURN;
    }

    IF (NOT this->ExecuteSave(FALSE))
      RETURN;

    this->RefreshState();
  }

  MACRO DoPublish() { //Save and publish the file
    RECORD metadata;
    OBJECT targetfile := OpenWHFSObject(this->itemid);

    IF(targetfile->publish = FALSE AND targetfile->ispinned) { // The file's publish status is locked by the pin
      this->RunSimpleScreen("error", this->GetTid(".cannotpublishpinnedfile"));
      RETURN;
    }

    this->UpdateAutosave();
    IF(NOT this->contexts->editdocumentapi->onconfirmsaveandpublish())
      RETURN;

    this->ExecuteSave(TRUE);
  }

  BOOLEAN FUNCTION DefaultConfirmAndSavePublishHandler()
  {
    RETURN this->RunSimpleScreen("confirm"
                              , GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmsaveandpublish")
                              , [ dontshowkey := "publisher.editdocument.confirmsaveandpublish"
                                , dontshowbuttons := ["yes"]
                                ]) = "yes";
  }

  MACRO DoSendPreviewLink() {
    IF (this->frame->flags.isdirty) { //you must save before sending a link, as autosaves are too transient
      IF(this->RunSimpleScreen("question",
        GetTid("publisher:tolliumapps.editdocument.messageboxes.savebeforepreview", OpenWHFSObject(this->itemid)->name),
        [ buttons := ["yes","no"], dontshowkey := "publisher.editdocument.savebeforepreview", dontshowbuttons := ["yes"]]) != "yes")
         RETURN; //cancelled

      this->ExecuteSave(FALSE);
    }

    this->RunScreen("#sendpreviewlink");
  }

  PUBLIC OBJECT FUNCTION GetPreviewObject(BOOLEAN autosave) {
    this->UpdateAutosave();
    RETURN autosave ? this->GetEditingSourceObject() : this->GetBaseSourceObject();
  }

  PUBLIC STRING FUNCTION CreatePreviewLink(DATETIME validuntil, STRING password, BOOLEAN autosave) {
    this->UpdateAutosave();
    RETURN CreatePreviewLink(validuntil, password, this->itemid, this->GetPreviewObject(autosave)->id);
  }

  MACRO DoPreviewCurrentContent(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(TRUE), TRUE);
    ELSE
    {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->CreatePreviewLink(AddDaysTodate(1, GetCurrentDatetime()), "", TRUE);
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoPreviewdraft(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(FALSE), FALSE);
    ELSE
    {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->CreatePreviewLink(AddDaysTodate(1, GetCurrentDatetime()), "", FALSE);
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoViewPublished(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onview(openhandler, OpenWHFSObject(this->itemid));
    ELSE
      openhandler->SendURL(OpenWHFSObject(this->itemid)->url);
  }

  BOOLEAN FUNCTION Cancel()
  {
    this->DoClose();
    RETURN this->tolliumresult != "";
  }

  MACRO DoClose()
  {
    IF (this->frame->flags.isdirty)
    {
      STRING res := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.savebeforeclose", OpenWHFSObject(this->itemid)->name), [ buttons := ["yes","no","cancel"]]);
      SWITCH (res)
      {
        CASE "yes"
        {
          IF (this->ExecuteSave(FALSE))
            this->tolliumresult := "ok";
        }
        CASE "no"
        {
          OBJECT work := this->BeginUnvalidatedWork();
          CleanupMyPrivateDrafts(this->itemid);
          this->currentautosave := DEFAULT OBJECT;
          work->Finish();

          // No autosave upon unload!
          this->canautosave := FALSE;
          this->tolliumresult := "ok";
        }
      }
    }
    ELSE
      this->tolliumresult := "ok";
  }

  MACRO DoCanceldraft()
  {
    IF (this->RunSimpleScreen("confirm", GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmcanceldraft")) != "yes")
      RETURN;

    OBJECT old_currentautosave := this->currentautosave;
    OBJECT old_currentdraft := this->currentdraft;

    OBJECT work := this->BeginUnvalidatedWork();

    IF (ObjectExists(this->currentautosave))
    {
      this->currentautosave->DeleteSelf();
      this->currentautosave := DEFAULT OBJECT;
    }
    IF (ObjectExists(this->currentdraft))
    {
      this->currentdraft->DeleteSelf();
      this->currentdraft := DEFAULT OBJECT;
    }

    IF (work->Finish())
    {
      this->ReloadRTEContents();
      this->RefreshState();

      this->RunSimpleScreen("info", GetTid("publisher:tolliumapps.editdocument.messageboxes.draftcancelled"));
    }
    ELSE
    {
      this->currentautosave := old_currentautosave;
      this->currentdraft := old_currentdraft;
    }
  }

  MACRO DoHidedraftbar()
  {
    this->showdraftbar := FALSE;
    ^draftbar->visible := FALSE;
  }

  MACRO DoOpenInPublisher()
  {
    RunPublisherFileManager(this, this->itemid);
  }

  MACRO DoInspectAutosave()
  {
    IF(ObjectExists(this->currentautosave))
      RunFSObjectInspectDialog(this, this->currentautosave->id);
    ELSE
      this->RunSimpleScreen("info", "No autosave present");
  }
  MACRO DoInspectDraft()
  {
    IF(ObjectExists(this->currentdraft))
      RunFSObjectInspectDialog(this, this->currentdraft->id);
    ELSE
      this->RunSimpleScreen("info", "No draft present");
  }
  MACRO DoInspectFinal()
  {
    RunFSObjectInspectDialog(this, this->itemid);
  }
>;

PUBLIC STATIC OBJECTTYPE SendPreviewLink EXTEND TolliumScreenBase <
  MACRO Init(RECORD data) {
    //round up to next hour, 7 days away
    ^validuntil->value := GetRoundedDatetime(AddTimeToDate(60*60000, AddDaysToDate(7, GetCurrentDateTime())), 60*60000);
  }
  BOOLEAN FUNCTION OnShareNext() {
    OBJECT work := this->BeginUnvalidatedFeedback();
    work->Validate(^sharepage);
    IF(^validuntil->value < GetCurrentDatetime())
      work->AddErrorFor(^validuntil, this->GetTid(".validinthepast"));

    RETURN work->Finish();
  }
  MACRO InitFinishPage(RECORD data) {
    IF (this->contexts->editdocumentapi->onpreviewlink != DEFAULT MACRO PTR)
      ^link->value := this->contexts->editdocumentapi->onpreviewlink(this->tolliumparent->GetPreviewObject(FALSE), ^validuntil->value, ^passwordprotect->value ? ^password->value : "");
    ELSE
      ^link->value := this->tolliumparent->CreatePreviewLink(^validuntil->value, ^passwordprotect->value ? ^password->value : "", FALSE);

    STRING mailbody := this->GetTid(".mailbodypreview") || "\n\n" || ^link->value || "\n\n" || this->GetTid(".mailbodypreview2");
    STRING mailsubject := this->GetTid(".mailbodysubject");
    ^maillink->htmlvalue := '<a href="mailto:?to=&subject=' || EncodeURL(mailsubject) || '&body=' || EncodeURL(mailbody) || '">' || EncodeHTML(this->GetTid(".maillink")) || '</a>';
  }
>;
