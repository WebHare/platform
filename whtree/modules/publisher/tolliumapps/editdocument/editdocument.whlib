<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::ipc.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/langspecific.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

LOADLIB "mod::publisher/lib/dialogs.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/internal/support.whlib";
LOADLIB "mod::publisher/lib/internal/tollium-helpers.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/versioning/support.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editapi.whlib";
LOADLIB "mod::publisher/tolliumapps/editdocument/editbase.whlib";

LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";


/* Saving logic:
   Always:
    autosave -> save to private draft

   File not in versioned site:
      File not published
         save -> save to live object
         publish -> save to live object, publish the live file
      File published
         save -> save to draft object
         publish -> save draft to live object

   File in versioned site:
      Contenttype not in versioned data
         save -> save to live object
         publish -> save to live object, create draft (+publish), ask 'submit for publicaction'

      Contenttype in versioned data
         locked? Exit now
         save -> save to public draft
         publish -> save to public draft, add (+publish), ask 'submit for publicaction'
*/

CONSTANT STRING ARRAY requiredapiproperties :=  [ "onsetenabled", "onloaddata", "onstoredata" ];

PUBLIC OBJECTTYPE EditDocumentApp EXTEND PublisherEditorBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  /// Last modificationdate of the published version (inited when opened)
  DATETIME lastmod;

  /// Rich document file instancedata
  RECORD filedata;

  /// Current autosave object
  OBJECT currentautosave;

  /// Current draft object
  OBJECT currentdraft;

  /// When autosave may start working (set to TRUE when is RTE initialized, set to FALSE after 'Save on close: NO')
  BOOLEAN canautosave;

  /// Whether or not to show the draft bar during this session
  BOOLEAN showdraftbar;

  /// Type of the file being edited
  INTEGER filetype;

  RECORD filetypeinfo;

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init(RECORD data)
  {
    this->frame->flags.isdeveloper := this->contexts->user->ShowDeveloperOptions();
    this->frame->flags.isdirty := FALSE;
    this->developersmenu->visible := this->frame->flags.isdeveloper;
    this->contexts->editdocumentapi := NEW EditDocumentAPI(PRIVATE this);

    this->frame->flags.canedit := FALSE;
    this->frame->flags.canpublish := FALSE;

    IF(CellExists(data,'assumewriteaccess'))
      this->assumewriteaccess := data.assumewriteaccess;

    this->contexts->editdocumentapi->internallinkroots := data.internallinkroots;
    this->itemid := data.id;

    OBJECT fsobject := OpenWHFSObject(this->itemid);

    IF(this->IsReadOnly(fsobject) AND NOT this->tolliumuser->HasRightOn("system:fs_browse", this->itemid)) //not even browse access?
    {
      this->tolliumresult := "cancel";
      RETURN;
    }
    IF (NOT this->GetExclusiveAccess([ id := this->itemid ], [ onbeforesteal := PTR this->GotScreenStolen ]))
      RETURN;

    this->showdraftbar := TRUE;

    //ADDME deal with cases where users already make other modifications while we're stil opening ?

    IF(NOT ObjectExists(fsobject))
    {
      this->tolliumresult := "cancel";
      RETURN;
    }

    this->filedata := [ name := fsobject->name, modificationdate := fsobject->modificationdate ];
    this->filetype := fsobject->type;
    this->filetypeinfo := DescribeContentTypeById(fsobject->type, [ isfolder := FALSE, mockifmissing := TRUE ]);

    //Loads the extensions for the file type and invokes additional editor hooks
    this->SetupForFileEdit(this->itemid, data.objeditor);
    this->RefreshReadonly(fsobject);

    this->publicationlistener->masks := [ "system:whfs.folder." || fsobject->parent, "publisher:publish.folder." || fsobject->parent ];

    RECORD ARRAY drafts := GetDrafts(fsobject->id);
    IF(Length(SELECT FROM drafts WHERE ispublic)>1)
      THROW NEW Exception("Multiple public drafts");

    /* Public draft present?
          Submitted for approval?
              Say so, exit
          Someone else created it?
              Say so, exit if user wants to exit

        Private draft present for current user?
            Open it, ask if user wants to resume
                No->revert to default version
                Yes->keep private draft as current version
    */

    STRING savemode := this->GetSaveMode();
    IF (savemode NOT IN [ "versioned-locked-editlive", "versioned-editlive" ])
    {
      //ADDME: Add a way for read-only users to switch between draft en live?
      IF (NOT this->readonly)
      {
        RECORD currentdraft := SELECT * FROM drafts WHERE ispublic;
        IF(RecordExists(currentdraft))
        {
          this->currentdraft := OpenDraft(fsobject->id, currentdraft.id);
          IF(NOT ObjectExists(this->currentdraft))
            THROW NEW Exception("Failed to open single draft #" || currentdraft.id);
        }
      }
    }
    ELSE
    {
      // The rich document data is not versioned, so we should ignore the draft
      DELETE FROM drafts WHERE ispublic;
    }


    RECORD otherdraft := SELECT * FROM drafts WHERE ispublic AND NOT mine ORDER BY creationdate DESC LIMIT 1;
    IF(NOT this->readonly AND RecordExists(otherdraft) AND savemode != "versioned-locked")
    {
      STRING user := this->contexts->userapi->GetUserDisplayName(otherdraft.user);
      STRING when := this->tolliumuser->FormatDatetime(otherdraft.creationdate, "minutes", TRUE, FALSE);

      IF (this->RunSimpleScreen("confirm"
                               , GetTid("publisher:tolliumapps.editdocument.messageboxes.otherhasdraft", user, when)
                               , [ buttons := [ "ok","cancel"]
                                 , dontshowkey := "publisher.editdocument.otherhasdraft"
                                 , dontshowbuttons := ["ok"]
                                 ]) != "ok")
      {
        this->tolliumresult := "cancel";
        RETURN;
      }
    }

    BOOLEAN mustloadrte := TRUE;
    IF (savemode != "versioned-locked" AND NOT this->readonly)
    {
      RECORD myautosave := SELECT * FROM drafts WHERE mine AND NOT ispublic ORDER BY creationdate DESC LIMIT 1;
      IF(RecordExists(myautosave))
      {
        this->currentautosave := OpenDraft(fsobject->id, myautosave.id);
        this->ReloadRTEContents();

        STRING response := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.resumeautosave"), [ buttons := [ "yes", "no", "cancel" ]]);
        IF(response != "yes" AND response != "no")
        {
          this->tolliumresult := "cancel";
          RETURN;
        }

        IF(response = "yes")
        {
          mustloadrte := FALSE;
          ^dirtylistener->SetDirty();
        }
        ELSE
        {
          this->currentautosave := DEFAULT OBJECT;
          OBJECT work := this->BeginUnvalidatedWork();
          CleanupMyPrivateDrafts(fsobject->id);
          work->Finish();
        }
      }
    }

    this->lastmod := this->filedata.modificationdate;
    //this->historybtn->pressed := this->historypart->visible ;

    IF(mustloadrte)
      this->ReloadRTEContents();

    // Can start autosaving when RTE is initialized (not before, would save empty document)
    this->canautosave := NOT this->readonly;


    /*
    When we are called from another application which tells us the user MUST be able to have write access,
    we can assume that application is meant to be used to manage this document.
    This means we don't want to confuse users by pointing them to the original source file/folder in the Publisher.
    Also they may not have access to the original location of the document.
    */
    IF (this->assumewriteaccess)
      ^openinpublisher->enabled := FALSE;

    this->RefreshState();

    RegisterTimedCallback(AddTimeToDate(1, GetCurrentDatetime()), PTR this->CalculateInitialDirty);
  }

  MACRO SetupForFileEdit(INTEGER objectid, RECORD objecteditor)
  {
    IF(objecteditor.documenteditor = "")
      objecteditor.documenteditor := "mod::tolliumapps/rtdedit/rtdedit.xml#richdoc";

    IF(this->SetupMetaTabs(objectid, 0, FALSE, 0))
    {
      ^metapart->visible := TRUE;
    }

    this->siteversioningpolicy := this->contexts->applytester->GetVersioningPolicy();

    OBJECT filehandler := this->LoadScreen(objecteditor.documenteditor);

    FOREVERY(STRING requiredprop FROM requiredapiproperties)
      IF(IsDefaultValue(GetMember(this->contexts->editdocumentapi, requiredprop)))
        THROW NEW Exception(`Document editor ${objecteditor.documenteditor} must implement '${requiredprop}'`);

    this->doceditor->contents := filehandler;

    // Merge document editor toolbar
    IF(Length(this->doceditor->contents->frame->toolbars) > 0)
      this->toolbar->items := this->toolbar->items CONCAT this->doceditor->contents->frame->toolbars[0]->items;

    // Merge document editor main menu
    IF (ObjectExists(this->doceditor->contents->frame->menubar))
      this->MergeIntoMainMenu(this->frame->menubar, this->doceditor->contents->frame->menubar->items);

    // Apply editor hooks. We do this after setting up the main screen (which will already have hooked into the EditDocumentAPI at this point)
    this->contexts->applytester->RunSiteprofileHookTarget("publisher:documenteditor",
        [ contexts := this->contexts
        , objectid := objectid
        ]);

    ^maintabs->RunExtensionsPostInit();
  }

  MACRO MergeIntoMainMenu(OBJECT mymenu, RECORD ARRAY newitems)
  {
    // Is there an 'editdocumentmenu' insertion point?
    INTEGER pos :=
        (SELECT AS INTEGER #items + 1
           FROM mymenu->items
          WHERE NOT isdivider
                AND menuitem->title = "#####") - 1;
    IF (pos < 0)
      pos := Length(mymenu->items);

    FOREVERY (RECORD newitem FROM newitems)
    {
      // Check if a submenu with the new item's name already exists (to merge submenus)
      OBJECT existing := NOT newitem.isdivider AND newitem.menuitem->name NOT LIKE "tollium_*$*" AND RecordExists(newitem.menuitem->items)
          ? SELECT AS OBJECT menuitem
              FROM mymenu->items
             WHERE NOT isdivider
                   AND menuitem->name NOT LIKE "tollium_*$*"
                   AND ToUppercase(menuitem->name) = ToUppercase(newitem.menuitem->name)
                   AND RecordExists(menuitem->items)
          : DEFAULT OBJECT;
      IF (ObjectExists(existing))
      {
        this->MergeIntoMainMenu(existing, newitem.menuitem->items);
      }
      ELSE
      {
        INSERT newitem INTO mymenu->items AT pos;
        pos := pos + 1;
      }
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO CalculateInitialDirty()
  {
    // Need the client state at this moment, so require a sync.
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->RefreshState();
  }

  MACRO SyncNameWithTitle()
  {
    ^name->placeholder := ^title->value != "" ? GetSafeName(^title->value) : "";
  }

  MACRO ReloadRTEContents()
  {
    this->contexts->editdocumentapi->onloaddata();
    IF(this->tolliumresult != "") //onloaddata may have cancelled us
      RETURN;

    this->LoadBaseData(this->GetBaseFSObject()); //TODO should be instance object once versioning works
    this->LoadMetadata(this->GetInstanceObject());
    this->RefreshState();
  }

  MACRO RefreshState()
  {
    // Do we have a published version?
    OBJECT fsobj := OpenWHFSObject(this->itemid);
    BOOLEAN ispublishabletoweb := ObjectExists(fsobj) AND fsobj->parentsite != 0 AND OpenSite(fsobj->parentsite)->outputweb != 0 AND this->filetypeinfo.ispublishable AND this->contexts->editdocumentapi->canpublish;
    this->RefreshReadonly(fsobj);
    IF (ObjectExists(fsobj))
    {
      IF (this->contexts->editdocumentapi->oncanpreview != DEFAULT MACRO PTR)
        this->frame->flags.canpreview := this->contexts->editdocumentapi->oncanpreview(fsobj);
      ELSE
        this->frame->flags.canpreview := ispublishabletoweb;
      IF (this->contexts->editdocumentapi->oncanview != DEFAULT MACRO PTR)
        this->frame->flags.canview := this->contexts->editdocumentapi->oncanview(fsobj);
      ELSE
        this->frame->flags.canview := GetOncePublishedFromPublished(fsobj->published) AND ispublishabletoweb;
    }
    ELSE
    {
      this->frame->flags.canpreview := FALSE;
      this->frame->flags.canview := FALSE;
    }
    this->frame->flags.canrevertdraft := ObjectExists(this->currentautosave ?? this->currentdraft) AND this->frame->flags.canedit AND NOT this->readonly;
    this->frame->flags.cancancelrequest := ObjectExists(this->currentautosave ?? this->currentdraft) AND NOT this->frame->flags.canedit AND NOT this->readonly;

    ^title->onchange := this->isnew ? PTR this->SyncNameWithTitle : DEFAULT MACRO PTR;
    IF(NOT this->isnew)
      ^name->placeholder := "";
    ^name->required := NOT this->isnew;

    //ADDME: Show draft if no published version available
    IF(ObjectExists(this->currentdraft) AND this->showdraftbar AND NOT this->readonly)
    {
      STRING when := this->tolliumuser->FormatDatetime(this->currentdraft->when, "minutes", TRUE, FALSE);
      INTEGER modifiedby := this->currentdraft->user;
      STRING basetext;
      IF(modifiedby = this->tolliumuser->authobjectid)
      {
        basetext := this->GetTid(".draftmode-by-you", when);
      }
      ELSE
      {
        STRING user := this->contexts->userapi->GetUserDisplayName(modifiedby);
        basetext := this->GetTid(".draftmode-by-other", user, when);
      }

      this->draftmode->htmlvalue := EncodeHTML(basetext);
      ^draftbar_canceldraft->visible := this->frame->flags.canrevertdraft;
      ^draftbar_cancelrequest->visible := FALSE;

      ^draftbar->visible := TRUE;
    }
    ELSE
      ^draftbar->visible := FALSE;

    STRING savemode := this->GetSaveMode();
    BOOLEAN allowpublish;
    IF (savemode = "versioned-locked")
    {
      this->contexts->editdocumentapi->onsetenabled(FALSE);
      this->frame->flags.canedit := FALSE;

      STRING basetext := this->readonly ? this->GetTid(".request-for-approval-readonly") : this->GetTid(".request-for-approval");
      ^draftbar->visible := TRUE;
      ^draftmode->htmlvalue := EncodeHTML(basetext);
      ^draftbar_canceldraft->visible := FALSE;
      ^draftbar_cancelrequest->visible := this->frame->flags.cancancelrequest;
    }
    ELSE IF (savemode = "versioned-locked-editlive")
    {
      this->contexts->editdocumentapi->onsetenabled(NOT this->readonly);
      this->frame->flags.canedit := NOT this->readonly;

      ^draftbar->visible := TRUE;
      STRING basetext := this->readonly ? this->GetTid(".request-for-approval-editlive-readonly") : this->GetTid(".request-for-approval-editlive");
      ^draftmode->htmlvalue := EncodeHTML(basetext);
      ^draftbar_canceldraft->visible := FALSE;
      ^draftbar_cancelrequest->visible := FALSE;
    }
    ELSE
    {
      this->contexts->editdocumentapi->onsetenabled(NOT this->readonly);
      this->frame->flags.canedit := NOT this->readonly;

      IF (savemode IN [ "versioned-editdraft", "versioned-editlive" ])
      {
        STRING warntype := GetVersioningDraftWarning(fsobj);
        SWITCH (warntype)
        {
          CASE "normal" // Keep usual texts
          {
          }
          CASE "justdenied"
          {
            ^draftbar->visible := TRUE;
            ^draftmode->htmlvalue := EncodeHTML(GetTid("publisher:versioning.editingjustdeniedversioneddraft"));
            ^draftbar_canceldraft->visible := FALSE;
            ^draftbar_cancelrequest->visible := FALSE;
          }
          DEFAULT
          {
            THROW NEW Exception(`Unhandled draft warn type '${warntype}'`);
          }
        }
      }
    }

    RECORD state;

    IF (this->frame->flags.isdirty)
    {
      state :=
          [ icon :=   "tollium:status/unknown"
          , text :=   this->GetTid(".documenthasunsavedchanges")
          ];
    }
    ELSE IF (ObjectExists(this->currentdraft))
    {
      state :=
          [ icon :=   "tollium:actions/edit"
          , text :=   this->GetTid(".documenthasunpublishedchanges")
          ];
    }
    ELSE
    {
      // Get published file FS object
      IF (ObjectExists(fsobj))
      {
        INTEGER published := fsobj->published;
        IF (fsobj->lastpublishdate > AddTimeToDate(-1000, GetCurrentDateTime()))
        {
          published := ConvertToWillPublish(published, FALSE, FALSE, PubPrio_DirectEdit);
          RegisterTimedCallback(AddTimeToDate(500, GetCurrentDateTime()), PTR this->RefreshState());
        }

        STRING publishedat := this->tolliumuser->FormatDateTime(fsobj->modificationdate, "minutes", TRUE, TRUE);
        state := GetFileStatusFromPublished(published, ispublishabletoweb, publishedat, DEFAULT RECORD);
      }
      ELSE
      {
        state := GetFileStatusFromPublished(0, FALSE, "", DEFAULT RECORD);
      }
    }

    BOOLEAN havesomethingtopublish := ObjectExists(this->currentdraft) OR (ObjectExists(fsobj) AND NOT fsobj->publish);
    //We do not care about ispublishabletoweb for the publish button as we let the Publish button act as 'publish when site goes online' button
    this->frame->flags.canpublish := this->frame->flags.canedit AND (this->frame->flags.isdirty OR havesomethingtopublish) AND this->contexts->editdocumentapi->canpublish;
    this->frame->flags.havesomethingtopublish := havesomethingtopublish;
    this->frame->title := this->isnew ? GetTid("publisher:tolliumapps.editdocument.newdocument") : this->filedata.name;

    this->filestatusicon->SetImageSrc(state.icon);
    this->filestatustext->value := state.text;

    this->publishmenu->visible := NOT this->readonly;
    this->savemenu->visible := NOT this->readonly;
    this->canceldraftmenu->visible := NOT this->readonly;
    this->cancelrequestmenu->visible := NOT this->readonly;
    this->publishbutton->visible := NOT this->readonly;
    this->saveasdraftbutton->visible := NOT this->readonly;
  }

  OBJECT FUNCTION GetEditingSourceObject()
  {
    RETURN ObjectExists(this->currentautosave) ? this->currentautosave->fsobject : this->GetBaseSourceObject();
  }

  OBJECT FUNCTION GetBaseSourceObject()
  {
    RETURN ObjectExists(this->currentdraft) ? this->currentdraft->fsobject : OpenWHFSObject(this->itemid);
  }

  MACRO OnDraftmodeclick(STRING link)
  {
    IF(link LIKE "*#publish")
      this->DoPublish();
    ELSE IF(link LIKE "*#cancel")
      this->DoCanceldraft();
  }

  /** Return the savemode for the current document, and calculates frame->flags.cancancelrequest
      @return savemode
      "unversioned-published": Site is not versioned, file is now published.
      "unversioned-unpublished": Site is not versioned, file is now not published
      "versioned-locked": Site is versioned, file has a pending request for approval and rtd data is now locked
      "versioned-locked-editlive": Site is versioned, file has a pending request for approval, but rtd data can be edited in live object
      "versioned-editlive": Site is versioned, rtd data is not protected (can be edited in live object)
      "versioned-editdraft": Site is versioned, rtd data is protected, must be edited in draft
  */
  STRING FUNCTION GetSaveMode()
  {
    // See if the current draft is locked for
    IF (NOT ObjectExists(this->siteversioningpolicy))
    {
      this->frame->flags.cancancelrequest := FALSE;
      IF (SELECT AS BOOLEAN IsPublish(published) FROM system.fs_objects WHERE id = this->itemid)
        RETURN "unversioned-published";
      ELSE
        RETURN "unversioned-unpublished";
    }

    RECORD data := SELECT id, isfolder, parent, published FROM system.fs_objects WHERE id = this->itemid;
    data := this->siteversioningpolicy->EnrichWithObjectPolicy([ data ], [ returnpolicyafteraccept := TRUE ])[0];

    RECORD fields := data.policy->GetFields();

    // If the draft is locked and the rich document metadata is versioned in the current or the new policy,
    // the document mey not be edited
    IF (data.draftlocked)
    {
      this->frame->flags.cancancelrequest := data.draftlockedby_authobjectid = this->tolliumuser->authobjectid;
      RECORD fields_after_accept := data.policyafteraccept->GetFields();

      IF (this->filetype IN fields.versioned_contenttypes
          OR this->filetype IN fields_after_accept.versioned_contenttypes)
        RETURN "versioned-locked";
    }

    this->frame->flags.cancancelrequest := FALSE;
    IF (this->filetype IN fields.versioned_contenttypes)
      RETURN "versioned-editdraft";

    RETURN data.draftlocked ? "versioned-locked-editlive" : "versioned-editlive";
  }

  BOOLEAN FUNCTION ExecuteSave(BOOLEAN withpublish, RECORD approval_metadata)
  {
    this->UpdateAutosave();

    IF(NOT this->isnew AND NOT this->assumewriteaccess AND NOT this->tolliumuser->HasRightOn("system:fs_fullaccess", this->itemid))
    {
      Print("save aborted - not privileged\n");
      RETURN FALSE; //FIXME report
    }

    IF (this->contexts->editdocumentapi->onvalidate != DEFAULT MACRO PTR)
    {
      OBJECT feedback := this->BeginFeedback();
      this->contexts->editdocumentapi->onvalidate(feedback);
      IF(NOT feedback->Finish())
        RETURN FALSE;
    }

    IF(this->contexts->editdocumentapi->onprechecksave != DEFAULT FUNCTION PTR AND NOT this->contexts->editdocumentapi->onprechecksave(withpublish))
      RETURN FALSE; //aborted because some warning was scary enough

    OBJECT old_currentautosave := this->currentautosave;
    OBJECT old_currentdraft := this->currentdraft;

    OBJECT work := this->BeginWork();
    STRING savemode;
    OBJECT finalitem := OpenWHFSObject(this->itemid); //represents OpenHWHFSObject(this->itemid) in the original code, but we can't update this->itemid until we've committed
    IF (NOT work->HasFailed())
    {
      savemode := this->GetSaveMode();
      STRING savetarget;
      SWITCH (savemode)
      {
        CASE "versioned-locked"         { RETURN FALSE; }
        CASE "unversioned-unpublished"
        {
          // File is not published now. If publishing, save to live
          // Otherwise, save to the draft only if there is one.
          savetarget := withpublish OR NOT ObjectExists(this->currentdraft) ? "live" : "draft";
        }
        CASE "unversioned-published"
        {
          // File is published now. If not publishing, always save to the draft
          savetarget := withpublish ? "live" : "draft";
        }
        CASE "versioned-editlive", "versioned-locked-editlive"
        {
          // File is versioned, but we can save to live data. Save to a draft is there is one
          // If publishing, save normally first, and create a draft after that
          savetarget := "versioned-live";
        }
        CASE "versioned-editdraft"
        {
          // File is versioned, can't save to live data.
          savetarget := "draft";
        }
      }

      IF(this->isnew) //TODO what we do here should be similar to objectprops name generation, can we share ? including the unique name check?
      {
        IF(savetarget NOT IN ["draft","live"])
          THROW NEW Exception(`Invalid savetarget '${savetarget}' for new document`);

        //New items, even *unpublished*, when created through objectprops, always live in their final folder. We basically simulate that: move the autosave to the final location
        RECORD draftmeta := OpenWHFSType("http://www.webhare.net/xmlns/publisher/draftmetadata")->GetInstanceData(this->itemid);
        IF(draftmeta.parent = 0)
          THROW NEW Exception(`Missing parent for new document #${this->itemid}`);

        OBJECT parentfolder := OpenWHFSObject(draftmeta.parent);
        finalitem->MoveTo(parentfolder, ^name->value ?? ^name->placeholder);
        this->currentdraft := DEFAULT OBJECT;
      }
      ELSE
      {
        //which fields do we copy anyway ?
        STRING ARRAY basefields := [ "DATA" ];
        IF(^metapart->visible AND ^title->enabled AND ^title->visible)
          INSERT "title" INTO basefields AT END;
        IF(^metapart->visible AND ^description->enabled AND ^description->visible)
          INSERT "description" INTO basefields AT END;

        IF (savetarget = "draft")
        {
          IF (ObjectExists(this->currentautosave)) //FIXME when doesn't it exist?
          {
            // Ensure the public draft exists if we're actually saving something
            IF (NOT ObjectExists(this->currentdraft))
              this->currentdraft := CreateDraft(finalitem, this->currentautosave->id, TRUE);

            this->currentautosave->SaveToPublicDraft(
                [ basefields := basefields
                , contenttypes := this->contexts->editdocumentapi->editcontenttypes
                ]);
          }
        }
        ELSE // savetarget = "live" or savetarget = "versioned-live"
        {
          OBJECT source := this->currentautosave ?? this->currentdraft;
          IF (ObjectExists(source))
          {
            // Deletes the autosave and the public draft too (but keep it when it's versioned, will probably contain other data)
            source->SaveAsFinal(GetEffectiveUser(),
                [ basefields := basefields
                , contenttypes := this->contexts->editdocumentapi->editcontenttypes
                , keeppublicdraft := savetarget = "versioned-live"
                ]);

            this->currentdraft := DEFAULT OBJECT;
          }
        }
      }

      this->currentautosave := DEFAULT OBJECT;

      IF (withpublish)
      {
        IF (savemode IN [ "unversioned-unpublished", "unversioned-published" ])
        {
          //Warn if publishing to an unpublished site (but not if publishing outside a site)
          IF(finalitem->parentsite !=0 AND OpenSite(finalitem->parentsite)->outputweb = 0)
            work->AddWarning(this->GetTid(".sitenotpublished"));
          IF(savemode = "unversioned-unpublished" AND finalitem->ispinned)
            work->AddError(this->GetTid(".cannotpublishpinnedfile"));
          finalitem->UpdateMetadata([ publish := TRUE ]);
        }
        ELSE IF (savemode IN [ "versioned-editlive", "versioned-editdraft" ])
        {
          // Make sure there is a draft to submit.
          IF (NOT ObjectExists(this->currentdraft))
            this->currentdraft := GetPublicDraft(finalitem->id) ?? CreateDraft(finalitem, finalitem->id, TRUE);

          // Set the 'published' flag of the draft (in the draftmetadata) to published.
          RECORD testdraft_draftdata := this->currentdraft->fsobject->GetInstanceData("http://www.webhare.net/xmlns/publisher/draftmetadata");
          testdraft_draftdata.flags := testdraft_draftdata.flags BITOR DraftFlag_Publish;
          this->currentdraft->fsobject->SetInstanceData("http://www.webhare.net/xmlns/publisher/draftmetadata", testdraft_draftdata, [ ifreadonly := "update" ]);

          // Submit for approval
          SubmitApprovalRequestFromMetadata(work, finalitem, approval_metadata);
        }
        ELSE IF (savemode = "versioned-locked-editlive")
        {
          RETURN FALSE; //should not be reachable, unless someone manages to race it?
        }
      }
    }

    IF (NOT work->HasFailed() AND this->contexts->editdocumentapi->onsave != DEFAULT FUNCTION PTR)
      this->contexts->editdocumentapi->onsave(work, withpublish);

    IF (NOT work->Finish())
    {
      this->currentautosave := old_currentautosave;
      this->currentdraft := old_currentdraft;
      RETURN FALSE;
    }

    IF(this->isnew) //it no longer is!
    {
      this->isnew := FALSE; //TODO merge into refreshtstae, just look at applytester or path
      ^name->value := finalitem->name;
      //Updates display info (eg frame title)
      this->filedata := [ name := finalitem->name, modificationdate := finalitem->modificationdate ];
    }

    ^dirtylistener->ClearDirty();
    //DumpValue(GetStackTrace(), [ name := "BE CLEAN!"]);
    this->RefreshState();

    // Warn that the current document needs to be submitted for approval to be published
    IF (savemode = "versioned-editdraft")
    {
      IF (withpublish)
      {
        IF (this->RunSimpleScreen("confirm", GetTid("publisher:tolliumapps.editdocument.messageboxes.closeeditorafterversionedpublish")) = "yes")
          this->tolliumresult := "ok";
      }
      ELSE
      {
        this->RunSimpleScreen("info"
                             , GetTid("publisher:tolliumapps.editdocument.messageboxes.publishversionedfiletoapplychanges")
                             , [ dontshowkey := "publisher.editdocument.confirmsaveandpublish"
                               ]);
      }
    }

    RETURN TRUE;
  }

  BOOLEAN FUNCTION UpdateAutosave()
  {
    IF (NOT this->frame->flags.isdirty AND NOT ObjectExists(this->currentautosave))
      RETURN FALSE; //nothing to do, don't bother the database with work

    OBJECT work := this->BeginUnvalidatedWork();
    IF (this->frame->flags.isdirty)
    {
      OBJECT toupdate := OpenWHFSObject(this->itemid);
      IF(NOT this->isnew AND NOT ObjectExists(this->currentautosave)) //no intermediate autosave needed for isnew .. as isnew *is* the autosave
      {
        //The autosave must be linked to the file we're editing, but we should grab instance data from the draft if it exists, otherwise we miss uncached data (as __FLushInstanceData won't see it)
        this->currentautosave := CreateDraft(toupdate, ObjectExists(this->currentdraft) ? this->currentdraft->id : this->itemid, FALSE);
        toupdate := this->currentautosave->fsobject;
      }

      this->contexts->editdocumentapi->__FlushInstanceData(toupdate);
      this->SaveMetadata(toupdate->id);
    }
    ELSE IF (ObjectExists(this->currentautosave))
    {
      // Object isn't dirty anymore, remove last autosave
      this->currentautosave->DeleteSelf();
      this->currentautosave := DEFAULT OBJECT;
    }

    work->Finish();
    RETURN ObjectExists(this->currentautosave);
  }

  OBJECT FUNCTION GetInstanceObject()
  {
    RETURN ObjectExists(this->currentautosave)
        ? this->currentautosave->fsobject
        : this->GetEditingSourceObject();
  }

  OBJECT FUNCTION GetBaseFSObject()
  {
    RETURN OpenWHFSObject(this->itemid);
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO GotUnload()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Just save what we have
    this->UpdateAutosave();
  }

  MACRO OnInterval()
  {
    IF (NOT this->canautosave)
      RETURN;

    // Request client state. Wait max 30 secs for client reply, else don't save.
    // Don't need to lock the client to wait for our updates
    STRING res := this->tolliumcontroller->GetClientState(
          [ timeout := AddTimeToDate(30000, GetCurrentDateTime())
          ]);
    IF (res != "ok")
      RETURN;

    this->UpdateAutosave();
  }

  MACRO GotFolderChangeEvents(RECORD ARRAY events)
  {
    BOOLEAN have_change;
    FOREVERY (RECORD event FROM events)
    {
      IF (RecordLowerBound(event.data.files, [ file := this->itemid ], [ "FILE" ]).found)
        have_change := TRUE;
    }

    IF (have_change)
      this->RefreshState();
  }

  MACRO GotDirty()
  {
    this->frame->flags.isdirty := ^dirtylistener->dirty;
    this->RefreshState();
  }

  MACRO GotScreenStolen()
  {
    // Screen was stolen, don't save changes on onload
    this->canautosave := FALSE;
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoSave()
  {
    IF(this->name->value = "" AND this->title->value = "")
    {
      this->RunSimpleScreen("error", this->GetTid(".missingtitleorname"));
      RETURN;
    }

    IF (NOT this->ExecuteSave(FALSE, DEFAULT RECORD))
      RETURN;

    this->RefreshState();
  }

  MACRO DoPublish()
  {
    RECORD metadata;
    STRING savemode := this->GetSaveMode();
    OBJECT targetfile := OpenWHFSObject(this->itemid);
    IF (savemode IN [ "versioned-editlive", "versioned-editdraft" ])
    {
      metadata := GetSubmitRequestApprovalMetadata(this, targetfile);
      IF (NOT RecordExists(metadata))
        RETURN;
    }
    ELSE IF (savemode = "versioned-locked-editlive")
    {
      RETURN; //should not be reachable, unless someone manages to race it?
    }
    ELSE IF(savemode = "unversioned-unpublished" AND targetfile->ispinned)
    {
      this->RunSimpleScreen("error", this->GetTid(".cannotpublishpinnedfile"));
      RETURN;
    }
    ELSE
    {
      this->UpdateAutosave();
      IF(NOT this->contexts->editdocumentapi->onconfirmsaveandpublish())
        RETURN;
    }
    this->ExecuteSave(TRUE, metadata);
  }

  BOOLEAN FUNCTION DefaultConfirmAndSavePublishHandler()
  {
    RETURN this->RunSimpleScreen("confirm"
                              , GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmsaveandpublish")
                              , [ dontshowkey := "publisher.editdocument.confirmsaveandpublish"
                                , dontshowbuttons := ["yes"]
                                ]) = "yes";
  }

  MACRO DoSendPreviewLink()
  {
    this->LoadScreen(".sendpreviewlink")->RunModal();
  }

  PUBLIC OBJECT FUNCTION GetPreviewObject(BOOLEAN autosave)
  {
    this->UpdateAutosave();

    RETURN autosave
        ? this->GetEditingSourceObject()
        : this->GetBaseSourceObject();
  }

  PUBLIC STRING FUNCTION CreatePreviewLink(DATETIME validuntil, STRING password, BOOLEAN autosave)
  {
    this->UpdateAutosave();

    RETURN CreatePreviewLink(validuntil, password, this->itemid, this->GetPreviewObject(autosave)->id);
  }

  MACRO DoPreviewCurrentContent(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(TRUE), TRUE);
    ELSE
    {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->CreatePreviewLink(AddDaysTodate(1, GetCurrentDatetime()), "", TRUE);
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoPreviewdraft(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onpreview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onpreview(openhandler, this->GetPreviewObject(FALSE), FALSE);
    ELSE
    {
      //ADDME Can we create a previewurl that ONLY works for the currentuser, as long as he's logged in ?
      STRING url := this->CreatePreviewLink(AddDaysTodate(1, GetCurrentDatetime()), "", FALSE);
      IF(url != "")
        openhandler->SendURL(url);
    }
  }

  MACRO DoViewPublished(OBJECT openhandler)
  {
    IF (this->contexts->editdocumentapi->onview != DEFAULT MACRO PTR)
      this->contexts->editdocumentapi->onview(openhandler, OpenWHFSObject(this->itemid));
    ELSE
      openhandler->SendURL(OpenWHFSObject(this->itemid)->url);
  }

  BOOLEAN FUNCTION Cancel()
  {
    this->DoClose();
    RETURN this->tolliumresult != "";
  }

  MACRO DoClose()
  {
    IF (this->frame->flags.isdirty)
    {
      STRING res := this->RunSimpleScreen("question", GetTid("publisher:tolliumapps.editdocument.messageboxes.savebeforeclose", OpenWHFSObject(this->itemid)->name), [ buttons := ["yes","no","cancel"]]);
      SWITCH (res)
      {
        CASE "yes"
        {
          IF (this->ExecuteSave(FALSE, DEFAULT RECORD))
            this->tolliumresult := "ok";
        }
        CASE "no"
        {
          OBJECT work := this->BeginUnvalidatedWork();
          CleanupMyPrivateDrafts(this->itemid);
          this->currentautosave := DEFAULT OBJECT;
          work->Finish();

          // No autosave upon unload!
          this->canautosave := FALSE;
          this->tolliumresult := "ok";
        }
      }
    }
    ELSE
      this->tolliumresult := "ok";
  }

  MACRO DoCanceldraft()
  {
    IF (this->RunSimpleScreen("confirm", GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmcanceldraft")) != "yes")
      RETURN;

    OBJECT old_currentautosave := this->currentautosave;
    OBJECT old_currentdraft := this->currentdraft;

    OBJECT work := this->BeginUnvalidatedWork();

    IF (ObjectExists(this->currentautosave))
    {
      this->currentautosave->DeleteSelf();
      this->currentautosave := DEFAULT OBJECT;
    }
    IF (ObjectExists(this->currentdraft))
    {
      this->currentdraft->DeleteSelf();
      this->currentdraft := DEFAULT OBJECT;
    }

    IF (work->Finish())
    {
      this->ReloadRTEContents();
      this->RefreshState();

      this->RunSimpleScreen("info", GetTid("publisher:tolliumapps.editdocument.messageboxes.draftcancelled"));
    }
    ELSE
    {
      this->currentautosave := old_currentautosave;
      this->currentdraft := old_currentdraft;
    }
  }

  MACRO DoCancelRequest()
  {
    IF (this->GetSaveMode() IN [ "versioned-locked" ]
        AND ObjectExists(this->currentdraft)
        AND this->RunSimpleScreen("confirm", GetTid("publisher:tolliumapps.editdocument.messageboxes.confirmcancelrequest")) = "yes")
    {
      OBJECT work := this->BeginWork();

      CancelApprovalRequestForDraft(this->currentdraft);

      IF (work->Finish())
      {
        this->RefreshState();
        this->RunSimpleScreen("info", GetTid("publisher:tolliumapps.editdocument.messageboxes.requestcancelled"));
      }
    }
  }

  MACRO DoHidedraftbar()
  {
    this->showdraftbar := FALSE;
    ^draftbar->visible := FALSE;
  }

  MACRO DoOpenInPublisher()
  {
    RunPublisherFileManager(this, this->itemid);
  }

  MACRO DoInspectAutosave()
  {
    IF(ObjectExists(this->currentautosave))
      RunFSObjectInspectDialog(this, this->currentautosave->id);
    ELSE
      this->RunSimpleScreen("info", "No autosave present");
  }
  MACRO DoInspectDraft()
  {
    IF(ObjectExists(this->currentdraft))
      RunFSObjectInspectDialog(this, this->currentdraft->id);
    ELSE
      this->RunSimpleScreen("info", "No draft present");
  }
  MACRO DoInspectFinal()
  {
    RunFSObjectInspectDialog(this, this->itemid);
  }
>;

PUBLIC STATIC OBJECTTYPE SendPreviewLink EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    //round up to next hour, 7 days away
    ^validuntil->value := GetRoundedDatetime(AddTimeToDate(60*60000, AddDaysToDate(7, GetCurrentDateTime())), 60*60000);
  }
  BOOLEAN FUNCTION OnShareNext()
  {
    OBJECT work := this->BeginUnvalidatedFeedback();
    work->Validate(^sharepage);
    IF(^validuntil->value < GetCurrentDatetime())
      work->AddErrorFor(^validuntil, this->GetTid(".validinthepast"));

    RETURN work->Finish();
  }
  MACRO InitFinishPage(RECORD data)
  {
    IF (this->contexts->editdocumentapi->onpreviewlink != DEFAULT MACRO PTR)
      ^link->value := this->contexts->editdocumentapi->onpreviewlink(this->tolliumparent->GetPreviewObject(TRUE), ^validuntil->value, ^passwordprotect->value ? ^password->value : "");
    ELSE
      ^link->value := this->tolliumparent->CreatePreviewLink(^validuntil->value, ^passwordprotect->value ? ^password->value : "", FALSE);

    STRING mailbody := this->GetTid(".mailbodypreview") || "\n\n" || ^link->value || "\n\n" || this->GetTid(".mailbodypreview2");
    STRING mailsubject := this->GetTid(".mailbodysubject");
    ^maillink->htmlvalue := '<a href="mailto:?to=&subject=' || EncodeURL(mailsubject) || '&body=' || EncodeURL(mailbody) || '">' || EncodeHTML(this->GetTid(".maillink")) || '</a>';
  }
>;
