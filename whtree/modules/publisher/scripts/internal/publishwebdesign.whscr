<?wh
LOADLIB "wh::javascript.whlib";
LOADLIB "mod::publisher/lib/internal/publishing-state.whlib";
LOADLIB "mod::publisher/lib/internal/outputmgmt.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/common-v2.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";

OBJECT uplink := publishingbackendlink;
RECORD publishfile := file;
RECORD publishfolder := folder;
RECORD publishsite := site;
BOOLEAN opened_firstpage;
INTEGER outputid;

OBJECT applytester := GetApplyTesterForObject(file.id);
IF(NOT ObjectExists(applytester))
  THROW NEW Exception("Unable to locate the webdesign for #" || publishfile.id);

RECORD webdesigninfo := applytester->GetWebDesignObjinfo();
IF(NOT RecordExists(webdesigninfo))
  THROW NEW Exception("Unable to locate the webdesign for #" || publishfile.id);

PUBLISHER_SetCaptureSubPaths(webdesigninfo.renderinfo.capturesubpaths);

IF(webdesigninfo.js) {
  RECORD response := CallJS("@webhare/router/src/corerouter.ts#executeContentPageRequestHS", file.id, CELL[ contentfile := contentfile.id ]);
  STRING ctype := SELECT AS STRING row[1] FROM ToRecordArray(response.headers,'row') WHERE row[0]="content-type";
  IF(ctype != "text/html;charset=utf-8")
    THROW NEW Exception(`Output of JS-rendered webdesign must be 'text/html;charset=utf-8', but got '${ctype}'`);

  INTEGER stream := OpenOutputFile(firstpagename, TRUE);
  SendBlobTo(stream, response.body);
  PUBLISHER_CloseOutput(stream);
  RETURN;
}

//Destroy any contact with the publication engine, to intercept attempts to access 'old' state
file := DEFAULT RECORD;
folder := DEFAULT RECORD;
site := DEFAULT RECORD;
cached_publish_info.istemplaterun := FALSE;
contentfile := DEFAULT RECORD;
contentfolder := DEFAULT RECORD;
contentsite := DEFAULT RECORD;

//Now get to work. GetWebDesign actually loads user code, but we should have done enough to disrupt any usage of template-v2 by now.
__ValidateWebdesign(applytester);
OBJECT webdesign := InstantiateWebDesign(applytester, webdesigninfo, 0, DEFAULT OBJECT, DEFAULT OBJECT, FALSE);
IF(NOT ObjectExists(webdesign))
  THROW NEW Exception("Unable to locate the webdesign for #" || publishfile.id);

/* FIXME and ADDME
   - support multipage publication, if static
   - alternatively
     - block word content if published dynamically, support image statically
     - background-convert word content to RTD
   - support database output
*/

opened_firstpage := TRUE;

//ADDME Set unique/generated names on followup pages

FOREVERY(RECORD page FROM webdesign->__staticpages)
{
  STRING pagename := #page = 0 ? firstpagename : page.name;

  outputid := OpenOutputFile(pagename, TRUE);

  RedirectOutputTo(outputid);

  IF(page.preparepage != DEFAULT MACRO PTR)
    page.preparepage();

  webdesign->__truecontentbody := page.runbody;
  webdesign->RunPage();
  webdesign->__truecontentbody := DEFAULT MACRO PTR;

  RedirectOutputTo(0);
  PUBLISHER_CloseOutput(outputid);
}

webdesign->RunAfterStaticPage();
IF(ObjectExists(webdesign->pageobject))
  webdesign->pageobject->RunAfterRendering(webdesign);

FOREVERY(RECORD dbfile FROM dbfiles)
{
  outputid := OpenDBOutputFile(dbfile.filename);
  RedirectOutputTo(outputid);
  dbfile.callback();
  RedirectOutputTo(0);
  PUBLISHER_CloseOutput(outputid);
}
