<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/cluster.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/internal/tasks/queue.whlib";


STATIC OBJECTTYPE OutputAnalyzerQueue EXTEND PersistentDiskQueue
<
  UPDATE STRING FUNCTION GetTaskContentId(RECORD task)
  {
    RETURN EncodeHSON(task);
  }

  UPDATE MACRO TryRebuildPersistentQueue()
  {
    PRINT(`Queue not consistent, scheduling full rescan\n`);
    // If the queue could not be restored, rescan all folders
    this->PushTask([ action := "GATHERFOLDERS" ]);
  }

  UPDATE MACRO HandleTaskError(OBJECT item, OBJECT error)
  {
    // Try this task again in a minute
    this->PushTask(item->taskdata, CELL
        [ item->priority
        , scheduledate :=   AddTimeToDate(60 * 1000, GetCurrentDateTime())
        ]);
  }
>;

RECORD args := ParseArguments(GetConsoleArguments(),
    [ [ name := "debuglevel", type := "stringopt" ]
    , [ name := "resetqueue", type := "switch" ]
    , [ name := "debug", type := "switch" ]
    ]);
IF (NOT RecordExists(args))
  TerminateScriptWithError(`Usage: outputanalyzer-persistent.whscr [--debuglevel] [--resetqueue] [--debug]`);

PUBLIC STATIC OBJECTTYPE OutputAnalyzerQueueController EXTEND PersistentQueueControllerBase
<

  UPDATE PUBLIC RECORD FUNCTION ScheduleMultiple(RECORD ARRAY tasks)
  {
    RETURN PersistentQueueControllerBase::ScheduleMultiple(tasks);
  }

  /** Tests which folders are currently scheduled
      @param folderids List of folder ids to test
      @return List of tested folders ids that are currently scheduled
  */
  PUBLIC INTEGER ARRAY FUNCTION TestFoldersScheduled(INTEGER ARRAY folderids)
  {
    RECORD state := this->queue->GetState();
    RETURN
        SELECT AS INTEGER ARRAY DISTINCT item->taskdata.folderid
          FROM ToRecordArray(state.running CONCAT state.runnable, "ITEM")
         WHERE item->taskdata.action = "ACTION"
           AND item->taskdata.folderid IN folderids;
  }
>;

OBJECT FUNCTION Constructor(OBJECT queue)
{
  RETURN NEW OutputAnalyzerQueueController(queue);
}


OBJECT queue := NEW OutputAnalyzerQueue("publisher:outputanalyzer", CELL
    [ ...args
    , debuglevel := ToInteger(args.debuglevel, args.debug ? debuglevel_all : debuglevel_none)
    , debugprint := args.debug
    , args.resetqueue
    ]);

queue->StartProcessing(); // Starts running the queue
RunWebHareService("publisher:outputanalyzer", PTR Constructor(queue), [ restartimmediately := TRUE ]);
