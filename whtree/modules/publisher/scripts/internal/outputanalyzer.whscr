<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::os.whlib";
LOADLIB "mod::system/lib/internal/consoleapps.whlib";
LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";


LOADLIB "mod::publisher/lib/internal/outputmedia/analyzer.whlib";
LOADLIB "mod::publisher/lib/internal/publication/support.whlib";

/* wh run mod::publisher/scripts/internal/outputanalyzer.whscr

   The output analyzer has a simple line-based command protocol.
   Possible inputs, used by the Publisher:
   QUIT - Abort
   SCAN\t<folderid>\t<recurse> - Scan the specified folder. If recurse=1, scan it recursively
   GATHERFOLDERS - Gather a list of all folder ids, listing all interesting folders for a rescan

   Possible inputs, for manual testing:
   SCANALL - Manually scan all folders

   Possible outputs:
   WAITING - The output analyzer is waiting for an instruction (it just started up, or completed a previous task)
   DEBUG\t<debug> - Debug data
   LOG\t<errordata> - Data that 'must' be logged
   ANALYZE\t<folderid>\t<recurse> - Schedule the specified folder further analysis
*/

OBJECT trans := OpenPrimary( [ waituntil := AddTimeToDate(60*1000, GetCurrentDatetime()) ]);
BOOLEAN debug := FALSE;
BOOLEAN nodelay;


MACRO ScanFolder(INTEGER folderid, BOOLEAN recursive)
{
  DATETIME start;

  trans->BeginWork();
  IF(NOT nodelay)
    Sleep(25);
  IF(debug)
    PRINT(`LOG\tAnalyzing folder #${folderid}\n`);
  ScanSingleFolder(folderid);

  IF(recursive)
  {
    FOREVERY(RECORD item FROM (SELECT id FROM system.fs_objects WHERE isfolder AND parent = folderid AND type != 1))
      PRINT("ANALYZE\t" || item.id || "\t1\n");
  }
  trans->CommitWork();
}

INTEGER ARRAY FUNCTION GetAllFolders()
{
  INTEGER ARRAY rootfolders := SELECT AS INTEGER ARRAY root FROM system.sites;
  RETURN SELECT AS INTEGER ARRAY id
                            FROM system.fs_objects
                           WHERE isfolder = TRUE
                                 AND type != 1 /*foreign*/
                                 AND isactive
                        ORDER BY id=16 DESC, id IN rootfolders DESC; // Put all root files first, they often matter more
}

MACRO GatherFolders()
{
  trans->BeginWork();

  INTEGER ARRAY allfolders;

  // Generate a list of folders to check (ADDME: Have separate keys for startup scans and scanning interval!)
  IF (ReadRegistryKey("publisher.publication.scanoutputatstartup"))
    allfolders := GetAllFolders();

  //Quick requeue of pending files
  RECORD ARRAY to_reschedule := SELECT id, parent, lastpublishtime, template, profile, published
                                  FROM system.fs_objects
                                 WHERE isfolder = FALSE
                                       AND published > 0
                                       AND publish = TRUE /*server side filters*/
                                       AND (published%100000) >= 1
                                       AND (published%100000) < 100;
  FOREVERY(RECORD file FROM to_reschedule)
    RequeueSubItem(file.parent, file);

  trans->RollbackWork();
  FOREVERY(INTEGER folderid FROM allfolders)
    PRINT("ANALYZE\t" || folderid || "\t0\n");
}

MACRO SCanAllFoldersNow()
{
  trans->BeginWork();

  /* Generate a list of folders to check */
  INTEGER ARRAY allfolders := GetAllFolders();

  trans->RollbackWork();

  FOREVERY( INTEGER folderid FROM allfolders)
    ScanFolder(folderid, /*recursive*/FALSE);
}

/////////////////////////////////////////////////////////////////////
//
// The main loop
//

RECORD info := ParseArguments(GetConsoleArguments(),
                              [ [ name := "debug", type := "switch" ]
                              , [ name := "exec", type := "stringlist" ]
                              ]);
IF(NOT RecordExists(info))
{
  Print("Syntax: outputanaylzer.whscr [--debug] [--exec <immediatecommand>]\n");
  SetConsoleExitCode(1);
  RETURN;
}

SetOutputBuffering(TRUE);

IF(info.debug)
{
  debug := TRUE;
  SetOutputBuffering(FALSE);
}

//PRINT("LOG\toutputanalyzer is starting!\n");
STRING ARRAY execs := info.exec;

IF(Length(info.exec) != 0)
  nodelay:=TRUE;

WHILE(TRUE)
{
  STRING cmd;
  IF(Length(info.exec) != 0)
  {
    IF(Length(execs)=0)
      RETURN;
    cmd := DecodeJava(execs[0]);
    DELETE FROM execs AT 0;
  }
  ELSE
  {
    PRINT("\nWAITING\n");
    cmd := GetConsoleLine();
  }

  STRING ARRAY cmdtokens := Tokenize(cmd,'\t');
  IF (cmdtokens[0]="GATHERFOLDERS")
  {
    GatherFolders();
    CONTINUE;
  }
  IF (cmdtokens[0]="SCAN")
  {
    INTEGER folderid := ToInteger(cmdtokens[1],-1);
    BOOLEAN recursive := cmdtokens[2]="1";
    ScanFolder(folderid,recursive);
    CONTINUE;
  }
  IF (cmdtokens[0]="SCANALL")
  {
    Print("LOG\tStarting full scan\n");
    FlushOutputBuffer();
    ScanAllFoldersNow();
    CONTINUE;
  }
  IF (cmdtokens[0]="QUIT" OR cmdtokens[0]="")
  {
    RETURN;
  }

  PRINT("LOG\tI don't understand your command '" || cmdtokens[0] || "'\n");
}
