<?wh
// command: publisher:checkpublicationqueue
// short: Ensures all files marked to be published are on the persistent publication Queue

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/services.whlib";

OpenPrimary();
GetPrimary()->BeginWork();

RECORD ARRAY shouldbequeued :=
    SELECT id
         , whfspath
         , priority := published % 100000
         , lastpublishtime
      FROM system.fs_objects
     WHERE isfolder = FALSE
           AND isactive
           AND published > 0
           AND publish = TRUE /*server side filters*/
           AND (published%100000) >= 1
           AND (published%100000) < 100
  ORDER BY id; //TODO ordering by whfspath or fullpath would be better ... do top level content first!   (persistent queue beats ALL though)

IF(Length(shouldbequeued) > 0)
{
  //TODO can we detect which weren't on the queue? those are worth logging
  Print(`Adding ${Length(shouldbequeued)} files to the queue\n`);
  //dumpvalue(shouldbequeued,'boxed');
}

OBJECT service := WaitForPromise(OpenWebHareService("publisher:publication"));
RECORD retval := WaitForPromise(service->ScheduleMultiple(SELECT id, lastpublishtime, priority FROM shouldbequeued));
service->CloseService();

IF (IsValueSet(retval.scheduled))
{
  PRINT(`The following files were not present in the publisher queue\n`);
  DumpValue((
        SELECT *
          FROM shouldbequeued
         WHERE id IN (SELECT AS INTEGER ARRAY id FROM retval.scheduled)
      ORDER BY whfspath), "boxed");
}
