<?wh

LOADLIB "wh::os.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::publisher/lib/internal/consiliohandler.whlib";


RECORD args := ParseArguments(GetConsoleArguments(),
    [ [ name := "debug", type := "switch", defaultvalue := GetEnvironmentVariable("PUBLISHER_CONSILIOHANDLER_DEBUG") != "" OR IsRunningInEditor() ]
    , [ name := "recursive", type := "switch" ]
    , [ name := "ids", type := "paramlist" ]
    ]);
IF (NOT RecordExists(args))
  TerminateScriptWithError("Usage: reindex_publisher.whscr <whfsid> [<whfsid>, ...]");

OpenPrimary();

RECORD ARRAY to_reindex;
FOREVERY (STRING idstr FROM args.ids)
{
  INTEGER objid := ToInteger(idstr, 0);
  IF (objid <= 0)
  {
    Print(`Invalid id '${idstr}'\n`);
    CONTINUE;
  }

  RECORD obj := SELECT isfolder, isactive FROM system.fs_objects WHERE id = objid;
  INSERT
      [ id := objid
      , isfolder := RecordExists(obj) ? obj.isfolder : TRUE //if unknown, assume it was a folder
      , isdelete := FALSE
      ] INTO to_reindex AT END;

  IF(args.recursive AND RecordExists(obj) AND obj.isfolder)
    to_reindex := to_reindex CONCAT SELECT id, isfolder, isdelete := NOT isactive FROM system.fs_objects WHERE parent IN OpenWHFSObject(objid)->GetDescendantFolderids();
}

IF (Length(to_reindex) > 0)
{
  RECORD ARRAY results := ReindexWHFSChanges(to_reindex, CELL[args.debug, progress := TRUE]);
  IF(args.debug AND Length(results) > 0)
    DumpValue(results,'tree');
}
ELSE
  Print("Nothing to do\n");
