<?wh
/** @topic widgets/widgets
*/

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::internet/webbrowser.whlib";
LOADLIB "wh::util/geo.whlib";
LOADLIB "mod::system/lib/internal/modules/defreader.whlib";

RECORD FUNCTION FixupLocation(RECORD loc)
{
  IF(RecordExists(loc))
    RETURN [ lat := FLOAT(loc.lat)
           , long := FLOAT(loc.long)
           ];
  RETURN DEFAULT RECORD;
}

/** Base class for video providers for the built-in video widget */
PUBLIC OBJECTTYPE EmbedVideoProviderBase
<
  PUBLIC RECORD ARRAY FUNCTION Search(STRING keywords, INTEGER maxresults)
  {
    THROW NEW Exception("Search not implemented");
  }
  PUBLIC STRING FUNCTION GetIFramePlayerURL(STRING videoid)
  {
    THROW NEW Exception("GetIFramePlayerURL not implemented");
  }
  UPDATE PUBLIC RECORD FUNCTION DescribeVideo(STRING videoid)
  {
    RECORD retval := this->Search(videoid, 1);
    RETURN retval;
  }
  /** Get the provider title */
  PUBLIC STRING FUNCTION GetTitle()
  {
    RETURN "";
  }
  /** Get the tollium icon to use in widget previews */
  PUBLIC STRING FUNCTION GetIcon()
  {
    RETURN "";
  }

  PUBLIC RECORD ARRAY FUNCTION EnrichVideos(RECORD ARRAY invideos)
  {
    // FIXME: biggestthumbnail?? if it get's too big it's not a thumbnail, but a large still/preview image....
    RETURN SELECT TEMPORARY biggestthumbnail := RECORD(SELECT * FROM invideos.thumbnails ORDER BY width*height DESC LIMIT 1)
                , title
                , description
                , videoid
                , duration
                , location := CellExists(invideos,'location') ? FixupLocation(location) : DEFAULT RECORD
                // use the thumbnail the provider suggests (if available)
                // The provider can decide based on the aspect ratio and resolution of the thumbnail (not too small or too large)
                , thumbnail := CellExists(invideos, "thumbnail") AND RecordExists(thumbnail) ? thumbnail : biggestthumbnail
                , playratio := 16f/9f // FIXME get actual playratio from video. note that thumbnail is unreliable, eg youtube happily returns 4:3 thumbnails for 16:9 videos
                                      // RecordExists(biggestthumbnail) AND biggestthumbnail.height > 0 ? (biggestthumbnail.width*1.0f)/biggestthumbnail.height : 0f
                , biggestthumbnail := biggestthumbnail
                , creationdate
             FROM invideos
            WHERE RecordExists(invideos); //callers might provide us with non-existing records. they shouldn't, but versions doing so exist in the wild, so just filter.
  }
>;

RECORD FUNCTION GetCacheableVideoEmbedProviders()
{
  RECORD ARRAY providers, detectors;

 FOREVERY(RECORD mod FROM GetWebHareModules())
 {
   detectors := detectors CONCAT mod.embedvideodetectors;
   providers := providers CONCAT mod.embedvideoproviders;
 }

  RETURN [ ttl := 5 * 60 * 1000
         , value := [ providers := providers
                    , detectors := detectors
                    ]
         , eventmasks := ["system:modulesupdate"]
         ];
}

PUBLIC RECORD ARRAY FUNCTION GetVideoEmbedProviders()
{
  RETURN GetAdhocCached ( [ type := "providers" ], PTR GetCacheableVideoEmbedProviders).providers;
}

PUBLIC OBJECT FUNCTION GetVideoEmbedProvider(STRING providername)
{
  STRING serv := SELECT AS STRING objectname FROM GetVideoEmbedProviders() WHERE name = providername;
  IF(serv="")
    THROW NEW Exception("No such video provider '" || providername || "'");

  RETURN MakeObject(serv);
}
PUBLIC RECORD FUNCTION GuessEmbeddedVideoFromCode(STRING code)
{
  IF(code LIKE "x-wh-embedvideo:*:*")
  {
    RETURN [ network := Tokenize(code,':')[1]
           , videoid := Tokenize(code,':')[2]
           ];
  }

  FOREVERY(RECORD detector FROM GetAdhocCached ( [ type := "providers" ], PTR GetCacheableVideoEmbedProviders).detectors)
  {
    FUNCTION PTR detectfunc := MakeFunctionPtr(detector.functionname, TYPEID(RECORD), [TYPEID(STRING)]);
    RECORD result := detectfunc(code);
    IF(RecordExists(result))
      RETURN result;
  }
  RETURN DEFAULT RECORD;
}

/** @short Get JSON-encodable data-video */
PUBLIC RECORD FUNCTION GetVideoEmbedInfo(RECORD video)
{
  IF(NOT CellExists(video, 'whfstype') OR video.whfstype != 'http://www.webhare.net/xmlns/publisher/embedvideo')
    THROW NEW Exception("GetVideoEmbedInfo expects an http://www.webhare.net/xmlns/publisher/embedvideo instance");

  RECORD videoinfo := CELL[ video.network, id := video.videoid ];
  IF(video.starttime > 0)
    INSERT CELL starttime := video.starttime/1000m INTO videoinfo;
  IF(video.endtime > video.starttime)
    INSERT CELL endtime := video.endtime/1000m INTO videoinfo;
  IF(video.autoplay)
    INSERT CELL autoplay := TRUE INTO videoinfo;
  IF(video.mute)
    INSERT CELL mute := TRUE INTO videoinfo;
  IF(video.loop)
    INSERT CELL loop := TRUE INTO videoinfo;
  RETURN videoinfo;
}

/** @short Get the data to store in a video embed instance */
PUBLIC RECORD FUNCTION MakeVideoEmbedInstance(STRING network, STRING videoid)
{
  RECORD described := GetVideoEmbedProvider(network)->DescribeVideo(videoid);
  IF (NOT RecordExists(described))
    RETURN DEFAULT RECORD;

  //ADDME Use timeouts and a service to avoid TCP/IP requirements
  OBJECT browser := NEW WebBrowser;
  RECORD thumb;

  IF(RecordExists(described.biggestthumbnail))
  {
    // Workaround for YouTube forgotting to specify there's a maxresdefault.jpg thumbnail available.
    // When YouTube doesn't return maxresdefault.jpg the next in line highest resolution thumbnail is hqdefault, which always is 4:3 (black bars around 16:9 video's) and is low quality compared to maxresdefault.jpg.
    // YouTube hosts images under //img.youtube.com/vi/<videoid>/ and //i.ytimg.com/vi/<videoid>/ or //i3.ytimg.com/vi/<videoid>/
    // This workaround is done here (instead of in Socialite) so code using Socialite (YouTubeNetworkAPI or EmbedProviders) don't get a link to a thumbnail that may not exist.
    IF (described.biggestthumbnail.url LIKE "*hqdefault.jpg" AND browser->GotoWebPage(Substitute(described.biggestthumbnail.url, "hqdefault.jpg", "maxresdefault.jpg")))
    {
      thumb := WrapBlob(browser->content, "preview-" || ToLowercase(videoid) || ".*");
    }
    ELSE IF(browser->GotoWebPage(described.biggestthumbnail.url))
    {
      thumb := WrapBlob(browser->content, "preview-" || ToLowercase(videoid) || ".*");
    }
    ELSE IF(RecordExists(described.thumbnail) AND described.thumbnail.url != described.biggestthumbnail.url) //alternate url might work? youtube sometimes fails on its biggest thumb, eg http://i.ytimg.com/vi/oSzwFOBq4Co/maxresdefault.jpg fails
    {
      IF(browser->GotoWebPage(described.thumbnail.url))
      {
        thumb := WrapBlob(browser->content, "preview-" || ToLowercase(videoid) || ".*");
      }
    }
  }

  RETURN [ whfstype :=     "http://www.webhare.net/xmlns/publisher/embedvideo"
         , network :=      network
         , videoid :=      videoid
         , thumbnail :=    thumb
         , creationdate := described.creationdate
         , title :=        described.title
         , description :=  LimitUTF8Bytes(described.description, 4095)  // YouTube's description limit isn't documented but it's about 5000 bytes
         , duration :=     described.duration
         , location :=     LatLngToString(described.location)
         , playratio :=    16f/9f //FIXME properly calculate this, but simply
         , starttime :=    0
         , endtime :=      0
         , autoplay :=     FALSE
         , loop :=         FALSE
         , mute :=        FALSE
         ];
}
