<?wh
/** @short APIs for managing form files
    @topic forms/api
*/

LOADLIB "mod::publisher/lib/internal/forms/opener.whlib" EXPORT OpenForm;
LOADLIB "mod::publisher/lib/internal/forms/formdef.whlib" EXPORT CreateNewFormdefinitionsFile, OpenWHFSFormDefinitionsFile;
LOADLIB "mod::publisher/lib/internal/forms/results.whlib";
LOADLIB "mod::publisher/lib/webtools/internal/formcomponents.whlib";
LOADLIB "mod::publisher/lib/webtools/internal/formhandlers.whlib";

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/resources.whlib";


/** @short Open a formdefinition WHFS file
    @param formfile File to open
    @return(object %FormDefinitionsFile) An opened formdefinition
*/
PUBLIC OBJECT FUNCTION OpenFormFileDefinition(OBJECT formfile, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ formname := "webtoolform" ], options);
  RETURN OpenWHFSFormDefinitionsFile(formfile)->OpenFormDefinition(options.formname);
}

/** @short Open the result storage of a formdefinition WHFS file
    @param formfile File to open
    @cell(string) options.formname The name of the form to open the results for, defaults to `webtoolform` for webtool forms
    @cell(string) options.formdefresouce The resource name of a custom form definitions file, defaults to the form definitions
        file for the formfile object if it's a webtool form file
    @return(object %WHFSForm) Form results, or DEFAULT OBJECT if the file does not contain a valid webtool form
*/
PUBLIC OBJECT FUNCTION OpenFormFileResults(OBJECT formfile, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ formdefresource := "", formname := "webtoolform" ], options);
  OBJECT formdef;
  IF (options.formdefresource != "")
  {
    TRY
    {
      BLOB formdefxml := GetWebHareResource(options.formdefresource);
      OBJECT formdeffile := CreateNewFormdefinitionsFile();
      formdeffile->ImportFromRecord([ formtext := formdefxml ]);
      formdef := formdeffile->OpenFormDefinition(options.formname);
    }
    CATCH (OBJECT e)
    {
      LogHareScriptException(e);
    }
  }
  ELSE
  {
    formdef := OpenFormFileDefinition(formfile, [ formname := options.formname ]);
  }
  IF(NOT ObjectExists(formdef))
    RETURN DEFAULT OBJECT;

  RETURN OpenWebtoolFormResultsForFormdef(formfile, /*formdef->ListFields(), */options.formname, formdef);
}

/** @short Initialize a webtool form storage handler
    @param form The form object this storage handler is storing the results for
    @param formid The id of the form file to store the results
    @return(object %StoreResultsHandler) The store results handler
*/
PUBLIC OBJECT FUNCTION InitializeStoreResultsHandler(OBJECT form, INTEGER formid)
{
  RECORD storehandler :=
      [ handlertype := `${xmlns_forms}#storeresultshandler`
      , guid := ""
      , condition := DEFAULT RECORD
      , settings := CELL[]
      , handlertask := ""
      , handlertaskfor := "submit"
      , handlerobject := "mod::publisher/lib/webtools/formhandlers/storeresults.whlib#StoreResultsHandler"
      ];
  RETURN InstantiateFormHandler(form, storehandler, formid);
}
