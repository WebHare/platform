<?wh
/** @short APIs for managing form files
    @topic forms/api
*/

LOADLIB "mod::publisher/lib/siteprofiles.whlib";
LOADLIB "mod::publisher/lib/internal/forms/opener.whlib";
LOADLIB "mod::publisher/lib/internal/forms/formdef.whlib" EXPORT CreateNewFormdefinitionsFile, OpenWHFSFormDefinitionsFile;
LOADLIB "mod::publisher/lib/internal/forms/parser.whlib";
LOADLIB "mod::publisher/lib/internal/forms/results.whlib";

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";


/** @short Open a formdefinition WHFS file
    @param formfile File to open
    @return(object %FormDefinitionsFile) An opened formdefinition
*/
PUBLIC OBJECT FUNCTION OpenFormFileDefinition(OBJECT formfile, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ formname := "webtoolform" ], options);
  RETURN OpenWHFSFormDefinitionsFile(formfile)->OpenFormDefinition(options.formname);
}

/** @short Open the result storage of a formdefinition WHFS file
    @param formfile File to open
    @cell(string) options.formname The name of the form to open the results for, defaults to `webtoolform` for webtool forms
    @cell(boolean) options.initcustomform If this is a custom form, initialize the custom form object. Make sure this doesn't
        run code that can only run within a request context!
    @return(object %WHFSForm) Form results, or DEFAULT OBJECT if the file does not contain a valid webtool form
*/
PUBLIC OBJECT FUNCTION OpenFormFileResults(OBJECT formfile, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
      [ formname := "webtoolform"
      , initcustomform := FALSE
      ], options);

  STRING formdefname;
  STRING formname := options.formname;
  IF (options.formname LIKE "*#*")
  {
    formdefname := Tokenize(formname, "#")[0];
    formname := Substring(formname, Length(formdefname) + 1);
  }

    // First try opening the form file directly
  OBJECT formdef := OpenFormFileDefinition(formfile, [ formname := options.formname ]);

  // Check if there are formdefinitions applied to the file
  IF (NOT ObjectExists(formdef))
  {
    OBJECT applytester := GetApplyTesterForObject(formfile->id);
    RECORD formdefs := applytester->GetFormDefinitionFile(formdefname);
    IF (RecordExists(formdefs))
    {
      // We have a form definitions file resource path
      TRY
      {
        // Open the form definitions file by resource path
        BLOB formdefxml := GetWebHareResource(formdefs.path);
        OBJECT formdeffile := CreateNewFormdefinitionsFile();
        formdeffile->ImportFromRecord([ text := formdefxml, type := "publisher:formdefinition" ], [ resourcename := formdefs.path ]);
        formdef := formdeffile->OpenFormDefinition(formname);
      }
      CATCH (OBJECT e)
      {
        LogHareScriptException(e);
      }
    }
  }
  IF(NOT ObjectExists(formdef))
    RETURN DEFAULT OBJECT;

  RETURN OpenWebtoolFormResultsForFormdef(formfile, /*formdef->ListFields(), */options.formname, formdef, CELL[ options.initcustomform ]);
}

PUBLIC OBJECT FUNCTION __DoOpenWebtoolForm(OBJECT formobject, STRING formname, OBJECT webcontext, OBJECT applytester, INTEGER targetid, STRING formref, BOOLEAN prepareforfrontend)
{
  //FIXME switching by name is dangerous! keep better track of how the form was opened, but for now we're experimenting whether a simple 'formtarget' is even possible. and we may need a full republish everywhere to even complete the transition for formwidgets
  IF(formname != "webtoolform")
    RETURN OpenFormWithContext(webcontext, formname, CELL[ formref, prepareforfrontend]);

  OBJECT formdefs := OpenWHFSFormDefinitionsFile(formobject);
  OBJECT mainform := formdefs->OpenFormDefinition(formname);

  IF(NOT ObjectExists(mainform))
    RETURN DEFAULT OBJECT;

  INTEGER webtoolformid := formobject->id;
  STRING objref := GetWHFSObjRef(formobject);
  OBJECT openasdraft := OpenWHFSDraft(webtoolformid);
  IF(ObjectExists(openasdraft))
  {
    webtoolformid := openasdraft->source;
    objref := GetWHFSObjRef(OpenWHFSObject(openasdraft->source));
  }

  RECORD parseresult := ParseFormDef(mainform->node, "webtoolform", "", "", formdefs, CELL[ webtoolformid, objref ]);
  IF(parseresult.objectname = "")
    parseresult.objectname := "mod::publisher/lib/internal/forms/webtool.whlib#WebtoolFormBase";

  RETURN OpenFormByDef(parseresult, CELL[ webcontext, applytester, targetid, incontext := webcontext->targetobject->id, formref, prepareforfrontend ]);
}

/** @short Open a webtool form directly
    @param formid File id to open
    @cell(boolean) options.prepareforfrontend Whether to invoke PrepareForFrontend on the opened form. Defaults to TRUE
    @return Opened form */
PUBLIC OBJECT FUNCTION OpenWebtoolForm(INTEGER formid, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ applytester := DEFAULT OBJECT
                             , webcontext := DEFAULT OBJECT
                             , formref := ""
                             , prepareforfrontend := TRUE
                             , __formname := "webtoolform" //TODO not sure if we should make this a parameter OR if OpenFormByTarget should not use OpenWebtoolForm directly
                             , __applycontext := FALSE //Used for the RPC path as some inits need to be done *just* before form opening
                             , __incontext := 0 //TODO not sure if we should make this a parameter OR if OpenFormByTarget should not use OpenWebtoolForm directly
                             ], options);

  IF(NOT ObjectExists(options.applytester))
    IF(ObjectExists(options.webcontext))
      options.applytester := options.webcontext->applytester;
    ELSE
      options.applytester := GetApplyTesterForObject(options.__incontext ?? formid);

  IF(options.webcontext = DEFAULT OBJECT)
    options.webcontext := __GetWebContextForApplyTester(options.applytester, options.__incontext ?? formid);

  IF(options.__applycontext)
  {
    SetTidLanguage(options.webcontext->languagecode);
  }

  OBJECT form := __DoOpenWebtoolForm(OpenWHFSObject(formid), options.__formname, options.webcontext, options.applytester, formid, options.formref, options.prepareforfrontend);
  IF(NOT ObjectExists(form))
    THROW NEW Exception(`Cannot open form for fsobject #${formid}`);

  RETURN form;
}

/** @short Open a webtool form by its formtarget (encrypted formid)
    @param formtarget Formref as returned by the form's GetFormTarget()
    @cell(boolean) options.prepareforfrontend Whether to invoke PrepareForFrontend on the opened form. Defaults to TRUE
    @return Opened form */
PUBLIC OBJECT FUNCTION OpenFormByTarget(STRING formtarget, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ applytester := DEFAULT OBJECT
                             , webcontext := DEFAULT OBJECT
                             , __applycontext := FALSE
                             , prepareforfrontend := TRUE
                             ], options);

  RECORD targetinfo := DecryptForThisServer("publisher:formtarget", formtarget);
  RETURN OpenWebtoolForm(targetinfo.fsobj, CELL[ ...options, __formname := targetinfo.name, targetinfo.formref, __incontext := targetinfo.incontext ]);
}
