<?wh
LOADLIB "wh::datetime.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/dbschema.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib" ;
LOADLIB "mod::system/lib/internal/whfs/drafts.whlib" EXPORT StartWorkflowUpdate;


CONSTANT STRING ARRAY eventnames := [ "recycled"   //0 whconstant_historytype_recycled
                                    , "saved"      //1 whconstant_historytype_saved
                                    , "#2"         //currently unused
                                    , "reverted"   //3 whconstant_historytype_reverted
                                    , "created"    //4 whconstant_historytype_created
                                    , "approved"   //5 whconstant_historytype_approved
                                    ];

/** Get the change history for this object
    @return Change history items
    @cell(string) return.type One of 'recycled', 'created', 'modified'
    @cell(integer) return.user User authobject id
    @cell(datetime) return.when Modification time
*/
PUBLIC RECORD ARRAY FUNCTION ListObjectHistory(INTEGER objid)
{
  RETURN SELECT id
              , type := type >= 0 AND type <= 5 ? eventnames[type] : "#" || type
              , user
              , userdata := userdata LIKE "hson:*" ? DecodeHSON(userdata) : DEFAULT RECORD
              , when
              , snapshot
              , version
           FROM system_internal.fs_history
          WHERE fs_object = objid
       ORDER BY when;
}

PUBLIC OBJECT FUNCTION CreateObjectWithHistory(OBJECT user, OBJECT parent, RECORD metadata, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  metadata := ValidateOptions(CELL[ typens := ""
                                  , instances := RECORD[]
                                  ], metadata, [ passthroughin := "objectdata"
                                               , required := ["typens"]
                                               , title := "metadata"
                                               ]);
  options := ValidateOptions(CELL[], options);

  OBJECT targettype := OpenWHFSType(metadata.typens);
  IF(NOT ObjectExists(targettype))
    THROW NEW Exception(`No such type: ${metadata.typens}`);
  IF(NOT (targettype->foldertype OR targettype->filetype))
    THROW NEW Exception(`Type is not a file/folder type: ${metadata.typens}`);

  DATETIME now := GetCurrentDatetime();
  RECORD objectdata := CELL[ ...metadata.objectdata
                           , modificationdate := now
                           ];
  OBJECT final := targettype->foldertype ? parent->CreateFolder(objectdata) : parent->CreateFile(objectdata);
  UPDATE system.fs_objects SET creationdate := now WHERE id = final->id; //can't set it directly through the Create APIs
  final := OpenWHFSObject(final->id); //force reload creationdate

  FOREVERY(RECORD instance FROM metadata.instances)
  {
    IF(NOT CellExists(instance,'whfstype'))
      THROW NEW Exception(`Instance #${#instance} has no cell 'whfstype'`);
    final->SetInstanceData(instance.whfstype, CELL [...instance, DELETE whfstype], [ isvisibleedit := FALSE ]);
  }

  RECORD userinfo := user->GetUserDataForLogging();
  INSERT CELL[ fs_object := final->id
             , user := user->authobjectid  //TODO obsolete in the future?
             , userdata := EncodeHSON(userinfo)
             , type := whconstant_historytype_created
             , when := now
             , version := "0.1"
             ] INTO system_internal.fs_history;

  RETURN final;
}
