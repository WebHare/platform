<?wh
/** @private This library should be private, get FormsapiFormTester from testfw/sites.whlib */
LOADLIB "wh::files.whlib";

LOADLIB "mod::publisher/lib/testfw/sites.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/cache.whlib";
LOADLIB "mod::publisher/lib/testfw/forms.whlib" EXPORT OpenFormsapiFormTester;

LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/testframework.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

MACRO CreateContent(OBJECT siteroot, BOOLEAN with_errors)
{

  /* ADDME allow combining
           To prevent a race between Publishing & siteprofile compile (which both start at COmmit, we'll commit the siteprofile before adding files to publish) */
  OBJECT dynamicpagewitty := siteroot->CreateFile([name:="dynamicpage.witty", type := 26, data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/dynamicpage.witty") ]);

/*  testfw->CommitWork();

  testfw->BeginWork(); */

  OBJECT dynamicpageshtml := siteroot->CreateFile([name:="dynamicpage.shtml", type := 25, data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/dynamicpage.shtml"), publish := TRUE ]);
  OBJECT prebuiltshtml := siteroot->CreateFile([name:="prebuiltshtml", type := 39, publish := TRUE ]);
  prebuiltshtml->SetInstanceData("http://www.webhare.net/xmlns/publisher/prebuiltpage", [ prebuilttag := "prebuiltshtml" ]);

  OBJECT prebuilthtml := siteroot->CreateFile([name:="prebuilthtml", type := 39, publish := TRUE ]);
  prebuilthtml->SetInstanceData("http://www.webhare.net/xmlns/publisher/prebuiltpage", [ prebuilttag := "prebuilthtml" ]);

  OBJECT standalonedynamicpageshtml := siteroot->CreateFile([name:="standalone-dynamicpage.shtml", type := 25, data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/standalone-dynamicpage.shtml"), publish := TRUE ]);
  OBJECT contentlinkshtml := siteroot->CreateFile([name:="contentlink.shtml", type := 20, filelink := dynamicpageshtml->id, publish := TRUE ]);
  OBJECT headingsdoc := siteroot->CreateFile([name:="headings.doc", data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/simpletest.doc"), publish := TRUE ]);
  OBJECT dynfolder := siteroot->CreateFolder( [ name:="dynfolder"
                                              , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/webdesign-dynfolder")->id
                                              ]);
  OBJECT emptyfolder := siteroot->CreateFolder( [ name:="emptyfolder"
                                                    ]);
  OBJECT emptyfilefolder := siteroot->CreateFolder( [ name:="emptyfilefolder"
                                                    ]);
  OBJECT emptyfile := emptyfilefolder->CreateFile([ name := "index.html"
                                                  , type := OpenWHFSType("http://www.webhare.net/xmlns/webhare_testsuite/webdesign/emptydocumentfile")->id
                                                  , publish := TRUE
                                                  ]);

  OBJECT externallinkshtml := siteroot->CreateFile([name:="externallink.shtml", type := 18, externallink := "http://www.b-lex.nl/", publish := TRUE ]);
  OBJECT internallinkemptyshtml := siteroot->CreateFile([name:="internallink-empty.shtml", type := 19, filelink := 0, publish := TRUE ]);
  OBJECT internallinkpublishedshtml := siteroot->CreateFile([name:="internallink-published.shtml", type := 19, filelink := dynamicpageshtml->id, publish := TRUE ]);
  OBJECT internallinkunpublishedshtml := siteroot->CreateFile([name:="internallink-unpublished.shtml", type := 19, filelink := dynamicpagewitty->id, publish := TRUE ]);

  OBJECT redirectshtml := siteroot->CreateFile([name:="redirect.shtml", type := 25, data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/redirect.shtml"), publish := TRUE ]);
  OBJECT redirectjsshtml := siteroot->CreateFile([name:="redirect-js.shtml", type := 25, data := testfw->GetModuleTestBlob("webhare_testsuite","publisher-webdesign/testdata/redirect-js.shtml"), publish := TRUE ]);

  dynfolder->CreateFile([name := "index.shtml", type := OpenWHFSType("http://www.webhare.net/xmlns/publisher/dynamicfoldercontents")->id, publish := TRUE ]);
  OBJECT statichtmldoc := siteroot->CreateFile([name:="static.html", data := StringToBlob("Een <b>test</b> pagina"), publish := TRUE, type := 5 ]);
  OBJECT indexhtmldoc := siteroot->CreateFile([name:="index.html", data := StringToBlob("Een homepage"), publish := TRUE, type := 5 ]);
  siteroot->UpdateMetadata([indexdoc := indexhtmldoc->id]);
  siteroot->SetInstanceData("http://www.webhare.net/xmlns/webhare_testsuite/webdesign/tests",
          [ file := WrapBLob(StringToBlob(RepeatText("testfile", 64)), "testfile.txt")
          ]);

  OBJECT image := siteroot->CreateFile([name:="bob.jpg", data := GetWebhareResource("mod::webhare_testsuite/data//test/bob.jpg"), publish := TRUE ]);
  OBJECT simplexlsx := siteroot->CreateFile([name:="simple.xlsx", data := GetWebhareResource("whres::ooxml/template.xlsx"), publish := TRUE ]);

  IF(with_errors)
  {
    OBJECT loadlibdoc := siteroot->CreateFile([name:="loadlib.html", data := StringToBlob("Een <b>loadlib</b> pagina"), publish := TRUE, type := 38 ]);
    OBJECT loadlibsdoc := siteroot->CreateFile([name:="loadlib.shtml", data := StringToBlob("Een <b>loadlib</b> pagina"), publish := TRUE, type := 38 ]);
    OBJECT compileerror := siteroot->CreateFile([name:="compileerror.shtml", type := 25, data := StringToBlob('<?wh XYZ;'), publish := TRUE ]);
    OBJECT runtimeerror := siteroot->CreateFile([name:="exception.shtml", type := 25, data := StringToBlob('<?wh THROW NEW Exception("this is an exception");'), publish := TRUE ]);

    OBJECT internallinkerrorsshtml := siteroot->CreateFile([name:="internallink-errors.shtml", type := 19, filelink := loadlibdoc->id, publish := TRUE ]);
  }
}

PUBLIC MACRO PrepareTestSiteDesignWebsite(STRING siteprofile, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  testfw->BeginWork();

  OBJECT newsite := testfw->SetupTestWebdesignWebsite(siteprofile);
  BOOLEAN witherrors := CellExists(options,'witherrors') AND options.witherrors;
  BOOLEAN waitpublish := (NOT CellExists(options,'waitpublish')) OR options.waitpublish = TRUE;
  OBJECT siteroot := newsite->rootobject;

  CreateContent(siteroot, witherrors);

  testfw->CommitWork();
  IF(waitpublish)
    testfw->WaitForPublishCompletion(siteroot->id);
}

PUBLIC MACRO PrepareTestModuleWebDesignWebsite(STRING webdesigntemplate, RECORD options  DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
      [ withcontent :=  TRUE
      , waitpublish :=  TRUE
      , witherrors :=   FALSE
      , enableoutput := TRUE
      //, aftercompiletask := ""
      ], options,
      [ passthroughin := "sitesettings"
      ]);

  OBJECT newsite := SetupTestWebsite(webdesigntemplate, options.sitesettings);

  IF(options.withcontent)
  {
    testfw->BeginWork();
    CreateContent(newsite->rootobject, options.witherrors);
    testfw->CommitWork();
  }

  IF(options.waitpublish)
    testfw->WaitForPublishCompletion(newsite->id);
}

PUBLIC ASYNC FUNCTION EnsureValidTestModuleWebDesignWebsiteBundle()
{
  //make sure the bundle compiled
  OBJECT service := WaitForPromise(OpenWebHareService("publisher:assetpackcontrol"));
  RECORD res := AWAIT service->WaitForCompile("webhare_testsuite_temp:testdesign", FALSE);
  TestEq(FALSE, res.haserrors, "compile should have succeeded");
  RETURN TRUE;
}

PUBLIC RECORD FUNCTION ExtractBundleOutputTag(OBJECT doc)
{
  OBJECT ARRAY scripts := doc->getElements('script[src]');
  TestEq(TRUE, Length(scripts) > 0);

  STRING jslink := scripts[0]->GetAttribute("src");
  TestEq(TRUE, jslink LIKE "/.ap/*/ap.js");

  STRING outputtag := Tokenize(jslink,'/')[2];
  INTEGER firstdot := SearchSubstring(outputtag,'.');
  outputtag := Left(outputtag, firstdot) || ':' || Substring(outputtag, firstdot+1);

  RETURN [ outputtag := outputtag
         , isdev := outputtag LIKE "*.dev"
         , jslink := jslink
         ];
}

PUBLIC MACRO TestSiteProfilesNoErrors()
{
  RECORD csp := GetCachedSiteProfiles();
  IF(Length(csp.errors)>0)
    DumpValue(csp.errors,'boxed');
  TestEq(0, Length(csp.errors), "There are siteprofile errors");
}

