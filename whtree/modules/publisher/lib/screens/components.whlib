<?wh
LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/support.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/internal/inputvalidation.whlib";

PUBLIC OBJECTTYPE IntExtLink EXTEND TolliumFragmentBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD ARRAY saveoptions;

  STRING current_linktype;

  BOOLEAN initialvalueset;

  // ---------------------------------------------------------------------------
  //
  // Public variables and properties
  //

  /// Type of the value ('intextlink' or 'string')
  PUBLIC STRING valuetype;

  /// Value
  PUBLIC PROPERTY value(GetValue, SetValue);

  /// Title
  UPDATE PUBLIC PROPERTY title(this->linktype->title, this->linktype->title);

  // ---------------------------------------------------------------------------
  //
  // Constructor
  //

  PUBLIC MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    EXTEND this BY TolliumIsDirtyable;
    this->current_linktype := "nolink";
    this->valuetype := "intextlink";
  }

  // ---------------------------------------------------------------------------
  //
  // Tollium admin stuff
  //

  UPDATE PUBLIC MACRO StaticInit(RECORD def)
  {
    TolliumFragmentBase::StaticInit(def);
    this->valuetype := def.valuetype;
    this->saveoptions := this->linktype->options;
    this->title := def.title;

    IF(this->pvt_required)
      DELETE FROM this->linktype->options WHERE rowkey="nolink";
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  RECORD FUNCTION GetCurrentInternalLinkValue()
  {
    INTEGER objid := this->internal->value;
    IF(objid!=0)
    {
      BOOLEAN valid_append := Left(this->append->value, 1) IN [ "#", "?", "!" ];
      RETURN MakeIntExtInternalLink(objid, valid_append ? this->append->value : "");
    }
    RETURN DEFAULT RECORD;
  }

  RECORD FUNCTION GetIntlinkValue()
  {
    IF(this->linktype->value="internal")
    {
      RETURN this->GetCurrentInternalLinkValue();
    }
    IF(this->linktype->value="external")
    {
      IF(this->external->value != "" AND ValidateInput(this->external->value, "dummy", [ "url-plus-relative" ]) = "")
      {
        RETURN MakeIntExtExternalLink(this->external->value);
      }
    }
    RETURN DEFAULT RECORD;
  }

  // ---------------------------------------------------------------------------
  //
  // Getters & setters
  //

  UPDATE PUBLIC MACRO SetRequired(BOOLEAN new_required)
  {
    IF(new_required = this->pvt_required)
      RETURN;

    TolliumFragmentBase::SetRequired(new_required);
    IF(this->pvt_required)
      DELETE FROM this->linktype->options WHERE rowkey="nolink";
    ELSE
      this->linktype->options := this->saveoptions;
  }

  UPDATE PUBLIC MACRO SetEnabled(BOOLEAN nowenable)
  {
    IF(this->pvt_enabled = nowenable)
      RETURN;

    this->pvt_enabled := nowenable;
    this->linktype->enabled := nowenable;
  }

  VARIANT FUNCTION GetValue()
  {
    RECORD value := this->GetIntLinkValue();
    IF(this->valuetype="intextlink")
      RETURN value;
    IF(this->valuetype="string")
      RETURN GetIntExtLinkAsString(value);

    THROW NEW Exception("Unrecognized valuetype '" || this->valuetype || "'");
  }

  MACRO SetValue(VARIANT value)
  {
    RECORD intextlink;
    IF(this->valuetype="intextlink")
      intextlink := value;
    ELSE IF(this->valuetype="string")
      intextlink := ParseIntExtLinkString(value);
    ELSE
      THROW NEW Exception("Unrecognized valuetype '" || this->valuetype || "'");

    // Set type to nolink, so no conversion is run
    this->linktype->value := "nolink";

    IF(RecordExists(intextlink) AND intextlink.externallink != "")
    {
      this->linktype->value := "external";
      this->external->value := intextlink.externallink;
      this->internal->value := 0;
      this->append->value := "";

    }
    ELSE IF(RecordExists(intextlink) AND intextlink.internallink != 0)
    {
      this->linktype->value := "internal";
      this->internal->value := intextlink.internallink;
      this->append->value := intextlink.append;
      this->external->value := "";
    }
    ELSE
    {
      this->external->value := "";
      this->internal->value := 0;
      this->append->value := "";
    }

    IF (this->initialvalueset)
      this->SetDirty();
    ELSE
      this->initialvalueset := TRUE;
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO OnLinkTypeChange()
  {
    // Try to convert internal links to external links and vice versa
    IF (this->linktype->value = "internal" AND this->current_linktype = "external")
    {
      IF (this->external->value != "")
      {
        //fill from external
        RECORD match := LookupPublisherURL(this->external->value);
        INTEGER matchid := match.file ?? match.folder;

        IF(matchid != 0)
        {
          this->internal->value := matchid;
          STRING baseurl := SELECT AS STRING indexurl FROM system.fs_objects WHERE id = matchid;
          IF (baseurl = "" OR Left(this->external->value, Length(baseurl)) != baseurl OR Substring(this->external->value, Length(baseurl), 1) NOT IN ['#','?','!'])
            baseurl := SELECT AS STRING objecturl FROM system.fs_objects WHERE id = matchid;

          IF (baseurl != "" AND Left(this->external->value, Length(baseurl)) = baseurl AND Substring(this->external->value, Length(baseurl), 1) IN ['#','?','!'])
            this->append->value := Substring(this->external->value, Length(baseurl));
        }

        this->external->value := "";
      }
    }
    ELSE IF (this->linktype->value = "external" AND this->current_linktype = "internal")
    {
      RECORD intextlink := this->GetCurrentInternalLinkValue();
      IF(RecordExists(intextlink))
      {
        // Not using GetIntextlinkTarget, because that returns '' when for a folder without index document
        // (because it uses indexurl instead of url)
        STRING exturl := SELECT AS STRING objecturl FROM system.fs_objects WHERE id = intextlink.internallink AND isactive;
        IF (exturl != "")
          exturl := exturl || intextlink.append;

        this->external->value := exturl;
      }

      this->internal->value := 0;
      this->append->value := "";
    }

    IF (this->current_linktype != this->linktype->value)
    {
      this->current_linktype := this->linktype->value;
      this->SetDirty();
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Public API
  //

  PUBLIC UPDATE MACRO ValidateValue(OBJECT work)
  {
    STRING fieldtitle := this->errorlabel ?? this->title;
    IF(NOT this->enabled) //nothing to check on inactive fields
      RETURN;

    IF(this->required AND this->linktype->value = "nolink")
    {
      work->AddErrorFor(this, GetTid("tollium:common.errors.field_required", fieldtitle));
    }
    ELSE IF(this->linktype->value="internal")
    {
      this->internal->ValidateValue(work);
      IF (Left(this->append->value, 1) NOT IN [ "", "#", "?", "!" ])
      {
        work->AddErrorFor(this, GetTid("publisher:components.errors.invalidappend", fieldtitle));
      }
      ELSE
      {
        STRING url := GetIntextlinkTarget(this->GetIntLinkValue());
        IF(url != "")
        {
          STRING validate_error := ValidateInput(url, fieldtitle, [ "url" ]);
          IF (validate_error != "")
            work->AddErrorFor(this, validate_error);
        }
      }
    }
    ELSE IF(this->linktype->value="external")
    {
      this->external->ValidateValue(work);
    }
  }

  PUBLIC MACRO SetLinkType(STRING type)
  {
    IF (type IN [ "external", "internal" ])
      this->linktype->value := type;
    ELSE
      this->linktype->value := "nolink";
  }
>;
