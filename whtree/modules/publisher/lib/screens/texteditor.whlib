<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "wh::devsupport.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

STRING output;
OBJECT siteprofschema;


PUBLIC OBJECTTYPE Main EXTEND TolliumScreenbase
<
  INTEGER errorfile;

  INTEGER openfileid;
  STRING origtext;
  DATETIME origmodtime;

  MACRO Init(RECORD data)
  {
    IF(data.calltype = "objecteditor")
    {
      this->LoadFile(data.id);
    }
    ELSE
    {
      this->errorfile := data.errorfile;
      this->LoadFile(data.fileid);
    }

    IF(this->errorfile != 0)
    {
      this->messagestabs->visible := TRUE;

      RECORD errorinfo := OpenWHFSType("http://www.webhare.net/xmlns/publisher/errorinfo")->GetInstanceData(this->errorfile);

      this->messages->rows := SELECT filename
                                   , line
                                   , col
                                   , error := GetHareScriptMessageText(type=0, code, param1, param2)
                                   , file
                                   , canopen := file!=0
                                FROM errorinfo.messages;
      this->trace->rows := SELECT filename
                                , line
                                , col
                                , func
                                , file
                                , canopen := file!=0
                             FROM errorinfo.trace AS trace
                         ORDER BY #trace DESC;
    }
  }

  BOOLEAN FUNCTION LoadFile(INTEGER fileid)
  {
    OBJECT fileobj := OpenWHFSObject(fileid);
    IF(NOT ObjectExists(fileobj) OR fileobj->isfolder)
      RETURN FALSE;

    //ADDME: Validate that this file is suitable for Unicode text editing. Editor should prevent launching its window if so or if too large!
    STRING filetext := BlobToString(fileobj->data, -1);
    filetext := Substitute(filetext,"\r","");

    this->openfileid := fileid;
    this->origtext := filetext;
    this->origmodtime := fileobj->modificationdate;
    this->source->value := filetext;

    this->frame->title := fileobj->name;
    this->statusbar->value := '';
    RETURN TRUE;
  }

  MACRO DoSetSyntaxToText()  { this->SetSyntaxTo('text'); }
  MACRO DoSetSyntaxToC()     { this->SetSyntaxTo('c');    }
  MACRO DoSetSyntaxToCPP()   { this->SetSyntaxTo('cpp');  }
  MACRO DoSetSyntaxToCSS()   { this->SetSyntaxTo('css');  }
  MACRO DoSetSyntaxToJAVA()  { this->SetSyntaxTo('java'); }
  MACRO DoSetSyntaxToJS()    { this->SetSyntaxTo('javascript'); }
  MACRO DoSetSyntaxToHS()    { this->SetSyntaxTo('harescript'); }
  MACRO DoSetSyntaxToHTML()  { this->SetSyntaxTo('html'); }
  MACRO DoSetSyntaxToPHP()   { this->SetSyntaxTo('php');  }
  MACRO DoSetSyntaxToXML()   { this->SetSyntaxTo('xml');  }
  MACRO DoSetSyntaxToSQL()   { this->SetSyntaxTo('sql');  }

  MACRO SetSyntaxTo(STRING newsyntax)
  {
    this->source->type := newsyntax;

    RECORD ARRAY mapping := [
                              [ syntax:='text',       item:=this->syntax_text ]
                            , [ syntax:='c',          item:=this->syntax_c    ]
                            , [ syntax:='cpp',        item:=this->syntax_cpp  ]
                            , [ syntax:='css',        item:=this->syntax_css  ]
                            , [ syntax:='harescript', item:=this->syntax_harescript ]
                            , [ syntax:='java',       item:=this->syntax_java ]
                            , [ syntax:='javascript', item:=this->syntax_javascript ]
                            , [ syntax:='html',       item:=this->syntax_html ]
                            , [ syntax:='php',        item:=this->syntax_php  ]
                            , [ syntax:='xml',        item:=this->syntax_xml  ]
                            , [ syntax:='sql',        item:=this->syntax_sql  ]
                            ];

    FOREVERY(RECORD item FROM mapping)
      item.item->selected := FALSE;

    OBJECT selecteditem := SELECT AS OBJECT item FROM mapping WHERE syntax = newsyntax;
    selecteditem->selected := TRUE;
  }

  PUBLIC MACRO DoSave()
  {
    this->TrySave();
  }

  BOOLEAN FUNCTION TrySave()
  {
    STRING datetimestr := this->tolliumuser->FormatTime(GetCurrentDateTime(), 'seconds', TRUE);
    this->statusbar->value := GetTid('publisher:texteditor.status.savedat', datetimestr);

    BLOB contents := StringToBlob(this->source->value);
    OBJECT fileobj := OpenWHFSObject(this->openfileid);

    // Warn against parallel updates (check mod date)
    WHILE(TRUE)
    {
      OBJECT work := this->BeginWork();
      fileobj := OpenWHFSObject(this->openfileid);

      IF(NOT ObjectExists(Fileobj))
      {
        work->Cancel();
        this->RunMessageBox(".failedtosave");
        RETURN FALSE;
      }

      IF(fileobj->modificationdate != this->origmodtime)
      {
        work->Cancel();
        IF(this->RunMessageBox(".saveignoreconflict")!="yes")
          RETURN FALSE; //save aborted

        this->origmodtime := fileobj->modificationdate;
        CONTINUE; //reloop
      }

      fileobj->UpdateMetadata( [data:=contents] );
      IF(NOT work->Finish())
        RETURN FALSE;

      this->RunMessageBox(".succesfullysaved");
      this->origmodtime := fileobj->modificationdate;
      RETURN TRUE;
    }
  }

  BOOLEAN FUNCTION CanUnloadFile()
  {
    IF(TrimWhitespace(this->source->value) = TrimWhitespace(this->origtext))
      RETURN TRUE;

    WHILE(TRUE)
    {
      STRING action := this->RunMessageBox(".unsavedchanges");
      IF(action = "no")
        RETURN TRUE;
      IF(action != "yes")
        RETURN FALSE;

      IF(this->TrySave())
        RETURN TRUE;
    }
  }

  MACRO DoOpenFile(RECORD row)
  {
    IF(this->openfileid != row.file)
    {
      IF(NOT this->CanUnloadFile())
        RETURN;

      RECORD file := SELECT * FROM system.fs_objects WHERE id = row.file AND isactive;
      IF(NOT RecordExists(file)) //ADDME Verify whether the erroring file is source code editable ?
      {
        this->RunMessageBox(".cannotopenerroredfile");
        RETURN;
      }

      //FIXME WARN IF UNSAVED CHANGES
      IF(NOT this->LoadFile(file.id))
        RETURN;
    }
    this->source->GotoLine(row.line);
    this->frame->focused := this->source;
  }

  MACRO DoOpenFile_Messages()
  {
    this->DoOpenFile(this->messages->selection);
  }
  MACRO DoOpenFile_StackTrace()
  {
    this->DoOpenFile(this->trace->selection);
  }
>;
