<?wh
/* Photoalbums are deprecated since WH6.0. Minimal support for backwards compatibility is maintained for now. As we've found it hard
   enough to make a usable, generic photoalbum in core, photoalbum support may be completely removed unless someone wants to maintain
   and further develop it */

LOADLIB "wh::witty.whlib";

LOADLIB "mod::publisher/lib/webdesign.whlib";

LOADLIB "mod::system/lib/cache.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";
LOADLIB "mod::system/lib/internal/whfs/base.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";

//TODO thumbnail sizes (and other settings?) should be configurable through <apply>s
RECORD gallery_gridthumb    := [ method := "fit", setheight := 150 ];
RECORD gallery_gridthumb_2x := [ method := "fit", setheight := 150*2, quality := 50 ];

RECORD FUNCTION GetCacheableImages(INTEGER forfolder)
{
  RECORD ARRAY images := SELECT TEMPORARY __wrapped := WrapFSObjectData(id, DEFAULT BLOB, CELL[scandata,name])
                              , id
                              , title
                              , description
                              , link //TODO if it turns out the image is REALLY big, we should probably still create an image cache link
                              , thumbnail := WrapCachedImage(__wrapped, gallery_gridthumb)
                              , thumbnail_2x := WrapCachedImage(__wrapped, gallery_gridthumb_2x)
                              , dominantcolor := __wrapped.dominantcolor
                              , width := __wrapped.rotation IN [90,270] ? __wrapped.height : __wrapped.width
                              , height := __wrapped.rotation IN [90,270] ? __wrapped.width : __wrapped.height
                           FROM system.fs_objects
                          WHERE parent = forfolder
                                AND type = VAR whconstant_whfstype_imagefile
                                AND link != ""
                      ORDER BY ordering, ToUppercase(name), id;

  //Remove misunderstood images eg WEBP/AVIF
  images := SELECT * FROM images WHERE RecordExists(thumbnail);

  RETURN [ value := images
         , ttl := 15 * 60 * 1000
         , eventmasks := OpenWHFSObject(forfolder)->GetEventMasks()
         ];
}

PUBLIC STATIC OBJECTTYPE RenderGalleryWidgetPage EXTEND StaticPageBase <
  UPDATE PUBLIC MACRO PTR FUNCTION GetPageBody(OBJECT webdesign) {
    RECORD ARRAY images := GetAdhocCached(CELL[ forfolder := webdesign->targetfolder->id ], PTR GetCacheableImages(webdesign->targetfolder->id));
    RETURN PTR EmbedWittyComponent("mod::publisher/widgets/widgets.witty#gallery", CELL[images]);
  }
>;
