<?wh
/** @short Templates
    @long This file must be included by all publication templates and offers
          various functions to control the publication proces.
    @private template-v2 is deprecated, need an alternative for static files without content
*/

LOADLIB "mod::publisher/lib/internal/outputmgmt.whlib";
LOADLIB "mod::publisher/lib/internal/publishing-state.whlib";
LOADLIB "mod::publisher/lib/common-v2.whlib" EXPORT file, folder, site, NotAPublishingContextException;
LOADLIB "mod::system/lib/whfs.whlib";

////////////////////////////////////////////////////////////////////////////
//
// External functions
//

PUBLIC MACRO __PUBLISHER_EXCEPTION(INTEGER exceptioncode, STRING exceptiondata)
{
  // Won't return anymore
  RECORD rec := publishingbackendlink->DoRequest(
     [ type := "EXCEPTION"
     , errorcode := exceptioncode
     , errordata := exceptiondata
     ]);

  IF (rec.status != "ok")
    ABORT("Messaging error");
}

/** @short Generate a publication warning
    @param text Warning text */
PUBLIC MACRO AddPublicationWarning(STRING text)
{
  IF(NOT ObjectExists(publishingbackendlink) OR NOT cached_publish_info.istemplaterun)
    THROW NEW NotAPublishingContextException;

  RECORD rec := publishingbackendlink->DoRequest(
      [ type := "ADDWARNING"
      , text := text
      ]);

  IF (rec.status != "ok")
    ABORT("Messaging error");
}

PUBLIC MACRO __SetCaptureSubPaths(BOOLEAN capturesubpaths)
{
  IF (NOT ObjectExists(publishingbackendlink) OR NOT cached_publish_info.istemplaterun)
    THROW NEW NotAPublishingContextException;

  RECORD rec := publishingbackendlink->DoRequest(CELL
      [ type := "SETCAPTURESUBPATHS"
      , capturesubpaths
      ]);

  IF (rec.status != "ok")
    ABORT("Messaging error");

  IF (NOT rec.msg.success)
    THROW NEW Exception(rec.msg.errordata);
}

////////////////////////////////////////////////////////////////////////////
//
// Global variables
//


//short Stack of currently open pages
//cell openpages.number Number used to open the page (0=CreateFile page)
//cell openpages.name Name given for the open page */
PRIVATE RECORD ARRAY openpages;

////////////////////////////////////////////////////////////////////////////
//
// Functions
//

PUBLIC RECORD FUNCTION FindPage(INTEGER pageid) {
  RETURN pageid = 1 ? [ id := 1, name := firstpagename ] : DEFAULT RECORD;
}

MACRO __Internal_CreateFile(STRING pagename, INTEGER pageid, BOOLEAN dbfile)
{
  IF(NOT dbfile AND NOT IsValidWHFSName(pagename,FALSE))
    THROW NEW Exception("'" || pagename || "' is not a valid filename for a published filed");
  IF(NOT ObjectExists(publishingbackendlink) OR NOT cached_publish_info.istemplaterun)
    THROW NEW NotAPublishingContextException;

  INTEGER newfileid := dbfile ? OpenDBOutputFile(pagename) : OpenOutputFile(pagename, TRUE);
  IF (newfileid=0)
    ABORT("Illegal/duplicate filename '" || pagename || "'");

  //Redirect all output (Print et-al) to this new object (FIXME: This may be dangerous?? We are redirecting output to the 'newfileid', not to the HTML output device - we might thus Write without the proper flushes ?!)
  RedirectOutputTo(newfileid);
  INSERT INTO openpages(number, name, fileid, dbfile)
         VALUES(pageid, pagename, newfileid, dbfile) AT 0;
}

/** @short   Closes the currently opened file.
    @long    CloseFile closes the file opened by the OpenFile or CreateFile macros.
             The macro will cause an error if it is called when there are no open files.
             The CloseFile and OpenFile/CreateFile macros are antagonists.
*/
PUBLIC MACRO CloseFile()
{
  IF (Length(openpages)=0)
    ABORT("CloseFile: Cannot close more files than there have been opened");

  RedirectOutputTo(Length(openpages)>1 ? openpages[1].fileid : 0); //restore output soon to ease debugging in intercept function

  INTEGER outputfileid := openpages[0].fileid;
  PUBLISHER_CloseOutput(outputfileid);

  //Pop the page number
  DELETE FROM openpages AT 0;
}




/** @short   Creates a new output file with a specific filename
    @long    CreateFile creates a new output file with the name filename. New output will be redirected to this newly created file
             until the file is closed using the CloseFile macro. You can nest up to 32 CreateFile macros, but make sure each file
             is closed using the CloseFile macro. The CreateFile and CloseFile macros are antagonists, and must be used in that
             way. CreateFile can be very useful for creating additional pages, next to the pages created by the conversion engine.
             You can use the GetPageLinkByName function to create links to the newly created files.
    @param   pagename File name for the new page
*/
PUBLIC MACRO CreateFile(STRING pagename)
{
  __Internal_CreateFile(pagename, 0, FALSE);
}


PUBLIC MACRO CreateDBFile(STRING pagename)
{
  __Internal_CreateFile(pagename, 0, TRUE);
}


/** @short   Open an output file.
    @long    OpenFile opens a new output file for the page with ID pagenum. All new output will be redirected to this file until
             the CloseFile macro is called. You can nest up to 32 OpenFile macros, but make sure each file is closed using the
             CloseFile macro. The OpenFile and CloseFile macros are antagonists, and must be used in that way. Nested OpenFile
             macros can for example be used to create TOC pages easily, without having to create and update local record arrays
             in which relevant DocObject are stored during template execution.
    @param   pagenum ID of the page for which an output page should be opened
*/
PUBLIC MACRO OpenFile(INTEGER pagenum)
{
  IF(pagenum != 1)
    ABORT("Multipage documents are not supported in WebHare 6.0+ - you may only open page #1");

  __Internal_CreateFile(firstpagename, pagenum, FALSE);
}

/** @short Rename a page
    @long Rename an existing page. This function should be called before the page is opened, and before any docobjects are printed which might refer to this page!
    @param pageid ID of the page to rename
    @param newpagename The new name of the page
*/
PUBLIC MACRO RenamePage(INTEGER pageid, STRING newpagename)
{
  IF(pageid != 1)
    Abort("Multipage documents are not supported in WebHare 6.0+ - you may only rename page #1");

  BOOLEAN is_index := IsDefaultPagename(newpagename);
  IF (is_index = TRUE AND firstmustbeindex = FALSE)
    Abort("Only documents which require the first page to be an index file may use the name of an index file");

  IF (is_index = FALSE AND firstmustbeindex = TRUE)
    Abort("The first page of a published document must have the name of an index file");

  firstpagename := newpagename;
}
