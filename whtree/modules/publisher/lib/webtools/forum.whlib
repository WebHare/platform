<?wh
LOADLIB "wh::datetime.whlib";
LOADLIB "mod::publisher/lib/database.whlib";
LOADLIB "mod::publisher/lib/siteprofiles.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/mailer.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

STRING nsforumsettings := "http://www.webhare.net/xmlns/publisher/siteprofile";


STATIC OBJECTTYPE ForumObject
<
  INTEGER forumid;
  RECORD pvt_config;
  OBJECT forumplugin;
  OBJECT webcontext;
  DATETIME __closedate;

  PUBLIC PROPERTY config(this->forumplugin->config, -);
  PUBLIC PROPERTY id(forumid, -);
  PUBLIC PROPERTY closedate(__closedate, -);

  MACRO NEW(INTEGER forumid)
  {
    this->forumid := forumid;
    this->webcontext := GetWebContext(forumid); //TODO rpc PostComment might have already retrieved this! - share ?
    this->forumplugin := this->webcontext->GetPlugin("http://www.webhare.net/xmlns/publisher/siteprofile", "forumplugin");
    IF(NOT ObjectExists(this->forumplugin))
      THROW NEW Exception("No forum plugin registered for #" || forumid);

    IF(this->config.postsperpage <= 0)
      THROW NEW Exception(`Invalid maximum posts per page: ${this->config.postsperpage}`);

    this->LoadForumFromDB();
  }

  MACRO LoadForumFromDB()
  {
    OBJECT trans := GetPrimary();
    RECORD foruminfo := SELECT * FROM publisher.forums WHERE id = this->forumid;
    IF(RecordExists(foruminfo))
      this->__closedate := foruminfo.closedate;
    ELSE
      this->__closedate := this->GetInitialTopicCloseDate();
  }

  DATETIME FUNCTION CalculateInitialCloseDate(DATETIME startdate)
  {
    RETURN GetRoundedDatetime(AddDaysToDate(this->config.autoclose, startdate), 86400 * 1000); //FIXME keep forum's timezone
  }

  DATETIME FUNCTION GetInitialTopicCloseDate()
  {
    IF(this->config.autoclose = 0)
      RETURN DEFAULT DATETIME;

    //The topic never started, so get the objects creationdate
    DATETIME topicstart := SELECT AS DATETIME creationdate FROM system.fs_objects WHERE id = this->id;
    IF(topicstart = DEFAULT DATETIME)
      RETURN DEFAULT DATETIME;
    RETURN this->CalculateInitialCloseDate(topicstart);
  }

  PUBLIC MACRO SetCloseDate(DATETIME newclosedate)
  {
    IF(newclosedate = this->__closedate)
      RETURN;

    //ADDME how to guard against dupe thread creation
    GetPrimary()->PushWork();
    IF(RecordExists(SELECT FROM publisher.forums WHERE id = this->id))
      UPDATE publisher.forums SET closedate := newclosedate WHERE id = this->id;
    ELSE
      INSERT INTO publisher.forums(id,closedate) VALUES(this->id, newclosedate);

    this->__closedate := newclosedate;
    GetPrimary()->PopWork();
  }

  /** @short Add a post
      @long Add post to currenly selected topic */
  PUBLIC INTEGER FUNCTION AddPost(RECORD postdata)
  {
    postdata := ValidateOptions(CELL[ messagetype := "", message := "", source := "", name := "", email := "", posteruid := "", sourceurl := "" ],
                                postdata,
                                [ title := "comment post", required := ["message"]]);
    IF(postdata.messagetype != "text/plain")
      THROW NEW Exception("only supporting text/plain now");
    IF(Length(postdata.message) >= 4096)
      THROW NEW Exception("only supporting posting up to 4k now"); //FIXME make sure formapi enforces this length
    IF(this->closedate != DEFAULT DATETIME AND this->closedate < GetCurrentDatetime())
      THROW NEW Exception("This topic is already closed");

    //Either way, we have a selectedtopic now, and must add our post to it
    OBJECT trans := GetPrimary();
    DATETIME now := GetCurrentDatetime();
    trans->BeginLockedWork("publisher:forumposting-" || this->id);

    IF(NOT RecordExists(SELECT FROM publisher.forums WHERE id = this->id))
    {
      INSERT INTO publisher.forums(id, closedate)
             VALUES(this->id, this->GetInitialTopicCloseDate());
    }

    INTEGER postid := MakeAutonumber(publisher.forumpostings,"id");
    INSERT INTO publisher.forumpostings(id, forum, creationdate, modificationdate, content
                                      ,postersource, postername, posteremail, posteruid)
           VALUES(postid, this->id, now, now, postdata.message
                 ,postdata.source, postdata.name, postdata.email, postdata.posteruid);

    this->HandlePostAfterEffects(postdata);

    trans->CommitWork();
    RETURN postid;
  }

  MACRO HandlePostAfterEffects(RECORD postdata)
  {
    IF(this->config.mailpostingsto != "")
    {
      RECORD mailinfo := CELL[ postdata.name
                             , postdata.email
                             , postdata.message
                             , url := postdata.sourceurl
                             , sourceresource := ""
                             , sourcetitle := ""
                             ];

      OBJECT targetfile := OpenWHFSObject(this->id);
      IF(ObjectExists(targetfile))
      {
        mailinfo.sourceresource := targetfile->GetResourceName();
        mailinfo.sourcetitle := targetfile->title ?? targetfile->name;
      }

      //if we want to customize more (Eg allow user to specify witty), take inspiration from PrepareAuthPageMail
      STRING lang := GetTidLanguage();
      TRY
      {
        SetTidLanguage(this->webcontext->languagecode);
        OBJECT applytester := GetApplyTesterForObject(this->id);
        STRING mailtemplate := applytester->GetDefaultMailTemplate();
        OBJECT webcontext := __GetWebContextForApplyTester(applytester, this->id);
        OBJECT mail := PrepareMailWitty(`mod::publisher/witty/forumposting.html.witty:forumpostingmail`, CELL[ mailtemplate, webcontext ]);
        mail->mailto := TokenizeEmailAddresses(this->config.mailpostingsto);
        mail->mergerecord := mailinfo;
        mail->QueueMailInWork();
      }
      FINALLY
      {
        SetTidLanguage(lang);
      }
    }
  }


  PUBLIC INTEGER FUNCTION GetNumEntries()
  {
    RETURN LENGTH(SELECT FROM publisher.forumpostings WHERE forum = this->id AND NOT deleted);
  }

  PUBLIC INTEGER FUNCTION GetNumMessagePages()
  {
    RETURN (this->GetNumEntries() + this->config.postsperpage - 1) / this->config.postsperpage;
  }

  RECORD ARRAY FUNCTION GetMessages(INTEGER ARRAY postids, STRING userguid)
  {
    //Get the individual posts
    RECORD ARRAY posts := SELECT id
                               , creationdate
                               , modificationdate
                               , content
                               , postername
                               , posteremail
                               , candelete := userguid != "" AND userguid = posteruid
                            FROM publisher.forumpostings
                           WHERE id IN postids
                        ORDER BY SearchElement(postids,id);

    RETURN posts;
  }

  PUBLIC MACRO DeleteMessage(INTEGER msgid, STRING userguid)
  {
    IF(userguid="")
      RETURN;

    OBJECT trans := GetPrimary();
    trans->BeginWork();

    UPDATE publisher.forumpostings SET deleted := TRUE
                                 WHERE deleted = FALSE
                                       AND id = msgid
                                       AND posteruid = userguid;

    trans->CommitWork();
  }

  PUBLIC RECORD FUNCTION GetMessage(INTEGER msgid, STRING userguid)
  {
    RETURN RECORD(this->GetMessages([msgid],userguid));
  }

  PUBLIC RECORD ARRAY FUNCTION GetMessagesByPage(INTEGER pagenum, STRING userguid)
  {
    INTEGER numpages := this->GetNumMessagePages();
    IF(pagenum<0 OR pagenum >= numpages)
      RETURN DEFAULT RECORD ARRAY;

    //Grab all message ids in the topic, sorted properly
    INTEGER ARRAY postids := SELECT AS INTEGER ARRAY id
                               FROM publisher.forumpostings
                              WHERE forum = this->id
                                    AND NOT deleted
                           ORDER BY (this->config.newestfirst ? creationdate : DEFAULT DATETIME) DESC
                                  , creationdate;

    //Slice the page
    postids := ArraySlice(postids, pagenum * this->config.postsperpage, this->config.postsperpage);
    RETURN this->GetMessages(postids, userguid);
  }
>;

PUBLIC OBJECT FUNCTION OpenForum(INTEGER forumid, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions( [ verifycreationdate := DEFAULT DATETIME ], options);

  IF(GetPrimary()->IsWorkOpen())
    THROW NEW Exception("OpenForum doesn't (currently?) support being invoked with open work"); //ADDME can we get rid of this if we get rid of the singleton forum object creation?

  IF(options.verifycreationdate != DEFAULT DATETIME)
  {
    RECORD fileinfo := SELECT creationdate FROM system.fs_objects WHERE id = forumid;
    IF(NOT RecordExists(fileinfo) OR fileinfo.creationdate != options.verifycreationdate)
      THROW NEW Exception("Invalid forum reference");
  }

  RETURN NEW ForumObject(forumid);
}

PUBLIC OBJECT FUNCTION GetForumPluginForWebdesign(OBJECT webcontext)
{
  RETURN webcontext->GetPlugin("http://www.webhare.net/xmlns/publisher/siteprofile","forumplugin");
}
