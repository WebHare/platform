<?wh
/** @short Standard form components
    @topic forms/standardcomponents
*/

LOADLIB "mod::publisher/lib/forms/components.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";


PUBLIC OBJECTTYPE CaptchaField EXTEND FormFieldBase
<
  PUBLIC BOOLEAN value;

  MACRO NEW(OBJECT form, OBJECT parent, RECORD field)
  : FormFieldBase(form, parent, field)
  {
    form->AddFormAttributesCallback(PTR this->GetFormAttributes);
  }

  UPDATE PUBLIC BOOLEAN FUNCTION IsSet()
  {
    RETURN this->value;
  }

  UPDATE PUBLIC MACRO UpdateFromJS(VARIANT jsdata)
  {
    this->value := this->form->formcontext->VerifyCaptchaResponse(jsdata);
  }

  STRING FUNCTION GetFormAttributes()
  {
    IF(NOT this->IsFrontendVisible())
      RETURN "";

    RETURN `data-wh-form-captcha="${EncodeValue(this->field.name)}"`;
  }

  UPDATE PUBLIC MACRO RenderField()
  {
  }

  PUBLIC MACRO ValidateCaptcha(OBJECT work)
  {
    IF(NOT this->IsSet() //we're only set if the captcha challenge succeeded
       AND NOT this->form->formcontext->AllowToSkipCaptchaCheck())
    {
      STRING apikey := this->form->formcontext->GetCaptchaAPIKey();
      work->AddErrorFor(this, GetTid("publisher:site.forms.commonerrors.required"), [ metadata := CELL[ apikey ]]);
    }
  }
>;
