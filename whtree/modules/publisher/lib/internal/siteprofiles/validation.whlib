<?wh

LOADLIB "mod::publisher/lib/internal/siteprofiles/lookups.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/parser.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/tasks.whlib";
LOADLIB "mod::tollium/lib/internal/validation.whlib";

PUBLIC STATIC OBJECTTYPE SiteprofValidator EXTEND ScreensFileValidator
<
  MACRO EnsureNoFSInstance(OBJECT extension)
  {
    FOREVERY(OBJECT node FROM extension->ListElements("*","*"))
      IF(node->GetAttribute("composition") = "fsinstance")
      {
        this->AddWarning(node, `This node refers to composition="fsinstance", are you sure you didn't mean composition="contentdata" ?`);
        BREAK; //one warning of this type per extension is enough
      }
  }

  UPDATE PUBLIC MACRO ValidateDocument()
  {
    ScreensFileValidator::ValidateDocument();
    IF(this->respath LIKE "mod*")
    {
      FOREVERY(OBJECT node FROM this->xml->GetElementsByTagNameNS("http://www.webhare.net/xmlns/system/moduledefinition","services")->GetCurrentElements())
        this->AddError(node, "Module siteprofiles should not contain service definitions - move your services to the module");
    }

    FOREVERY(OBJECT widgettype FROM this->xml->ListElements("http://www.webhare.net/xmlns/publisher/siteprofile","widgettype"))
    {
      OBJECT extension := this->VerifyScreenReferenceExistence(widgettype->linenum, widgettype->GetAttribute("editextension"), "tabsextension");
      //extra sanity check, composition name=fsinstance is always a mistake?
      IF(ObjectExists(extension))
        this->EnsureNoFSInstance(extension);

      IF(widgettype->HasAttribute("wittycomponent"))
        this->VerifyWittyComponentExistence(widgettype->linenum, widgettype->GetAttribute("wittycomponent"));
    }

    RECORD parseres := ParseSiteProfile(this->respath, [ xmldoc := this->xml ]); //ADDME forwards XSD?
    this->errors := this->errors CONCAT SELECT code := 0, col, line, message, resourcename FROM parseres.value.errors;
    this->warnings := this->warnings CONCAT SELECT code := 0, col, line, message, resourcename FROM parseres.value.warnings;
    this->hints := this->hints CONCAT SELECT code := 0, col, line, message, resourcename FROM parseres.value.hints;
    this->icons := this->icons CONCAT SELECT col, line, icon, resourcename FROM parseres.value.icons;

    FOREVERY(RECORD type FROM parseres.value.contenttypes)
    {
      IF(RecordExists(type.filetype) AND type.filetype.searchcontentprovider != "")
      {
        TRY GetSearchContentProvider(type.filetype.searchcontentprovider);
        CATCH (OBJECT e) this->AddErrorForLine(type.line, e->what);
      }
    }

    FOREVERY(RECORD apply FROM parseres.value.rules)
    {
      IF(CellExists(apply,"setobjecteditor") AND RecordExists(apply.setobjecteditor))
      {
        IF(apply.setobjecteditor.screen != "")
          this->VerifyScreenReferenceExistence(apply.line, apply.setobjecteditor.screen, "screen");
      }

      IF(CellExists(apply,"schedulemanagedtasks"))
        FOREVERY(RECORD task FROM apply.schedulemanagedtasks)
        {
          TRY GetTaskTypeSettings(task.task, FALSE);
          CATCH (OBJECT e) this->AddErrorForLine(apply.line, e->what);
        }

      IF(CellExists(apply,"usepublishtemplate") AND RecordExists(apply.usepublishtemplate) AND apply.usepublishtemplate.script != "")
        IF(NOT RecordExists(RetrieveWebHareResource(apply.usepublishtemplate.script, [ allowmissing := TRUE ])))
          this->AddErrorForLine(apply.line, `No such file: ${apply.usepublishtemplate.script}`);
    }
  }
>;
