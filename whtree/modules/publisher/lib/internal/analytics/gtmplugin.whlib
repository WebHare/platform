<?wh
LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::socialite/lib/internal/embedscripts.whlib";

PUBLIC STATIC OBJECTTYPE GTMPlugin EXTEND WebDesignPluginBase
<
  RECORD ARRAY datalayerpushes;
  OBJECT webdesign;
  STRING pvt_account;
  STRING pvt_integration;
  STRING pvt_launch;
  STRING pvt_script;
  STRING pvt_pixel;

  PUBLIC PROPERTY account(pvt_account, SetAccount);
  PUBLIC PROPERTY integration(pvt_integration, SetIntegration);
  PUBLIC PROPERTY launch(pvt_launch, SetLaunch);
  PUBLIC PROPERTY script(pvt_script, SetScript);
  PUBLIC PROPERTY pixel(pvt_pixel, SetPixel);

  UPDATE PUBLIC RECORD FUNCTION ParseConfigurationNode(OBJECT siteprofile, OBJECT node)
  {
    RETURN [ account := node->GetAttribute("account")
           , integration := node->GetAttribute("integration") ?? "script"
           , launch := node->GetAttribute("launch") ?? "pagerender"
           , script := node->GetAttribute("script")
           , pixel := node->GetAttribute("pixel")
           ];
  }

  MACRO ValidateCode(STRING code)
  {
    IF(code != "")
      ValidateGTMCode(code);
  }

  UPDATE PUBLIC MACRO ConfigurePlugin(OBJECT webdesign, RECORD config)
  {
    this->webdesign := webdesign;

    this->pvt_account := config.account;
    this->pvt_integration := config.integration;
    this->pvt_launch := config.launch;
    IF(CellExists(config,'script')) //5.5/5.6: in case we *just* upgraded and still need to recompile CSP
    {
      this->pvt_script := config.script;
      this->pvt_pixel := config.pixel;
    }
  }

  UPDATE PUBLIC MACRO DatalayerPush(RECORD instruction)
  {
    INSERT instruction INTO this->datalayerpushes AT END;
  }

  MACRO PrintDatalayerPushes()
  {
    IF(Length(this->datalayerpushes) = 0)
      RETURN;

    STRING pushes := EncodeJSON(this->datalayerpushes);

    IF(this->pvt_integration = "script" AND this->pvt_launch="pagerender")
    {
      pushes := Substring(pushes, 1, Length(pushes)-2); //Remove the [ and ] wrapping the array
      Print(`<script>(window.dataLayer||[]).push(${pushes})</script>`);
    }
    ELSE //yep, we can legally do this: https://html.spec.whatwg.org/multipage/custom-elements.html#custom-elements
    {
      Print(`<wh-socialite-gtm push="${EncodeValue(pushes)}"></wh-socialite-gtm>`);
    }
  }

  MACRO SetupPluginConfig()
  {
    RECORD settings;
    IF((this->pvt_integration != "script" OR this->pvt_launch != "pagerender") AND this->pvt_account != "")
    {
      settings := [ a := this->pvt_account, h := this->pvt_integration = "selfhosted", m := this->pvt_launch = "manual" ];
      IF(this->pvt_script != "")
        INSERT CELL s := this->pvt_script INTO settings;
    }
    this->webdesign->SetFrontendData("socialite:gtm", settings);
  }
  MACRO SetAccount(STRING newaccount)
  {
    IF(this->webdesign->startedrendering)
      THROW NEW Exception("The GTM account cannot be modified when the page has started rendering - update should have been done in the prepare phase");

    this->pvt_account := newaccount;
    this->SetupPluginConfig();
  }
  MACRO SetIntegration(STRING newintegration)
  {
    IF(this->webdesign->startedrendering)
      THROW NEW Exception("The GTM integration cannot be modified when the page has started rendering - update should have been done in the prepare phase");

    this->pvt_integration := newintegration;
    this->SetupPluginConfig();
  }
  MACRO SetLaunch(STRING newlaunch)
  {
    IF(this->webdesign->startedrendering)
      THROW NEW Exception("The GTM integration cannot be modified when the page has started rendering - update should have been done in the prepare phase");

    this->pvt_launch := newlaunch;
    this->SetupPluginConfig();
  }
  MACRO SetScript(STRING newscript)
  {
    IF(this->webdesign->startedrendering)
      THROW NEW Exception("The GTM integration cannot be modified when the page has started rendering - update should have been done in the prepare phase");

    this->pvt_script := newscript;
    this->SetupPluginConfig();
  }
  MACRO SetPixel(STRING newpixel)
  {
    IF(this->webdesign->startedrendering)
      THROW NEW Exception("The GTM integration cannot be modified when the page has started rendering - update should have been done in the prepare phase");

    this->pvt_pixel := newpixel;
    this->SetupPluginConfig();
  }

  UPDATE PUBLIC MACRO PrepareForRendering(OBJECT webdesign)
  {
    this->SetupPluginConfig();

    /* https://support.google.com/tagmanager/answer/6103696?hl=en
       - the script part as high as possible in the head, the noscript as high as possible in the body
       */
    webdesign->InsertWithCallback(PTR this->DoTagManager_Head(webdesign), "dependencies-top");
    webdesign->InsertWithCallback(PTR this->DoTagManager_Body(webdesign), "body-top");
    webdesign->InsertWithCallback(PTR this->PrintDatalayerPushes, "body-bottom");
  }

  MACRO DoTagManager_Head(OBJECT webdesign)
  {
    IF(this->pvt_account = "" OR this->pvt_integration != "script" OR this->pvt_launch != "pagerender" OR webdesign->ispreviewpage)
      RETURN;

    BOOLEAN compact := webdesign->wittyencoding LIKE "*-NI";
    STRING script := this->pvt_script ?? "//www.googletagmanager.com/gtm.js";
    STRING code := "<script>";
    code := code || `(function(w,d,s,l,i){w[l]=w[l]||[];w[l].push({'gtm.start':new Date().getTime(),event:'gtm.js'});var f=d.getElementsByTagName(s)[0],j=d.createElement(s),dl=l!='dataLayer'?'&l='+l:'';j.async=true;j.src='${EncodeJava(script)}?id='+i+dl;f.parentNode.insertBefore(j,f);})(window,document,'script','dataLayer','${EncodeJava(this->pvt_account)}');`;
    code := code || "</script>" || (compact?'':'\n');
    Print(code);
  }
  MACRO DoTagManager_Body(OBJECT webdesign)
  {
    IF(this->pvt_account = "" OR this->pvt_launch != "pagerender" OR webdesign->ispreviewpage)
      RETURN;

    //The noscript code is probably always useful. no need to intercept it
    BOOLEAN compact := webdesign->wittyencoding LIKE "*-NI";
    STRING pixel := this->pvt_pixel ?? "//www.googletagmanager.com/ns.html";
    STRING code := `<noscript><iframe src="${EncodeJava(pixel)}?id=${EncodeURL(this->pvt_account)}" height="0" width="0" style="display:none;visibility:hidden"></iframe></noscript>` || (compact?'':'\n');
    Print(code);
  }
>;
