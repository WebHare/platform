<?wh

LOADLIB "wh::util/algorithms.whlib";

LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/internal/modules/defreader.whlib";

LOADLIB "mod::publisher/lib/internal/versioning/policybase.whlib";


PUBLIC OBJECT FUNCTION OpenVersioningPolicy(STRING versioningpolicy)
{
  //Versioningpolicy is a module:name identifier.
  STRING modulename := Left(versioningpolicy, SearchSubstring(versioningpolicy, ':'));
  STRING policyname := Substring(versioningpolicy, Length(modulename) + 1);

  RECORD moduleinfo := GetWebhareModuleInfo(modulename);
  IF(RecordExists(moduleinfo))
    FOREVERY(RECORD policy FROM moduleinfo.versioningpolicies)
      IF(policy.name = policyname)
      {
        OBJECT policyobject := MakeObject(policy.objectname);
        IF(NOT (policyobject EXTENDSFROM VersioningPolicyBase))
          THROW NEW Exception(`The policyobject '${policy.objectname}' must extend from VersioningPolicyBase`);
        RETURN policyobject;
      }
  THROW NEW Exception("Undefined versioning policy '" || versioningpolicy || "'");
}

PUBLIC STRING ARRAY FUNCTION ListVersioningPolicies()
{
  STRING ARRAY results;
  FOREVERY (STRING modulename FROM GetInstalledModuleNames())
  {
    RECORD moduleinfo := GetWebhareModuleInfo(modulename);
    IF(RecordExists(moduleinfo))
      FOREVERY(RECORD policy FROM moduleinfo.versioningpolicies)
        INSERT `${modulename}:${policy.name}` INTO results AT END;
  }

  RETURN GetSortedSet(results);
}
