<?wh
LOADLIB "mod::publisher/lib/internal/forms/opener.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/reader.whlib";
LOADLIB "mod::publisher/lib/internal/rtd/publishable.whlib";
LOADLIB "mod::publisher/lib/siteapi.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/structuredef.whlib";
LOADLIB "mod::publisher/lib/internal/siteprofiles/widgets.whlib";

LOADLIB "mod::system/lib/cluster.whlib";
LOADLIB "mod::system/lib/resources.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

PUBLIC STATIC OBJECTTYPE PreviewContext
<
  OBJECT fsobject;
  OBJECT applytester;
  RECORD rtdtypeinfo;
  STRING ARRAY indextypes;
  STRING ARRAY indexversions;
  PUBLIC STRING languagecode;
  PUBLIC STRING imgroot;

  //This preview context is running for search previews
  PUBLIC BOOLEAN __previewsearch;
  //The folder containing the targetobject, if targetobject is a file. Otherwise, the target object
  PUBLIC PROPERTY targetfolder(GetTargetFolder, -);
  //The site containing the targetobject;
  PUBLIC PROPERTY targetsite(GetTargetSite, -);
  //NOTE! Not exposing fsobject, not sure if we'll always have this in the future (think 'new file' without reserving an on-disk id)
  //The apply tester for the targetobject
  PUBLIC PROPERTY targetapplytester(applytester, -);
  PUBLIC OBJECT __mergefields;

  MACRO NEW(OBJECT fsobject, OBJECT applytester, RECORD rtdtypeinfo, BOOLEAN forsearch)
  {
    this->fsobject := fsobject;
    this->applytester := applytester;
    this->rtdtypeinfo := rtdtypeinfo;

    this->languagecode := ObjectExists(applytester) ? applytester->GetSiteLanguage() : "en";
    this->imgroot := ObjectExists(applytester) ? GetModuleResourceURL(applytester->GetWebDesignObjinfo().designfolder || "web/img/") : "";

    this->__previewsearch := forsearch;
  }

  /** Is this a RTD preview? The preview context always returns true, webdesign returns false */
  PUBLIC BOOLEAN FUNCTION IsRTDPreview()
  {
    RETURN TRUE;
  }

  /** Return gathered indexinformation for widgets and other content generators */
  PUBLIC RECORD FUNCTION __GetIndexInfo()
  {
    RETURN CELL [ this->indextypes, this->indexversions ];
  }

  /** Register indexinformation for widgets and other content generators */
  PUBLIC MACRO AddIndexInfo(STRING indextype, STRING indexversion)
  {
    IF(indextype != "" AND indextype NOT IN this->indextypes)
      INSERT indextype INTO this->indextypes AT END;
    IF(indexversion != "" AND indexversion NOT IN this->indexversions)
      INSERT indexversion INTO this->indexversions AT END;
  }

  /** @short Open RTD data using an rtdtype
      @signature OBJECT FUNCTION OpenRTD(RECORD richdoc, RECORD options DEFAULTSTO DEFAULT RECORD)
      @param richdoc The rich document
      @cell(string) options.rtdtype Path to structure to use. If not set, the parent RTD type is used
      @return(object %RichDocument) The requested document */
  PUBLIC OBJECT FUNCTION OpenRTD(RECORD richdoc, VARIANT options DEFAULTSTO DEFAULT RECORD)
  {
    IF(TypeID(options) = TypeID(STRING))
      options := [ rtdtype := options ];
    options := ValidateOptions([ rtdtype := "" ], options);

    STRING rtdtype := options.rtdtype;
    RECORD typeinfo := this->rtdtypeinfo;
    IF(rtdtype!="") //override
    {
      typeinfo := GetRTDType(rtdtype);
      IF(NOT RecordExists(typeinfo))
        THROW NEW Exception("No such RTDType registered: " || rtdtype);
    }

    OBJECT rtd := NEW PublishableRichDocument;
    rtd->ImportFromRecord(richdoc);
    rtd->pvt_baseobject := this->fsobject;
    rtd->pvt_applytester := this->applytester;
    rtd->previewcontext := this;
/* ADDME ?
    rtd->webdesign := this;
    rtd->pvt_baseobject := this->targetobject;
    rtd->pvt_applytester := this->pvt_targetapplytester;
*/
    IF(RecordExists(typeinfo))
      rtd->ApplyRTDType(typeinfo);
    RETURN rtd;
  }

  /** @short Open and render a RTD */
  PUBLIC MACRO RenderRTD(RECORD richdoc, VARIANT options DEFAULTSTO DEFAULT RECORD)
  {
    IF(NOT RecordExists(richdoc))
      RETURN;

    OBJECT torender := this->OpenRTD(richdoc, options);
    torender->RenderAllObjects();
  }

  /** @short Return a renderer for a RTD from an instance record
      @param richdoc The rich document
      @cell(string) options.rtdtype Path to structure to use. If not set, the parent RTD type is used
      @return A Function PTR rendering the RTD, or a DEFAULT MACRO PTR if the RTD was empty */
  PUBLIC MACRO PTR FUNCTION GetRTDBody(RECORD richdoc, RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    options := ValidateOptions([ rtdtype := "" ], options);
    IF(NOT RecordExists(richdoc))
      RETURN DEFAULT MACRO PTR;

    OBJECT rtd := this->OpenRTD(richdoc, options);
    RETURN PTR rtd->RenderAllObjects;
  }

  OBJECT FUNCTION GetTargetSite()
  {
    RETURN ObjectExists(this->targetapplytester) ? OpenSite(this->targetapplytester->objsite) : DEFAULT OBJECT;
  }
  OBJECT FUNCTION GetTargetFolder()
  {
    RETURN ObjectExists(this->targetapplytester) ? OpenWHFSObject(this->targetapplytester->objparent) : DEFAULT OBJECT;
  }

  PUBLIC RECORD FUNCTION GetWittyDataForForm(STRING formname)
  {
    RETURN DEFAULT RECORD; //just a stub for now...
  }
  PUBLIC OBJECT FUNCTION OpenForm(STRING formname)
  {
    RETURN OpenFormWithContext(this, formname);
  }
  PUBLIC MACRO AddPublicationWarning(STRING text)
  {
    OBJECT targetfolder := this->targetfolder;
    LogWarning("publisher:addpublicationwarning", text, [ stacktrace := GetStackTrace()
                                                        , targetfolder := ObjectExists(targetfolder) ? targetfolder->whfspath : ""
                                                        ]);
  }

  /** @short Render a widget directly
      @param widgetdata Widget instance data. If unset, renders nothing
      @cell(string) options.whfstype WHFS Type of the widget to render */
  PUBLIC MACRO RenderWidgetInstance(RECORD widgetdata, RECORD options DEFAULTSTO DEFAULT RECORD)
  {
    //TODO Merge both RenderWidgetInstance implemntations, they mostly share..
    options := ValidateOptions( [ whfstype := "" ], options);

    IF(NOT RecordExists(widgetdata) AND options.whfstype = "") //accepting default records simplifies usage (similar to WrapCachedImage)
      RETURN;

    IF(options.whfstype != "")
    {
      DELETE CELL whfstype FROM widgetdata;
      INSERT CELL whfstype := options.whfstype INTO widgetdata;
    }
    ELSE
    {
      IF(NOT CellExists(widgetdata,'whfstype'))
        THROW NEW Exception("The passed object does not appear to be a widget instance (did not find whfstype cell, and it wasn't explicitly specified)");
    }

    IF(NOT CellExists(widgetdata,'whfssettingid'))
      INSERT CELL whfssettingid := 0 INTO widgetdata;
    IF(NOT CellExists(widgetdata,'whfsfileid'))
      INSERT CELL whfsfileid := 0 INTO widgetdata;

    RECORD rtdinfo := GetRTDTypeSettings(this->targetapplytester); //TODO don't we already have this?
    IF(NOT RecordExists(rtdinfo))
      THROW NEW Exception("No rtdtype setup for the current object");

    OBJECT embobj := GetEmbeddedObjectRenderer(DEFAULT OBJECT, this->targetapplytester, rtdinfo.rtdtype, widgetdata, this).widget;
    embobj->Render();
  }
>;
