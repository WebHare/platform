<?wh

LOADLIB "wh::crypto.whlib";
LOADLIB "wh::datetime.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

PUBLIC RECORD FUNCTION GetSlotInfo(INTEGER slotid)
{
  RECORD slotinfo := SELECT id, name, creationdate
                       FROM system.fs_objects
                      WHERE id = slotid;
  IF (NOT RecordExists(slotinfo))
    RETURN DEFAULT RECORD;

  RECORD ARRAY widgets :=
      SELECT id, name, modificationdate, ordering
        FROM system.fs_objects
       WHERE parent = slotid
    ORDER BY ordering, id;

  widgets := OpenWHFSType("http://www.webhare.net/xmlns/publisher/contentlibraries/slottedwidget")->Enrich(widgets, "id", [ "condition" ]);
  RETURN CELL[ ...slotinfo
             , widgets
             , lastmodifiedwidget := (SELECT AS DATETIME MAX(modificationdate) FROM widgets) ?? slotinfo.creationdate
             ];
}


PUBLIC STRING FUNCTION GetSlotGuid(RECORD slotinfo)
{
  RETURN ToLowercase(EncodeBase16(GetMD5Hash(`${slotinfo.id}|${FormatISO8601DateTime(slotinfo.creationdate)}`)));
}

PUBLIC INTEGER ARRAY FUNCTION GetBeaconReferences(INTEGER beaconid)
{
  RECORD beaconmember := SELECT * FROM OpenWHFSType("http://www.webhare.net/xmlns/publisher/widgets/triggerbeacon")->members WHERE name = "beacon";
  // Return all fsobjects that have a richvalue with a triggerbeacon instance
  RETURN
      SELECT AS INTEGER ARRAY DISTINCT fs_objects.id
        FROM system.fs_settings
           , system.fs_instances
           , system.fs_objects
       WHERE fs_settings.fs_member = beaconmember.id
             AND fs_settings.fs_object = beaconid
             AND fs_settings.fs_instance = fs_instances.id
             AND fs_instances.fs_object = fs_objects.id
             AND fs_objects.isactive;
}

PUBLIC INTEGER ARRAY FUNCTION GetSlotReferences(INTEGER slotid)
{
  // Return all fsobjects that have an instance referencing this slot
  //FIXME: This may return fsobjects referencing the slot by another whfsref, but this will do for now, maybe after connect
  //       is merged into webhare a new 'slotref' member type can be introduced?
  RETURN
      SELECT AS INTEGER ARRAY DISTINCT fs_objects.id
        FROM system.fs_settings
           , system.fs_instances
           , system.fs_objects
       WHERE fs_settings.fs_object = slotid
             AND fs_settings.fs_instance = fs_instances.id
             AND fs_instances.fs_object = fs_objects.id
             AND fs_objects.isactive;

}

