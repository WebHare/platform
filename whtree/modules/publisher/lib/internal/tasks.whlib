<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::graphics/canvas.whlib";

LOADLIB "mod::publisher/lib/control.whlib";
LOADLIB "mod::publisher/lib/publisher.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/printer.whlib";
LOADLIB "mod::system/lib/tasks.whlib";
LOADLIB "mod::system/lib/whfs.whlib";

PUBLIC STATIC OBJECTTYPE RepublishAssetpackUsersTask EXTEND ManagedTaskBase
<
  UPDATE PUBLIC MACRO RunTask(RECORD updateinfo)
  {
    STRING assetpack := updateinfo.assetpack;
    OBJECT sitesettingstype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/sitesettings");
    RECORD ARRAY associatedsites := SELECT id FROM system.sites;
    associatedsites := sitesettingstype->Enrich(associatedsites, "ID", ["SITEDESIGN"]);
    associatedsites := SELECT * FROM associatedsites WHERE sitedesign = VAR assetpack;
    FOREVERY(RECORD site FROM associatedsites)
      ScheduleFolderRepublish(site.id, TRUE);

    this->ResolveByCompletion([ siteids := SELECT AS INTEGER ARRAY id FROM associatedsites ]);
  }
>;

RECORD FUNCTION GenerateBrowserScreenshot(STRING url, RECORD options DEFAULTSTO CELL[]) {
  options := ValidateOptions([ withalpha := FALSE
                             , screenwidth := 1920
                             , screenheight := 1080
                             ], options);


  STRING res := CallJS("mod::platform/js/services/puppeteer.ts#generateBrowserScreenshot", url, options);
  RETURN [ screenshot := WrapBlob(StringToBlob(DecodeBase64(res)), "screenshot.png")
         ];
}

PUBLIC STATIC OBJECTTYPE GenerateWidgetPreviewTask EXTEND ManagedTaskBase //publisher:generatewidgetpreview
<
  UPDATE PUBLIC MACRO RunTask(RECORD taskdata)
  {
    OBJECT previewtype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/previewinfo");

    RECORD widget :=
        SELECT fs_objects.id
             , modificationdate
             , type
             , generatepreview
          FROM system.fs_objects
             , system.fs_types
         WHERE fs_objects.id = taskdata.id
           AND fs_types.id = type;

    IF (NOT RecordExists(widget))
    {
      this->ResolveByCompletion([ status := "nosuchfile"
                                ]);
      RETURN;
    }
    IF (NOT widget.generatepreview)
    {
      previewtype->SetInstanceData(widget.id, DEFAULT RECORD);
      this->ResolveByCompletion([ status := "notpreviewable"
                                ]);
      RETURN;
    }

    STRING link := GetPreviewLink(widget.id);
    IF(link="")
    {
      this->ResolveByCompletion([ status := "nourl"
                                ]);
      RETURN;
    }

    RECORD screenshotdata := GenerateBrowserScreenshot(link, [ withalpha := TRUE ]);
    OBJECT canvas := CreateCanvasFromBlob(screenshotdata.screenshot.data);
    RECORD bbox := canvas->GetBoundingBox();
    RECORD previewimage;

    IF(bbox.width = screenshotdata.screenshot.width AND bbox.height = screenshotdata.screenshot.width)
    {
      previewimage := screenshotdata.screenshot;
    }
    ELSE IF(bbox.width > 0 AND bbox.height > 0)
    {
      OBJECT clippedcanvas := canvas->CreateCroppedCanvas(bbox.x, bbox.y, bbox.x + bbox.width, bbox.y + bbox.height);
      previewimage := WrapBlob(clippedcanvas->ExportAsPNG(FALSE), "croppedpreview.png");
      clippedcanvas->Close();
    }
    canvas->Close();

    previewtype->SetInstanceData(widget.id,
        [ previewmodificationdate :=  widget.modificationdate
        , previewimage :=             previewimage
        ]);

    this->ResolveByCompletion([ status := "ok"
                              ]);
  }
>;

PUBLIC MACRO ExecuteLifecycleCleanup()
{
  OBJECT lifecycletype := OpenWHFSType("http://www.webhare.net/xmlns/publisher/lifecycle");

  RECORD ARRAY instances := lifecycletype->ListAllInstances();
  instances := SELECT * FROM instances WHERE fsobject != 0;
  instances := lifecycletype->Enrich(instances, "FSOBJECT", ["DELETION"]);
  instances := SELECT * FROM instances WHERE deletion != DEFAULT DATETIME AND deletion < GetCurrentDatetime();

  FOREVERY(RECORD instance FROM instances)
  {
    OBJECT todelete := OpenWHFSObject(instance.fsobject);
    //FIXME postpone to on-succesful-commit
    LogAuditEvent("publisher:lifecycle", [ objectid := instance.fsobject
                                         , type := "DELETE"
                                         , whfspath := todelete->whfspath
                                         , planneddeletion := instance.deletion
                                         ]);
    todelete->DeleteSelf();
  }
}
