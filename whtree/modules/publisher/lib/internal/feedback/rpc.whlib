<?wh


LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";
LOADLIB "mod::publisher/lib/internal/feedback/feedbacksupport.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";


PUBLIC RECORD FUNCTION RPC_StoreFeedback(STRING pathname, RECORD data)
{
  data := EnforceStructure(
      [ version := 0
      , screenshot :=
        [ htmlattrs := [ [ name := "", value := "" ] ]
        , stylesheets := STRING[]
        , bodyattrs := [ [ name := "", value := "" ] ]
        , bodyContents := ""
        ]
      , size :=
        [ width := 0
        , height := 0
        ]
      , element :=
        [ top := 0
        , left := 0
        , width := 0
        , height := 0
        ]
      , browser := ""
      , device := ""
      , useragent := ""
      , extradata := DEFAULT RECORD
      , token := ""
      , url := ""
      ], data);

  //Do not allow improper cells to be set through RPC
  data := PickCells(data, ["VERSION","SCREENSHOT","SIZE","ELEMENT","BROWSER","DEVICE","USERAGENT","extradata","TOKEN","url"]);

  INTEGER targetfile := LookupPublisherURL(data.url).file;
  IF(targetfile != 0)
    INSERT CELL whfsobject := targetfile
              , targetcreationdate := (SELECT AS DATETIME creationdate FROM system.fs_objects WHERE id = targetfile)
           INTO data;

  RECORD userdata := VerifyFeedbackWebToken(data.token);
  DELETE CELL token FROM data;
  IF (NOT RecordExists(userdata))
  {
    TRY
    {
      OBJECT authplugin := GetWRDAuthPlugin(GetWebOriginURL(pathname));
      IF (ObjectExists(authplugin->userapi))
        userdata := authplugin->userapi->GetUser(authplugin->GetLoggedInEntity())->GetUserDataForLogging();
    }
    CATCH(OBJECT e)
    {
      LogHarescriptException(e);
    }
  }

  GetPrimary()->BeginWork();
  TRY
  {
    RECORD result := StoreFeedback(CELL[ ...data, userdata ]);
    RETURN CELL
        [ success := TRUE
        , ...result
        ];
  }
  // Use FINALLY here to catch the exception, so we can safely rollback or commit in case of error resp. success (using CATCH
  // to catch the exception 'eats' the exception)
  FINALLY (OBJECT e)
  {
    IF (ObjectExists(e))
    {
      GetPrimary()->RollbackWork();
      RETURN
          [ success := FALSE
          , error := e->what
          ];
    }
    ELSE
      GetPrimary()->CommitWork();
  }
}
