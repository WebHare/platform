<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::witty.whlib";
LOADLIB "wh::internet/jwt.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/internal/resourcemanager.whlib";
LOADLIB "mod::system/lib/internal/webhareconstants.whlib";
LOADLIB "mod::system/lib/internal/cluster/secrets.whlib";
LOADLIB "mod::system/lib/services.whlib";

LOADLIB "mod::wrd/lib/api.whlib";


PUBLIC CONSTANT STRING tollium_feedback_scope := "tollium:webharebackend";

PUBLIC OBJECT FUNCTION GetScreenshotWitty()
{
  RETURN LoadWittyLibrary(Resolve("screenshot.witty"), "HTML-NI");
}

PUBLIC STRING FUNCTION GetFeedbackScreenshotURL(INTEGER issueid)
{
  RETURN GetPrimaryWebhareInterfaceURL() || ".publisher/common/feedback/screenshot.shtml?ss=" || EncryptForThisServer("tollium:screenshot",CELL[issueid]);
}

PUBLIC RECORD ARRAY FUNCTION GetFeedbackExtraFields(RECORD extradata)
{
  RECORD ARRAY fields;

  // The 'extrafields' cell contains field definitions to show extra data
  IF (NOT (CellExists(extradata, "extrafields") AND TypeID(extradata.extrafields) = TypeID(RECORD ARRAY)))
    RETURN fields;

  FOREVERY (RECORD extra FROM extradata.extrafields)
  {
    extra := EnforceStructure(
        [ field := ""
        , type := ""
        , title := ""
        , tid := ""
        , optiongid := ""
        ], extra);

    IF (NOT CellExists(extradata, extra.field))
      CONTINUE;

    STRING type, value;
    SWITCH (extra.type)
    {
      CASE "string"
      {
        type := "text";
        value := GetCell(extradata, extra.field);
      }
      CASE "integer"
      {
        type := "text";
        value := ToString(GetCell(extradata, extra.field));
      }
      CASE "tid"
      {
        type := "text";
        STRING tid := GetCell(extradata, extra.field);

        // Try to resolve the tid if this is not an absolute tid reference
        IF (tid NOT LIKE "*:*")
        {
          IF (extra.optiongid != "")
            tid := extra.optiongid || "." || tid;
          ELSE
            tid := extra.tid || "-" || tid;
        }
        IF (tid LIKE "*:*")
          value := GetTid(tid);
        ELSE
          value := tid;
      }
      DEFAULT
      {
        CONTINUE; //not understood..
      }
    }

    INSERT CELL[ name := extra.field
               , title := extra.tid = "" ? extra.title : GetTid(extra.tid)
               , type
               , value
               ] INTO fields AT END;
  }
  RETURN fields;
}

/** @short Store feedback
    @param data The feedback data
    @cell(integer) data.version The feedback data's version, currently 1 (can be used by the Feedback application to determine which
        fields are available)
    @cell(record) data.screenshot The screenshot data
    @cell(record array) data.screenshot.htmlattrs The html node's attributes
    @cell(string) data.screenshot.htmlattrs.name Attribute name
    @cell(string) data.screenshot.htmlattrs.value Attribute value
    @cell(string array) data.screenshot.stylesheets The contents of each of the document's stylesheets
    @cell(record array) data.screenshot.bodyattrs The body node's attributes
    @cell(string) data.screenshot.bodyattrs.name Attribute name
    @cell(string) data.screenshot.bodyattrs.value Attribute value
    @cell(string) data.screenshot.bodycontents Basically the innerHTML of the body node
    @cell(record) data.size Browser viewport (inner window) size
    @cell(integer) data.size.width Viewport width
    @cell(integer) data.size.height Viewport height
    @cell(record) data.element The selected element's bounding box
    @cell(integer) data.element.top Top position
    @cell(integer) data.element.left Left position
    @cell(integer) data.element.width Width
    @cell(integer) data.element.height Height
    @cell(string) data.browser Browser triplet
    @cell(string) data.device Device (e.g. "desktop", "tablet", "phone")
    @cell(string) data.useragent The browser's User-Agent string
    @cell(record) data.extradata Additional data to store with the feedback
    @cell(record array) data.extradata.extrafields Supply this field to display extra data in the Feedback application
    @cell(string) data.extradata.extrafields.field The cell name within the extradata record
    @cell(string) data.extradata.extrafields.type The value type, one of "string", "integer" or "tid" (if "tid" is specified,
        the value is treated as a tid, which can be an absolute tid or may be resolved relative to this field's tid or to
        optiongid, if supplied)
    @cell(string) data.title The field title
    @cell(string) data.tid The field tid (if a title is supplied, the tid is ignored)
    @cell(string) data.optiongid Can be supplied for fields of type "tid" to resolve tid values
    @cell(string) return.guid The stored feedback's GUID, which can be used to store extra data using %StoreExtraData
    @cell(record array) return.topics The available topics
    @cell(string) return.topics.tag Topic tag (the topic value to submit to %StoreFeedbackExtraData)
    @cell(string) return.topics.title Topic title
*/
PUBLIC RECORD FUNCTION StoreFeedback(RECORD data)
{
  OBJECT wrdschema := OpenWRDSchema("publisher:feedback");

  //TODO commit hash, branch?
  RECORD ARRAY appversions := [[ name := "webhare", version := GetWebhareVersionInfo().version ]]
                              CONCAT
                              SELECT name, version
                                FROM GetWebhareModules()
                               WHERE version != ""
                                     AND NOT excludefromfeedbackversions
                                     AND name NOT IN whconstant_builtinmodules
                            ORDER BY name;

  data := CELL[ ...data
              , wrd_leftentity := wrdschema->^scope->Search("wrd_tag", ToUppercase(tollium_feedback_scope))//FIXME: scope is no longer used, but wrd_leftentity is required
              , feedbackid := GetWRDSequenceNext(wrdschema->id, "publisher:feedbackid", [ mutex := "publisher:feedbackid"
                                                                                        , nrformat := "FB%04n"
                                                                                        ])
              , appversions
              , status := "creating"
              ];

  OBJECT entity := wrdschema->^feedback->CreateEntity(data);
  RETURN CELL
      [ entity->guid
        //TODO: For now we'll just return all available topics, these may be restricted by webdesign and/or reporting rights
      , topics :=
          (SELECT tag := wrd_tag ?? wrd_guid
                , title
             FROM wrdschema->^topic->RunQuery([ outputcolumns := CELL[ "wrd_tag", "wrd_guid", title := "wrd_title", "wrd_ordering","wrd_id" ] ])
            ORDER BY wrd_ordering
                   , ToUppercase(title)
                   , wrd_id)
      ];
}

/** @short Store additional data with existing feedback (new extradata is merged with existing extradata)
    @param guid The feedback's GUID, as returned by %StoreFeedback
    @cell(string) settings.topic The topic to store the feedback for
    @cell(string) settings.remarks User's remarks
    @cell(record) settings.extradata @includecelldef #StoreFeedback.data.extradata
    @return The feedback id
*/
PUBLIC STRING FUNCTION StoreFeedbackExtraData(STRING guid, RECORD settings)
{
  settings := ValidateOptions([ topic := ""
                              , remarks := ""
                              , extradata := DEFAULT RECORD
                              ], settings, [ discard := [ "type" ] ]);

  OBJECT wrdschema := OpenWRDSchema("publisher:feedback");
  INTEGER feedbackid := wrdschema->^feedback->Search("wrd_guid", guid);
  IF (feedbackid = 0)
    THROW NEW Exception(`No feedback with guid '${guid}'`);

  INTEGER topicid := wrdschema->^topic->Search("wrd_tag", ToUppercase(settings.topic));
  IF (settings.topic != "" AND topicid = 0)
    THROW NEW Exception(`No such topic '${settings.topic}'`);

  RECORD feedback := wrdschema->^feedback->GetEntityFields(feedbackid, [ "extradata","status","wrd_leftentity" ]);
  IF(feedback.status != "creating")
    THROW NEW Exception(`Feedback with guid '${guid}' has already been completed`);

  wrdschema->^feedback->UpdateEntity(feedbackid,
      CELL[ topic := topicid
          , settings.remarks
          , extradata := CELL[...feedback.extradata, ...settings.extradata ]
          , status := "new"
          ]);
  RETURN wrdschema->^feedback->GetEntityField(feedbackid, "feedbackid");
}

STRING FUNCTION GetFeedbackSecret(STRING kid_unused)
{
  // The secret key should be at least 256 bits, which the cookiesecret already is
  RETURN GetClusterKeyBase("cookiesecret") || tollium_feedback_scope;
}

// @cell(string) userdata.name The user's full name
// @cell(string) userdata.email The user's email address
// @return A JSON Web Token
PUBLIC STRING FUNCTION GetFeedbackWebToken(OBJECT tolliumuser)
{
  RECORD userdata := tolliumuser->GetUserDataForLogging();
  // Convert GetUserDataForLogging data to JWT claims (see https://www.iana.org/assignments/jwt/jwt.xhtml)
  userdata :=
      [ iap := userdata.when
      , sub := userdata.wrd_guid
      , name := userdata.realname
      , email := userdata.emailaddress
      , preferred_username := userdata.login
      ];
  OBJECT token := NEW JSONWebToken("HS256", GetfeedbackSecret(""));
  token->payload := userdata;
  RETURN token->GetAuthorizationValue();
}

// @param token A JSON Web Token
// @cell(string) return.name The user's full name
// @cell(string) return.email The user's email address
PUBLIC RECORD FUNCTION VerifyFeedbackWebToken(STRING token)
{
  IF (token != "")
  {
    TRY
    {
      RECORD userdata := EnforceStructure(
          [ iap := DEFAULT DATETIME
          , sub := ""
          , name := ""
          , email := ""
          , preferred_username := ""
          ], VerifyJSONWebToken(token, [ alg := [ "HS256" ], secret_callback := PTR GetFeedbackSecret ])->payload);
      // Convert JWT claims to GetUserDataForLogging data (see https://www.iana.org/assignments/jwt/jwt.xhtml)
      RETURN
          [ when := userdata.iap
          , wrd_guid := userdata.sub
          , realname := userdata.name
          , emailaddress := userdata.email
          , login := userdata.preferred_username
          ];
    }
    CATCH (OBJECT e)
      LogHareScriptException(e);
  }
  RETURN DEFAULT RECORD;
}
