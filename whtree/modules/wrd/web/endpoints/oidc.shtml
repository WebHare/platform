<?wh

LOADLIB "wh::javascript.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/auth.whlib";
LOADLIB "mod::wrd/lib/internal/auth/webdesignplugin.whlib";

//FIXME this should be shared with the actual TS algorithms - and oidc.shtml itself should be cancelled together
RECORD FUNCTION GetOIDCLoginName(RECORD providerinfo, RECORD payload)
{
  IF(providerinfo.loginfield != "")
  {
    IF(CellExists(payload, providerinfo.loginfield) AND TYPEID(GetCell(payload, providerinfo.loginfield)) = TYPEID(STRING))
      RETURN CELL [ field := providerinfo.loginfield, value := GetCell(payload, providerinfo.loginfield) ];
    RETURN DEFAULT RECORD;
  }

  //Fallback configuration.
  IF(CellExists(payload, 'email') AND CellExists(payload, 'email_verified') AND payload.email_verified)
    RETURN [ field := "email", value := payload.email ];
  ELSE IF(CellExists(payload, 'employeeid')) //FIXME remove this exception (currently used for UT/Azure) and explicitly configure the provider to look in the employeeid claim
    RETURN [ field := "employeeid", value := payload.employeeid ];
  ELSE
    RETURN [ field := "sub", value := payload.sub ];

  RETURN DEFAULT RECORD;
}

MACRO AbortLogin(STRING redirecturl, RECORD data) {
  IF(data.reason = "UNKNOWNLOGIN") //HS value
    data.reason := "unknown-login"; //TS LoginErrorCode
  IF(data.reason = "NOACCESS") //HS value
    data.reason := "account-disabled"; //TS LoginErrorCode

  RECORD url := UnpackURL(redirecturl);
  //TODO we should recover the original login request from the oauth2session/state= so we can send the user back to the actual original login page
  Redirect(UpdateURLVariables(url.origin || "/.wh/common/authpages/",
      CELL[ wrd_pwdaction := "oidc-failure"
          , pathname := url.urlpath
          , reason := data.reason
          , loginname := CellExists(data,"loginname") ? data.loginname : ""
          ]));
}

MACRO RunOpenIDAuth() {
  //FIXME replay protections. we currently just keep accepting the URL

  //TODO can't 'why' be combined into the oauth2session we're creating anyway during oidc ?
  RECORD decodedinfo := HandleOAuth2AuthorizeRequestLanding("platform:openidlogin", GetWebVariable("oauth2session"));
  RECORD clientinfo := DescribeWRDEntity(decodedinfo.userdata.provider);
  OBJECT wrdschema := OpenWRDSchemaById(clientinfo.wrdschema);
  IF(NOT RecordExists(decodedinfo)) //we lost the session
    AbortWithHTTPError(410);
  IF(NOT CellExists(decodedinfo,'tokens')) //eg a 'none' prompt that failed to find a session simply redirects back to us
    Redirect(decodedinfo.userdata.redirect);

  IF(NOT CellExists(decodedinfo,'idPayload'))
    THROW NEW Exception(`No ID token received from OIDC provider. Was 'openid' scope passed?`);

  RECORD spinfo := wrdschema->GetTypeById(clientinfo.wrdtype)->GetEntityFields(decodedinfo.userdata.provider, ["WRD_ID", "WRD_TAG", "LOGINFIELD"]);
  RECORD payload := decodedinfo.idPayload;

  IF(IsDebugTagEnabled("oauth2"))
    LogDebug("platform:oauth2", CELL[ reason := "Received JWT for oidc auth", payload, decodedinfo.userdata ]);

  OBJECT targetplugin := GetWRDAuthPlugin(decodedinfo.userdata.redirect);
  IF(NOT ObjectExists(targetplugin))
    THROW NEW Exception(`No authplugin found for target ` || decodedinfo.userdata.redirect);

  INTEGER userid;
  VARIANT lookupresult := CallJS("@mod-platform/js/auth/harescript.ts#lookupOIDCUser", targetplugin->__WRDAuthPluginSettings_HS(), decodedinfo.tokens.id_token, spinfo.loginfield, decodedinfo.userdata.provider);
  IF(TYPEID(lookupresult) = TYPEID(INTEGER))
    userid := lookupresult;;

  IF(TYPEID(lookupresult) = TYPEID(RECORD) AND NOT RecordExists(lookupresult)) {  //'null' then we fall back to a wrdauthsupport plugin
    RECORD provider := [ oidc_client := spinfo.wrd_id, oidc_tag := spinfo.wrd_tag, loginfield := spinfo.loginfield ];
    RECORD result := targetplugin->GetSupportObject()->LookupOIDCLogin(payload, provider);
    IF(NOT RecordExists(result) OR (CellExists(result,'userid') AND result.userid = 0)) {
      RECORD logininfo := GetOIDCLoginName(provider, payload);
      AbortLogin(decodedinfo.userdata.redirect, CELL[ reason := "unknown-login", loginname := CellExists(logininfo, 'value') ? logininfo.value : "" ]);
    }
    //TODO support 'navigateto'
    IF(NOT CellExists(result,'userid'))
      THROW NEW Exception("LookupOIDCLogin did not return a userid");

    userid := result.userid;
  }

  IF(TYPEID(lookupresult) = TYPEID(RECORD) AND CellExists(lookupresult,"error")) {
    //Then it's a LoginDenied structure
    AbortLogin(decodedinfo.userdata.redirect, CELL[ reason := lookupresult.code ]);
  }

  IF(TYPEID(lookupresult) = TYPEID(RECORD)) //NavigateInstruction
    ExecuteSubmitInstruction(lookupresult);

  //user is recognized. log him in
  RECORD loginres :=  targetplugin->LoginById(userid, FALSE, [ auditmessage := CELL[ oidcclient := spinfo.wrd_id
                                                                                          , oidctag := spinfo.wrd_tag
                                                                                          ]
                                                                    , remoteip := GetClientRemoteIP()
                                                                    , thirdparty := TRUE
                                                                    ]);
  IF(loginres.success)
    Redirect(decodedinfo.userdata.redirect);
  ELSE
    AbortLogin(decodedinfo.userdata.redirect, CELL[ reason := loginres.code ]);
}

OpenPrimary();
RunOpenIDAuth();
