<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::internet/jwt.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/auth.whlib";
LOADLIB "mod::wrd/lib/internal/auth/oidc.whlib";

//FIXME this should be shared with the actual TS algorithms - and oidc.shtml itself should be cancelled together
RECORD FUNCTION GetOIDCLoginName(RECORD providerinfo, RECORD payload)
{
  IF(providerinfo.loginfield != "")
  {
    IF(CellExists(payload, providerinfo.loginfield) AND TYPEID(GetCell(payload, providerinfo.loginfield)) = TYPEID(STRING))
      RETURN CELL [ field := providerinfo.loginfield, value := GetCell(payload, providerinfo.loginfield) ];
    RETURN DEFAULT RECORD;
  }

  //Fallback configuration.
  IF(CellExists(payload, 'email') AND CellExists(payload, 'email_verified') AND payload.email_verified)
    RETURN [ field := "email", value := payload.email ];
  ELSE IF(CellExists(payload, 'employeeid')) //FIXME remove this exception (currently used for UT/Azure) and explicitly configure the provider to look in the employeeid claim
    RETURN [ field := "employeeid", value := payload.employeeid ];
  ELSE
    RETURN [ field := "sub", value := payload.sub ];

  RETURN DEFAULT RECORD;
}

MACRO AbortLogin(STRING redirecturl, RECORD data) {
  IF(data.reason = "UNKNOWNLOGIN") //HS value
    data.reason := "unknown-login"; //TS LoginErrorCode
  IF(data.reason = "NOACCESS") //HS value
    data.reason := "account-disabled"; //TS LoginErrorCode

  RECORD url := UnpackURL(redirecturl);
  //TODO we should recover the original login request from the oauth2session/state= so we can send the user back to the actual original login page
  Redirect(UpdateURLVariables(url.origin || "/.wh/common/authpages/",
      CELL[ wrd_pwdaction := "oidc-failure"
          , pathname := url.urlpath
          , reason := data.reason
          , loginname := CellExists(data,"loginname") ? data.loginname : ""
          ]));
}

MACRO RunOpenIDAuth()
{
  //FIXME replay protections. we currently just keep accepting the URL

  RECORD whyreason := DecryptForThisServer("wrd:oidcauth", GetWebVariable("why"), [ fallback := DEFAULT RECORD ]);
  RECORD clientinfo := DescribeWRDEntity(whyreason.client);

  OBJECT wrdschema := OpenWRDSchemaById(clientinfo.wrdschema);
  RECORD spinfo := wrdschema->GetTypeById(clientinfo.wrdtype)->GetEntityFields(whyreason.client, ["WRD_ID", "WRD_TAG", "METADATAURL", "CLIENTID", "CLIENTSECRET", "LOGINFIELD"]);
  OBJECT oauth2 := GetOIDCOauth2Connection(spinfo);

  RECORD authres := oauth2->AuthorizeClient();
  IF(NOT authres.success) //eg a 'none' prompt that failed to find a session
    Redirect(whyreason.redirect);

  RECORD loginconfig := GetOIDCMetadata(spinfo.metadataurl);
  RECORD payload := DecodeVerifyJWT(loginconfig.keys, oauth2->token.id_token, GetCurrentDateTime(), [ algorithms := [ "RS256" ], issuer := STRING[loginconfig.issuer] ]);
  IF(NOT RecordExists(payload))
    THROW NEW Exception(`Invalid JWT token received from OIDC provider`);

  LogDebug("wrd:auth-oidc", [ reason := "Received JWT for oidc auth", payload := payload, why := whyreason ]);
  OBJECT targetplugin := GetWRDAuthPlugin(whyreason.redirect);
  IF(NOT ObjectExists(targetplugin))
    THROW NEW Exception(`No authplugin found for target ` || whyreason.redirect);

  INTEGER userid := CallJS("@mod-platform/js/auth/harescript.ts#lookupOIDCUser", targetplugin->__WRDAuthPluginSettings_HS(), oauth2->token.id_token, spinfo.loginfield, whyreason.client);
  IF(userid = 0) { //then we fall back to a wrdauthsupport plugin
    RECORD provider := [ oidc_client := spinfo.wrd_id, oidc_tag := spinfo.wrd_tag, loginfield := spinfo.loginfield ];
    RECORD result := targetplugin->GetSupportObject()->LookupOIDCLogin(payload, provider);
    IF(NOT RecordExists(result) OR (CellExists(result,'userid') AND result.userid = 0)) {
      RECORD logininfo := GetOIDCLoginName(provider, payload);
      AbortLogin(whyreason.redirect, CELL[ reason := "unknown-login", loginname := CellExists(logininfo, 'value') ? logininfo.value : "" ]);
    }
    //TODO support 'navigateto'
    IF(NOT CellExists(result,'userid'))
      THROW NEW Exception("LookupOIDCLogin did not return a userid");

    userid := result.userid;
  }

  //user is recognized. log him in
  RECORD loginres :=  targetplugin->LoginById(userid, FALSE, [ auditmessage := CELL[ oidcclient := spinfo.wrd_id
                                                                                          , oidctag := spinfo.wrd_tag
                                                                                          ]
                                                                    , remoteip := GetClientRemoteIP()
                                                                    , thirdparty := TRUE
                                                                    ]);
  IF(loginres.success)
    Redirect(whyreason.redirect);
  ELSE
    AbortLogin(whyreason.redirect, CELL[ reason := loginres.code ]);
}

OpenPrimary();
RunOpenIDAuth();
