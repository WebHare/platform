<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::javascript.whlib";
LOADLIB "wh::internet/jwt.whlib";
LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/auth.whlib";
LOADLIB "mod::wrd/lib/internal/auth/oidc.whlib";

MACRO RunOpenIDAuth()
{
  //FIXME replay protections. we currently just keep accepting the URL

  RECORD whyreason := DecryptForThisServer("wrd:oidcauth", GetWebVariable("why"), [ fallback := DEFAULT RECORD ]);
  RECORD clientinfo := DescribeWRDEntity(whyreason.client);

  OBJECT wrdschema := OpenWRDSchemaById(clientinfo.wrdschema);
  RECORD spinfo := wrdschema->GetTypeById(clientinfo.wrdtype)->GetEntityFields(whyreason.client, ["WRD_ID", "WRD_TAG", "METADATAURL", "CLIENTID", "CLIENTSECRET", "LOGINFIELD"]);
  OBJECT oauth2 := GetOIDCOauth2Connection(spinfo);

  RECORD authres := oauth2->AuthorizeClient();
  IF(NOT authres.success) //eg a 'none' prompt that failed to find a session
    Redirect(whyreason.redirect);

  RECORD loginconfig := GetOIDCMetadata(spinfo.metadataurl);
  RECORD payload := DecodeVerifyJWT(loginconfig.keys, oauth2->token.id_token, GetCurrentDateTime(), [ algorithms := [ "RS256" ], issuer := STRING[loginconfig.issuer] ]);
  IF(NOT RecordExists(payload))
    THROW NEW Exception(`Invalid JWT token received from OIDC provider`);

  LogDebug("wrd:auth-oidc", [ reason := "Received JWT for oidc auth", payload := payload, why := whyreason ]);
  OBJECT targetplugin := GetWRDAuthPlugin(whyreason.redirect);
  IF(NOT ObjectExists(targetplugin))
    THROW NEW Exception(`No authplugin found for target ` || whyreason.redirect);

  INTEGER userid := CallJS("@mod-platform/js/auth/harescript.ts#lookupOIDCUser", targetplugin->__WRDAuthPluginSettings_HS(), oauth2->token.id_token, spinfo.loginfield);
  IF(userid = 0) { //then we fall back to a wrdauthsupport plugin
    RECORD result := targetplugin->GetSupportObject()->LookupOIDCLogin(payload, [ oidc_client := spinfo.wrd_id, oidc_tag := spinfo.wrd_tag, loginfield := spinfo.loginfield ]);
    IF(NOT RecordExists(result) OR (CellExists(result,'userid') AND result.userid = 0))
    {
      //what to do? we need to redirect/submit to the caller and we need to inform him what went wrong (unknown login)
      Redirect(UpdateURLVariables(whyreason.redirect,
        [ "wrdauth-loginfail-reason" := "unmappedlogin" //TODO remove at some point, user should rely on the encrypted info
        , "wrdauth-loginfail" := EncryptForThisServer("wrd:authloginfail",
            CELL[ reason := "unmappedlogin"
                , ...PickCells(result, ["field", "value"]) //may not exist, though the LookupOIDCLogin override is out of spec then!
                ])
        ]));
    }
    //TODO support 'navigateto'
    IF(NOT CellExists(result,'userid'))
      THROW NEW Exception("LookupOIDCLogin did not return a userid");

    userid := result.userid;
  }

  //user is recognized. log him in
  RECORD loginres :=  targetplugin->LoginById(userid, FALSE, [ auditmessage := CELL[ oidcclient := spinfo.wrd_id
                                                                                          , oidctag := spinfo.wrd_tag
                                                                                          ]
                                                                    , remoteip := GetClientRemoteIP()
                                                                    , thirdparty := TRUE
                                                                    ]);
  IF(loginres.success)
    Redirect(whyreason.redirect);
  ELSE
    Redirect(UpdateURLVariables(whyreason.redirect, [
      "wrdauth-loginfail-reason" := "loginfailure", //TODO remove at some point, user should rely on the encrypted info
      "wrdauth-loginfail" := EncryptForThisServer("wrd:authloginfail",
           CELL[ reason := "loginfailure"
               ])
    ]));
}

OpenPrimary();
RunOpenIDAuth();
