<?wh
// Fix all password fields (old SSHA1: with improper encoding) - note that these are always OLD fields as we haven't generated SSHA1: passwords for YEARS now....
// You may need to rerun this script if you import WRD schemas from pre-4.27 WebHares

LOADLIB "mod::system/lib/database.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/internal/dbschema.whlib";

STRING FUNCTION MakePasswordUTF8Safe(STRING inpassword)
{
  IF(Length(inpassword) != 34 OR inpassword NOT LIKE "SSHA1:*")
    RETURN inpassword;
  RETURN "XSHA1:" || EncodeBase64(Substring(inpassword,6));
}

OBJECT trans := OpenPrimary();
FOREVERY(RECORD schemarec FROM SELECT * FROM ListWRDSchemas() ORDER BY ToUppercase(tag))
{
  OBJECT wrdschema := OpenWRDSchemaById(schemarec.id);
  FOREVERY(RECORD typerec FROM wrdschema->ListTypes())
  {
    OBJECT wrdtype := wrdschema->GetTypeById(typerec.id);
    FOREVERY(RECORD attr FROM SELECT * FROM wrdtype->ListAttributes(-1) WHERE attributetypename = "PASSWORD" ORDER BY tag)
    {
      RECORD ARRAY tofix := SELECT id, rawdata
                              FROM wrd.entity_settings
                             WHERE attribute = attr.id
                                   AND rawdata LIKE "SSHA1:*"
                                   AND Length(rawdata) = 34;
      IF(Length(tofix) > 0)
      {
        Print(`Fixing ${Length(tofix)} password(s) in field ${wrdschema->tag}.${wrdtype->tag}.${attr.tag}\n`);

        trans->BeginWork();
        UPDATE wrd.entity_settings SET rawdata := MakePasswordUTF8Safe(rawdata) WHERE id IN (SELECT AS INTEGER ARRAY tofix.id FROM tofix);
        trans->CommitWork();
      }
    }
  }
}
