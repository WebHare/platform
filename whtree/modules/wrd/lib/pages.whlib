<?wh
/** @short Frontend pages
    @topic wrdauth/frontendpages
*/

LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::wrd/lib/internal/pages/authpages.whlib";

/** Implement standard login/passwordreset etc pages
    @param(object %WebdesignBase) webdesign Webdesign
    @cell options.ongetloginwittydata Callback invoked for extra witty variables for the login page, if rendered
    @cell options.fallbacktologin Fallback to the login page if not logged in. Defaults to true
    @return Page to render, if current state requires one (DEFAULT OBJECT if no special page and currently logged in or fallbacktologin unset)
*/
PUBLIC OBJECT FUNCTION GetWRDAuthPages(OBJECT webdesign, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
    [ ongetloginwittydata := DEFAULT FUNCTION PTR
    , fallbacktologin := TRUE //Return loginpage if no explicit route found
    , passwordresetrouter := DEFAULT MACRO PTR
    , loginmethods := RECORD[]
    , isglobalpage := FALSE
    ], options);
  options.loginmethods :=
      SELECT AS RECORD ARRAY ValidateOptions(
                 [ tag := ""
                 , title := ""
                 , icon := ""
                 ], loginmethods,
                 [ title := `loginmethods#${#loginmethods}`
                 , required := [ "tag", "title" ]
                 ])
        FROM options.loginmethods;

  OBJECT authplugin := webdesign->GetWRDAuthPlugin();
  IF(NOT ObjectExists(authplugin))
    THROW NEW Exception("No wrdauth plugin available");



  STRING action := GetWebVariable("wrd_pwdaction");
  IF(action IN ["forgotpassword"])
  {
    IF(authplugin->IsLoggedIn())
      Redirect(GetWRDPagesCleanURL());
    IF(options.isglobalpage AND "forgotpassword" NOT IN authplugin->routerfeatures)
      AbortWithHTTPError(403, "forgotpassword not allowed by routerfeatures");
    RETURN options.passwordresetrouter != DEFAULT MACRO PTR ? options.passwordresetrouter(webdesign, CELL [ authplugin ]) : GetForgotPasswordPage(webdesign, options);
  }
  IF(action IN ["resetpassword","setpassword"])
  {
    RETURN GetResetPasswordPage(webdesign, action, options);
  }
  IF(action = "completeaccount") {
    RETURN GetCompleteAccountPage(webdesign, options);
  }
  IF(action = "totp") {
    RETURN GetTotpPage(webdesign, options);
  }
  IF(action = "oidc-failure") {
    RETURN GetOIDCFailurePage(webdesign, options);
  }
  IF(action = "emailchange")
  {
    IF(NOT authplugin->IsLoggedIn())
      Redirect(GetWRDPagesCleanURL());
    IF("emailchange" NOT IN authplugin->routerfeatures)
      AbortWithHTTPError(403, "emailchange not allowed by routerfeatures");
    IF(GetWebVariable("_ed") != "")
      RETURN GetEmailChangedPage(webdesign, options);
    RETURN GetEmailChangePage(webdesign, options);
  }
  IF(action = "passwordchange")
  {
    IF(NOT authplugin->IsLoggedIn())
      Redirect(GetWRDPagesCleanURL());
    IF("passwordchange" NOT IN authplugin->routerfeatures)
      AbortWithHTTPError(403, "passwordchange not allowed by routerfeatures");
    RETURN GetPasswordChangePage(webdesign, options);
  }

  IF(action = "")
  {
    IF(options.isglobalpage AND "login" NOT IN authplugin->routerfeatures)
      AbortWithHTTPError(403, "login not allowed by routerfeatures");

    STRING logincontrol := GetWebVariable("wrdauth_logincontrol");
    IF(options.fallbacktologin) {
      RECORD descr := authplugin->__DecryptLoginControlToken(logincontrol);
      IF(NOT authplugin->IsLoggedIn() OR (RecordExists(descr) AND descr.prompt = "login"))
        RETURN GetLoginPage(webdesign, options);
    }

    // STRING logincontrol := GetWebVariable("wrdauth_logincontrol");
    IF (logincontrol != "") {
      authplugin->ProcessReturnTo(logincontrol);
      //If we get here, ProcessReturnTo didn't handle the control. remove it
      Redirect(UpdateURLVariables(GetRequestURL(), [ "wrdauth_logincontrol" := ""]));
    }

    RETURN DEFAULT OBJECT; //you'll need to route this yourself
  }

  Redirect(GetWRDPagesCleanURL()); //if it did look like on of our urls, we'll at least fix it
}
