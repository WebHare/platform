<?wh

LOADLIB "wh::datetime.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::filetypes/pkcs.whlib";
LOADLIB "wh::internet/webbrowser.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/keystore.whlib";

LOADLIB "mod::publisher/lib/publisher.whlib";

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/internal/auth/samlsupport.whlib";


PUBLIC STATIC OBJECTTYPE Main EXTEND TolliumScreenBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  OBJECT wrd_providerbase;
  OBJECT wrd_sp;
  OBJECT wrd_idp;

  OBJECT wrd_connectedproviders;

  INTEGER curprovider;

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init(RECORD data)
  {
    RECORD connectparams;
    this->wrd_idp := this->contexts->wrdschema->GetType("WRD_AUTHDOMAIN_SAML_IDP");
    this->wrd_sp := this->contexts->wrdschema->GetType("WRD_AUTHDOMAIN_SAML_SP");
    this->wrd_providerbase := this->contexts->wrdschema->GetType("WRD_AUTHDOMAIN_SAMLPROVIDERBASE");
    this->wrd_connectedproviders := this->contexts->wrdschema->GetType("WRD_AUTHDOMAIN_SAML_CONNECTED_ENTITY");
    OBJECT wrd_authdomain := this->contexts->wrdschema->GetType("wrd_authdomain");

    IF (NOT ObjectExists(wrd_authdomain))
    {
      this->RunMessageBox(".authnotcorrectlyconfigured");
      this->tolliumresult := "cancel";
      RETURN;
    }

    IF (NOT ObjectExists(this->wrd_providerbase))
    {
      this->RunMessageBox(".notconfiguredforsaml");
      this->tolliumresult := "cancel";
      RETURN;
    }

    ^eventlistener->masks :=
          this->wrd_providerbase->GetEventMasks() CONCAT
          this->wrd_connectedproviders->GetEventMasks();

    // Samlproviders need to refresh when connected providers refresh too
    ^samlproviders->eventmasks := this->wrd_connectedproviders->GetEventMasks();

    ^samlproviders->wrdtype := this->wrd_providerbase;

    ^connectedproviders->wrdtype := this->wrd_connectedproviders;

    ^samlproviders->embeddedlist->rowkeytype := TypeID(INTEGER);
    this->CalculateFrameFlags();
    this->OnProviderSelect();
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO CalculateFrameFlags()
  {
    this->frame->flags.haveidp := RecordExists(this->wrd_idp->RunQuery(CELL[]));

    IF (^samlproviders->value = 0)
      this->frame->flags.haveconnected := FALSE;
    ELSE
      this->frame->flags.haveconnected := RecordExists(this->wrd_connectedproviders->RunQuery(
        [ filters := [ [ field := "WRD_LEFTENTITY", value := ^samlproviders->value ] ]
        ]));
  }

  // ---------------------------------------------------------------------------
  //
  // Callbacks
  //

  MACRO GotEvents(RECORD ARRAY events)
  {
    this->CalculateFrameFlags();
  }

  RECORD ARRAY FUNCTION OnMapSAMLProvidersRows(RECORD ARRAY rows)
  {
    // Get list of all connected providers
    RECORD ARRAY connected :=
        SELECT *
          FROM this->wrd_connectedproviders->RunQuery(
                [ outputcolumns := [ "WRD_LEFTENTITY" ]
                ])
      ORDER BY wrd_leftentity;

    rows :=
        SELECT *
             , sp :=              wrd_type = this->wrd_sp->id
             , idp :=             wrd_type = this->wrd_idp->id
             , type :=            wrd_type = this->wrd_sp->id ? this->GetTid(".sp") : this->GetTid(".idp")
             , haveconnected :=   RecordLowerBound(connected, [ wrd_leftentity := rowkey ], [ "WRD_LEFTENTITY" ]).found
          FROM rows;

    RETURN rows;
  }

  RECORD ARRAY FUNCTION OnMapConnectedProvidersRows(RECORD ARRAY rows)
  {
    rows :=
        SELECT *
             , sp :=    this->frame->flags.selected_idp
             , idp :=   this->frame->flags.selected_sp
          FROM rows;

    RETURN rows;
  }

  MACRO OnProviderSelect()
  {
    BOOLEAN selected_sp;
    BOOLEAN selected_idp;
    BOOLEAN haveconnected;

    this->curprovider := ^samlproviders->value;

    IF (^samlproviders->value != 0)
    {
      RECORD data := this->wrd_providerbase->GetEntityFields(^samlproviders->value, [ "WRD_TYPE" ]);
      selected_sp := data.wrd_type = this->wrd_sp->id;
      selected_idp := data.wrd_type = this->wrd_idp->id;

      haveconnected := RecordExists(this->wrd_connectedproviders->RunQuery(
            [ filters := [ [ field := "WRD_LEFTENTITY", value := ^samlproviders->value ] ] ]));
    }

    this->frame->flags.selected_sp := selected_sp;
    this->frame->flags.selected_idp := selected_idp;
    this->frame->flags.haveconnected := haveconnected;

    // -1 is somewhat faster to generate no results
    ^connectedproviders->listfilters :=
        [ [ field := "WRD_LEFTENTITY", value := ^samlproviders->value ?? -1 ] ];
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoAddSP()
  {
    INTEGER id := this->RunScreen("#editprovider", [ wrdtype := this->wrd_sp, id := 0 ]);
    IF (id != 0)
      ^samlproviders->value := id;
  }

  MACRO DoAddIDP()
  {
    INTEGER id := this->RunScreen("#editprovider", [ wrdtype := this->wrd_idp, id := 0 ]);
    IF (id != 0)
      ^samlproviders->value := id;
  }

  MACRO DoEditSP()
  {
    this->LoadScreen(".editprovider", [ wrdtype := this->wrd_sp, id := ^samlproviders->value  ])->RunModal();
  }

  MACRO DoEditIDP()
  {
    this->LoadScreen(".editprovider", [ wrdtype := this->wrd_idp, id := ^samlproviders->value  ])->RunModal();
  }

  MACRO DoDeleteSP()
  {
    IF (this->RunMessageBox(".suredeletesp") = "yes")
    {
      OBJECT work := this->BeginWork();
      this->wrd_sp->GetEntity(^samlproviders->value)->CloseOrDeleteEntity();
      work->Finish();
    }
  }

  MACRO DoDeleteIDP()
  {
    IF (this->RunMessageBox(".suredeleteidp") = "yes")
    {
      OBJECT work := this->BeginWork();
      this->wrd_idp->GetEntity(^samlproviders->value)->CloseOrDeleteEntity();
      work->Finish();
    }
  }

  MACRO DoAddConnectedIDP()
  {
    this->LoadScreen(".editconnectedprovider",
        [ wrdtype :=  this->wrd_connectedproviders
        , id :=       0
        , type :=     "idp"
        , parent :=   ^samlproviders->value
        ])->RunModal();
  }

  MACRO DoAddConnectedSP()
  {
    this->LoadScreen(".editconnectedprovider",
        [ wrdtype :=  this->wrd_connectedproviders
        , id :=       0
        , type :=     "sp"
        , parent :=   ^samlproviders->value
        ])->RunModal();
  }

  MACRO DoEditConnectedSP()
  {
    this->LoadScreen(".editconnectedprovider",
        [ wrdtype :=  this->wrd_connectedproviders
        , id :=       ^connectedproviders->value
        , type :=     "sp"
        , parent :=   ^samlproviders->value
        ])->RunModal();
  }

  MACRO DoEditConnectedIDP()
  {
    this->LoadScreen(".editconnectedprovider",
        [ wrdtype :=  this->wrd_connectedproviders
        , id :=       ^connectedproviders->value
        , type :=     "idp"
        , parent :=   ^samlproviders->value
        ])->RunModal();
  }

  MACRO DoDeleteConnectedIDP()
  {
    IF (this->RunMessageBox(".suredeleteconnectedidp") = "yes")
    {
      OBJECT work := this->BeginWork();
      this->wrd_connectedproviders->GetEntity(^connectedproviders->value)->CloseOrDeleteEntity();
      work->Finish();
    }
  }

  MACRO DoDeleteConnectedSP()
  {
    IF (this->RunMessageBox(".suredeleteconnectedsp") = "yes")
    {
      OBJECT work := this->BeginWork();
      this->wrd_connectedproviders->GetEntity(^connectedproviders->value)->CloseOrDeleteEntity();
      work->Finish();
    }
  }

  MACRO DoIdPInitiatedLogin(OBJECT receiver)
  {
    RECORD redir := this->contexts->controller->wrdauthplugin->InitiateSAMLIdPInitiatedLogin(^connectedproviders->selection.samlentityid);
    IF (NOT redir.success)
    {
      receiver->Cancel();
      SWITCH (redir.code)
      {
        CASE "noidpendpointfound" { this->RunSimpleScreen("error", this->GetTid(".noidpendpointfound")); }
        DEFAULT                   { THROW NEW Exception(`No message for error ${EncodeHSON(redir.code)}`); }
      }
      RETURN;
    }

    receiver->SendSubmitInstruction(redir);
  }
>;

PUBLIC STATIC OBJECTTYPE EditProvider EXTEND TolliumScreenBase
<

  MACRO Init(RECORD data)
  {

    ^keypair->options := [ [ rowkey := 0, title := this->GetTid(".chooseakeypair"), invalidselection := TRUE ] ] CONCAT
        SELECT rowkey :=    id
             , title :=     title = "" ? name : title || " (" || name || ")"
          FROM ListKeyPairs();

    BOOLEAN is_sp := data.wrdtype->tag = "WRD_AUTHDOMAIN_SAML_SP";
    BOOLEAN is_idp := data.wrdtype->tag = "WRD_AUTHDOMAIN_SAML_IDP";

    // also disable, to keep out of updates to record
    ^nameidformatsheading->visible := is_idp;
    ^supportednameidformats->visible := is_idp;
    ^nameidpolicy->visible := is_sp;
    IF (NOT is_idp)
      ^supportednameidformats->composition := DEFAULT OBJECT;
    ELSE
      ^nameidpolicy->composition := DEFAULT OBJECT;

    ^entity->LoadEntity(data.wrdtype, data.id);

    IF (data.id != 0)
    {
      RECORD intextlink := ^entity->wrdentity->GetField("KEYPAIR");
      IF (RecordExists(intextlink))
        ^keypair->value := intextlink.internallink;

      IF (is_sp)
      {
        // default policies
        OBJECT wrd_connected := data.wrdtype->wrdschema->^wrd_authdomain_saml_connected_entity;
        INTEGER connected_idp := wrd_connected->Search("WRD_LEFTENTITY", data.id);
        IF (connected_idp != 0)
        {
          RECORD ARRAY formats := wrd_connected->GetEntityField(connected_idp, "SUPPORTEDNAMEIDFORMATS");
          IF (LENGTH(formats) != 0)
            ^nameidpolicy->options := SELECT title := nameidformat FROM formats;
        }
      }
    }
    ELSE
    {
      // New providers want SHA-256 as digest method
      ^entity->value.hashmethod := "SHA-256";
    }
  }

  INTEGER FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    IF (work->HasFailed() AND NOT work->Finish())
      RETURN 0;

    ^entity->StoreEntity(work,
        [ keypair := MakeIntExtInternalLink(^keypair->value, "")
        ]);

    RETURN work->Finish() ? ^entity->entityid : 0;
  }
>;

PUBLIC OBJECTTYPE EditConnectedProvider EXTEND TolliumScreenBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  INTEGER parent;

  STRING type;

  // ---------------------------------------------------------------------------
  //
  // Init & submit
  //

  MACRO Init(RECORD data)
  {
    this->type := data.type;
    this->parent := data.parent;

    BOOLEAN is_sp := this->type = "sp";
    BOOLEAN is_idp := this->type = "idp";

    ^nameidformatsheading->visible := is_idp;
    ^supportednameidformats->visible := is_idp;

    ^othersettingsheading->visible := NOT is_idp;
    ^signature_optional->visible := NOT is_idp;

    ^entity->LoadEntity(data.wrdtype, data.id);
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT work := this->BeginWork();
    IF (work->HasFailed())
      RETURN work->Finish();

    ^entity->StoreEntity(work, [ wrd_leftentity := this->parent ]);

    RETURN work->Finish();
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  RECORD ARRAY FUNCTION OnMapServiceEndpointRows(RECORD ARRAY rows)
  {
    // ADDME: binding -> use tid with 'HTTP-POST' / 'Redirect'
    RETURN
        SELECT TEMPORARY indexed := type IN [ "ArtifactResolutionService", "AssertionConsumerService" ]
             , *
             , "index" :=       indexed ? ToString(COLUMN "index") : ""
             , isdefault :=     indexed AND isdefault ? this->GetTid(".default") : ""
          FROM rows;
  }

  RECORD ARRAY FUNCTION OnMapCertificateRows(RECORD ARRAY rows)
  {
    FOREVERY (RECORD row FROM rows)
    {
      RECORD decoded := DecodePEMFile(BlobToString(row.data.data));

      INSERT CELL commonname := (SELECT AS STRING value FROM decoded.subjectfields WHERE fieldname="CN") ?? this->GetTid(".na") INTO rows[#row];
      INSERT CELL validuntil := decoded.valid_until INTO rows[#row];
    }

    RETURN rows;
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO ImportMetadataDocument(OBJECT feedback, OBJECT doc)
  {
    RECORD parsed := ParseConnectedSAMLProviderMetadata(doc);
    IF (NOT parsed.success)
    {
      FOREVERY (STRING message FROM parsed.messages)
        feedback->AddError(message);

      RETURN;
    }

    RECORD ARRAY providers := SELECT * FROM parsed.providers WHERE type = this->type;
    IF (NOT RecordExists(providers))
    {
      feedback->AddError(this->GetTid(".norelevantservicesfound"));
      RETURN;
    }
    IF (LENGTH(providers) = 1)
    {
      IF (^entity->value.samlentityid != "" AND providers[0].value.entityid != ^entity->value.samlentityid)
        feedback->AddError(this->GetTid(".samlentityiddiffers", ^entity->value.samlentityid, providers[0].value.entityid));
    }
    ELSE
    {
      IF (^entity->value.samlentityid = "")
        feedback->AddError(this->GetTid(".multipledescriptorsnofilter"));
      ELSE
      {
        providers :=
            SELECT *
              FROM providers
             WHERE value.entityid = ^entity->value.samlentityid;

        IF (LENGTH(providers) != 1)
          feedback->AddError(this->GetTid(".multipledescriptorsbutnotfound", ^entity->value.samlentityid));
      }
    }

    IF (feedback->HasFailed())
      RETURN;

    RECORD provider := providers[0];

    RECORD ARRAY certs := ^entity->value.certificates;

    IF (RecordExists(provider.value.certificates))
    {
      RECORD certrow := provider.value.certificates[0];
      STRING cert := BlobToString(certrow.data.data);

      BOOLEAN found;
      FOREVERY (RECORD rec FROM certs)
        IF (BlobToString(rec.data.data) = cert)
          found := TRUE;

      IF (NOT found)
      {
        IF (LENGTH(certs) != 0)
        {
          OBJECT dialog := this->LoadScreen(".importchangedcertificate",
              [ newcert :=    cert
              ]);

          IF (dialog->RunModal() != "ok")
            RETURN;

          IF (dialog->choice = "replace")
            certs := DEFAULT RECORD ARRAY;
          ELSE
            UPDATE certs SET useuntil := dialog->useuntil WHERE useuntil > dialog->useuntil;
        }

        INSERT certrow INTO certs AT END;

        // Try longest accepted certificates first
        certs := SELECT * FROM certs ORDER BY acceptuntil DESC;
      }
    }

    RECORD value := CELL
        [ ...provider.value
        , certificates :=               certs
        ];

    ^entity->value := MakeReplacedRecord(^entity->value, value);
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoDownloadMetadata()
  {
    OBJECT feedback := this->BeginUnvalidatedFeedback();
    IF (this->metadataurl->value = "")
      feedback->AddError(this->GetTid(".fieldmetadataurlisrequired"));
    ELSE
      this->metadataurl->ValidateValue(feedback);
    IF (feedback->HasFailed())
    {
      feedback->Finish();
      RETURN;
    }

    OBJECT browser := NEW WebBrowser;
    IF (NOT browser->GotoWebpage(this->metadataurl->value) OR NOT ObjectExists(browser->document))
    {
      PRINT(AnyToString(browser->GetHTTPStatus(), "tree"));
      feedback->AddError(this->GetTid(".cannotretrievemetadata"));
      feedback->Finish();
      RETURN;
    }

    OBJECT doc := browser->document;
    browser->Close();

    this->ImportMetadataDocument(feedback, doc);
  }

  MACRO DoEnterMetadata()
  {
    this->LoadScreen(".entermetadata", [ func := PTR this->ImportMetadataDocument ])->RunModal();
  }
>;


PUBLIC OBJECTTYPE EnterMetadata EXTEND TolliumScreenBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  FUNCTION PTR func;

  // ---------------------------------------------------------------------------
  //
  // Init & submit
  //

  MACRO Init(RECORD data)
  {
    this->func := data.func;
  }

  BOOLEAN FUNCTION Submit()
  {
    OBJECT feedback := this->BeginFeedback();
    IF (feedback->HasFailed())
      RETURN feedback->Finish();

    this->metadata->value := TrimWhitespace(this->metadata->value);

    OBJECT doc := MakeXMLDocument(StringToBlob(this->metadata->value));
    IF (NOT ObjectExists(doc) OR NOT ObjectExists(doc->documentelement) OR LENGTH(doc->GetParseErrors()) != 0)
    {
      feedback->AddError(this->GetTid(".notvalidxmldocument"));
      RETURN feedback->Finish();
    }

    this->func(feedback, doc);
    RETURN feedback->Finish();
  }
>;

PUBLIC OBJECTTYPE ImportChangedCertificate EXTEND TolliumScreenBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  // ---------------------------------------------------------------------------
  //
  // Public properties & variables
  //

  PUBLIC PROPERTY choice(this->certchoice->value, -);

  PUBLIC PROPERTY useuntil(this->certuseuntil->value, -);

  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init(RECORD data)
  {
    this->certuseuntil->value := GetRoundedDateTime(AddDaysToDate(7, UTCToLocal(GetCurrentDateTime(), "CET")), 86400 * 1000);
  }
>;
