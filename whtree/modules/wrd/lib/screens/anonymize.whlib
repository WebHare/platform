<?wh
LOADLIB "mod::tollium/lib/commondialogs.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";
LOADLIB "mod::tollium/lib/backgroundtask.whlib";

LOADLIB "mod::wrd/lib/anonymize.whlib";
LOADLIB "mod::wrd/lib/objectapi.whlib";
LOADLIB "mod::wrd/lib/names.whlib";


PUBLIC STATIC OBJECTTYPE AnonymizeBGTask EXTEND TolliumBackgroundTask
<
  RECORD data;

  MACRO Init(RECORD data)
  {
    this->data := data;
  }

  MACRO OnProgress(RECORD state)
  {
    this->status :=
        [ progress := INTEGER(100 * state.progress)
        , status := state.typeprogress > 0 ? GetTid("wrd:browser.anonymize.status.converting", state.type, ToString(INTEGER(100*state.typeprogress)))
                                           : GetTid("wrd:browser.anonymize.status.getentities", state.type)
        ];
  }

  MACRO Run()
  {
    OBJECT work := this->BeginWork();
    OBJECT wrdschema := OpenWRDSchemaByName(this->data.wrdschema);
    AnonymizeWRDSchema(wrdschema, [ onprogress := PTR this->onprogress]);
    work->Finish();
  }
>;

PUBLIC STATIC OBJECTTYPE Anonymize EXTEND TolliumScreenBase
<  // ---------------------------------------------------------------------------
  //
  // Init
  //

  MACRO Init(RECORD data)
  {
    this->frame->title := GetTid("wrd:browser.anonymize.title", this->contexts->wrdschema->tag);

    this->AnalyzeSchema();

    IF (this->contexts->wrdschema->protected)
    {
      this->RunSimpleScreen("error", GetTid("wrd:browser.anonymize.messages.schema_protected"));
      this->tolliumresult := "cancel";
      RETURN;
    }
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO AnalyzeSchema()
  {
    ^attrlist->rows :=
        SELECT type := type
             , attr := attr.tag
             , atype
          FROM __GetAttributesToAnonyimize(this->contexts->wrdschema);
  }

  // ---------------------------------------------------------------------------
  //
  // Actions
  //

  MACRO DoAnonymize()
  {
    IF (this->RunSimpleScreen("confirm", GetTid("wrd:browser.anonymize.messages.sure_anonymize", this->contexts->wrdschema->tag)) != "yes")
      RETURN;

    OBJECT task := CreateProgressDialog(
        this,
        "module::wrd/screens/anonymize.whlib",
        "AnonymizeBGTask",
        [ wrdschema := this->contexts->wrdschema->tag
        ]);

    IF (task->RunModal() = "ok")
    {
      this->RunSimpleScreen("info", GetTid("wrd:browser.anonymize.messages.schema_anonymized"));
      this->tolliumresult := "ok";
    }
  }
>;
