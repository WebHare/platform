<?wh
LOADLIB "mod::system/lib/resources.whlib";

PUBLIC RECORD ARRAY FUNCTION GetAvailableHareScriptPSPs() {
  RETURN [[ tag := "wrd:external"
          , title := /*tid*/"wrd:tolliumapps.payments.external.methodtitle"
          , objectname := "mod::wrd/lib/internal/psp/external.whlib#ExternalPaymentProvider"
          , configextensions := "mod::wrd/tolliumapps/payments/builtins.xml#configexternalpsp"
          , payinfoscreen := ""
          , metadataoptions := [ methodtitle := ""
                               , title := "" //backwards compatibility, ignored if methodtitle is set
                               , expiredays := 14
                               ]
          ]
         ,[ tag := "wrd:unavailable"
          , title := /*tid*/"wrd:tolliumapps.payments.unavailable.methodtitle"
          , objectname := "mod::wrd/lib/internal/psp/unavailable.whlib#UnavailablePaymentProvider"
          , configextensions := ""
          , payinfoscreen := ""
          , metadataoptions := DEFAULT RECORD
          , metadataoptionsoptions := [ passthrough := TRUE ]
          ]
         ,[ tag := "wrd:mollie"
          , title := ":Mollie"
          , objectname := "mod::wrd/lib/internal/psp/mollie.whlib#MolliePaymentProvider"
          , configextensions := "mod::wrd/tolliumapps/payments/mollie.xml#configmollie"
          , payinfoscreen := "mod::wrd/tolliumapps/payments/mollie.xml#payinfo"
          , metadataoptions := [ apikey := ""
                               , methods := RECORD[]
                               , webhooks := TRUE
                               ]
          ]
         ,[ tag := "wrd:multisafepay"
          , title := ":multisafepay"
          , objectname := "mod::wrd/lib/internal/psp/multisafepay.whlib#MultisafepayPaymentProvider"
          , payinfoscreen := "mod::wrd/tolliumapps/payments/multisafepay.xml#payinfo"
          , configextensions := "mod::wrd/tolliumapps/payments/multisafepay.xml#configmultisafepay"
          , metadataoptions := [ apikey := ""
                               , methods := DEFAULT RECORD ARRAY
                               , testmode := "test"
                               ]
          ]
         ,[ tag := "wrd:pay_nl"
          , title := ":Pay.nl"
          , objectname := "mod::wrd/lib/internal/psp/pay_nl.whlib#PayNLPaymentProvider"
          , configextensions := "mod::wrd/tolliumapps/payments/pay_nl.xml#configpaynl"
          , payinfoscreen := "mod::wrd/tolliumapps/payments/pay_nl.xml#payinfo"
          , metadataoptions := [ apiuser := ""
                               , apitoken := ""
                               , saleslocation := "'"
                               , methods := RECORD[]
                               , testmode := FALSE
                               , webhooks := TRUE
                               ]
          ]
         ,[ tag := "wrd:js"
          , title := ":JS plugin"
          , objectname := "mod::wrd/lib/internal/psp/js.whlib#JSPaymentProvider"
          , configextensions := "mod::wrd/tolliumapps/payments/js.xml#configjs"
          , payinfoscreen := "mod::wrd/tolliumapps/payments/js.xml#payinfo"
          , metadataoptions := [ driver := ""
                               , configuration := ""
                               , methods := DEFAULT RECORD ARRAY
                               , islive := FALSE
                               ]
          ]
         ,[ tag := "wrd:buckaroo"
          , title := ":Buckaroo"
          , objectname := "mod::wrd/lib/internal/psp/buckaroo.whlib#BuckarooPaymentProvider"
          , payinfoscreen := "mod::wrd/tolliumapps/payments/buckaroo.xml#payinfo"
          , configextensions := "mod::wrd/tolliumapps/payments/buckaroo.xml#configbuckaroo"
          , metadataoptions := [ websiteid := ""
                               , secretid := ""
                               , methods := DEFAULT STRING ARRAY
                               , testmode := FALSE
                               ]
          ]
         ,[ tag := "wrd:test"
          , title := ":Test"
          , objectname := "mod::wrd/lib/internal/psp/test.whlib#TestPaymentProvider"
          , payinfoscreen := ""//paymentproviders:test.payinfo"
          , configextensions := "mod::wrd/tolliumapps/payments/builtins.xml#configtestpsp"
          , metadataoptions := [ disablewithissuer := FALSE
                               , disablenoissuer := FALSE
                               , interactiontype := "redirect"
                               , sleep := 0
                               ]
          ]
         ];
}

PUBLIC RECORD ARRAY FUNCTION GetAvailablePSPs() {
  RECORD pspinfo := __GetExtractedConfig("wrdschemas");
  RECORD ARRAY psps :=
      SELECT tag
           , title := ":" || tag
           , objectname := "mod::wrd/lib/internal/psp/js.whlib#JSPaymentProvider"
           , payinfoscreen := paymentInfoScreen
           , configextensions := configExtensions
           , metadataoptions := [ driver := ""
                               , configuration := ""
                               , methods := DEFAULT RECORD ARRAY
                               , islive := FALSE
                               ]
           , js := TRUE
           , legacy := FALSE
       FROM pspinfo.psp;

  RETURN psps CONCAT SELECT *, js := FALSE, legacy := tag NOT IN ["wrd:external","wrd:unavailable"] FROM GetAvailableHareScriptPSPs();
}

PUBLIC RECORD FUNCTION GetPSPInfo(STRING createtype)
{
  STRING searchtype := createtype;

  RECORD methodinfo := SELECT * FROM GetAvailablePSPs() WHERE ToUppercase(tag) = ToUppercase(VAR searchtype);
  IF(NOT RecordExists(methodinfo))
    methodinfo := SELECT * FROM GetAvailablePSPs() WHERE tag = "wrd:unavailable";

  RETURN CELL[ manual := ""
             , manuallanguages := ["en"]
             , manualaccesstoken := ""
             , ...methodinfo
             ];
}

PUBLIC OBJECT FUNCTION OpenPSPHandler(STRING createtype, RECORD metadata)
{
  RECORD methodinfo := GetPSPInfo(createtype);
  IF(NOT RecordExists(methodinfo))
    THROW NEW Exception(`Unsupported payment type '${createtype}'`);

  RECORD pspoptions := ValidateOptions(methodinfo.metadataoptions, metadata, [ passthroughin := "__discard" ] );
  DELETE CELL __discard FROM pspoptions;
  RETURN methodinfo.js ? MakeObject(methodinfo.objectname, CELL[ type := createtype, pspoptions ]) : MakeObject(methodinfo.objectname, pspoptions);
}
