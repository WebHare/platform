<?wh
LOADLIB "wh::money.whlib";
LOADLIB "wh::util/comparisons.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::tollium/lib/componentbase.whlib";

LOADLIB "mod::wrd/lib/address.whlib";
LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/internal/support.whlib";


////////////////////
//
// WRD result lists
//

STRING FUNCTION MyConvertToNiceType(OBJECT tolliumuser, OBJECT wrdtype, RECORD attributeinfo, VARIANT value)
{
  SWITCH (attributeinfo.attributetype)
  {
    CASE wrd_attributetype_domain
    {
      RETURN wrdtype->GetDomValTitle(attributeinfo.tag, value);
    }
    CASE wrd_attributetype_date
    {
      RETURN tolliumuser->FormatDate(value, TRUE, TRUE);
    }
    CASE wrd_attributetype_time
    {
      RETURN tolliumuser->FormatTimestamp(value, "milliseconds");
    }
    CASE wrd_attributetype_datetime
    {
      RETURN tolliumuser->FormatDateTime(value, "milliseconds", TRUE, TRUE);
    }
    CASE wrd_attributetype_boolean
    {
      RETURN value ? "TRUE" : "FALSE";
    }
    CASE wrd_attributetype_money
    {
      RETURN FormatMoney(value, 1, ".", ",", TRUE);
    }
    CASE wrd_attributetype_address
    {
      RETURN FormatAddress(value);
    }
    DEFAULT //try sane default handling so we don't have to spell out every string variant
    {
      IF(TYPEID(value) = TYPEID(STRING))
        RETURN value;

      IF(TYPEID(value) IN [TYPEID(INTEGER),TYPEID(INTEGER64)])
        RETURN ToString(value);

      RETURN attributeinfo.attributetypename;
    }
  }
}

PUBLIC MACRO FillWRDList(OBJECT wrdlist, RECORD ARRAY examplerows, RECORD ARRAY typeinfo)
{
  IF(Length(examplerows)=0)
  {
    wrdlist->columns := DEFAULT RECORD ARRAY;
    RETURN;
  }

  RECORD ARRAY outputcolumns, outputrows;

  //Set up headers using the first row (ADDME how to get proper orderings?)
  FOREVERY(RECORD type FROM typeinfo)
  {
    RECORD matchingrow := GetCell(examplerows[0], type.tag);
    FOREVERY(RECORD cellrec FROM UnpackRecord(matchingrow))
    {
      RECORD attrinfo := type.type->GetAttribute(cellrec.name);
      RECORD columndescr := [ title := attrinfo.title
                            , type := "text"
                            , name := "fld" || Length(outputcolumns)
                            , __wrdlist_rowtag := type.tag
                            , __wrdlist_wrdtype := type.type
                            , __wrdlist_cellname := cellrec.name
                            , __wrdlist_attribute := attrinfo
                            ];
      INSERT columndescr INTO outputcolumns AT END;
    }
  }

  FOREVERY(RECORD row FROM examplerows)
  {
    RECORD outrow := [ rowkey := #row+1 ];
    FOREVERY(RECORD col FROM outputcolumns)
    {
      RECORD inrow := GetCell(row, col.__wrdlist_rowtag);
      STRING outcell;

      IF(CellExists(inrow, col.__wrdlist_cellname))
      {
        VARIANT inval := GetCell(inrow, col.__wrdlist_cellname);
        outcell := MyConvertToNiceType(wrdlist->owner->tolliumuser, col.__wrdlist_wrdtype, col.__wrdlist_attribute, inval);
      }
      outrow := CellInsert(outrow, col.name, outcell);
    }
    INSERT outrow INTO outputrows AT END;
  }

  wrdlist->columns := outputcolumns;
  wrdlist->rows := outputrows;
}

PUBLIC STRING FUNCTION RegSafeName(STRING inname)
{
  inname:=Substitute(inname,' ','_');
  inname:=Substitute(inname,'.','_');
  inname:=ToLowercase(inname);
  RETURN inname;
}
PUBLIC STRING FUNCTION GetSchemaBaseKey(OBJECT wrdschema)
{
  RETURN "wrd.interface." || RegSafeName(wrdschema->tag) || '.';
}

PUBLIC OBJECT FUNCTION GetTypeFromData(OBJECT component, OBJECT screen, RECORD indata)
{
  IF(indata.wrdtype = "" OR NOT HavePrimaryTransaction()) // skip when validating
    RETURN DEFAULT OBJECT;

  OBJECT wrdschema;
  IF(ObjectExists(component->contexts->wrdschema))
  {
    wrdschema := component->contexts->wrdschema;
  }
  ELSE IF(indata.wrdschema != "")
  {
    wrdschema := OpenWRDSchema(indata.wrdschema);
    IF(NOT ObjectExists(wrdschema))
      THROW NEW TolliumException(component, "No WRD schema named '" || indata.wrdschema || "' found");
  }
  ELSE
  {
    THROW NEW TolliumException(component, "Must specify wrdcontext or wrdschema if a wrdtype is specified");
  }

  OBJECT wrdtype := wrdschema->GetType(indata.wrdtype);
  IF(NOT ObjectExists(wrdtype))
  {
    STRING bestmatch := GetBestMatch(indata.wrdtype, SELECT AS STRING ARRAY tag FROM wrdschema->ListTypes());
    THROW NEW TolliumException(component, "No such WRD type '" || indata.wrdtype || "' in schema '" || wrdschema->tag || "'"
        || (bestmatch != "" ? ", did you mean type '" || bestmatch || "'?" : ""));
  }
  RETURN wrdtype;
}
