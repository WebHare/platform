<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::xml/xsd.whlib";

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/resources.whlib";


STRING ns_schemadef := "http://www.webhare.net/xmlns/wrd/schemadefinition";

RECORD FUNCTION GetCacheableModuleDefinedWRDSchemas(STRING modulename)
{
  OBJECT xmldef;
  TRY
    xmldef := GetModuleDefinitionXML(modulename);
  CATCH
    RETURN [ eventmasks := [ "system:modulesupdate" ], value := DEFAULT RECORD ARRAY ];
  IF(NOT ObjectExists(xmldef))
    RETURN [ eventmasks := [ "system:modulesupdate" ], value := DEFAULT RECORD ARRAY ];

  OBJECT wrdschemas := xmldef->documentelement->GetChildElementsByTagNameNS(ns_schemadef,"wrdschemas")->Item(0);
  IF(NOT ObjectExists(wrdschemas))
    RETURN [ eventmasks := [ "system:modulesupdate" ], value := DEFAULT RECORD ARRAY ];

  OBJECT ARRAY schemanodes := wrdschemas->GetChildElementsByTagNameNS(ns_schemadef,"schema")->GetCurrentElements();
  RECORD ARRAY schemas;
  FOREVERY(OBJECT schemanode FROM schemanodes)
  {
    IF(NOT IsNodeApplicableToThisWebHare(schemanode))
      CONTINUE;

    STRING tag, deffile;
    IF(schemanode->HasAttribute("name")) //old style
    {
      tag := schemanode->GetAttribute("name");
      IF(schemanode->HasAttribute("definitionfile"))
      {
        deffile := RecordExists(RetrieveWebhareResource("mod::" || modulename || "/" || schemanode->GetAttribute("definitionfile"), [allowmissing:=TRUE]))
                                ? "mod::" || modulename || "/" || schemanode->GetAttribute("definitionfile")
                                : "moduledata::" || modulename || "/" || schemanode->GetAttribute("definitionfile");
      }
    }
    ELSE
    {
      tag := modulename || ":" || schemanode->GetAttribute("tag");
      deffile := MakeAbsoluteResourcePath("mod::" || modulename || "/", schemanode->GetAttribute("definitionfile"));
    }

    IF(deffile = "")
      CONTINUE;

    BOOLEAN isexact := SearchSubstring(tag,'*') = -1 AND SearchSubstring(tag,'?') = -1;

    RECORD schemarec :=
      CELL[ tag
          , isexact
          , definitionfile := deffile
          , autocreate     := isexact AND ParseXSBoolean(schemanode->GetAttribute("autocreate"))
          , originalname   := isexact ? schemanode->GetAttribute("originalname") : ""
          , title          := schemanode->GetAttribute("title")
          ];
    INSERT schemarec INTO schemas AT END;
  }
  RETURN [ eventmasks := [ "system:modulesupdate" ], value := schemas ];
}

PUBLIC RECORD ARRAY FUNCTION GetModuleDefinedWRDSchemas(STRING modulename)
{
  RETURN GetAdhocCached(CELL[ modulename ], PTR GetCacheableModuleDefinedWRDSchemas(modulename));
}

PUBLIC RECORD FUNCTION GetModuleWRDSchemaDefinition(STRING tag, BOOLEAN legacylookup)
{
  STRING ARRAY modstocheck := GetInstalledModuleNames();

  IF(tag LIKE "*:*") //try a fast look up
  {
    STRING fastcheckmod := Tolowercase(Tokenize(tag,":")[0]);
    IF(fastcheckmod IN modstocheck)
    {
      DELETE FROM modstocheck AT SearchElement(modstocheck, fastcheckmod);
      modstocheck := [ fastcheckmod, ...modstocheck ];
    }
  }

  FOREVERY(STRING mod FROM modstocheck)
  {
    RECORD ARRAY schemas := GetModuleDefinedWRDSchemas(mod);
    RECORD attemptlookup := SELECT * FROM schemas WHERE ToUppercase(VAR tag) LIKE ToUppercase(schemas.tag);
    IF(legacylookup AND NOT RecordExists(attemptlookup))
      attemptlookup := SELECT * FROM schemas WHERE ToUppercase(VAR tag) = ToUppercase(schemas.originalname);

    IF(RecordExists(attemptlookup))
      RETURN attemptlookup;
  }
  RETURN DEFAULT RECORD;
}
