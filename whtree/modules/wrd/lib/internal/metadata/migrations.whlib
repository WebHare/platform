<?wh
LOADLIB "wh::crypto.whlib";

LOADLIB "mod::wrd/lib/internal/wrdsettings.whlib";

PUBLIC RECORD FUNCTION MigrateSettings(OBJECT wrdschema)
{
  RECORD data := wrdschema->GetSchemaSetting("wrd:syncpoints", [ fallback := [ __doesnotexist := TRUE ] ]);
  RECORD newsettings;
  IF(RecordExists(data) AND NOT CellExists(data,"__doesnotexist"))
  {
    newsettings := CELL[ ...newsettings, syncpoints := SELECT peer, remotewrdschema := remotewrdschematag FROM data.syncpoints ];
    wrdschema->RemoveSchemaSetting("wrd:syncpoints");
  }
  ELSE IF(NOT RecordExists(data))
  {
    //migrations revision 1 (only active during 5.3 development) & 2 (never merged) set the old wrd:syncpoints to DEFAULT RECORD instead of removing it, so we use the fallback to detect that
    wrdschema->RemoveSchemaSetting("wrd:syncpoints");
  }

  //Migrate/generate wrdauth settings
  RECORD cursettings := GetWRDSettings(wrdschema, [ "domain_secret", "login_settings"]);
  IF(cursettings.domain_secret = "")
  {
    STRING domainsecret := wrdschema->GetSchemaSetting("wrd:auth.domainsecret", [ fallback := "" ]) ?? GenerateUFS128BitId() || GenerateUFS128BitId();
    newsettings := CELL[ ...newsettings, domain_secret := domainsecret ];
    wrdschema->RemoveSchemaSetting("wrd:auth.domainsecret");
  }
  IF(NOT RecordExists(cursettings.login_settings))
  {
    newsettings := CELL[ ...newsettings, login_settings := wrdschema->GetSchemaSetting("wrd:auth.localsettings", [ fallback := DEFAULT RECORD ]) ];
    wrdschema->RemoveSchemaSetting("wrd:auth.localsettings");
  }

  //Migrate wrd:addressverification
  RECORD addressverification := wrdschema->GetSchemaSetting("wrd:addressverification", [ fallback := DEFAULT RECORD ]);
  IF(RecordExists(addressverification))
    newsettings := CELL[ ...newsettings, address_verification := addressverification ];
  wrdschema->RemoveSchemaSetting("wrd:addressverification");

  //Migrate wrd:auth.passwordpolicy.validationchecks
  STRING validationchecks := wrdschema->GetSchemaSetting("wrd:auth.passwordpolicy.validationchecks", [ fallback := "" ]);
  IF(validationchecks != "" )
    newsettings := CELL[ ...newsettings, password_validation_checks := validationchecks ];
  wrdschema->RemoveSchemaSetting("wrd:auth.passwordpolicy.validationchecks");

  IF(RecordExists(newsettings))
    SetWRDSettings(wrdschema, newsettings);

  RETURN DEFAULT RECORD;
}
