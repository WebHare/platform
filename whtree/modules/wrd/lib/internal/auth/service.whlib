<?wh

LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::system/lib/logging.whlib";
LOADLIB "mod::system/lib/webserver.whlib";

LOADLIB "mod::wrd/lib/auth.whlib";


PUBLIC RECORD FUNCTION RPC_Login(STRING url, STRING login, STRING password, BOOLEAN persistent, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
      [ challenge :=      ""
      , returnto :=       ""
      , samlidpreq :=     ""
      , browsertriplet := ""
      ], options);

  OBJECT wrdauthplugin := GetWRDAuthPlugin(GetWebOriginURL(url), [ clientwebserver := GetClientWebserver() ]);
  IF(persistent)
    wrdauthplugin->__GetWRDAuth()->persistentcookie := TRUE;

  UpdateAuditContext(CELL[ options.browsertriplet ]);

  RECORD result := wrdauthplugin->Login(login, password, CELL
        [ options.challenge
        , options.returnto
        , options.samlidpreq
        , options.browsertriplet
        ]);

  IF(result.success)
  {
    RECORD userinfo := wrdauthplugin->GetUserInfo();
    IF(RecordExists(userinfo))
      INSERT CELL userinfo := userinfo INTO result;
  }
  RETURN result;
}

/** @param url URL to find the wrdauth plugin
    @param firstfactorproof Login proof generated by Login
    @param persistent
    @param type Type of login
    @param data Login verification data
    @cell options.challenge
    @cell options.returnto
    @cell options.samlidpreq
    @cell options.browsertriplet
*/
PUBLIC RECORD FUNCTION RPC_LoginSecondFactor(STRING url, STRING firstfactorproof, BOOLEAN persistent, STRING type, RECORD data, RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions(
      [ challenge :=      ""
      , returnto :=       ""
      , samlidpreq :=     ""
      , browsertriplet := ""
      ], options);

  OBJECT wrdauthplugin := GetWRDAuthPlugin(GetWebOriginURL(url), [ clientwebserver := GetClientWebserver() ]);
  IF(persistent)
    wrdauthplugin->__GetWRDAuth()->persistentcookie := TRUE;

  UpdateAuditContext(CELL[ options.browsertriplet ]);

  RECORD result := wrdauthplugin->LoginSecondFactor(firstfactorproof, type, data, CELL
      [ options.challenge
      , options.returnto
      , options.samlidpreq
      , options.browsertriplet
      ]);

  IF(result.success)
  {
    RECORD userinfo := wrdauthplugin->GetUserInfo();
    IF(RecordExists(userinfo))
      INSERT CELL userinfo := userinfo INTO result;
  }
  RETURN result;
}

PUBLIC RECORD FUNCTION RPC_StartLogin(STRING type, STRING tag, STRING url, RECORD options)
{
  OBJECT wrdauthplugin := GetWRDAuthPlugin(url, [ clientwebserver := GetClientWebserver() ] );
  // Remove wrdauth_returned variable from url we return to, it isn't reset in the return path
  STRING redirectto := DeleteVariableFromURL(url, "wrdauth_returned");
  RETURN wrdauthplugin->GenerateLoginRequest(type, tag, redirectto, PickCells(options, [ "action", "passive", "forcelogin", "allowlogout" ]));
}
