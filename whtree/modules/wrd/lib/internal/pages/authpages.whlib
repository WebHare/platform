<?wh

LOADLIB "wh::javascript.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::witty.whlib";
LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/otp.whlib";
LOADLIB "wh::xml/dom.whlib";

LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/services.whlib";
LOADLIB "mod::system/lib/webserver.whlib";
LOADLIB "mod::system/lib/internal/cluster/logging.whlib";

LOADLIB "mod::publisher/lib/webdesign.whlib";
LOADLIB "mod::publisher/lib/forms/base.whlib";
LOADLIB "mod::publisher/lib/forms/conditions.whlib";

LOADLIB "mod::tollium/lib/gettid.whlib";

LOADLIB "mod::wrd/lib/api.whlib";
LOADLIB "mod::wrd/lib/internal/auth/legacy-api.whlib";

RECORD FUNCTION GetIncompleteAccountSessionData() {
  /* identity.ts:
  "platform:incomplete-account": {
      user: number;
      returnTo: string;
      failedchecks: string[];
      badPasswordTime: Temporal.Instant | null;
    };
  }*/

  RECORD session := GetWebSessionData(GetFormWebVariable("token"), "platform:incomplete-account");
  IF(NOT RecordExists(session))
    RETURN DEFAULT RECORD;

  RETURN EnforceStructure(CELL[
    user := 0,
    returnto := "",
    failedchecks := STRING[],
    badpasswordtime := DEFAULT DATETIME
  ], session);
}

MACRO RunWRDAuthPageWitty(OBJECT webcontext, STRING component, RECORD data)
{
  STRING witty := webcontext->GetWRDAuthPlugin()->authpageswitty;
  IF(witty != "")
  {
    OBJECT userwitty := LoadWittyLibrary(witty, "HTML-NI");
    IF(userwitty->HasComponent(component))
    {
      userwitty->RunComponent(component,data);
      RETURN;
    }
  }

  OBJECT systemwitty := LoadWittyLibrary("mod::wrd/witty/authpages.witty","HTML-NI");
  systemwitty->RunComponent(component, data);
}

PUBLIC STRING FUNCTION GetWRDPagesCleanURL(RECORD options DEFAULTSTO DEFAULT RECORD)
{
  options := ValidateOptions([ currenturl := ""], options);
  IF(options.currenturl = "")
    options.currenturl := GetFormRequestURL();

  RETURN UpdateURLVariables(options.currenturl, [ wrd_pwdaction := ""
                                                , _ed := ""
                                                , verifier := ""
                                                ]);
}

MACRO PTR FUNCTION RenderLinkExpiredText(OBJECT webdesign) {
  OBJECT wrdschema := webdesign->GetWRDAuthPlugin()->wrdschema;
  RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
  RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "linkexpired-intro" AND language = webdesign->languagecode;
  IF (NOT RecordExists(introtext))
    introtext :=
        [ htmltext := StringToBlob(
            `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.linkexpired-title")}</h2>
            <p class="normal">${GetTid("wrd:site.forms.authpages.linkexpired")}</p>`)
        ];
  RETURN PTR webdesign->RenderRTD(introtext, [ rtdtype := "http://www.webhare.net/xmlns/wrd/authtexttype" ]);
}

/////////////////////////////////////////////////////////////////////////////
//
// Login
//

PUBLIC STATIC OBJECTTYPE LoginForm EXTEND Formbase <
  STRING logincontrol;

  PUBLIC BOOLEAN loginemail;
  PUBLIC STRING webharelogintitle;

  MACRO NEW()
  {
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    IF(wrdschema->accountemailtag = wrdschema->accountlogintag)
    {
      ^login->title := GetTid("wrd:site.forms.authpages.email");
      ^login->validationchecks := ["email"];
      ^login->autocomplete := ["email"];
      this->loginemail := TRUE;
    }

    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD intro_services_text := SELECT AS RECORD text FROM authpages_texts WHERE tag = "login-services-intro" AND language = this->formlanguagecode;
    IF (RecordExists(intro_services_text))
      ^intro_services->richvalue := intro_services_text;
    ELSE
      ^intro_services->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.login-services")}</h2>`)
          ];

    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "login-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.login-title")}</h2>
              <p class="normal">${this->loginemail
                ? GetTid("wrd:site.forms.authpages.login-email-intro")
                : GetTid("wrd:site.forms.authpages.login-username-intro")}
              </p>`)
          ];

    RECORD webharebutton := SELECT AS RECORD text FROM authpages_texts WHERE tag = "login-services-webhare" AND language = this->formlanguagecode;
    IF(RecordExists(webharebutton)) //quick and dirty html->text
      this->webharelogintitle := MakeXMLDocumentFromHTML(webharebutton.htmltext)->documentelement->textcontent;
    ELSE
      this->webharelogintitle := GetTid("wrd:site.forms.authpages.login-withemail");

    this->logincontrol := GetFormWebVariable("wrdauth_logincontrol");
    // FIXME should this even be here, shouldn't a router handle it before invoking LoginPage ?
    // IF (this->logincontrol != "")
    //   this->formcontext->GetWRDAuthPlugin()->ProcessReturnTo(this->logincontrol);
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata) {
    OBJECT authplugin := this->formcontext->GetWRDAuthPlugin();
    RECORD result := authplugin->__DoLoginTS(^login->value, ^password->value, GetFormRequestURL(), CELL [ useragent := GetWebHeader("User-Agent"), this->logincontrol ]);
    IF(CellExists(result,'submitinstruction'))
      RETURN CELL[ result.submitinstruction ];

    OBJECT work := this->BeginWork([ database := FALSE]);
    work->AddErrorFor(^password, result.error);
    work->Finish();
    RETURN DEFAULT RECORD;
  }
>;

PUBLIC OBJECT FUNCTION GetLoginPage(OBJECT webdesign, RECORD options) {
  IF(IsRequestPost()) {
    STRING loginmethod := GetWebVariable("wrdauth_loginmethod");
    RECORD method := SELECT * FROM options.loginmethods WHERE tag = loginmethod;
    STRING landing := UpdateURLVariables(GetRequestURL(), CELL[wrdauth_loginmethod := ""]);
    IF(NOT RecordExists(method))
      Redirect(landing);

    STRING ARRAY toks := Tokenize(loginmethod,':');
    ExecuteSubmitInstruction(webdesign->GetWRDAuthPlugin()->GenerateLoginRequest(toks[0], toks[1], landing));
  }

  OBJECT loginform := webdesign->OpenForm("wrd:authpages#login");
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.login-title");

  //TODO: extract loginmethods from WRD instead of requiring an option.  we can do this as soon as siteprofile <loginmethods> is dropped
  RECORD wittyvars := [ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                      , loginmethods := (SELECT action := UpdateURLVariables(GetRequestURL(), CELL["wrdauth_loginmethod" := tag])
                                              , title
                                              , icon
                                              , formid := "sso" || #loginmethods
                                           FROM options.loginmethods
                                          WHERE visibility = "always") //filtering for display, but not when processing POSTs
                      , form := loginform->GetWittyData()
                      , webharelogintitle := loginform->webharelogintitle
                      ];

  IF(options.ongetloginwittydata != DEFAULT FUNCTION PTR)
  {
    wittyvars := CELL[ ...wittyvars
                     , ...options.ongetloginwittydata()
                     ];
  }

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "login", wittyvars));
}


PUBLIC STATIC OBJECTTYPE TotpForm EXTEND FormBase <
  MACRO NEW() {
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "totp-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.totp-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.totp-intro")}</p>`)
          ];
  }

  UPDATE PUBLIC MACRO PrepareForFrontend() {
    ^token->value := GetWebVariable("token"); //insert the POST variable, formapi's variable-based prefilling is client side so it can't use POST content
    FormBase::PrepareForFrontend();
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata) {
    RECORD state := DecryptForThisServer("platform:totpchallenge", GetFormWebVariable("token") ?? ^token->value, [ fallback := DEFAULT RECORD]);
    IF(NOT RecordExists(state))
      THROW NEW Exception("Invalid token"); //FIXME redirect/exit to login?

    OBJECT work := this->BeginWork([database := FALSE]);

    //TODO quick format checks/rate limiting, avoid actual lock as long as we can ?
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD result := ValidateSecondFactor(wrdschema, state.userid, "totp", CELL[ code := ^totp->value ], state);
    IF(NOT result.success) {
      work->AddErrorFor(^totp, GetTid("wrd:site.forms.authpages.totp-invalid"));
    }

    IF(work->HasFailed()){
      work->Finish();
      RETURN DEFAULT RECORD;
    }

    IF(NOT work->HasFailed()) {
      RECORD checkresult := CalLJS("@mod-platform/js/auth/harescript.ts#verifyPasswordComplianceForHS", this->formcontext->GetWRDAuthPlugin()->__WRDAuthPluginSettings_HS(), state.userid, state.password, GetFormWebVariable("pathname"), state.returnto, GetEnrichedAuditContext());

      IF(CellExists(checkresult,'navigateto'))
        RETURN work->Finish() ? [ submitinstruction := checkresult.navigateto ] : DEFAULT RECORD;

      work->AddErrorFor(^totp, checkresult.error); //FIXME use 'code' for translateable error
    }

    work->Finish();
    RETURN DEFAULT RECORD;
  }
>;


PUBLIC OBJECT FUNCTION GetTotpPage(OBJECT webdesign, RECORD options) {
  OBJECT totpform := webdesign->OpenForm("wrd:authpages#totp");
  RECORD wittyvars := [ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                      , form := totpform->GetWittyData()
                      , token := GetWebVariable("token")
                      , backlink := ""//FIXME
                      ];

  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.login-title");

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "totp", wittyvars));
}


/////////////////////////////////////////////////////////////////////////////
//
// Forgot password (sends you the email)
//


PUBLIC OBJECTTYPE ForgotPasswordForm EXTEND Formbase
<
  MACRO NEW() {
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "forgotpassword-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.forgotpassword-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.forgotpassword-intro")}</p>`)
          ];

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "forgotpassword-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.forgotpassword-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.forgotpassword-processed")}</p>`)
          ];
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata)
  {
    OBJECT work := this->BeginWork([database := FALSE]);
    IF(NOT work->Finish())
      RETURN DEFAULT RECORD;

    this->formcontext->GetWRDAuthPlugin()->HandleForgotPassword(^email->value, [ currenturl := GetWRDPagesCleanURL() ]);
    RETURN DEFAULT RECORD;
  }
>;


PUBLIC OBJECT FUNCTION GetForgotPasswordPage(OBJECT webdesign, RECORD options)
{
  OBJECT loginform := webdesign->OpenForm("wrd:authpages#forgotpassword");
  loginform->^email->value := GetWebVariable("email");
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.forgotpassword-title");

  RECORD wittyvars := [ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                      , form := loginform->GetWittyData()
                      , backlink := GetWRDPagesCleanURL()
                      ];
  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "forgotpassword", wittyvars));
}

/////////////////////////////////////////////////////////////////////////////
//
// Reset password (link from email)
//

STATIC OBJECTTYPE PasswordFormBase EXTEND FormBase <
  OBJECT wrdschema;

  MACRO NEW() {
    this->wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    IF(this->wrdschema->accountemailtag = this->wrdschema->accountlogintag)
      ^login->title := GetTid("wrd:site.forms.authpages.email");
  }

  MACRO CheckPasswordUpdate(OBJECT work, INTEGER entityid)
  {
    RECORD result := this->formcontext->GetWRDAuthPlugin()->CheckPasswordUpdate(entityid, ^passwordnew->value);
    IF(NOT result.success)
      work->AddErrorFor(^passwordnew, result.message);
  }
>;

PUBLIC STATIC OBJECTTYPE ResetPasswordForm EXTEND PasswordFormBase <
  PUBLIC RECORD verifyresult;

  UPDATE PUBLIC MACRO PrepareForFrontend() {
    PasswordFormBase::PrepareForFrontend();

    OBJECT plugin := this->formcontext->GetWRDAuthPlugin();
    // checkResetPassword(schemaTag: string, tok: string, verifier: string, skipVerifierCheck: boolean)
    this->verifyresult := CallJS("@mod-platform/js/auth/harescript.ts#checkResetPassword", plugin->wrdschema->tag, GetFormWebVariable("_ed"), "", TRUE, GetTidLanguage());
    IF(this->verifyresult.result = "ok")
      ^login->value := this->verifyresult.login;

    ^verifier->visible := this->verifyresult.needsverifier;
    ^verifier->required := ^verifier->visible;
  }

  PUBLIC MACRO SetAction(STRING action) {
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = `${action}-intro` AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${action = "setpassword" ? GetTid("wrd:site.forms.authpages.setpassword-title") : GetTid("wrd:site.forms.authpages.resetpassword-title")}</h2>
              <p class="normal">${action = "setpassword" ? GetTid("wrd:site.forms.authpages.setpassword-intro") : GetTid("wrd:site.forms.authpages.resetpassword-intro")}</p>`)
          ];
    INSERT `wh-wrdauth-${action}__intro` INTO ^intro->groupclasses AT END;

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "passwordchange-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${action = "setpassword" ? GetTid("wrd:site.forms.authpages.setpassword-title") : GetTid("wrd:site.forms.authpages.resetpassword-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.passwordchange-processed")}</p>`)
          ];
    INSERT `wh-wrdauth-${action}__done` INTO ^intro->groupclasses AT END;
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata) {
    OBJECT plugin := this->formcontext->GetWRDAuthPlugin();
    OBJECT work := this->BeginWork( [ database := FALSE ] );

    IF(^passwordnew->value != ^passwordrepeat->value) //mark the 2nd password as failed immediately
      work->AddErrorFor(^passwordrepeat, GetTid("wrd:site.forms.authpages.passwordsmismatch"));

    RECORD setresult := CallJS("@mod-platform/js/auth/harescript.ts#doResetPassword", plugin->wrdschema->tag, GetFormWebVariable("_ed"), ^verifier->value, ^passwordnew->value, GetTidLanguage());
    IF(CellExists(setresult, "verifyFailed")) {
      IF(setresult.verifyFailed.result IN ["expired","alreadychanged"]) {
        work->AddErrorFor(^passwordnew, GetTid("wrd:site.forms.authpages.linkexpired"));
      } ELSE IF(setresult.verifyFailed.result = "badverifier") {
        work->AddErrorFor(^verifier, GetTid("wrd:site.forms.authpages.invalidverifier"));
      } ELSE {
        THROW NEW Exception("Unexpected verify result: " || setresult.verifyFailed.result);
      }
    }
    IF(CellExists(setresult, "updateFailed")) {
      work->AddErrorFor(^passwordnew, setresult.updatefailed.message);
    }

    IF(work->Finish()) {
      OBJECT thankyoupage := this->GetFormPages()[END-1].page;
      //FIXME if navigateto is not there, we may be dealing with eg. account-disabled. how to handle that? (I guess redirecting such pages to an explicit error page instead )
      thankyoupage->redirect := [ type := "redirect", url := this->verifyresult.returnto ];
      thankyoupage->exitbutton := GetTid("wrd:site.forms.authpages.resetpassword-continue");
    }

    RETURN DEFAULT RECORD;
  }
>;

PUBLIC OBJECT FUNCTION GetResetPasswordPage(OBJECT webdesign, STRING action, RECORD options)
{
  OBJECT loginform := webdesign->OpenForm("wrd:authpages#resetpassword");
  loginform->SetAction(action);
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := loginform->verifyresult.issetpassword
                                                 ? GetTid("wrd:site.forms.authpages.setpassword-title")
                                                 : GetTid("wrd:site.forms.authpages.resetpassword-title");

  RECORD wittyvars := CELL[ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                          ];
  IF(loginform->verifyresult.result IN ["expired","alreadychanged"]) {
    INSERT CELL intro_render := RenderLinkExpiredText(webdesign) INTO wittyvars;
    RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "linkexpired", wittyvars));
  }

  wittyvars := CELL[ ...wittyvars
                   , form := loginform->GetWittyData()
                   , continuelink := loginform->verifyresult.returnto
                   ];

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, action, wittyvars));
}

/////////////////////////////////////////////////////////////////////////////
//
// Change email
//

PUBLIC OBJECTTYPE EmailChangeForm EXTEND Formbase
<
  MACRO NEW()
  {
    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "emailchange-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.emailchange-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.emailchange-intro")}</p>`)
          ];

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "emailchange-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.emailchange-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.emailchange-processed", `<span data-merge="textContent:form.email"></span>`)}</p>`)
          ];
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata)
  {
    OBJECT work := this->BeginWork();

    OBJECT authplugin := this->formcontext->GetWRDAuthPlugin();
    RECORD userinfo := authplugin->GetLoggedinEntityFields( STRING[authplugin->wrdschema->accountemailtag] );
    IF(NOT RecordExists(userinfo))
      THROW NEW Exception("User does not appear to be logged in");

    STRING oldemail := GetCell(userinfo, authplugin->wrdschema->accountemailtag);
    IF(^email->value = oldemail)
    {
      work->AddErrorFor(^email, GetTid("wrd:site.forms.authpages.emailchange-isyourmail"));
    }

    IF(NOT work->HasFailed())
    {
      //FIXME check if email already in use
      //FIXME fixup the audit event generation around passwordreset links and verification links. it looks a bit messed up now, different things going to different logs

      RECORD emaildata := [ oldemail := oldemail
                          , newemail := ^email->value
                          , hostname := UnpackURL(GetFormRequestURL()).host
                          , confirmlink := ""
                          ];

      IF(ToUppercase(oldemail) != ToUppercase(^email->value)) //different email
      {
        INTEGER match := authplugin->wrdschema->accounttype->Search(authplugin->wrdschema->accountemailtag, ^email->value, [matchcase := FALSE]);
        IF(match = 0) //not in use
        {
          emaildata.confirmlink := authplugin->CreateVerificationLink(GetFormRequestURL(), authplugin->GetLoggedinEntity(), [ secret := [ newemail := ^email->value ]]).verifiedlink;
        }
      }

      STRING mailtemplate := this->formcontext->targetapplytester->GetDefaultMailTemplate();
      OBJECT mail := authplugin->PrepareAuthPageMail("emailchangemail");
      mail->mergerecord := emaildata;
      mail->mailto := STRING[^email->value];

      mail->QueueMailInWork();
    }

    work->Finish();
    RETURN DEFAULT RECORD;
  }
>;

PUBLIC OBJECT FUNCTION GetEmailChangePage(OBJECT webdesign, RECORD options)
{
  OBJECT loginform := webdesign->OpenForm("wrd:authpages#emailchange");
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.emailchange-title");

  RECORD wittyvars := CELL[ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                          ];

  STRING text_emailchange_processed := EncodeHTML(GetTid("wrd:site.forms.authpages.emailchange-processed", "$$$EMAILADDRESS$$$"));
  text_emailchange_processed := Substitute(text_emailchange_processed, "$$$EMAILADDRESS$$$", `<span data-merge="textContent:form.email"></span>`);

  wittyvars := CELL[ ...wittyvars
                   , form := loginform->GetWittyData()
                   , text_emailchange_processed := PTR Print(text_emailchange_processed)
                   ];

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "emailchange", wittyvars));
}

PUBLIC OBJECT FUNCTION GetEmailChangedPage(OBJECT webdesign, RECORD options)
{
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.emailchange-title");

  OBJECT authplugin := webdesign->GetWRDAuthPlugin();
  RECORD verifyresult := authplugin->ProcessVerificationLink(GetWebVariable("_ed"),GetWebVariable("verifier"));
  RECORD wittyvars := CELL[ ...authplugin->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                          ];
  IF(NOT verifyresult.success) {
    INSERT CELL intro_render := RenderLinkExpiredText(webdesign) INTO wittyvars;
    RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "linkexpired", wittyvars));
  }

  GetPrimary()->BeginWork();
  OBJECT toupdate := authplugin->wrdschema->accounttype->GetEntity(verifyresult.entityid);
  RECORD updaterec := CellInsert(CELL[], authplugin->wrdschema->accountemailtag, verifyresult.secret.newemail);
  toupdate->UpdateEntity(updaterec);
  GetPrimary()->CommitWork();
  authplugin->MarkVerificationLinkUsed(verifyresult.entityid, verifyresult);

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "emailchanged", wittyvars));
}

/////////////////////////////////////////////////////////////////////////////
//
// Change password
//

PUBLIC STATIC OBJECTTYPE PasswordChangeForm EXTEND PasswordFormBase <
  MACRO NEW() {
    ^login->value := this->wrdschema->accounttype->GetEntityField(this->formcontext->GetWRDAuthPlugin()->GetLoggedinEntity(), this->wrdschema->accountlogintag);

    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "passwordchange-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.passwordchange-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.passwordchange-intro")}</p>`)
          ];

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "passwordchange-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.passwordchange-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.passwordchange-processed")}</p>`)
          ];
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata)
  {
    OBJECT work := this->BeginWork();
    OBJECT authplugin := this->formcontext->GetWRDAuthPlugin();

    IF(NOT authplugin->VerifyCurrentPassword(^currentpassword->value))
      work->AddErrorFor(^currentpassword, GetTid("wrd:site.forms.authpages.incorrectpassword"));
    IF(^passwordnew->value != ^passwordrepeat->value) //mark the 2nd password as failed immediately
      work->AddErrorFor(^passwordrepeat, GetTid("wrd:site.forms.authpages.passwordsmismatch"));

    //We'll still check your new password though as it's nicer to dismiss it immediately if unusable, and not after repeating it right
    RECORD updateres := authplugin->ExecutePasswordSet(^passwordnew->value, [ dryrun := work->HasFailed() ]);
    IF(NOT updateres.success)
      work->AddErrorFor(^passwordnew, updateres.message);

    work->Finish();
    RETURN DEFAULT RECORD;
  }
>;

PUBLIC OBJECT FUNCTION GetPasswordChangePage(OBJECT webdesign, RECORD options)
{
  OBJECT loginform := webdesign->OpenForm("wrd:authpages#passwordchange");
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.passwordchange-title");

  OBJECT authplugin := webdesign->GetWRDAuthPlugin();
  RECORD wittyvars := CELL[ ...authplugin->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                          ];
  STRING currentlogin := authplugin->accounttype->GetEntityField(authplugin->GetLoggedinEntity(), authplugin->wrdschema->accountlogintag);

  wittyvars := CELL[ ...wittyvars
                   , currentlogin
                   , form := loginform->GetWittyData()
                   ];

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "passwordchange", wittyvars));
}

PUBLIC STATIC OBJECTTYPE CompleteAccountPasswordForm EXTEND FormBase <
  PUBLIC RECORD session;

  MACRO NEW() {
    this->session := GetIncompleteAccountSessionData();
    IF(NOT RecordExists(this->session))
      THROW NEW Exception("Session not found"); //FIXME redirect away or refresh the form and let upper error handlers deal with this

    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    IF(wrdschema->accountemailtag = wrdschema->accountlogintag)
      ^login->title := GetTid("wrd:site.forms.authpages.email");
    ^login->value := wrdschema->accounttype->GetEntityField(this->session.user, wrdschema->accountlogintag);

    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "completeaccount-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(`<h2 class="heading2">${GetTid("wrd:site.forms.authpages.completeaccount-title")}</h2>`)
          ];

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "passwordchange-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.completeaccount-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.passwordchange-processed")}</p>`)
          ];
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata) {
    OBJECT plugin := this->formcontext->GetWRDAuthPlugin();

    OBJECT work := this->BeginWork();

    IF(^passwordnew->value != ^passwordrepeat->value) //mark the 2nd password as failed immediately
      work->AddErrorFor(^passwordrepeat, GetTid("wrd:site.forms.authpages.passwordsmismatch"));

    //We'll still check your new password though as it's nicer to dismiss it immediately if unusable, and not after having to repeat it
    RECORD checkresult := plugin->CheckPasswordUpdate(this->session.user, ^passwordnew->value);

    IF(NOT checkresult.success)
      work->AddErrorFor(^passwordnew, checkresult.message);

    IF(NOT work->HasFailed())
      plugin->UpdateUserPassword(this->session.user, ^passwordnew->value);

    IF(work->Finish()) {
      //FIXME invalidate the session for further password changing, or auto-skip this form once complexity requirements are reached
      OBJECT thankyoupage := this->GetFormPages()[END-1].page;
      thankyoupage->redirect := [ type := "redirect", url := GetFormRequestURL() ];
      thankyoupage->exitbutton := GetTid("wrd:site.forms.authpages.login-button");

      RETURN [ success := TRUE ];
    } ELSE {
      RETURN DEFAULT RECORD;
    }
  }
>;

PUBLIC STATIC OBJECTTYPE CompleteAccountTotpForm EXTEND FormBase <
  PUBLIC RECORD session;

  MACRO NEW() {
    this->session := GetIncompleteAccountSessionData();
    IF(NOT RecordExists(this->session))
      THROW NEW Exception("Session not found"); //FIXME redirect away or refresh the form and let upper error handlers deal with this

    ^secret->visiblecondition := FCIsSet(^showsecretkey);

    OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
    RECORD ARRAY authpages_texts := GetWRDSetting(wrdschema, "authpages_texts");
    RECORD introtext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "completeaccount-totp-intro" AND language = this->formlanguagecode;
    IF (RecordExists(introtext))
      ^intro->richvalue := introtext;
    ELSE
      ^intro->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.completeaccount-totp-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.completeaccount_totp_intro")}</p>`)
              //TODO: if *forced* to setup, explain that too
          ];

    RECORD donetext := SELECT AS RECORD text FROM authpages_texts WHERE tag = "completeaccount-totp-done" AND language = this->formlanguagecode;
    IF (RecordExists(donetext))
      ^done->richvalue := donetext;
    ELSE
      ^done->richvalue :=
          [ htmltext := StringToBlob(
              `<h2 class="heading2">${GetTid("wrd:site.forms.authpages.completeaccount-totp-title")}</h2>
              <p class="normal">${GetTid("wrd:site.forms.authpages.totpsetup-processed")}</p>`)
          ];
  }

  PUBLIC RECORD FUNCTION Submit(RECORD extradata) {
    RECORD totpsetup := DecodeHSON(^setup->value);
    OBJECT work := this->BeginWork();

    IF (NOT TestTOTPCode(DecodeBase32(totpsetup.secret), ^totp->value).success) {
      work->AddErrorFor(^totp, GetTid("wrd:site.forms.authpages.totp-invalid"));
    }

    IF(NOT work->HasFailed()) {
      //FIXME ensure that the auditlog shows the 2FA setup
      OBJECT wrdschema := this->formcontext->GetWRDAuthPlugin()->wrdschema;
      RECORD userauthinfo := wrdschema->accounttype->GetEntityField(this->session.user, wrdschema->accountpasswordtag);
      userauthinfo := CELL[...userauthinfo
                          , totp := CELL[ totpsetup.url
                                        , totpsetup.backupcodes
                                        ]
                          ];
      wrdschema->accounttype->UpdateEntity(this->session.user, CellInsert(DEFAULT RECORD, wrdschema->accountpasswordtag, userauthinfo));
      ^backupcodes->value := Detokenize((SELECT AS STRING ARRAY code FROM totpsetup.backupcodes), "\n");

      OBJECT thankyoupage := this->GetFormPages()[END-1].page;
      thankyoupage->redirect := [ type := "redirect", url := GetFormRequestURL() ];
      thankyoupage->exitbutton := GetTid("wrd:site.forms.authpages.login-button");
    }

    IF(work->Finish()) {
      //FIXME invalidate the session for further password changing
      RETURN [ success := TRUE ];
    } ELSE {
      RETURN DEFAULT RECORD;
    }
  }
>;

PUBLIC OBJECT FUNCTION GetCompleteAccountPage(OBJECT webdesign, RECORD options) {
  //FIXME dupe work, forms do this too
  RECORD session := GetIncompleteAccountSessionData();
  BOOLEAN require2fa := "require2fa" IN session.failedchecks;
  STRING ARRAY failedpwdchecks := ArrayDelete(session.failedchecks,["require2fa"]);

  RECORD wittyvars := CELL[ ...webdesign->GetWRDAuthPlugin()->GetWRDAuthRouterWittyData(GetWRDPagesCleanURL())
                          ];
  webdesign->isstandardcontentpage := TRUE;
  webdesign->pagetitle := GetTid("wrd:site.forms.authpages.passwordchange-title");

  IF(NOT RecordExists(session)) {
    INSERT CELL intro_render := RenderLinkExpiredText(webdesign) INTO wittyvars;
    RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "linkexpired", wittyvars));
  }

  OBJECT wrdschema := webdesign->GetWRDAuthPlugin()->wrdschema;
  IF(NOT wrdschema->accountpasswordisauthsettings)
    THROW NEW Exception(`The complete account page is not available for wrdschema '${wrdschema->tag}' as it still uses a PASSWORD field`); //as we wouldn't have dates

  RECORD userauthinfo := wrdschema->accounttype->GetEntityField(session.user, wrdschema->accountpasswordtag);
  IF(Length(failedpwdchecks) > 0) { //there were issues with the password itself
    IF (Length(userauthinfo.passwords) = 0 OR userauthinfo.passwords[END-1].validfrom <= session.badpasswordtime) { //you haven't fixed the password yet
      OBJECT completeform := webdesign->OpenForm("wrd:authpages#completeaccountpassword");
      completeform->^message->richvalue := [ htmltext := StringToBlob(`<p class="normal">${GetHTMLTid("system:tolliumapps.login.forgotpassword.warning_passwordmustbereset")}</p>`) ];
      completeform->^message->visible := TRUE;

      wittyvars := CELL[ ...wittyvars
                      , form := completeform->GetWittyData()
                      // , message := GetTid("system:tolliumapps.login.forgotpassword.warning_passwordmustbereset")
                      // , continuelink :=
                      ];

      RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "completeaccount_password", wittyvars));
    }
  }

  IF(require2fa) { //we wanted you to setup 2FA
    IF(NOT RecordExists(userauthinfo.totp)) {
      OBJECT completeform := webdesign->OpenForm("wrd:authpages#completeaccounttotp");
      STRING issuer := GetWRDSetting(wrdschema, "password_totp_issuer") ?? "WebHare"; // Issuer may be shown in the authenticator app
      STRING username := wrdschema->accounttype->GetEntityField(session.user, wrdschema->accountlogintag);
      RECORD newtotp := PrepareTOTPForUser(username, issuer);

      //TODO do we need encryption/signature here? as the secret is exposed anyway in the setup page
      completeform->^setup->value := EncodeHSON(newtotp);
      completeform->^secret->value := newtotp.secret;

      wittyvars := CELL[ ...wittyvars
                      , form := completeform->GetWittyData()
                      // , continuelink := GetRequestURL()
                      , qrcodelink := GetDataUrl(newtotp.qrpng, "image/png")
                      ];

      RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "completeaccount_totp", wittyvars));
    }
  }

  //FIXME ensure that the auditlog is clear about what's happening in this flow
  RECORD logininstruction := CallJS("@webhare/auth#prepareFrontendLogin", session.returnto, session.user);
  ExecuteSubmitInstruction(logininstruction);
  ABORT("This line should never be reached");
}

PUBLIC OBJECT FUNCTION GetOIDCFailurePage(OBJECT webdesign, RECORD options) {
  STRING reason := GetWebVariable("reason");
  STRING loginname := GetWebVariable("loginname");
  STRING backlink := ResolveToAbsoluteURL(GetFormRequestURL(), "/" || GetWebVariable("pathname"));

  webdesign->isstandardcontentpage := TRUE;

  SWITCH(reason) {
    CASE "unknown-login" {
      reason := GetTid("wrd:site.forms.authpages.extloginfailure-unknownlogin", loginname);
    }
    CASE "account-disabled" {
      reason := GetTid("wrd:site.forms.authpages.extloginfailure-accountdisabled", loginname);
    }
  }

  RETURN NEW SimpleWebPage(PTR RunWRDAuthPageWitty(webdesign, "extloginfailure",
    CELL[ reason, backlink ]));
}
