<?wh

LOADLIB "wh::adhoccache.whlib";
LOADLIB "wh::files.whlib";
LOADLIB "wh::util/algorithms.whlib";
LOADLIB "wh::util/langspecific.whlib";
LOADLIB "wh::util/localization.whlib";

LOADLIB "mod::system/lib/configure.whlib";


PUBLIC RECORD ARRAY FUNCTION GetWRDCountryList(STRING language, STRING selected, INTEGER wrd_schema DEFAULTSTO 0)
{
  selected := ToUppercase(selected);
  language := ToUppercase(language);

  RECORD ARRAY countries := [ [ isempty := true, selected := FALSE, code := "", title := "" ] ]
                            CONCAT
                            SELECT isempty := FALSE, selected := FALSE, code, title := GetCell(country,language)
                              FROM GetCountryList(language) AS country;

  countries := SELECT *
                    , selected := code = VAR selected
                 FROM countries
             ORDER BY code != "", NormalizeText(title, language);

  RETURN countries;
}

PUBLIC RECORD ARRAY FUNCTION GetWRDCountryPhoneCodes(STRING language)
{
  RETURN GetAdhocCached([ type := "phonecodes", language := language ], PTR GetCachedWRDCountryPhoneCodes(language));
}

RECORD FUNCTION GetCachedWRDCountryPhoneCodes(STRING language)
{
  language := ToUppercase(language);

  RECORD ARRAY countrylist := SELECT code, title := GetCell(country,language) FROM GetCountryList(language) AS country;

  RECORD ARRAY phonecodes :=
      SELECT code
           , phone
        FROM GetAdhocCached(CELL[ type := "phone" ], PTR GetWRDPhoneData()) AS info
       WHERE CellExists(info, "phone") ? phone != "" : FALSE;

  RETURN
      [ value := (SELECT * FROM JoinArrays(phonecodes, "code", countrylist, [ title := "" ]) ORDER BY NormalizeText(title, language))
      , eventmasks := [ "system:softreset" ]
      ];
}

RECORD FUNCTION GetWRDPhoneData()
{
  // Retrieve phone number metadata from https://www.npmjs.com/package/libphonenumber-js
  BLOB metadatajson := GetDiskResource(MergePath(GetInstallationRoot(), "node_modules/libphonenumber-js/metadata.full.json"));
  RECORD metadata := DecodeJSONBlob(metadatajson);

  /*
    A country looks like this:

      "NL": [
        "31", // country calling code
        "00", // international call prefix
        // ... validation regexes and data follows
      ],

    We're only interested in the country calling code, which is the first value of the country's array of data
  */
  RECORD ARRAY results :=
      SELECT code := name
           , phone := value[0]
        FROM UnpackRecord(metadata.countries);

  RETURN
      [ value := results
      , eventmasks := [ "system:softreset" ]
      ];
}
