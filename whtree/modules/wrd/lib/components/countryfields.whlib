<?wh
LOADLIB "mod::tollium/lib/componentbase.whlib";
LOADLIB "mod::tollium/lib/gettid.whlib";
LOADLIB "mod::wrd/lib/address.whlib";
LOADLIB "mod::wrd/lib/internal/addressfields.whlib";
LOADLIB "mod::wrd/lib/internal/addressverification/support.whlib";


BOOLEAN FUNCTION IsAddressEqual(RECORD lhs, RECORD rhs)
{
  IF(RecordExists(rhs) != RecordExists(lhs))
    RETURN FALSE;

  FOREVERY(RECORD lhscell FROM UnpackRecord(lhs))
  {
    IF(lhscell.name = "COUNTRY_FULL")
      CONTINUE; //don't compare country full names
    IF(NOT CellExists(rhs, lhscell.name) OR lhscell.value != GetCell(rhs, lhscell.name))
      RETURN FALSE;
  }
  //no need to inspect RHS if all cells in LHS match (considering that they _should_ have the same record structure anyway IF their countries match)
  RETURN TRUE;
}

PUBLIC OBJECTTYPE TolliumWRDCountryFields EXTEND TolliumComponentBase
< // ---------------------------------------------------------------------------
  //
  // Variables
  //

  RECORD ARRAY country_fields;


  STRING currentcountry;
  STRING pvt_country;
  OBJECT lookupbutton;
  OBJECT lookupaction;


  // ---------------------------------------------------------------------------
  //
  // Public variables
  //

  PUBLIC BOOLEAN have_nl_validation; //FIXME cleanup


  PUBLIC STRING defaultcountry;

  PUBLIC STRING ARRAY checks;

  // ---------------------------------------------------------------------------
  //
  // Properties
  //

  PUBLIC PROPERTY country(pvt_country, SetCountry);

  PUBLIC PROPERTY currentfields(country_fields,-);


  PUBLIC OBJECT wrdschema;


  PUBLIC PROPERTY value(GetValue, SetValue);

  //The address we verified (after explicit check or initially set to assume it)
  PUBLIC RECORD verified_address;

  // ---------------------------------------------------------------------------
  //
  // Constructor & tollium admin
  //

  MACRO NEW()
  {
    EXTEND this BY TolliumIsComposable;
    this->invisibletitle := TRUE;
    this->checks := default_addressfield_options;
  }


  PUBLIC UPDATE MACRO CompositionMetadataIsUpdated()
  {
    this->wrdschema := ObjectExists(this->composition->wrdtype) ? this->composition->wrdtype->wrdschema : DEFAULT OBJECT;
  }


  PUBLIC UPDATE MACRO StaticInit(RECORD description)
  {
    TolliumComponentBase::StaticInit(description);
    this->checks := description.checks;
    this->defaultcountry := description.defaultcountry;
    this->pvt_country := this->defaultcountry;
    this->required := description.required;
  }

  PUBLIC BOOLEAN FUNCTION IsAddressVerified()
  {
    RETURN IsAddressEqual(this->verified_address, this->value);
  }

  PUBLIC BOOLEAN FUNCTION CheckVerifyAddress(BOOLEAN used_lookup_button) {
    RECORD addressdata := this->value;
    IF (NOT RecordExists(addressdata)) {
      IF(used_lookup_button)
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_not_enough_data"));
      RETURN TRUE;
    }

    OBJECT lookup;
    IF(ObjectExists(this->contexts->applytester))
      lookup := GetAddressLookupAPI(this->contexts->applytester, addressdata.country);

    IF(NOT ObjectExists(lookup)) {
      OBJECT wrdschema := this->wrdschema ?? this->contexts->wrdschema;
      IF(ObjectExists(wrdschema))
        lookup := GetWRDAddressLookupAPI(wrdschema, addressdata.country);
    }

    IF(NOT ObjectExists(lookup)) {
      IF(used_lookup_button)
        this->owner->RunSimpleScreen("info", GetTid("wrd:commondialogs.messages.addressverify_unavailable"));
      RETURN TRUE;
    }

    RECORD verify_data := lookup->CheckAddress(addressdata, [ alwaysverify := TRUE ]);
    RECORD newaddr := CELL[ ...verify_data.data, ...verify_data.looked_up ];

    SWITCH (verify_data.status) {
      CASE "ok" {
        this->value := newaddr;
        this->verified_address := newaddr;

        IF (used_lookup_button)
          this->owner->RunSimpleScreen("info", GetTid("wrd:commondialogs.messages.addressverify_ok"));

        RETURN TRUE;
      }
      CASE "incomplete" {
        this->value := newaddr;
        this->verified_address := newaddr;
      }
      CASE "different_citystreet" {
        OBJECT dialog := this->owner->LoadScreen("wrd:commondialogs.verificationresult");
        dialog->original->value := FormatAddress(addressdata);
        dialog->returned->value := FormatAddress(verify_data.data);
        dialog->problems_street->visible := ToUppercase(addressdata.street) != ToUppercase(verify_data.data.street);
        dialog->problems_city->visible := ToUppercase(addressdata.city) != ToUppercase(verify_data.data.city);
        IF(dialog->RunModal()="overwrite")         {
          this->value := verify_data.data;
        }
      }
      CASE "not_enough_data" {
        this->value := newaddr; //copy what we did get
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_not_enough_data"));
      }
      CASE "zip_not_found" {
        this->value := newaddr; //copy what we did get
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_zip_not_found"));
      }
      CASE "address_not_found" {
        this->value := newaddr; //copy what we did get
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_address_not_found"));
      }
      CASE "invalid_nr_detail" {
        this->value := newaddr; //copy what we did get
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_invalid_nr_detail"));
      }
      CASE "invalid_zip" {
        this->value := newaddr; //copy what we did get
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_invalid_zip"));
      }
      CASE "lookup_failed" {
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_lookup_failed"));
      }
      CASE "invalid_settings" {
        this->owner->RunSimpleScreen("error", GetTid("wrd:commondialogs.messages.addressverify_invalid_settings"));
      }
    }
    RETURN FALSE;
  }

  // ---------------------------------------------------------------------------
  //
  // Getters/setters
  //

  MACRO SetCountry(STRING countrycode)
  {
    this->pvt_country := countrycode;
    this->UpdateFields();
  }


  RECORD FUNCTION GetValue()
  {
    IF(this->pvt_country = "")
      RETURN DEFAULT RECORD;

    RECORD address := [ country := this->pvt_country ];
    FOREVERY(RECORD field FROM this->country_fields)
      IF(field.tag!="")
        address := CellInsert(address, field.tag, field.component->value);

    RETURN address;
  }

  MACRO SetValue(RECORD address)
  {
    STRING newcountry := RecordExists(address) AND CellExists(address,"COUNTRY") ? address.country : this->defaultcountry;
    IF(newcountry != this->pvt_country)
      this->SetCountry(newcountry);
    this->SetSubFields(address);
  }

  MACRO SetSubFields(RECORD address)
  {
    FOREVERY(RECORD field FROM this->country_fields)
    {
      STRING setvalue := CellExists(address,field.tag) ? GetCell(address, field.tag) : "";
      IF(setvalue LIKE "*\n*" AND NOT field.textarea)
        setvalue := Substitute(setvalue,"\n"," ");

      field.component->value := setvalue;
      field.component->visible := field.hide = FALSE OR setvalue != ""; //we show hidden fields if they already contain data
    }
  }

  UPDATE MACRO SetReadonly(BOOLEAN newstate)
  {
    TolliumComponentBase::SetReadonly(newstate);

    FOREVERY(RECORD field FROM this->country_fields)
      field.component->readonly := newstate;
  }


  UPDATE MACRO SetRequired(BOOLEAN newrequired)
  {
    TolliumComponentBase::SetRequired(newrequired);

    // Base behaviour of subcomponents is to inherit requiredness from parent, so
    // change them back to the value we want
    FOREVERY (RECORD rec FROM this->country_fields)
      rec.component->required := rec.required AND this->required;
  }

  // ---------------------------------------------------------------------------
  //
  // Helper functions
  //

  MACRO RegenerateFields()
  {
    this->currentcountry := ToUppercase(this->pvt_country);

    //FIXME Skip deletion if there is no change in fields
    FOREVERY(RECORD field FROM this->country_fields)
      field.component->DeleteComponent();

    IF(ObjectExists(this->lookupbutton))
      this->lookupbutton->RemoveFromParent();

    DELETE FROM this->country_fields ALL;
    IF(this->pvt_country = '')
      RETURN;

    RECORD format := GetAddressFormat(this->currentcountry, [ language := GetTidLanguage() ]);

//FIXME use wrdschema for validation data
    BOOLEAN haszipforce := this->currentcountry="NL" AND "nl-zip-force" IN this->checks;
    BOOLEAN hasziplookup := haszipforce OR (this->currentcountry="NL" AND (this->have_nl_validation OR "nl-zip-suggest" IN this->checks));
    IF(hasziplookup)
    {
      // We're rearranging the fields when we can verify the address
      //ADDME: hard-coded voor Nederland op het moment...
      format.fields := SELECT * FROM format.fields ORDER BY SearchElement(["ZIP","NR_DETAIL","STREET","CITY","STATE"], tag);
    }

    OBJECT lastfield := this;
    FOREVERY(RECORD field FROM format.fields)
    {
      BOOLEAN textarea := field.tag = "STREET" AND NOT format.onlystreet;
      OBJECT extracell := this->owner->CreateTolliumComponent(textarea ? "textarea" : "textedit");
      extracell->maxlength := 512; // 512 bytes should be enough for every address line :-)
      extracell->title := field.title;
      extracell->required := field.required AND this->required; //TODO why is 'this->required' even an option? shouldn't we just have a 'clear address' button higher up ?
      extracell->readonly := this->readonly;
      extracell->enabled := this->enabled AND (haszipforce = FALSE OR field.tag NOT IN ["STREET","CITY"]);
      IF(textarea)
        extracell->height := "3gr";

      INSERT CELL[ tag := ToUppercase(field.tag)
                 , component := extracell
                 , field.required
                 , field.hide
                 , textarea
                 ]  INTO this->country_fields AT END;

      this->parent->InsertComponentAfter(extracell, lastfield, #field > 0);
      lastfield := extracell;

      IF(field.tag = "NR_DETAIL" AND hasziplookup)
      {
        IF(NOT ObjectExists(this->lookupbutton))
          this->BuildLookupButton();

        this->parent->InsertComponentAfter(this->lookupbutton, lastfield, FALSE);
        lastfield := this->lookupbutton;
      }
    }
  }

  MACRO BuildLookupButton()
  {
    this->lookupaction := this->owner->CreateTolliumComponent("action");
    this->lookupaction->onexecute := PTR this->DoLookup;

    this->lookupbutton := this->owner->CreateTolliumComponent("button");
    this->lookupbutton->action := this->lookupaction;
    this->lookupbutton->title := GetTid("wrd:components.addressfield.lookup");
    this->lookupbutton->enabled := this->enabled;
  }

  MACRO UpdateFields()
  {
    IF(this->currentcountry = this->pvt_country)
      RETURN; //fields are already in sync

    RECORD saveaddress := this->value;
    this->RegenerateFields();
    this->SetSubFields(saveaddress);
  }

  MACRO DoLookup()
  {
    //TODO share with RPC_VerifyAddress
    this->CheckVerifyAddress(TRUE);
  }

  UPDATE PUBLIC MACRO ValidateValue(OBJECT work)
  {
    IF (ToUppercase(this->pvt_country) = "NL")
    {
      // Get 'postcode' field, validate it
      RECORD zipfield := (SELECT * FROM this->country_fields WHERE ToUppercase(tag) = "ZIP");
      IF (zipfield.component->value != "")
      {
        STRING normalized := FixupZipCodeNL(zipfield.component->value);
        IF (normalized = "")
          work->AddError(GetTid("wrd:commondialogs.messages.addressverify_invalid_zip"));
      }
    }

    FOREVERY (RECORD field FROM this->country_fields)
      field.component->ValidateValue(work);
  }
>;
