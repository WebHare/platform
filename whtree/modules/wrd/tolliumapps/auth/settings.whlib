<?wh

LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/internal/auth/support.whlib";
LOADLIB "mod::wrd/lib/internal/wrdsettings.whlib";


MACRO SetTimeUnit(OBJECT textedit, OBJECT unitpulldown, INTEGER64 setvalue)
{
  unitpulldown->options := [[ rowkey := "days", title := GetTid("~units.days") ]
                           ,[ rowkey := "hours", title := GetTid("~units.hours") ]
                           ,[ rowkey := "minutes", title := GetTid("~units.minutes") ]
                           ,[ rowkey := "seconds", title := GetTid("~units.seconds") ]
                           ];

  IF((setvalue % (86400*1000)) = 0)
  {
    unitpulldown->value := "days";
    textedit->value := setvalue / (86400*1000);
  }
  ELSE IF((setvalue % (3600*1000)) = 0)
  {
    unitpulldown->value := "hours";
    textedit->value := setvalue / (3600*1000);
  }
  ELSE IF((setvalue % (60*1000)) = 0)
  {
    unitpulldown->value := "minutes";
    textedit->value := setvalue / (60*1000);
  }
  ELSE //round up anything ms to 1 second
  {
    unitpulldown->value := "seconds";
    textedit->value := (setvalue + 999) / (1000);
  }
}

INTEGER64 FUNCTION GetTimeUnit(OBJECT textedit, OBJECT unitpulldown)
{
  IF(unitpulldown->value = "days")
    RETURN textedit->value * (86400 * 1000i64);
  IF(unitpulldown->value = "hours")
    RETURN textedit->value * (3600 * 1000i64);
  IF(unitpulldown->value = "minutes")
    RETURN textedit->value * (60 * 1000i64);
  RETURN textedit->value * 1000i64;
}

/*
    Direct access: https://my.webhare.dev/?app=wrd(system:usermgmt)/authsettings
*/

PUBLIC STATIC OBJECTTYPE WRDAuthSettings EXTEND TolliumScreenBase <
  RECORD ARRAY authtexts;

  MACRO Init(RECORD data) {
    //get settings, coerce them to the proper types
    RECORD settings := EnforceStructure(wrdauth_defaultconfiguration, GetWRDSetting(this->contexts->wrdschema, "login_settings"));
    SetTimeUnit(^expire_login, ^expire_login_unit, settings.expire_login);
    SetTimeUnit(^expire_persistentlogin, ^expire_persistentlogin_unit, settings.expire_persistentlogin);

    IF(settings.round_longlogins_to >= 0) {
      ^roundlogins->value := TRUE;
      ^round_longlogins_to->value := settings.round_longlogins_to;
    }
    ^round_longlogins_tz->value := settings.round_longlogins_tz;
    SetTimeUnit(^round_minduration, ^round_minduration_unit, settings.round_minduration);
    this->OnRoundLoginsChange();

    ^passwordchecks->value := GetWRDSetting(this->contexts->wrdschema, "password_validation_checks");
    
    RECORD ARRAY authpages_texts := GetWRDSetting(this->contexts->wrdschema, "authpages_texts");
    INTEGER first_text := -1;
    FOREVERY (STRING tag FROM SELECT AS STRING ARRAY rowkey FROM ^tag->options)
      FOREVERY (STRING language FROM SELECT AS STRING ARRAY rowkey FROM ^language->options) {
        RECORD result := ^texts->LoadTabsExtension(Resolve("settings.xml#authtext"));
        OBJECT addedtab := result.addedtabs[0];
        addedtab->title := language || "/" || tag;
        OBJECT text := SELECT AS OBJECT comp FROM ToRecordArray(result.components, "comp") WHERE comp->name LIKE "*!text";
        text->value := SELECT AS RECORD COLUMN text FROM authpages_texts WHERE COLUMN language = VAR language AND COLUMN tag = VAR tag;
        IF (first_text < 0 AND RecordExists(text->value))
          first_text := Length(this->authtexts);
        INSERT CELL[ tag, language, addedtab, text ] INTO this->authtexts AT END;
      }
    ^texts->RunExtensionsPostInit();
    IF (first_text >= 0) {
      ^tag->value := this->authtexts[first_text].tag;
      ^language->value := this->authtexts[first_text].language;
    } ELSE
      this->OnTextSelect();
  }

  MACRO OnRoundLoginsChange() {
    ^round_longlogins_to->enabled := ^roundlogins->value;
    ^round_longlogins_tz->enabled := ^roundlogins->value;
  }
  
  MACRO OnTextSelect() {
    ^texts->selectedtab := SELECT AS OBJECT addedtab FROM this->authtexts WHERE language = ^language->value AND tag = ^tag->value;
  }

  BOOLEAN FUNCTION Submit() {
    OBJECT work := this->BeginWork();
    SetWRDSetting(this->contexts->wrdschema, "password_validation_checks", ^passwordchecks->value);

    SetWRDSetting(this->contexts->wrdschema, "login_settings",
      CELL[ ...GetWRDSetting(this->contexts->wrdschema, "login_settings")
          , expire_login := GetTimeUnit(^expire_login, ^expire_login_unit)
          , expire_persistentlogin := GetTimeUnit(^expire_persistentlogin, ^expire_persistentlogin_unit)
          , round_minduration := GetTimeUnit(^round_minduration, ^round_minduration_unit)
          , round_longlogins_to := ^roundlogins->value ? ^round_longlogins_to->value : -1
          , round_longlogins_tz := ^round_longlogins_tz->value
          ]);

    RECORD ARRAY authpages_texts;
    FOREVERY (RECORD authtext FROM this->authtexts)
      IF (RecordExists(authtext.text->value))
        INSERT CELL[ authtext.language, authtext.tag, text := authtext.text->value ] INTO authpages_texts AT END;
    SetWRDSetting(this->contexts->wrdschema, "authpages_texts", authpages_texts);

    RETURN work->Finish();
  }
>;
