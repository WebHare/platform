<?wh
LOADLIB "wh::javascript.whlib";

LOADLIB "mod::tollium/lib/dialogs.whlib";
LOADLIB "mod::tollium/lib/screenbase.whlib";

LOADLIB "mod::wrd/lib/screenbase.whlib";


PUBLIC STATIC OBJECTTYPE ConfigJS EXTEND PSPSettingsExtensionBase
<
  RECORD ARRAY methods;
  BOOLEAN islive;

  UPDATE PUBLIC RECORD FUNCTION GetPaymentProviderValue()
  {
    RETURN CELL[ ...^methodsettings->value
               , this->methods
               , this->islive
               ];
  }
  UPDATE PUBLIC MACRO SetPaymentProviderValue(RECORD settings)
  {
    ^methodsettings->value := settings;
    IF(RecordExists(settings))
    {
      this->methods := settings.methods;
      this->islive := settings.islive;
      this->ProcessConfig();
    }
  }
  MACRO ProcessConfig()
  {
    ^methods->value := Detokenize((SELECT AS STRING ARRAY title FROM this->methods ORDER BY ToUppercase(title)),', ');
  }
  MACRO DoConnect()
  {
    TRY
    {
      RECORD result := CallJS("@mod-wrd/js/internal/paymentbridge.ts#connectPSP", ^methodsettings->^driver->value, ^methodsettings->^configuration->value);
      IF(result.error != "")
      {
        this->owner->RunSimpleScreen("error", result.error);
        RETURN;
      }

      this->methods := result.methods;
      this->islive := result.islive;
      this->ProcessConfig();
      this->owner->RunSimpleScreen("info", GetTid("wrd:tolliumapps.payments.common.connectionsucceeded"));
    }
    CATCH(OBJECT e)
    {
      RunExceptionReportDialog(this, e);
    }
  }
>;

PUBLIC STATIC OBJECTTYPE PayInfo EXTEND TolliumScreenBase
<
  MACRO Init(RECORD data)
  {
    //TODO JS drivers need to return a set of displayable fields
  }
>;
