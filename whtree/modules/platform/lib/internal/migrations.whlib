<?wh

LOADLIB "mod::system/lib/configure.whlib";


PUBLIC RECORD FUNCTION MigrateAccountStatus(OBJECT wrdschema) {
  IF(NOT RecordExists(wrdschema->^wrd_person->GetAttribute("whuser_disabled")))
    RETURN DEFAULT RECORD; //this schema never had whuser_disabled. even though we use at=update this may happen if usermgmt is applied to an already created wrdschema

  /* Transfer whuser_disabled and whuser_disablereason to wrdauth_account_status
     and remove the attributes from wrd_person. Remove whuser_disable_type if already created (april 2023)
  */
  RECORD outputcolumns := CELL[ "wrd_id", "whuser_disablereason", "whuser_disabled" ];
  BOOLEAN has_whuser_disable_type := RecordExists(wrdschema->^wrd_person->GetAttribute("whuser_disable_type"));
  IF(has_whuser_disable_type)
    INSERT CELL whuser_disable_type := "whuser_disable_type" INTO outputcolumns;

  RECORD ARRAY users := wrdschema->^wrd_person->RunQuery(CELL [outputcolumns, historymode := "all"]);
  INTEGER numinactive, numactive, numblocked;

  FOREVERY(RECORD user FROM users) {
    RECORD set_accountstatus;
    IF(user.whuser_disabled)
      IF(has_whuser_disable_type AND user.whuser_disable_type = "inactive") {
        set_accountstatus := CELL[ status := "inactive" ];
        numinactive := numinactive + 1;
      } ELSE {
        set_accountstatus := CELL[ status := "blocked", reason := user.whuser_disablereason ];
        numblocked := numblocked + 1;
      }
    ELSE {
      set_accountstatus := CELL[ status := "active" ];
      numactive := numactive + 1;
    }

    wrdschema->^wrd_person->UpdateEntity(user.wrd_id, CELL[ wrdauth_account_status := set_accountstatus ]);
  }

  wrdschema->^wrd_person->DeleteAttribute("whuser_disabled");
  wrdschema->^wrd_person->DeleteAttribute("whuser_disablereason");
  IF(has_whuser_disable_type)
    wrdschema->^wrd_person->DeleteAttribute("whuser_disable_type");

  RETURN CELL[ numinactive, numactive, numblocked ];
}

PUBLIC RECORD FUNCTION MigrateMailServers(OBJECT wrdschema) {
  IF(NOT RecordExists(wrdschema->^mailroute->GetAttribute("serverurl")))
    RETURN DEFAULT RECORD; //nothing to migrate

  RECORD ARRAY servermapping;
  RECORD ARRAY currentroutes := wrdschema->^mailroute->RunQuery([outputcolumns := CELL[ "wrd_id", "serverurl" ]]);
  STRING defaultroute := ReadRegistryKey("system.services.smtp.defaultmailroute", [ fallback := "" ]);
  INTEGER defaultrouteid;

  IF(defaultroute != "")
    defaultrouteid := wrdschema->^mailserver->CreateEntity(CELL[wrd_title := "Default route",  serverurl := defaultroute, wrd_tag := "DEFAULTSERVER" ])->id;

  DeleteRegistryKey("system.services.smtp.defaultmailroute");

  FOREVERY(RECORD route FROM currentroutes) {
    IF(route.serverurl = "null") //blackhole
      wrdschema->^mailroute->UpdateEntity(route.wrd_id, CELL[ rewritemailaddress := "null" ]);
    ELSE {
      INTEGER newserver := defaultrouteid;
      IF(route.serverurl != "") {
        newserver := wrdschema->^mailserver->Search("serverurl", route.serverurl);
        IF(newserver = 0)
          newserver := wrdschema->^mailserver->CreateEntity(CELL[ wrd_title := "Mailserver #" || Length(wrdschema->^mailserver->RunQuery(CELL[])) + 1, serverurl := route.serverurl ])->id;
        ELSE
          print('reuse server ' || newserver ||'\n');
      }
      wrdschema->^mailroute->UpdateEntity(route.wrd_id, CELL[ server := newserver ]);
    }
  }

  wrdschema->^mailroute->DeleteAttribute("serverurl"); //not backing up as it may contain passwords

  RETURN DEFAULT RECORD;
}
