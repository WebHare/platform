<?wh

LOADLIB "wh::javascript.whlib";
LOADLIB "wh::xml/xsd.whlib";
LOADLIB "mod::system/lib/resources.whlib";

/** for efficiency, combine all loaded parser hooks into one whlib.
    keep LOADLIBS to a minimum, but there's no harm in loading librarys the SPParser would have loaded anyway */

PUBLIC RECORD FUNCTION ParseWRDAuthConfigurationNode(OBJECT siteprofile, OBJECT node)
{
  RECORD config :=
          [ wrdschema := node->GetAttribute("wrdschema")
          , supportobjectname := node->GetAttribute("supportobjectname")
          , passwordresetlifetime := ToInteger(node->GetAttribute("passwordresetlifetime"), 3 * 24 * 60) //in minutes
          , cachefields := ParseXSList(node->GetAttribute("cachefields"))
          , servicemailtemplate := siteprofile->ParseFSPath(node, "servicemailtemplate")
          , loginpage := node->GetAttribute("loginpage")
          , cookiename := node->GetAttribute("cookiename")
          , customizer := MakeAbsoluteResourcePath(siteprofile->siteprofile->name, node->GetAttribute("customizer"))
          , firstloginfield := node->GetAttribute("firstloginfield")
          , lastloginfield := node->GetAttribute("lastloginfield")
          , samesitecookie := node->HasAttribute("samesitecookie") ? node->GetAttribute("samesitecookie") : "lax"
          , routerfeatures := ParseXSList(node->GetAttribute("routerfeatures"))
          , authpageswitty := siteprofile->ParseFSPath(node, "authpageswitty")
          , cookiedomain := node->GetAttribute("cookiedomain")
          ];

  IF(config.supportobjectname != "" AND config.supportobjectname NOT LIKE "*#*")
  {
    STRING supportlib := siteprofile->ParseFSPath(node, "supportlibrary");
    config.supportobjectname := supportlib || "#" || config.supportobjectname;
  }
  ELSE
  {
    config.supportobjectname := MakeAbsoluteResourcePath(siteprofile->siteprofile->name, config.supportobjectname);
  }

  IF (config.cookiename != "" AND config.cookiename NOT LIKE "*webharelogin*")
    THROW NEW Exception(`Invalid wrdauth cookie name '${config.cookiename}', must contain 'webharelogin'`);

  RETURN config;
}

PUBLIC RECORD FUNCTION ParseWRDPaymentsConfigurationNode(OBJECT siteprofile, OBJECT node)
{
  RECORD config :=
          [ wrdschema := node->GetAttribute("wrdschema")
          , providerfield := node->GetAttribute("providerfield")
          , paymentfield := node->GetAttribute("paymentfield")
          ];

  RETURN config;
}

PUBLIC RECORD FUNCTION GetDebugSettingsAsYaml(OBJECT node) {
  /* Imagined YAML
     debugSettings:
     - userWarning: ....
       groupTitle: Goodies
       flags:
         win:
           title: "Pxl events"
           requireRight: xx
  */

  RECORD setting := [
    flags := DEFAULT RECORD,
    group_title := node->GetAttribute("title")
  ];

  IF(node->HasAttribute("userwarning"))
    setting := CELL[...setting, user_warning := node->GetAttribute("userwarning") ];

  FOREVERY (OBJECT subnode FROM node->childnodes->GetCurrentElements())
    IF(subnode->localname = "option") {
      RECORD flag := [
        title := subnode->GetAttribute("title")
      ];
      IF(subnode->HasAttribute("requireright"))
        INSERT CELL require_right := subnode->GetAttribute("requireright") INTO flag;
      setting.flags := CellInsert(setting.flags, ToSnakeCase(subnode->GetAttribute("tag")), flag);
    }

  RETURN setting;
}

PUBLIC RECORD FUNCTION GetFormIntegrationAsYaml(OBJECT node) {
  RECORD formintegration := CELL[];

  IF(node->HasAttribute("usecaptcha"))
    formintegration := CELL[ ...formintegration, captcha := ParseXSBoolean(node->GetAttribute("usecaptcha")) ];
  IF(node->HasAttribute("allowsubmittype"))
    formintegration := CELL[ ...formintegration, match_submit_type := ParseXSBoolean(node->GetAttribute("allowsubmittype")) ];
  IF(node->HasAttribute("enablepagetitles"))
    formintegration := CELL[ ...formintegration, page_titles := ParseXSBoolean(node->GetAttribute("enablepagetitles")) ];
  IF(node->HasAttribute("enableinfotexts"))
    formintegration := CELL[ ...formintegration, info_texts := ParseXSBoolean(node->GetAttribute("enableinfotexts")) ];
  IF(node->HasAttribute("infotextrtdtype"))
    formintegration := CELL[ ...formintegration, info_text_rtd_type := node->GetAttribute("infotextrtdtype") ];
  IF(node->HasAttribute("mailrtdtype"))
    formintegration := CELL[ ...formintegration, mail_rtd_type := node->GetAttribute("mailrtdtype") ];
  IF(node->HasAttribute("autocompleteusername"))
    formintegration := CELL[ ...formintegration, auto_complete_user_name := node->GetAttribute("autocompleteusername") ];
  IF(node->HasAttribute("webtoolformhooks"))
    formintegration := CELL[ ...formintegration, page_hooks := node->GetAttribute("webtoolformhooks") ];

  IF(node->HasAttribute("countrylist") OR node->HasAttribute("addressoptions") OR node->HasAttribute("addressvalidationkey") OR node->HasAttribute("addressvalidationschema")) {
    formintegration := CELL[ ...formintegration, address_options := DEFAULT RECORD ];
    IF(node->HasAttribute("countrylist"))
      formintegration.address_options := CELL[ ...formintegration.address_options, country_list := ParseXSList(node->GetAttribute("countrylist")) ];
    IF(node->HasAttribute("addressoptions"))
      formintegration.address_options := CELL[ ...formintegration.address_options, derive_from_zip := node->GetAttribute("addressoptions") = "nl-zip-force" ? "force": "suggest" ];
    IF(node->HasAttribute("addressvalidationkey"))
      formintegration.address_options := CELL[ ...formintegration.address_options, validation_registry_key := node->GetAttribute("addressvalidationkey") ];
    IF(node->HasAttribute("addressvalidationschema"))
      formintegration.address_options := CELL[ ...formintegration.address_options, validation_wrd_schema := node->GetAttribute("addressvalidationschema") ];
  }

  RETURN formintegration;
}

PUBLIC RECORD FUNCTION GetCaptchaIntegrationAsYaml(OBJECT node) {
  RECORD captchaintegration := CELL[];
  IF(node->HasAttribute("skipfunction"))
    captchaintegration := CELL[ ...captchaintegration, skip_function := node->GetAttribute("skipfunction") ];
  RETURN captchaintegration;
}
