<?wh

LOADLIB "mod::system/lib/configure.whlib";
LOADLIB "mod::system/lib/database.whlib";
LOADLIB "mod::system/lib/internal/whfs/support.whlib";

CONSTANT RECORD shareopts := CELL[ verb := "", baseurl := ""];

STRING FUNCTION GetWebHareApplicationLink(STRING objectpath, RECORD options) {
  STRING url := (options.baseurl ?? GetPrimaryWebHareInterfaceURL()) || "#applink/" || EncodeURL(objectpath) || (options.verb != "" ? "/-/" || EncodeURL(options.verb) : "");
  RETURN url;
}

STRING FUNCTION GetFullWHFSObjLink(INTEGER objid) {
  RECORD cd := SELECT creationdate FROM system.fs_objects WHERE id = objid;
  IF(NOT RecordExists(cd))
    THROW NEW Exception(`No such object #${objid}`);

  RETURN BuildWHFSObjectRef(objid, cd.creationdate);
}

/** Implement platform object links */
PUBLIC STRING FUNCTION GetWHFSSApplicationLink(INTEGER fsobjid, RECORD options DEFAULTSTO DEFAULT RECORD) {
  options := ValidateOptions(shareopts, options, CELL [ enums := CELL [ verb := [ "" , "edit"] ] ]);

  RETURN GetWebHareApplicationLink("whfs/" || GetFullWHFSObjLink(fsobjid), options);
}

PUBLIC STRING FUNCTION GetFormResultApplicationLink(INTEGER fsobjid, STRING resultguid, RECORD options DEFAULTSTO DEFAULT RECORD) {
  options := ValidateOptions(shareopts, options, CELL [ enums := CELL [ verb := [ "" ] ] ]);

  RETURN GetWebHareApplicationLink("formresult/" || GetFullWHFSObjLink(fsobjid) || "/" || EncodeURL(resultguid), options);
}

PUBLIC RECORD FUNCTION __DecodeWHFSApplicationLink(STRING applink) {
  STRING ARRAY parts := Tokenize(applink, "/-/");
  STRING verb := Length(parts) > 1 ? parts[1] : "";

  STRING ARRAY linktoks := Tokenize(parts[0],'/');
  IF((linktoks[0] = "whfs" AND Length(linktoks) = 2) OR (linktoks[0] = "formresult" AND Length(linktoks) = 3)) {
    STRING fsobjpart := linktoks[1];

    INTEGER objid := ToInteger(Tokenize(fsobjpart, "-")[0],0);
    IF(objid = 0 OR fsobjpart != GetFullWHFSObjLink(objid))
      RETURN DEFAULT RECORD;

    STRING formresult := linktoks[0] = "formresult" ? linktoks[2] : "";
    RETURN CELL [ type := "whfs", fsobjid := objid, verb, formresult ];
  }

  RETURN DEFAULT RECORD;
}
