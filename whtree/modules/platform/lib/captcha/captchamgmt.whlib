<?wh

LOADLIB "wh::internet/urls.whlib";
LOADLIB "wh::internet/webbrowser.whlib";

LOADLIB "mod::system/lib/cluster.whlib";
LOADLIB "mod::system/lib/keystore.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";

PUBLIC RECORD FUNCTION GetCaptchaProvider(STRING url) {
  IF(IsWHDebugOptionSet("nsc")) //the mock for CI is currently provided by the google recaptcha code
    RETURN CELL[ name := "google-recaptcha", apikey := "mock" ];

  RECORD keyinfo := LookupAPIKey("google-recaptcha,friendly-captcha", url);
  IF(RecordExists(keyinfo))
    RETURN CELL[ name := keyinfo.keytype
               , apikey := keyinfo.apikey
               , privatekey := keyinfo.privatekey
               ];

  THROW NEW Exception("Unconfigured URL for recaptcha: " || url);
}

PUBLIC BOOLEAN FUNCTION VerifyCaptchaResponse(STRING url, STRING response)  {
  IF(IsWHDebugOptionSet("nsc"))
    RETURN response = "mock";

  RECORD provider := GetCaptchaProvider(url);
  RECORD ARRAY postvars := [[ name := "secret", value := provider.privatekey ]
                           ,[ name := "response", value := response ]
                           ];

  OBJECT browser := NEW WebBrowser;
  IF(NOT browser->PostWebPage("https://www.google.com/recaptcha/api/siteverify", postvars, ""))
  {
    LogError("socialite:recaptcha", "Unexpected response from siteverify", [ error := "UNEXPECTED-SITEVERIFY-RESPONSE", secret := provider.privatekey, response := response, status := browser->GetHTTPStatus() ]);
    THROW NEW Exception("Unexpected response from siteverify");
  }
  RECORD result := DecodeJSONBlob(browser->content);
  //We need to verify the hostname, otherwise a key not locked to sites can be used elsewhere
  RETURN result.success AND ToUppercase(result.hostname) = ToUppercase(UnpackURL(url).host);
}
