<?wh

LOADLIB "wh::internet/urls.whlib";

LOADLIB "mod::platform/lib/captcha/friendly-captcha.whlib";
LOADLIB "mod::platform/lib/captcha/google-recaptcha.whlib";

LOADLIB "mod::system/lib/keystore.whlib";
LOADLIB "mod::system/lib/webserver/whdebug.whlib";

PUBLIC RECORD FUNCTION GetCaptchaProvider(STRING url) {
  IF(IsWHDebugOptionSet("nsc")) //the mock for CI is currently provided by the google recaptcha code
    RETURN CELL[ name := "google-recaptcha", apikey := "mock" ];

  RECORD keyinfo := LookupAPIKey("google-recaptcha,friendly-captcha", url);
  IF(RecordExists(keyinfo))
    RETURN CELL[ name := keyinfo.keytype
               , apikey := keyinfo.apikey
               , privatekey := keyinfo.privatekey
               ];

  THROW NEW Exception("Unconfigured URL for recaptcha: " || url);
}

PUBLIC BOOLEAN FUNCTION VerifyCaptchaResponse(STRING url, STRING response)  {
  IF(response = "") //Don't send anyone a request if we know it won't pass
    RETURN FALSE;

  IF(IsWHDebugOptionSet("nsc"))
    RETURN response = "mock";

  RECORD provider := GetCaptchaProvider(url);
  RECORD verifyresult;
  IF(provider.name = "friendly-captcha")
    verifyresult := VerifyFriendlyCaptchaResponse(provider, response);
  ELSE IF(provider.name = "google-recaptcha")
    verifyresult := VerifyGoogleReCaptchaResponse(provider, response);
  ELSE
    THROW NEW Exception(`Unsupported captcha provider: ${provider.name}`);

  //We need to verify the hostname, otherwise a key not locked to sites can be used elsewhere
  RETURN RecordExists(verifyresult) AND ToUppercase(verifyresult.hostname) = ToUppercase(UnpackURL(url).host);
}
